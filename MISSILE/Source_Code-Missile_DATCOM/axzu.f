      SUBROUTINE AXZU(RLE, T, C, SWP, XU, ZU, DZDX, XM, ZM, RR)
C
C     CALCULATE THE INTERSECTION POINT OF THE ELLIPTICAL NOSE
C     CAP AND CIRCULAR ARC SURFACE AND LOCATION OF POINT WHERE
C     LOCAL SLOPE IS 15 DEG.
C
C     INPUT VARIABLES
C         RLE - L.E. RADIUS
C           T - THICKNESS
C           C - CHORD OF WEDGE
C         SWP - SWEEP ANGLE, DEG.
C
C     OUTPUT VARIABLES
C          XU - X-LOCATION OF INTERSECTION
C          ZU - Z-LOCATION OF INTERSECTION
C        DZDX - SURFACE SLOPE AT INTERSECTION
C          XM - X-LOCATION WHERE SLOPE IS 15 DEG.
C          ZM - Z-LOCATION WHERE SLOPE IS 15 DEG.
C          RR - CIRCULAR ARC RADIUS
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      CS = COS(SWP/RAD)
      C2 = C/2.0
      XU = 0.0
      ZU = RLE
      DO 10 I=1,15
        FR  = (C2-XU)/(T-2.0*ZU)
        RR  = (T-2.0*ZU)*(FR**2+0.25)
        DEL = ASIN((C2-XU)/RR)
        TD = TAN(DEL)
        GG = SQRT(TD*TD/(CS*CS+TD*TD))
        XU2 = RLE*(1.0-GG)/CS
        ZU = RLE*CS*GG/TD
        IF(ABS(XU-XU2) .LT. 1.E-6) GO TO 20
        XU = XU2
   10 CONTINUE
   20 CONTINUE
      XU   = XU2
      DZDX = TD
C
      TM = TAN(15.0/RAD)
      GG = SQRT(TM*TM/(CS*CS+TM*TM))
      XM = RLE*(1.0-GG)/CS
      ZM = RLE*CS*GG/TM
C
      RETURN
      END
