      SUBROUTINE HYPERS(ALPHA,AM,X,R,NPTS,SREF,DREF,XCG,IPRINT,
     1 CA,CN,CM)
C
C***  COMPUTE NEWTONIAN CA, CN, AND CM AT HYPERSONIC SPEEDS
C***  METHOD FROM NASA TN D-176
C
C  ROUTINE ADAPTED FROM DIGITAL DATCOM
C  ORIGINAL ROUTINE WRITTEN BY J.E. WILLIAMS, MCDONNELL DOUGLAS
C  MODIFIED BY W. BLAKE, WL/FIGC
C
C***  INPUTS
C
C  ALPHA - ANGLE OF ATTACK, DEG.
C     AM - FREE STREAM MACH NUMBER
C      X - ARRAY OF BODY LONGITUDINAL STATIONS
C      R - ARRAY OF BODY RADII
C   NPTS - NUMBER OF X AND R COORDINATE POINTS
C   SREF - REFERENCE AREA
C   DREF - REFERENCE LENGTH
C    XCG - LOCATION OF MOMENT REFERENCE CENTER FROM NOSE
C IPRINT - PRESSURE DISTRIBUTION PRINT FLAG
C          0=DO NOT PRINT PRESSURE DISTRIBUTIONS
C          1=PRINT PRESSURE DISTRIBUTION DATA
C          2=PRINT BODY COORDINATES TO TAPE UNIT 3
C
C***  OUTPUTS
C
C     CA - AXIAL FORCE (LESS FRICTION) BASED ON SREF
C     CN - NORMAL FORCE BASED ON SREF
C     CM - PITCHING MOMENT ABOUT XCG, BASED ON SREF AND DREF
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      DIMENSION X(NPTS),R(NPTS),CNN(101),CAA(101),CMM(101)
      DIMENSION XX(101),RR(101),DRDX(101),CP(7),PHI(7),IROUT(2)
C
      DATA GAM / 1.4 /
      DATA PHI / 0., 30., 60., 90., 120., 150., 180. /
C
C***  SET DELTA-X AND BODY LENGTH
C
      DX=(X(NPTS)-X(1))/100.
      AL=X(NPTS)-X(1)
C
C***  GET MAXIMUM RADIUS
C
      RMAX=R(1)
      DO 1000 I=2,NPTS
         IF(R(I) .GT. RMAX)RMAX=R(I)
 1000 CONTINUE
      DMAX=2.*RMAX
C
C***  COMPUTE RADII AND INTEGRAL FACTORS ALONG THE BODY
C
      DO 1010 I=1,101
         XX(I)=X(1)+FLOAT(I-1)*DX
 1010 CONTINUE
C
C ... INTERPOLATE FOR RADIUS AND DIFFERENCE FOR BODY SLOPE
C
      DO 1015 I=1,101
         CALL LNTRP(X,R,NPTS,XX(I),RR(I))
         IF(I.GT.2) DRDX(I-1)=(RR(I)-RR(I-2))/(XX(I)-XX(I-2))
 1015 CONTINUE
         DRDX(1)=  (RR(2) - RR(1))  /(XX(2) - XX(1))
         DRDX(101)=(RR(101)-RR(100))/(XX(101)-XX(100))
C
C ... STAGNATION PRESSURE COEFFICIENT
C
      AK=(GAM+3.)/(GAM+1.)*(1.-2./(GAM+3.)/AM**2)
C
C ... NOW GET INTEGRAL FACTORS
C
      DO 1030 I=1,101
         THETA=ATAN(DRDX(I))*RAD
         CALL NEWTON(THETA,ABS(ALPHA),PHI1,CAF,CNF,CMF)
         CNN(I)=CNF*RR(I)/RMAX
         CAA(I)=CAF*RR(I)/RMAX
         CMM(I)=CMF*RR(I)/RMAX*(XCG-XX(I)+XX(1))/AL
C
         DO 1020 J=1,7
            JJ=J
            IF(ALPHA .LT. 0)JJ=8-J
            CETA=SIN(THETA/RAD)*COS(ALPHA/RAD)-COS(THETA/RAD)*
     1           SIN(ALPHA/RAD)*COS(PHI(J)/RAD)
            CP(JJ)=AK*CETA*CETA
            IF(ALPHA .NE. 0. .AND. PHI(J) .LT. PHI1*RAD)CP(JJ)=0.
 1020    CONTINUE
         XXX=(XX(I)-XX(1))/DMAX
C
C  PRINT PRESSURE COEFFICIENT DATA IF REQUESTED
C
      IF(IPRINT.NE.1) GO TO 1025
C        IF(I.EQ.1.OR.I.EQ.42.OR.I.EQ.82)CALL HEADR9
C        IF(I.EQ.1.OR.I.EQ.42.OR.I.EQ.82)WRITE(6,1040)AM,ALPHA
C        IF(I.EQ.1.OR.I.EQ.42.OR.I.EQ.82)WRITE(6,1050)
         WRITE(10,1060) XXX,(CP(J),J=1,7)
C        IF(I.EQ.41 .OR. I.EQ.81 .OR. I.EQ.101)WRITE(6,1070)
 1025 CONTINUE
C
C  PRINT BODY COORDINATES TO TAPE UNIT 9 IF REQUESTED
C
      IF(IPRINT.NE.2) GO TO 1030
C
C  FOLLOWING CARD REMOVED FOR TECPLOT FORMAT
C
C        IF(I.EQ.1) WRITE(9,1100)
         WRITE(9,1110) XXX*DMAX,RR(I)
 1030 CONTINUE
C
C***  INTEGRATE FOR BODY TOTAL CA, CN, AND CM
C
      CALL QUAD1(CNN,101,DX,ANSCN)
      CALL QUAD1(CAA,101,DX,ANSCA)
      CALL QUAD1(CMM,101,DX,ANSCM)
C
C ... COMPLETE FORCE AND MOMENT CALCULATIONS
C
      CN=AK/PI/RMAX*ANSCN
      CA=AK/PI/RMAX*ANSCA + AK*(R(1)/RMAX*COS(ALPHA/RAD))**2
      CM=AK/PI/RMAX*ANSCM
C
C***  CORRECT FOR NEGATIVE ANGLES OF ATTACK
C
      IF(ALPHA .LT. 0.)CN=-CN
      IF(ALPHA .LT. 0.)CM=-CM
C
C***  CORRECT REFERENCE QUANTITIES
C
      CN=CN*PI*DMAX**2/4./SREF
      CA=CA*PI*DMAX**2/4./SREF
      CM=CM*PI*DMAX**2*AL/4./SREF/DREF
C
C1040 FORMAT(51X,26HBODY ALONE PRESSURE OUTPUT//
C    1 43X,6HMACH =,F5.2,2X,17HANGLE OF ATTACK =,F7.2,5H DEG.)
C1050 FORMAT(//,52X,21HPRESSURE COEFFICIENTS,//,7X,10H  X/DMAX  ,9X,
C    15HPHI=0,8X,6HPHI=30,8X,6HPHI=60,8X,6HPHI=90,7X,7HPHI=120,7X,
C    27HPHI=150,7X,7HPHI=180)
 1060 FORMAT(F14.6,7F10.5)
C1070 FORMAT(49H0 NOTE - PHI=  0 IS TOP VERTICAL CENTER (LEEWARD) /
C    1       53H         PHI=180 IS BOTTOM VERTICAL CENTER (WINDWARD) )
C
C  BODY COORDINATE COMMENT REMOVED FOR TECPLOT FORMAT
C
C1100 FORMAT(3X,43HNEWTONIAN THEORY BODY COORDINATES (IN FEET))
 1110 FORMAT(3X,2F14.6)
C
      RETURN
      END
