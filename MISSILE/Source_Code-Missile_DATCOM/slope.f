      SUBROUTINE SLOPE(RHO,MACH,CLALPA,RENN,XACM,CMCO4)
C
C***  CALCULATE THE SECTION LIFT CURVE SLOPE AND AERODYNAMIC
C***  CENTER AT THE DESIRED MACH/REYNOLDS NUMBER.  THE METHOD
C***  INCLUDES A CORRECTION DUE TO COMPRESSIBILITY.
C
C  ROUTINE ADAPTED FROM DIGITAL DATCOM
C  ORIGINAL ROUTINE WRITTEN BY D. KINSEY, D. BOWERS, AFFDL
C  MODIFIED BY S.R. VUKELICH, MCDONNELL DOUGLAS
C
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      COMMON /ASMVAR/ CBAR,A0,XC,MCC,CLCC,AI,ALO,CLI,ASEP,CLA0,
     1                M,ZTNU(32),ZCNU(32),ST1NU(32),ST2NU(32),
     2                ST3NU(32),ST4NU(32),ST5NU(32),X(32),THNU(32),
     3                THMU(32),VXOVU(32),VXOVL(32),CPUM(32),
     4                CPLM(32),BO(32),CPI(32)
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      DIMENSION CL(2),ALPHA(2),CM(2),XI(32),STNU(32),IROUT(2)
C
C***  WEBER PRESSURE DISTRIBUTION AND CL-ALPHA
C***  METHOD INCLUDES COMPRESSIBILITY
C
      REAL LX,MACH,MCC
C
      XN=32.
      N=XN
      NM1=N-1
      CLALPA=UNUSED
      IF(MACH.GE.1.) GO TO 1070
      ALPHA(1)=ALO
      ALPHA(2)=ALO+1.
      BETA=SQRT(1.-MACH**2)
      A0=SQRT(2.*RHO)
      TMACH=0.
      IF(MACH.NE.0.) TMACH=1./(.7*MACH**2)
 1000 DO 1050 I=1,2
      COSA=COS(ALPHA(I)/RAD)
      SINA=SIN(ALPHA(I)/RAD)
      DO 1020 L=1,NM1
      CPI(L)=1.-(1.+ST1NU(L))**2/(1.+ST2NU(L)**2)
      BO(L)=1.-MACH**2*(1.-MACH*CPI(L))
      IF(BO(L) .LT. 0.) GO TO 1080
      BO(L)=SQRT(BO(L))
      VXOVU(L)=(COSA*(1.+ST1NU(L)/BO(L)+ST4NU(L)/BETA)+SINA/BETA*
     1 (1.+ST3NU(L)/BO(L))*SQRT((1.-X(L))/X(L)))/SQRT(1.+((ST2NU(L)+
     2 ST5NU(L))/BO(L))**2)
      VXOVL(L)=(COSA*(1.+ST1NU(L)/BO(L)-ST4NU(L)/BETA)-SINA/BETA*
     1 (1.+ST3NU(L)/BO(L))*SQRT((1.-X(L))/X(L)))/SQRT(1.+((ST2NU(L)-
     2 ST5NU(L))/BO(L))**2)
      IF(MACH .EQ. 0.) GO TO 1010
      CPUVD=1.+.2*MACH**2*(1.-VXOVU(L)**2)
      IF(CPUVD .LT. 0.) GO TO 1080
      CPLVD=1.+.2*MACH**2*(1.-VXOVL(L)**2)
      IF(CPLVD .LT. 0.) GO TO 1080
      CPUM(L)=TMACH*((CPUVD)**(3.5)-1.)
      CPLM(L)=TMACH*((CPLVD)**(3.5)-1.)
      GO TO 1020
 1010 CPUM(L)=1.-(VXOVU(L))**2
      CPLM(L)=1.-(VXOVL(L))**2
 1020 CONTINUE
C
C***  PRESSURE COEFFICIENT AT THE LEADING EDGE
C
      IF(A0.NE.0.)VXOVU(N)=SINA*((1.+ST3NU(N))/(.5*A0))
      IF(A0.EQ.0.)VXOVU(N)=0.
      VXOVL(N)=VXOVU(N)
      CPUVDN=1.+.2*MACH**2*(1.-(VXOVU(N)/BETA)**2)
      IF(CPUVDN .LT. 0.) GO TO 1080
      CPUM(N)=TMACH*((CPUVDN)**(3.5)-1.)
      IF(TMACH .EQ. 0.) CPUM(N)=1.-(VXOVU(N))**2
      CPLM(N)=CPUM(N)
C
C***  INTEGRATE THE CP CURVE AT THIS ALPHA
C
      SUMCL=0.0
      DO 1030 L=1,NM1
      LX=-(CPUM(L)-CPLM(L))
      SINTH=SIN(THNU(L))/2.
      SUMCL=SUMCL+LX*SINTH
 1030 CONTINUE
      CL(I)=SUMCL*PI/(COSA*XN)
C
C***  PITCHING MOMENT COEFFICIENT
C
      SUMCM=0.0
      DO 1040 L=1,NM1
      LX=CPUM(L)-CPLM(L)
      DCM=LX*(X(L)-.25)*SIN(THNU(L))/2.
      SUMCM=SUMCM+DCM
 1040 CONTINUE
      CM(I)=SUMCM*PI/XN
 1050 CONTINUE
      IF(ABS(CL(1)) .LE. .001) GO TO 1060
      CLALPA=CL(2)-CL(1)
      ALO=ALO-CL(1)/CLALPA
      ALPHA(1)=ALO
      ALPHA(2)=ALO+1.
      GO TO 1000
 1060 CONTINUE
C
C***  GET CORRECTION FACTOR FOR VISCOUS AFFECTS
C
      REN=1.E6
      IF(RENN.GE.1.E6.AND.MCC.NE.0..AND.MACH.NE.0.) REN=RENN
      PHITE=((ZTNU(6)+ZTNU(7))/2.-ZTNU(2))/.089272624
      EXPN=-1.+5.*PHITE/2.
      CON=1.-(ALOG(REN/1.E5))**EXPN*(.232+1.785*PHITE-2.950*PHITE**2)
C
C***  GET LIFT CURVE SLOPE FROM THESE TWO ANGLES OF ATTACK
C
      CLALPA=(CL(2)-CL(1))*CON*1.05
C
C***  GET PITCHING MOMENT SLOPE
C
      CMALPA=CM(2)-CM(1)
      CMCO4=CM(1)
C
C***  NOW THE AERODYNAMIC CENTER
C
      XACM=0.25-CMALPA/CLALPA
 1070 CONTINUE
      GO TO 1090
 1080 CONTINUE
C
C***  ANALYSIS FAILING -- PRINT INFORMATIVE DIAGNOSTIC
C
      IF(PARTS(18))WRITE(6,1160)
 1090 CONTINUE
      IF(MACH .NE. 0.) GO TO 1150
C
C***  CALCULATE THE CREST CRITICAL CONDITIONS FOR THIS AIRFOIL
C
      CLM=CLALPA
      J=N-1
      DO 1100 I=1,NM1
      XI(I)=X(J)
      STNU(I)=ST2NU(J)+ST5NU(J)
      J=J-1
 1100 CONTINUE
      STC=SINA/COSA
      DO 1110 I=1,NM1
      II=I
      IF(STC .GT. STNU(I)) GO TO 1120
 1110 CONTINUE
 1120 XC=(STC-STNU(II))/(STNU(II-1)-STNU(II))*(XI(II-1)-XI(II))+XI(II)
      DO 1130 I=1,NM1
      II=I
      IF(XC .GT. X(I)) GO TO 1140
 1130 CONTINUE
 1140 VOVU=(XC-X(II))/(X(II-1)-X(II))*(VXOVU(II-1)-VXOVU(II))+VXOVU(II)
      CPCC=1.-VOVU**2
      MCC=1.023-.9507*CPCC-.4140*CPCC**2-.1506*CPCC**3-.0212*CPCC**4
      MCC=1./MCC
      IF(PARTS(18) .AND. MCC .GT. 1.)WRITE(6,1170)MCC
      IF(MCC .GT. 1.)MCC=0.99
      BCC=SQRT(1.-MCC**2)
      CLCC=(ABS(CLM)/(BCC-(MCC**2/(1.+BCC))*ABS(CLM)/2.))*CON
 1150 CONTINUE
C
 1160 FORMAT(4X,'*** CALCULATIONS IN SUBROUTINE SLOPE HAVE FAILED.',/
     1 4X,'*** ANALYSIS FOR THIS AIRFOIL IMPOSSIBLE.')
 1170 FORMAT(4X,'*** SQUARE ROOT OF A NEGATIVE NUMBER IN SUBROUTINE',/
     1 ' SLOPE.  CREST CRITICAL MACH NUMBER ADJUSTED FROM',
     2   F10.4, 'TO 0.99')
      RETURN
      END
