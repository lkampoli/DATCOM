      SUBROUTINE FOIL(IFIN,SECTYP,YMGC,SSPAN,LMAXU,LFLATU,THICKU,
     1   LER,RHO,LNACA,NACA)
C
C***  ROUTINE TO COMPUTE THE AIRFOIL SECTION DESIGNATION OF A USER INPUT
C***  SECTION
C
C***  INPUTS
C
C    IFIN -- FIN SET NUMBER
C  SECTYP -- SECTION TYPE
C            (0=HEX, 1=NACA, 2=ARC, 3=USER)
C    YMGC -- SPAN STATION OF MEAN GEOMETRIC CHORD
C   SSPAN -- ARRAY OF SEMI-SPAN STATIONS
C   LMAXU -- ARRAY OF DISTANCES FROM L.E. TO MAX. THICKNESS, FRAC. C
C  LFLATU -- ARRAY OF LENGTH OF CONSTANT THICKNESS, FRAC. C
C  THICKU -- ARRAY OF THICKNESS, FRAC. C
C
C***  OUTPUTS
C
C     LER -- ARRAY OF L.E. RADII FOR PANEL
C     RHO -- INTERPOLATE L.E. RADIUS AT M.G.C.
C   LNACA -- .TRUE. IF NACA CARD HAS BEEN GENERATED
C    NACA -- NACA DESIGNATION COMPUTED AT Y-M.G.C.
C
C***  NOTE - AIRFOIL IS ASSUMED TO BE SYMMETRIC ABOUT CHORD LINE
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      CHARACTER*4 ICARD,NACA,IDUMMY
C
      REAL LER,LMAXU,LFLATU
      LOGICAL LNACA
C
      DIMENSION SSPAN(10),LMAXU(10),LFLATU(10),THICKU(10),LER(10)
      DIMENSION NACA(80),ICARD(80),IDUMMY(4),IROUT(2)
C
      DATA ICARD / 'N   ','A   ','C   ','A   ','-   ','1   ',
     1             '-   ','S   ','-   ','1   ','-   ','5   ',
     2             '0   ','.   ','0   ','-   ','1   ','0   ',
     3             '.   ','0   ','-   ','0   ','0   ','.   ',
     4             '0   ',55*'    ' /
C
C***  SKIP IF USER DEFINED COORDINATES OR NACA EXISTS
C
      IF(SECTYP .EQ. 1. OR. SECTYP .EQ. 3.)GO TO 1050
C
      DO 1000 I=1,4
         IF(NACA(I) .NE. ICARD(I))GO TO 1010
 1000 CONTINUE
      GO TO 1050
C
 1010 CONTINUE
C
C***  GEOMETRY IS HEXAGONAL OR CIRCULAR ARC
C
C ... FIND NUMBER OF SPAN STATIONS INPUT
C
      NSTA=0
      DO 1020 I=1,10
         IF(SSPAN(I) .EQ. UNUSED)GO TO 1030
         NSTA=NSTA+1
 1020 CONTINUE
C
 1030 CONTINUE
C
C ... FILL NACA ARRAY WITH PRESET NACA CARD
C
      DO 1040 I=1,80
         NACA(I)=ICARD(I)
 1040 CONTINUE
C
C ... INTERPOLATE AT Y-M.G.C. FOR AIRFOIL SHAPE
C
      CALL LNTRP(SSPAN,LMAXU,NSTA,YMGC,UPMAX)
      CALL LNTRP(SSPAN,LFLATU,NSTA,YMGC,UPFLAT)
      CALL LNTRP(SSPAN,THICKU,NSTA,YMGC,UPTHK)
      CALL LNTRP(SSPAN,LER,NSTA,YMGC,RHO)
C
      IF(UPFLAT .LE. UNUSED)UPFLAT=0.
C
C ... CONVERT VALUES IN PERCENT TO HOLLERITH AND INSERT IN NACA
C
      CALL TOHOLL(100.*UPMAX,1,4,NACA(12))
      CALL TOHOLL(100.*UPFLAT,1,4,NACA(22))
      CALL TOHOLL(200.*UPTHK,1,4,NACA(17))
C
C ... NOW FIN NUMBER AND SECTION SHAPE
C
      IF(SECTYP .EQ. 0. .AND. UPFLAT .EQ. 0.)SECT=1.
      IF(SECTYP .EQ. 0. .AND. UPFLAT .NE. 0.)SECT=3.
      IF(SECTYP .EQ. 2.)SECT=2.
      CALL TOHOLL(SECT,1,4,IDUMMY)
      NACA(10)=IDUMMY(2)
      CALL TOHOLL(FLOAT(IFIN),1,4,IDUMMY)
      NACA(6)=IDUMMY(2)
C
C ... NACA ARRAY CONTAINS NACA CARD AT M.G.C.
C
      LNACA=.TRUE.
C
 1050 CONTINUE
C
      RETURN
      END
