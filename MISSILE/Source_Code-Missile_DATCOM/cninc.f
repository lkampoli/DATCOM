      SUBROUTINE CNINC(ICASE,CNS,ICON,DUMMY,PHI)
C      
C        SUBROUTINE TO CALCULATE CN INCREMENTALS AND TO COMPUTE
C     NEW CN VALUES WITH THOSE INCREMENTALS.  IF AERODYNAMIC ROLL
C     ANGLE IS ZERO, INCREMENTALS ARE DETERMINED FOR THE POTENTIAL
C     TERM AND THE VISCOUS TERM.  IF AERODYNAMIC ROLL ANGLE IS NOT 
C     ZERO, A TABLE IF INCREMENTAL MULTIPLIERS IS CALCULATED.
C      
C***  INPUTS
C      
C     ICASE = RUN CASE NUMBER
C     CNS   = ARRAY FROM SYNTHS CONTAINING CN VALUES
C     DUMMY = ARRAY THAT CONTAINS EXPERIMENTAL COEFFICIENTS AND WILL
C             CONTAIN UPDATED COEFFICIENTS CN = DUMMY 1-20
C     ICON  = CONFIGURATION TO BE INCREMETED
C             ICON = 1    BODY
C             ICON = 2    BODY + 1 FIN SET
C             ICON = 3    BODY + 2 FIN SETS
C             ICON = 4    BODY + 3 FIN SETS
C             ICON = 5    BODY + 4 FIN SETS
C     PHI   = BODY AXIS AERODYNAMIC ROLL ANGLE
C      
C***  OUTPUT
C      
C     CN = UPDATED CN VALUES EQUIVALENT TO DUMMY 1-20
C          NOTE: ARRAY DUMMY IN THE FIRST CASE COMES IN WITH THE EXPERIMENTAL
C                DATA AND RETURNS WITH INCREMENTED CN
C      
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /FLC   / NA,ALPHA(20),FLDUM(123)
      COMMON /INC   / C1,C2(20),DCN0,DIP(106),ZIP,ALP(20),NAP
      DIMENSION CN(20),CNEXP(20),CNTH(20),CNATH(20),
     1          CNPTH(20),CNVTH(20),CNPEXP(20),DUMMY(120),Z(20),
     2          CNS(20,5)
C      
C       USE APPROPRIATE IOM COMMON BLOCK TO FILL THE WORKING ARRAYS
C      
      REAL NA,NAP
C
C **  INITIALIZE WORKING ARRAYS
C      
      DO 1000 I=1,20
         CNTH(I)=UNUSED
         CNEXP(I)=UNUSED
         CNPEXP(I)=UNUSED
         CNPTH(I)=UNUSED
         CNVTH(I)=UNUSED
 1000 CONTINUE
C      
      NAP1=NAP+.5
      NALPHA = NA +.5
      DO 1010 I=1,NALPHA
         IF(ICON .EQ. 1)CNTH(I)=CNS(I,5)
         IF(ICON .GT. 1)CNTH(I)=CNS(I,ICON-1)
 1010 CONTINUE
C
C     ZIP=0 FOR THE FIRST TIME THROUGH BETADR AND PHI IS AS INPUT
C     ZIP=1 FOR THE SECOND TIME THROUGH AND PHI HAS BEEN INCREASED BY 1
C
      IF(ZIP .EQ. 0. .AND. PHI .NE. 0.)GO TO 1030
C      
C      COMPUTE THEORETICAL CNA
C      
      CALL CALIB(4,NALPHA,ALPHA,CNEXP,NALPHA,ALPHA,CNTH,SLOPE)
      CNATH(1)=SLOPE*RAD
 1030 IF(ICASE .NE. 1 .OR. ZIP .EQ. 1)GO TO 1070
      DO 1040 I=1,NALPHA
         CNEXP(I)= DUMMY(I)
 1040 CONTINUE
      IF(PHI .NE. 0.)GO TO 1110
C      
C       DETERMINE THE DIFFERENCE BETWEEN CN EXPERIMENTAL AND CN
C          THEORETICAL AT ALPHA = 0
C      
      CALL LNTRP(ALPHA,CNEXP,NALPHA,0.,CNEXP0)
      CALL LNTRP(ALPHA,CNTH,NALPHA,0.,CNTH0)
      DCN0= CNEXP0 - CNTH0
C
C**   SUBTRACT DCN0 FROM CN EXPERIMENTAL TO BRING IT DOWN TO THE THEORETICAL
C     CN CURVE
C
      DO 1060 I=1,NALPHA
         CNEXP(I)=CNEXP(I) - DCN0
 1060 CONTINUE
C      
C     USE CALIB TO DETERMINE THE POTENTIAL INCREMENTAL C1, A SCALAR MULTIPLIER
C      
       CALL CALIB(-3,NALPHA,ALPHA,CNEXP,NALPHA,ALPHA,CNTH,Z)
       C1=Z(1)
C      
C         CALCULATE THE THEORETICAL POTENTIAL CN AND VISCOUS CN
C         FROM TOTAL CN WHICH IS THE EXPERIMENTAL CN POTENTIAL
C      
 1070 IF(PHI .NE. 0.)GO TO 1120
      DO 1080 I=1,NALPHA
         CNPTH(I)=.5*CNATH(1)*SIN(2.*ALPHA(I))*COS(ALPHA(I)/2.)
         CNVTH(I)=CNTH(I) - CNPTH(I)
 1080 CONTINUE
C      
C        USE THE INCREMENTAL FACTOR C1 TO DETERMINE THE CORRECTED
C          POTENTIAL CN
C      
      DO 1090 I=1,NALPHA
         CNPEXP(I)=C1*CNPTH(I)
 1090 CONTINUE
C
C     ONLY NEED TO DETERMINE CN VISCOUS ON THE FIRST TIME THROUGH THE FIRST
C     CASE
C
      IF (ICASE .NE. 1 .OR. ZIP .EQ. 1)GO TO 1140
C      
C       DETERMINE THE VISCOUS INCREMENTAL BY TAKING THE RATIO OF
C         EXPERIMENTAL CN VISCOUS TO THEORETICAL CN VISCOUS
C      
      DO 1100 I=1,NALPHA
         IF(ABS(CNTH(I)-CNPTH(I)) .LT. UNUSED)C2(I)=1.
         IF(ABS(CNTH(I)-CNPTH(I)) .LT. UNUSED)GO TO 1100
         C2(I)=(CNEXP(I) - CNPEXP(I))/CNVTH(I)
 1100 CONTINUE
      GO TO 1140
C
C***  HERE FOR AERODYNAMIC ROLL NOT ZERO
C     USE CALIB TO DETERMINE TABLE OF INCREMENTAL MULTIPLIERS
C
 1110 CALL CALIB(2,NALPHA,ALPHA,CNEXP,NALPHA,ALPHA,CNTH,C2)
C
C     USE LINEAR INTERPOLATION ON INCREMENTAL TABLE AT ALPHA OF INTEREST
C       BECAUSE INCRMENTALS ARE CALCULATED AT FIRST CASE ALPHA'S
C
 1120 DO 1130 I=1,NALPHA
         CALL LNTRP(ALP,C2,NAP1,ALPHA(I),C2I)
         CN(I)=CNTH(I)*C2I
 1130 CONTINUE
      GO TO 1160
C      
C***  COMPUTE THE INCREMENTED CN FOR AERODYNAMIC ROLL = 0
C      
 1140 DO 1150 I=1,NALPHA
         CALL LNTRP(ALP,C2,NAP1,ALPHA(I),C2I)
         CN(I)=CNPEXP(I) + C2I*CNVTH(I) + DCN0
 1150 CONTINUE
C
C     PUT INCREMENTED CN INTO ARRAY DUMMY
C
 1160 DO 1170 I=1,NALPHA
         DUMMY(I)=CN(I)
 1170 CONTINUE
      RETURN
      END
