      SUBROUTINE GEO(X,Y,Z,SLL,SLR,RO,B,NN,XX,FF,FX,FXX,GM,SG,CG,J)
C
C  SUBROUTINE TO INTERPOLATE THE GEOMETRY FOR LOCAL RADIUS,
C  SLOPE AND SECOND DERIVATIVE OF THE RADIUS
C
C  INPUTS
C
C      X -- ARRAY OF X COORDINATES
C      Y -- ARRAY OF BODY RADII
C      Z -- ARRAY OF FITTING PARAMETERS (FROM SETGEO)
C    SLL -- ARRAY OF LEFT HAND SLOPES FOR EACH SEGMENT
C    SLR -- ARRAY OF RIGHT HAND SLOPES FOR EACH SEGMENT
C     RO -- NOSE BLUNTNESS FACTOR IN CONIC FIT
C      B -- NOSE BLUNTNESS FACTOR IN CONIC FIT
C     NN -- ARRAY OF POINTS FOR EACH SECTION
C     XX -- LOCAL VALUE OF X
C      J -- SEGMENT NUMBER BEING ANALYIZED
C
C  OUTPUTS
C
C     FF -- INTERPOLATED RADIUS
C     FX -- DERIVATIVE D(RADIUS)/D(X)
C    FXX -- DERIVATIVE D2(RADIUS)/D(X)2
C     GM -- LOCAL BODY SLOPE, RADIANS
C     SG -- SIN OF GM
C     CG -- COSINE OF GM
C
C  ROUTINES CALLED -- NONE
C
      DIMENSION X(50),Y(50),Z(50),SLL(5),SLR(5),NN(5)
      IF(J.EQ.1)GO TO 1050
      NM=NN(J)-1
      NZ=NN(J)
      NB=NN(J-1)
      DO 1000 I=NB,NM
         XM=(X(I+1)+X(I))/2.
         IF(XX.LT.XM)GO TO 1010
 1000 CONTINUE
      GO TO 1030
 1010 IF(I.EQ.NB)GO TO 1020
      DXP=X(I+1)-X(I)
      DX=X(I)-X(I-1)
      T1=(Z(I)-Y(I))/DXP/(DXP+DX)
      T2=(Z(I-1)-Y(I))/DX/(DXP+DX)
      DYX=2.*(DX*T1-DXP*T2)
      FXX=8.*(T1+T2)
      GO TO 1040
C
C    BEG OF SEGMENT
C
 1020 FXX=8.*(Z(NB)-Y(NB))/(X(NB+1)-X(NB))**2-4.*SLR(J-1)/(X(NB+1)-X(NB)
     1)
      DYX=SLR(J-1)
      GO TO 1040
C
C     END OF SEGMENT
C
 1030 FXX=8.*(Z(NM)-Y(NZ))/(X(NZ)-X(NM))**2+4.*SLL(J)/(X(NZ)-X(NM))
      DYX=SLL(J)
 1040 FF=Y(I)+DYX*(XX-X(I))+FXX*(XX-X(I))**2/2.
      FX=DYX+FXX*(XX-X(I))
      GO TO 1060
C
C    BLUNTED NOSE FIT BY CONIC
C
 1050 FF=SQRT(2.*RO*XX+B*XX**2)
      FX=(RO+B*XX)/FF
      FXX=(B-FX**2)/FF
 1060 GM=ATAN(FX)
      SG=SIN(GM)
      CG=COS(GM)
      RETURN
      END
