      SUBROUTINE BLUNTN(X1,Y1,X2,Y2,X,B,RO,Y,DYDX,D2YDX2,DEL,SD,CD)
C
C  SUBROUTINE TO FIT THE BLUNTED NOSE WITH AN ELLIPSE AND SOLVE FOR
C  THE LOCAL RADIUS AT THE INPUT STATION
C
C  FIT IS OF THE FORM
C        Y**2 = 2*RO*X + B*X**2
C        DRDX = (RO + B*X)/Y
C      D2RDX2 = (B - DRDX**2)/Y
C
C
C  INPUTS
C
C     X1 -- X COORDINATE OF FIRST POINT ON THE BODY
C     Y1 -- Y COORDINATE OF FIRST POINT ON THE BODY
C     X2 -- X COORDINATE OF THE SECOND POINT ON THE BODY
C     Y2 -- Y COORDINATE OF THE SECOND POINT ON THE BODY
C      X -- DESIRED X STATION FOR RESULTS
C
C  OUTPUTS
C
C      B -- SECOND DERIVATIVE OF FIT
C     RO -- FIRST DERIVATIVE OF FIT
C      Y -- RADIUS AT X
C   DYDX -- SLOPE AT X
C D2YDX2 -- SECOND DERIVATIVE AT X
C    DEL -- ANGULAR SLOPE AT X
C     SD -- SINE OF ANGULAR SLOPE
C     CD -- COSINE OF ANGULAR SLOPE
C
C  ROUTINES CALLED -- NONE
C
      RO=0.
      Y=0.
      DYDX=0.
      D2YDX2=0.
      IF(X1.EQ.0.)GO TO 1000
C
         RO=(X2**2*Y1**2-X1**2*Y2**2)/(2.*X1*X2*(X2-X1))
C
         B=(Y1**2/X1-2.*RO)/X1
C
         Y=SQRT(2.*RO*X+B*X**2)
C
         IF(Y.NE.0.)DYDX=(RO+B*X)/Y
         IF(Y.EQ.0.)DYDX=1.E+10
C
         IF(Y.NE.0.)D2YDX2=(B-DYDX**2)/Y
         IF(Y.EQ.0.)D2YDX2=1.E+10
C
 1000 DEL=ATAN(DYDX)
      SD=SIN(DEL)
      CD=COS(DEL)
      RETURN
      END
