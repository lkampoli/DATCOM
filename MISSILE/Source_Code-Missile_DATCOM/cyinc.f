      SUBROUTINE CYINC(ICASE,CYS,CNS,ICON,DUMMY,PHI)
C      
C        SUBROUTINE TO CALCULATE CY INCREMENTALS AND TO COMPUTE NEW CY
C     VALUES WITH THOSE INCREMENTALS.  IF AERODYNAMIC ROLL ANGLE IS
C     ZERO, INCREMENTALS ARE COMPUTED FOR CN TOTAL POTENTIAL AND CN TOTA
C     VISCOUS.  THEN USING THE INCREMENTED CN TOTAL AND THE INCREMENTED
C     THE INCREMENTED CY IS DETERMINED.  IF AERODYNAMIC ROLL ANGLE IS NO
C     ZERO, A TABLE OF MULTIPLIERS IS USED.
C      
C***  INPUTS
C      
C     ICASE = RUN CASE NUMBER
C     CYS   = ARRAY OF CY VALUES FROM SYNTHS
C     CNS   = ARRAY OF CN VALUES FROM SYNTHS
C     DUMMY = ARRAY THAT CONTAINS EXPERIMENTAL COEFFICIENTS AND WILL
C             CONTAIN UPDATED COEFFICIENTS CY = DUMMY 61-80
C     ICON  = CONFIGURATION TO BE INCREMETED
C             ICON = 1    BODY
C             ICON = 2    BODY + 1 FIN SET
C             ICON = 3    BODY + 2 FIN SETS
C             ICON = 4    BODY + 3 FIN SETS
C             ICON = 5    BODY + 4 FIN SETS
C     PHI   = BODY AXIS ROLL ANGLE
C      
C***  OUTPUT
C      
C     CY = UPDATED CY VALUES EQUIVALENT TO DUMMY 61-80
C          NOTE: ONE THE FIRST TIME THROUGH DUMMY ENTERS THE ROUTINE
C                WITH EXPERIMENTAL VALUES. IT RETURNS WITH INCREMENTED
C                CY VALUES
C      
      COMMON /FLC   / NA,ALPHA(20),FLDUM(123)
      COMMON /INC   / DUMMEE(64),C1,C2(20),CN0T,DIP(42),ZIP,ALP(20),
     1                NAP
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      DIMENSION CNTTH(20),CNTEXP(20),CNATTH(20),CYTH(20),CYEXP(20),
     1     CNATEX(20),CNPTTH(20),CNVTTH(20),CNPTEX(20),CNT(20),
     2     CY(20),DUMMY(120),CNS(20,5),CYS(20,5),Z(20)
      REAL NA,NAP
C
C ** INITIALIZE ARRAYS
C      
      DO 1000 I=1,20
         CYTH(I)=UNUSED
         CNTTH(I)=UNUSED
         CYEXP(I)=UNUSED
         CNTEXP(I)=UNUSED
         CNVTTH(I)=UNUSED
         CNPTEX(I)=UNUSED
         CY(I)=UNUSED
         CNT(I)=UNUSED
 1000 CONTINUE
C      
      NALPHA = NA + .5
      NAP1 = NAP + .5
C      
C       USE APPROPRIATE IOM COMMON BLOCK TO FILL THE WORKING ARRAYS
C       AND DETERMINE CN TOTAL
C      
      DO 1020 I=1,NALPHA
          IF(ICON .GT. 1)GO TO 1010
          CYTH(I)=CYS(I,5)
          CNTTH(I)=SQRT(CNS(I,5)**2 + CYS(I,5)**2)
          GO TO 1020
 1010     CYTH(I)=CYS(I,ICON-1)
          CNTTH(I)=SQRT(CNS(I,ICON-1)**2 + CYS(I,ICON-1)**2)
 1020 CONTINUE
      IF(PHI .NE. 0.)GO TO 1040
C      
C     COMPUTE THEORETICAL SLOPE OF CN TOTAL VS. ALPHA
C      
      DO 1030 I=1,NALPHA
         CALL POLINT(ALPHA,CNTTH,NALPHA,ALPHA(I),CNTTH(I),CNATTH(I))
 1030 CONTINUE
 1040 IF(ICASE .NE. 1 .OR. ZIP .EQ. 1)GO TO 1090
      IF(PHI .NE. 0.)GO TO 1160
C      
C       COMPUTE EXPERIMENTAL CN TOTAL
C      
      DO 1060 I=1,NALPHA
         CNTEXP(I)=SQRT(DUMMY(I)**2 + DUMMY(I+60)**2)
 1060 CONTINUE
C      
C      COMPUTE EXPERIMENTAL SLOPE OF CN TOTAL VS. ALPHA
C      
      DO 1070 I=1,NALPHA
         CALL POLINT(ALPHA,CNTEXP,NALPHA,ALPHA(I),CNTEXP(I),CNATEX(I))
 1070 CONTINUE
C      
C       DETERMINE THE DIFFERENCE BETWEEN CN TOTAL EXPERIMENTAL AND CN
C          TOTAL THEORETICAL AT ALPHA = 0
C      
      CALL LNTRP(ALPHA,CNTEXP,NALPHA,0.,CNTEX0)
      CALL LNTRP(ALPHA,CNTTH,NALPHA,0.,CNTTH0)
      CN0T= CNTEX0 - CNTTH0
C      
C      SUBTRACT CNOT FROM THE EXPERIMENTAL CN TOTAL TO BRING THE CURVE
C        DOWN TO CN THEORETICAL
C      
      DO 1080 I=1,NALPHA
         CNTEXP(I)=CNTEXP(I) - CN0T
 1080 CONTINUE
C      
C     USE CALIB TO DETERMINE THE POTENTIAL INCREMENTAL C1,A SACALAR MULT
C      
      CALL CALIB(-3,NALPHA,ALPHA,CNATEX,NALPHA,ALPHA,CNATTH,Z)
      C1=Z(1)
 1090 IF(PHI .NE. 0.)GO TO 1180
C      
C     CALCULATE THE THEORETICAL POTENTIAL CN TOTAL AND VISCOUS CN
C       TOTAL FROM CN TOTAL
C      
      DO 1100 I=1,NALPHA
         CNPTTH(I)=.5*CNATTH(I)*SIN(2.*ALPHA(I))*COS(ALPHA(I)/2.)
         CNVTTH(I)=CNTTH(I) - CNPTTH(I)
 1100 CONTINUE
C      
C        USE THE INCREMENTAL FACTOR C1 TO DETERMINE THE CORRECTED
C          POTENTIAL CN, WHICH IS THE EXPERIMENTAL CN POTENTIAL
C      
      DO 1110 I=1,NALPHA
         CNPTEX(I)=C1*CNPTTH(I)
 1110 CONTINUE
C      
C     ONLY ON THE FIRST CASE THE FIRST TIME THROUGH ARE INCREMENTALS
C        DETERMINED
C      
      IF (ICASE .NE. 1 .OR. ZIP .EQ. 1)GO TO 1130
C      
C       DETERMINE THE VISCOUS INCREMENTAL BY TAKING THE RATIO OF
C         EXPERIMENTAL CN TOTAL VISCOUS TO THEORETICAL CN TOTAL VISCOUS
C      
      DO 1120 I=1,NALPHA
         IF(CNTTH(I)-CNPTTH(I) .EQ. 0.)C2(I)=1.
         IF(CNTTH(I)-CNPTTH(I) .EQ. 0.)GO TO 1120
         C2(I)=(CNTEXP(I) - CNPTEX(I))/(CNTTH(I) - CNPTTH(I))
 1120 CONTINUE
 1130 CONTINUE
C      
C***  COMPUTE THE INCREMENTED CN TOTAL FOR PHI = 0
C      
      DO 1140 I=1,NALPHA
         CALL LNTRP(ALP,C2,NAP1,ALPHA(I),C2I)
         CNT(I)=CNPTEX(I) + C2I * CNVTTH(I) + CN0T
 1140 CONTINUE
C      
C       USING INCREMENTED CN TOTAL AND INCREMENTED CN DETERMINE
C             INCREMENTED CY
C      
      DO 1150 I=1,NALPHA
         SIGN=1.
         IF(CYTH(I) .LT. -.0000001)SIGN=-1.
         CY(I)=SIGN*SQRT(ABS(CNT(I)**2-DUMMY(I)**2))
 1150 CONTINUE
      GO TO 1200
C      
C**** HERE FOR AERODYNAMIC ROLL ANGLE NOT ZERO
C      
 1160 DO 1170 I=1,NALPHA
         CYEXP(I)=DUMMY(I+60)
 1170 CONTINUE
C      
C     USE CALIB TO DETERMINE TABLE OF MULTIPLIERS FOR CY INCRMENTALS
C      
      CALL CALIB(2,NALPHA,ALPHA,CYEXP,NALPHA,ALPHA,CYTH,C2)
C      
C***  INCREMENT CY USING INCREMENTALS INTERPOLATED FOR THE ALPHA OF INTE
C      
 1180 DO 1190 I=1,NALPHA
         CALL LNTRP(ALP,C2,NAP1,ALPHA(I),C2I)
         CY(I)=CYTH(I)*ABS(C2I)
 1190 CONTINUE
C      
C        UPDATE ARRAY DUMMY TO CONTAIN INCREMENTED CY
C      
 1200 DO 1210 I=1,NALPHA
         DUMMY(I+60)=CY(I)
 1210 CONTINUE
      RETURN
      END
