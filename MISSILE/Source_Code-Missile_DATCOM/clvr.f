      SUBROUTINE CLVR(M,CDC,BLUNT,RN,ETA,XOR,RL,AR,S,TAPER,DIH,
     1 ALPHA,VLR)
C
C***  SUBROUTINE TO COMPUTE THE PANEL LIFT INCREMENT DUE TO BODY
C***  VORTICES AS A FRACTION OF THEORETICAL PANEL LIFT USING THE
C***  METHOD FROM NWC TP-5761.
C
C  *** INPUTS ***
C
C     M - FREESTREAM MACH NUMBER
C    CDC - CROSS FLOW DRAG COEFFICIENT
C BLUNT - NOSE RADIUS TO BODY RADIUS (BLUNTNESS RATIO)
C    RN - RADIUS OF NOSE AT NOSE/CENTERBODY JUNCTURE
C   ETA - NOSE SEMI-APEX ANGLE, DEG.
C   XOR - DISTANCE FROM NOSE APEX TO FIN ROOT EXPOSED L.E.
C    RL - BODY RADIUS AT FIN
C    AR - ASPECT RATIO OF TWO FIN PANELS (SIDE-TO-SIDE AT ROOT)
C     S - FIN SEMI-SPAN (FROM BODY CENTERLINE)
C TAPER - FIN EXPOSED TAPER RATIO
C   DIH - FIN PANEL DIHEDRAL ANGLE, DEG.
C ALPHA - BODY ANGLE OF ATTACK, DEG.
C
C *** OUTPUT ***
C
C   VLR - VORTEX LIFT RATIO
C
      REAL M
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      DIMENSION IROUT(2),XMXS(11),YVRSUB(11),YVRSUP(11),
     1  ZVR(11),GAMSUB(11),GAMSUP(11)
C
      DATA XMXS / 0.,1.,2.,3.,4.,5.,6.,7.,8.,9.,10. /
      DATA YVRSUB / .405,.430,.450,.480,.495,.505,.525,
     1              .550,.565,.590,.595 /
      DATA YVRSUP / .600,.605,.615,.650,.680,.695,.700,.705,
     1              .725,.730,.730 /
      DATA ZVR / 1.0,1.17,1.29,1.44,1.58,1.70,1.83,1.97,2.05,
     1           2.15,2.20 /
      DATA GAMSUB / .31,.31,.33,.40,.46,.60,.80,.90,1.0,1.1,1.15 /
      DATA GAMSUP / .58,.60,.62,.79,.88,1.05,1.25,1.35,1.50,1.55,1.60 /
C
C***  ANGLES IN RADIANS
C
      CDIH=COS(DIH/RAD)
      SDIH=SIN(DIH/RAD)
      SALP=SIN(ALPHA/RAD)
C
C***  NO NOSE VORTEX IF ALPHA .LE. 4 DEGREES
C
      IF(ALPHA.LE.4.)GO TO 1070
C
C***  FOR NOSE BLUNTNESS, USE EQUATION 5
C
      IF(BLUNT.GT..2 .OR. ETA.GT.30.)GO TO 1000
C
C***  IF ALPHA .GT. SEMI-APEX ANGLE, SEPARATION AT NOSE TIP
C
      IF(ALPHA.LT.ETA)GO TO 1010
      XSOR=0.
      GO TO 1020
C
C***  CORRELATION OF SEPARATION USING EQUATION 2
C
 1000 XSOR=(2.+10./(ALPHA-4.))*RL
      GO TO 1020
C
C***  CORRELATION OF SEPARATION USING EQUATION 1
C
 1010 XSOR=(32.*(1.-SQRT((ALPHA-4.)/(ETA-4.))))*RL
C
C***  SEPARATION DISTANCE
C
 1020 C1=(XOR-XSOR)*SALP/RL
C
C***  DETERMINE IF SEPARATION POINT AHEAD OF FIN, IF SO SET TO ZERO
C
      IF(C1.LE.0.)GO TO 1070
C
C***  FIGURE 4B (VERTICAL VORTEX POSITION)
C
      CALL LNTRP(XMXS,ZVR,11,C1,ZOR)
      ZOR=ZOR*RN
C
      IF(M.GE.2.)GO TO 1030
C
C***  FIGURE 4A, SUBSONIC (HORIZONTAL VORTEX POSITION)
C
      CALL LNTRP(XMXS,YVRSUB,11,C1,YOR)
      YOR=YOR*RN
C
C***  FIGURE 5, SUBSONIC
C
      CALL LNTRP(XMXS,GAMSUB,11,C1,TAU)
      TAU=TAU*RL/S
      IF(M.LE.1.)GO TO 1050
C
C***  FIGURES 4 AND 5, SUPERSONIC
C
C***  (HORIZONTAL VORTEX POSITION)
C
 1030 CALL LNTRP(XMXS,YVRSUP,11,C1,YORS)
      YORS=YORS*RN
C
C***  (NOSE VORTEX STRENGTH)
C
      CALL LNTRP(XMXS,GAMSUP,11,C1,TAUS)
      TAUS=TAUS*RL/S
C
      IF(M.LT.2.)GO TO 1040
C
      YOR=YORS
      TAU=TAUS
      GO TO 1050
C
C***  USE EQUATION 5
C
 1040 YOR=(YORS-YOR)*(M-1.)+YOR
      TAU=(TAUS-TAU)*(M-1.)+TAU
C
 1050 CONTINUE
C
C***  CHECK IF CAN USE VORTEX IMPULSE THEOREM
C
      IF(C1 .GE. 4.0) GO TO 1060
C
C***  VORTEX IMPULSE THEOREM APPLICABLE
C
      SC=(XOR-XSOR)*(RN+RL)
      TAU=CDC*SC*SALP/(8.*PI*S*YOR*(1.-(RN**2/(YOR**2+ZOR**2))))
C
C***  COMPUTE THE VORTEX INTERFERENCE FACTORS
C
 1060 CALL SVRINT(YOR,ZOR,CDIH,SDIH,S,RL,TAPER,VRINT)
C
      VLR=VRINT*TAU
C
C***  LIMIT TO +1 TO -1 FOR ARC-SINE FUNCTION
C
      IF(VLR.LT.-1.)VLR=-1.
      IF(VLR.GT.1.)VLR=1.
      VLR=ASIN(VLR*SALP)
C
      GO TO 1080
C
 1070 VLR=0.
C
 1080 CONTINUE
C
      RETURN
      END
