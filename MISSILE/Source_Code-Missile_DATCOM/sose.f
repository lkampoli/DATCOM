      SUBROUTINE SOSE(AL,AM,X,Y,NPTS,NNN,IPRINT,NBLUNT,MNO,
     1 CAP,XCP,CNALP,CMALP,CAAA,DMAX)
C
C   BODY ALONE PROGRAM, FOR M .GT. 1.2, AND ALPHA .LT. 15 DEG
C   FOR BLUNTED,TRUNCATED, OR POINTED BODIES OF REVOLUTION.
C   PRESSURES BY MODIFIED NEWTONIAN AND IMPROVED 2ND ORDER SHOCK
C   EXPANSION OF DEJARNETTE (AIAA J. SPACECRAFT, NOV-DEC 1980, P.529)
C
C  NOTE -- THE BODY IS TREATED WITH THE X STATIONS AND RADII IN CALIBERS
C
C X(1) MUST BE ZERO FOR SHARP OR TRUNCATED BODIES
C X(1) MUST BE NON-ZERO FOR BLUNTED BODIES (PGM WILL FAIR TO X(1)=0)
C
C
C  INPUTS
C
C     AL -- ANGLE OF ATTACK, DEGREES
C     AM -- FREESTREAM MACH NUMBER
C      X -- ARRAY OF X COORDINATES
C      Y -- ARRAY OF BODY RADII AT EACH INPUT X STATION
C   NPTS -- NUMBER OF BODY INPUT POINTS
C    NNN -- ARRAY WHICH DEFINES THE NUMBER OF POINTS BEFORE THE
C           SURFACE DISCONTINUITY FOR EACH BODY SEGMENT
C IPRINT -- PRINT FLAG
C           0=NO PRINT-OUT
C           1=PRINT PRESSURE DISTRIBUTION
C           2=PRINT BODY COORDINATES TO TAPE UNIT 3
C NBLUNT -- BLUNTNESS FLAG
C           0=SHARP NOSE
C           1=BLUNTED NOSE
C           2=TRUNCATED NOSE
C    MNO -- TOTAL NUMBER OF SEGMENTS OF BODY DEFINED
C
C  OUTPUTS
C
C    CAP -- AXIAL FORCE INCREMENT DUE TO PRESSURE DRAG
C    XCP -- LOCATION OF CENTER OF PRESSURE, CALIBERS FROM NOSE
C  CNALP -- NORMAL FORCE DERIVATIVE WITH ANGLE OF ATTACK, /RADIAN
C  CMALP -- PITCHING MOMENT DERIVATIVE WITH ANGLE OF ATTACK, /RADIAN
C   CAAA -- SECOND DERIVATIVE OF AXIAL FORCE WITH ANGLE OF ATTACK,
C           /RADIAN SQUARED
C
C  ROUTINES CALLED -- GEO,CONEP,MATCHX,EQN44,SHOCK,LOADF,EXPAND,CPDIST
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      DIMENSION CPP(7),X(50),Y(50),Z(50),XT(50),YT(50),ZT(50),
     1 NN(20),NNN(20),SLL(20),SLR(20),NC(50),ACAI(301),ACNI(301),
     2 ACMI(301),ACAAI(301)
      REAL LAMDA,LAMDA1,LAMDA2
      DATA GAM /1.400/
      DO 990 I=1,20
      NN(I)=NNN(I)
  990 CONTINUE
C
C  FOLLOWING CARD REMOVED FOR TECPLOT FORMAT
C
C     IF(IPRINT.EQ.2) WRITE(9,1300)
C
C  SET-UP GEOMETRY
C
      M=MNO
C
      XB=0.
      CALL SETGEO(NN,M,XB,NBLUNT,X,Y,Z,NPTS,B,RO,XT,YT,ZT,SLL,SLR)
      ALPD=AL/RAD
      ALP=ALPD
      SALP=SIN(ALP)
      CALP=COS(ALP)
      AMS=AM**2
C
C  FIND MAXIMUM DEFLECTION ANGLE AT THIS MACH TO GET A
C  DETACHED SHOCK.  THIS IS THE MATCHING CONDITION
C  FOR THE BLUNTED NOSE
C
C
C  NACA 1135 EQN. 100
C
      PT=((GAM+1.)*AMS/2.)**(GAM/(GAM-1.))*((GAM+1.)
     1   /(2.*GAM*AMS-(GAM-1.)))**(1./(GAM-1.))
      CPSTG=(PT-1.)/(GAM/2.)/AMS
      AMM=1.1
      AX2=AMM**2
C
C  NACA 1135 EQN. 44
C
      POP=(1.+AX2*(GAM-1.)/2.)**(-GAM/(GAM-1.))
      PST=POP*PT
      CPM=(PST-1.)/(GAM/2.)/AMS
C
C  NOW COMPUTE THE MAXIMUM DEFLECTION ANGLE
C
      DELM=ASIN(SQRT(CPM/CPSTG))
C
C  BLUNTED NOSE STAGNATION PRESSURE COEFFICIENT
C
      CPN=CPSTG*(1.0-SALP**2)
C
C  INITIALIZE
C
      NCMAX=300
      NC(1)=40
      XINC=0.05
      DO 1000 I=2,M
        NC(I)=X(NPTS)/XINC+0.5
        IF (NC(I).GT.NCMAX) NC(I)=NCMAX
 1000 CONTINUE
      B1=2.-1./SQRT(AMS-1.)
      IF(B1.LT.1.)B1=1.
      B2=6.-2.*B1
      CAI=0.0
      CAAAI=0.0
      CNI=0.0
      CMI=0.0
      L=0
      TH=0.0
C
C  START SOLUTION JUST BEHIND NOSE TIP
C
      P=PT
      XB=.003*RO
      XNC=NC(1)
      DX=(X(1)-XB)/XNC
C     IF(IPRINT.EQ.1.AND.ALP.EQ.0.)CALL HEADR9
C     IF(IPRINT.EQ.1.AND.ALP.EQ.0.)WRITE(6,1190)AM,AL
C     IF(IPRINT.EQ.1.AND.ALP.EQ.0.)WRITE(6,1200)
      IF(NBLUNT .EQ. 2)GO TO 1020
      IF(DX.EQ.0.)GO TO 1010
C
C  HERE FOR BLUNTED NOSE
C
      XPT=0.
      R=0.
C     IF(IPRINT.EQ.1.AND.ALP.NE.0.)WRITE(10,1210)XPT,CPN,CPN,CPN,CPN,
C    1                                          CPN,CPN,CPN
      IF(IPRINT.EQ.1)WRITE(10,1210)XPT,CPN,CPN,CPN,CPN,
     1                                          CPN,CPN,CPN
C     IF(IPRINT.EQ.1.AND.ALP.EQ.0.)WRITE(6,1220)XPT,CPN
      CALL GEO(X,Y,Z,SLL,SLR,RO,B,NN,XB,R,RX,RXX,DEL,SD,CD,1)
      CAI=CPSTG*R**2/2.0
      CNI=CPSTG*(R*CD)**2/4.0
      GO TO 1020
 1010 CAI=4.*CPSTG*0.9*Y(1)**2
 1020 CONTINUE
C
C  LOOP FOR EACH BODY SEGMENT
C
      DO 1170 J=1,M
C
C  J=1 IS THE FIRST BODY SEGMENT (THE SPHERICAL NOSE CAP)
C  J=2 ... M IS THE REST OF THE BODY SEGMENTS
C
         IF(J.EQ.1)GO TO 1030
C
C  ARRAY NN IS THE NUMBER OF POINTS IN EACH SEGMENT
C  ARRAY NC IS THE NUMBER OF POINTS TO BE USED IN THE SOLUTION
C
            NNJ1=NN(J-1)
            NNJ=NN(J)
            XNC=NC(J)
            XB=X(NNJ1)
            DX=(X(NNJ)-XB)/XNC
 1030    IMX=NC(J)+1
C
C  SKIP THE PRESSURE CALCULATIONS IF THE NOSE IS NOT BLUNTED
C  ON THE FIRST PASS
C
         IF(DX.EQ.0.0)GO TO 1170
C
C  TRANSVERSE THIS BODY SEGMENT AT AN INCREMENT OF DX
C
         DO 1160 I=1,IMX
            XXI=I-1
            XPT=XB+XXI*DX
C
C  INTERPOLATE FOR RADIUS (R), DR/DX, D2R/DX2, BODY SLOPE,
C  SIN(DELTA), AND COS(DELTA)
C
            CALL GEO(X,Y,Z,SLL,SLR,RO,B,NN,XPT,R,RX,RXX,DEL,SD,CD,J)
         IF(L.NE.0)GO TO 1080
            L=0
            RXL=RX
            RXXL=RXX
            DELL=DEL
C
C  GET CONE PRESSURE USING RASMUSSEN EQUATION
C
            CALL CONEP(GAM,AM,SD,PC)
C
C  IF X(1)=0., HAVE A SHARP OR TRUNCATED BODY
C
         IF(X(1).EQ.0.0)GO TO 1050
C
C  HERE FOR SPHERICALLY BLUNTED NOSE
C
            CP=CPSTG*SD**2
            P=1.+CP*(GAM/2.)*AMS
C
C  SEE DEJARNETTE FOR THE EXACT DEFINITION OF THESE LOADING TERMS
C    LAMDA = D (P)/D(ALPHA)  AT PHI=ALPHA=0
C    GAMMA = D2(P)/D(ALPHA)2 AT PHI=ALPHA=0
C    DELTA = D2(P)/D(ALPHA)2 AT PHI=90. DEG, ALPHA=0
C
            LAMDA=CPSTG*SD*CD
            GAMMA=2.0*CPSTG*(CD**2-SD**2)
            DELTA=-2.0*CPSTG*SD**2
         IF(P.LT.PST)GO TO 1040
            PL=P
            XL=XPT
         GO TO 1150
 1040    L=1
         IF(I.EQ.1)GO TO 1050
C
C  COMPUTE THE X MATCHING POINT
C
            CALL MATCHX(J,RO,B,DELM,XL,PST,PL,XPT,P,XST)
C
C  GET GEOMETRIC CHARACTERISTICS AT THE MATCH POINT
C
            CALL GEO(X,Y,Z,SLL,SLR,RO,B,NN,XST,RL,RXL,RXXL,
     1                  DELL,SDL,CDL,J)
C
C  COMPUTE RADIUS OF CURVATURE AT THIS POINT
C
            CURV=-RXXL/(1.+RXL**2)**1.5
C
C  GET CONICAL PRESSURE FROM METHOD OF RASMUSSEN
C
            CALL CONEP(GAM,AM,SDL,PC)
C
            CP=CPSTG*SDL**2
            P=1.+(GAM/2.)*CP*AMS
            CP=(P-1.)/(GAM/2.)/AMS
C
C  COMPUTE MACH NUMBER
C
            CALL EQN44(GAM,PT,P,AMX)
            DPSS=(1.0-PT)*2.0*CURV*SDL*CDL
            AX2=AMX**2
C
C  COMPUTE SMALL LAMBDA*CURVATURE, LEFT SIDE OF EQUATION 3
C  OF DEJARNETTE
C
            SLAM=GAM*P*AX2/SQRT(AX2-1.)*CURV
            DPS2=DPSS+SLAM
            PM=PC-P
            P2=P
C
C  SET UP MATCHING PARAMETERS
C
            LAMDA=CPSTG*SDL*CDL
            LAMDA2=LAMDA
            GAMMA=2.0*CPSTG*(1.0-2.0*SDL**2)
            GAMMA2=GAMMA
            DELTA=-2.0*CPSTG*SDL**2
            DELTA2=DELTA
            BTL=XST*RXL-RL
            XXL=XST
            XX=XXL
            BT=BTL
C
C  COMPUTE CHANGE IN ANGLE BETWEEN STATIONS
C
            TH=DELL-DEL
         IF(ABS(TH).LT.1.E-3)GO TO 1150
         GO TO 1090
C
C  HERE FOR SHARP NOSE OR COMPRESSION SURFACE
C
 1050    CONTINUE
C
C ...    USE WEDGE PRESSURE AT TRUNCATED NOSE, ELSE USE CONE
C
         IF(NBLUNT .EQ. 2 .AND. XPT .EQ. 0.)
     1      CALL WEDGE(AM,ASIN(SD)*RAD,THE,CPW)
         IF(NBLUNT .EQ. 2 .AND. XPT .EQ. 0.)
     1      PC=1.+.7*CPW*AM**2
         P=PC
         IF(P.GE.PT)GO TO 1070
            P2=P
            DPS2=0.
            LAMDA=2.0*CD*SD
            LAMDA2=LAMDA
            GAMMA=2.0*B1*(1.0-2.0*SD**2)
            GAMMA2=GAMMA
            DELTA=GAMMA-2.0*B2*CD**2
            DELTA2=DELTA
            XX=XPT
            BT=XPT*RX-R
            RR=R
         IF(XPT.GT.0.0.OR.R.GT.0.0)GO TO 1060
C
C  HERE AT SHARP NOSE TIP
C
            SD2=SD**2
C
C  GET SHOCK ANGLE AND PROPERTIES BEHIND THE SHOCK
C
            CALL SHOCK(GAM,AM,SD2,P2,PC,AMX,PT)
C
C  COMPUTE THE MACH NUMBER
C
 1060    CALL EQN44(GAM,PT,P,AMX)
            L=0
         IF(AMX.LT.1.1)GO TO 1150
            L=1
         GO TO 1120
 1070    P=PT
            LAMDA=0.0
            GAMMA=0.0
            DELTA=0.0
         GO TO 1150
C
C  COMPUTE CHANGE IN ANGLE BETWEEN STATIONS
C
 1080    TH=DELL-DEL
         IF(ABS(TH).LT.1.E-03)GO TO 1130
 1090    BT=XPT*RX-R
C
C   XX AND RR ARE COORDINATES AT JUNCTURE OF TANGENT CONES
C
            XX=(BT-BTL)/(RX-RXL)
            RR=R-(XPT-XX)*RX
         IF(ABS(PM).LT.1.E-3)GO TO 1100
C
C  EQUATION 2 OF DEJARNETTE
C
            ETA=DPS2*(XX-XXL)/PM/CDL
         IF(ETA.LE.0.0)GO TO 1100
            EXETA=EXP(-ETA)
C
C  EQUATION 1 OF DEJARNETTE
C
            P1=PC-PM*EXETA
            DPS1=DPS2*(PC-P1)/PM
C
C  EQUATION 29 OF DEJARNETTE (LOADING FUNCTIONS)
C
            CALL LOADF(AMS,EXETA,SDL,CDL,LAMDA2,GAMMA2,DELTA2,
     1                 LAMDA1,GAMMA1,DELTA1,F2MF1)
         GO TO 1110
 1100    P1=PC
            DPS1=0.0
C
C  COMPUTE LOADING FUNCTIONS BEFORE EXPANSION
C
            LAMDA1=2.0*CDL*SDL
            F2MF1=2.0*B1*(1.0-2.0*SDL**2)
            GAMMA1=F2MF1
            DELTA1=F2MF1-2.0*B2*CDL**2
C
C  GET CONICAL PRESSURE FROM METHOD OF RASMUSSEN
C
 1110    CALL CONEP(GAM,AM,SD,PC)
C
C   PRANDTL MEYER EXPANSION
C
C
C  COMPUTE MACH NUMBER BEFORE EXPANSION
C
            CALL EQN44(GAM,PT,P1,AM1)
         IF(AM1.LT.1.0001)GO TO 1050
C
C  EXPAND THE FLOW AROUND THE CORNER, GET AM2 AND P2
C
            CALL EXPAND(GAM,TH,AM1,P1,AM2,P2,ICOMP)
C
         IF(ICOMP.EQ.1)GO TO 1050
C
C  COMPUTE COS(MU), MU IS THE MACH ANGLE
C
            CM1=SQRT(AM1**2-1.0)/AM1
            CM2=SQRT(AM2**2-1.0)/AM2
C
C  COMPUTE OMEGA*MACH (EQUATION 6 OF DEJARNETTE)
C
            OM1=(1.0+(GAM-1.)/2.*AM1**2)**3
            OM2=(1.0+(GAM-1.)/2.*AM2**2)**3
C
C  COMPUTE LAMBDA (EQUATION 5 OF DEJARNETTE)
C
            ALM1=GAM*P1*AM1/CM1
            ALM2=GAM*P2*AM2/CM2
C
C  EQUATION 19 OF DEJARNETTE D(G)/D(C1)
C
            DG1=2.0*CM1*DPS1/ALM1+SDL/RR/AM1
C
C  EQUATION 20 OF DEJARNETTE, RATIO BY STREAM TUBE AREA AROUND CORNER
C
            DG2=DG1*OM1/OM2
C
C  SOLVE EQUATION 19 BACKWARDS FOR (DP/DS)2
C
            DPS2=(DG2-SD/RR/AM2)*ALM2/CM2/2.0
C
C  EQUATION 31 OF DEJARNETTE, AFTER EXPANSION LOADING
C
            LAMDA2=ALM2/ALM1*LAMDA1
C
C  SOLVE FOR F1 AND F2 USING EQUATION 34 OF DEJARNETTE
C
            F1=((GAM+1.)/2.*AM1**4-2.0*AM1**2+2.0)/(AM1*CM1)**3
            F2=((GAM+1.)/2.*AM2**4-2.0*AM2**2+2.0)/(AM2*CM2)**3
            F2MF1=F2-F1
C
C  EQUATION 33 OF DEJARNETTE
C
            GAMMA2=ALM2/ALM1*(GAMMA1+2.*GAM*AMS*LAMDA1**2/ALM1*F2MF1)
C
C  EQUATION 32 OF DEJARNETTE
C
            DELTA2=ALM2/ALM1*DELTA1
C
C  SET THIS STATION AS THE LAST STATION FOR NEXT PASS
C
 1120    DELL=DEL
            BTL=BT
            RXL=RX
            XXL=XX
            CDL=CD
            SDL=SD
            PM=PC-P2
 1130    IF(DPS2.EQ.0.0.AND.ABS(TH).LT.0.001)GO TO 1150
         IF(ABS(PM).LT.1.E-3)GO TO 1140
         IF((DPS2/PM).LE.0.0)GO TO 1140
C
C  EQUATION 2 OF DEJARNETTE
C
            ETA=DPS2*(XPT-XX)/PM/CDL
            EXETA=EXP(-ETA)
C
C  EQUATION 1 OF DEJARNETTE
C
            P=PC-PM*EXETA
C
C  LOADING FUNCTIONS
C
            CALL LOADF(AMS,EXETA,SD,CD,LAMDA2,GAMMA2,DELTA2,
     1                 LAMDA,GAMMA,DELTA,F2MF1)
         GO TO 1150
 1140    DPS2=0.
            P=PC
            IF(TH.LT.-0.001)P=P2
            PM=0.0
C
C  LOADING FUNCTIONS
C
            LAMDA=2.0*CD*SD
            GAMMA=2.0*B1*(1.0-2.0*SD**2)
            DELTA=GAMMA-2.0*B2*CD**2
C
C  COMPUTE MACH NUMBER
C
 1150    CALL EQN44(GAM,PT,P,AMX)
            CP =(P-1.)/(GAM/2.)/AMS
C
C  COMPUTE INCREMENTAL AERODYNAMICS FOR THIS STATION
C
            ACAI(I)=8.*CP*R*RX
            ACAAI(I)=2.*(GAMMA+DELTA)*R*RX
            ACNI(I)=8.*LAMDA*R
            ACMI(I)=-ACNI(I)*(XPT+R*RX)
C
         IF(IPRINT.EQ.0)GO TO 1160
C
C  HERE TO PRINT RESULTS IF REQUIRED
C
C  COMPUTE THE PRESSURES AROUND THE BODY
C
            CALL CPDIST(SALP,CALP,LAMDA,GAMMA,DELTA,CP,CPP)
C
C  WRITE PRESSURE DISTRIBUTION ABOUT THE BODY TO UNITS 10,12 
C
C         IF(IPRINT.EQ.1.AND.ALP.NE.0.)WRITE(10,1210)XPT,(CPP(K),K=1,7)
          IF(IPRINT.EQ.1) WRITE(10,1210)XPT,(CPP(K),K=1,7)
          IF(IPRINT.EQ.1.AND.ALP.EQ.0.)WRITE(12,1350)XPT,CPP(1),AMX
C         IF(IPRINT.EQ.1.AND.ALP.EQ.0.)WRITE(6,1220)XPT,CPP(1),AMX
C
C  PRINT BODY COORDINATES USED BY SOSE TO TAPE UNIT 9
C
         IF(IPRINT.EQ.2.AND.ALP.EQ.0.) 
     1           WRITE(9,1310) XPT*DMAX,R*DMAX
C
 1160    CONTINUE
C        IF(IPRINT.EQ.1 .AND. ALP.NE.0.)WRITE(6,1230)
C        IF(IPRINT.EQ.1 .AND. J.NE.M.AND.ALP.EQ.0.)CALL HEADR9
C        IF(IPRINT.EQ.1 .AND. J.NE.M.AND.ALP.EQ.0.)WRITE(6,1190)AM,AL
C        IF(IPRINT.EQ.1 .AND. J.NE.M .AND. ALP.NE.0.)WRITE(6,1180)
C        IF(IPRINT.EQ.1 .AND. J.NE.M .AND. ALP.EQ.0.)WRITE(6,1200)
C
C  INTEGRATE FOR INCREMENTAL AERODYNAMICS ON THIS SECTION
C
         CALL QUAD1(ACAI,IMX,DX,DCAI)
         CALL QUAD1(ACAAI,IMX,DX,DCAAI)
         CALL QUAD1(ACNI,IMX,DX,DCNI)
         CALL QUAD1(ACMI,IMX,DX,DCMI)
         CAI=CAI+DCAI
         CAAAI=CAAAI+DCAAI
         CNI=CNI+DCNI
         CMI=CMI+DCMI
C
 1170 CONTINUE
C
C  COMPUTE FINAL AERODYNAMICS
C
      CAP=CAI
      CNALP=CNI
      CMALP=CMI
      CAAA=CAAAI
      XCP=-CMALP/CNALP
C
C1180 FORMAT(//,52X,21HPRESSURE COEFFICIENTS,//,7X,10H  X/DMAX  ,9X,
C    15HPHI=0,8X,6HPHI=30,8X,6HPHI=60,8X,6HPHI=90,8X,7HPHI=120,7X,
C    27HPHI=150,7X,7HPHI=180)
 1190 FORMAT(25X,'BODY ALONE PRESSURE OUTPUT',//
     1 20X,'MACH =',F5.2,' ANGLE OF ATTACK =',F7.2,' DEG.')
 1200 FORMAT(//,22X,'PRESSURE COEFFICIENTS',//,4X,'X/DMAX',
     1 10X,'CP',9X,'M-LOCAL')
 1210 FORMAT(F14.6,7F10.5)
 1220 FORMAT(4X,4F14.6)
C1230 FORMAT(49H0 NOTE - PHI=  0 IS TOP VERTICAL CENTER (LEEWARD) /
C    1       53H         PHI=180 IS BOTTOM VERTICAL CENTER (WINDWARD) )
C
C BODY COORDINATE COMMENT CARD COMMENTED OUT FOR TECPLOT FORMAT
C
C1300 FORMAT(3X,31HSOSE BODY COORDINATES (IN FEET))
 1310 FORMAT(4X,2F14.6)
 1350 FORMAT(4X,3F14.6)
      RETURN
      END
