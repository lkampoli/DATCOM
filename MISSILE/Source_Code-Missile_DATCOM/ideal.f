      SUBROUTINE IDEAL(NACA,RHO,X0,THN,CAM)
C
C***  CALCULATE THE SECTION IDEAL AERODYNAMIC PARAMETERS
C
C  ROUTINE ADAPTED FROM DIGITAL DATCOM
C  ORIGINAL ROUTINE WRITTEN BY D. KINSEY, D. BOWERS, AFFDL
C  MODIFIED BY S.R. VUKELICH, MCDONNELL DOUGLAS
C
      COMMON /ASMVAR/ CBAR,A0,XC,MCC,CLCC,AI,ALO,CLI,ASEP,CLA0,
     1                M,ZTNU(32),ZCNU(32),ST1NU(32),ST2NU(32),
     2                ST3NU(32),ST4NU(32),ST5NU(32),X(32),THNU(32),
     3                THMU(32),VXOVU(32),VXOVL(32),CPUM(32),
     4                CPLM(32),BO(32),CPI(32)
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      CHARACTER*4 A4HS,NACA
      DIMENSION NACA(80),X0(50),THN(50),CAM(50)
      DATA A4HS/'S   '/
C
      XN=32.
      N=XN
      L=N-1
      DO 1000 J=1,L
      XMU=J
      THMU(J)=XMU*PI/XN
 1000 CONTINUE
      DO 1010 I=1,N
      XNU=I
      THNU(I)=XNU*PI/XN
      X(I)=.5*(COS(THNU(I))+1.)
 1010 CONTINUE
      A0=SQRT(2.*RHO)
C
C***  OBTAIN THICKNESS AT CALCULATED X(I) FOR WEBER ANALYSIS
C
      IF(NACA(8).EQ.A4HS)GO TO 1030
      DO 1020 I=1,N
      CALL POLINT(X0,THN,M,X(I),ZTNU(I),DYDX)
 1020 CONTINUE
      GO TO 1050
 1030 CONTINUE
C
C***  HERE FOR SUPERSONIC AIRFOILS
C
      IN=0
      MI=0
      NG=0
      DO 1040 I=1,N
      CALL LNTRP(X0,THN,M,X(I),ZTNU(I))
 1040 CONTINUE
 1050 CONTINUE
C
C***  DEFINE THE WEBER TERMS FOR THIS AIRFOIL
C
C***    ST1NU DEFINES THE SOURCE DISTRIBUTION IN PARALLEL FLOW
C***    ST2NU DEFINES THE SLOPE OF THE THICKNESS DISTRIBUTION
C
      DO 1080 I=1,N
      ST1NU(I)=0.0
      ST2NU(I)=0.0
      DO 1082 J=1,L
      IF(I.EQ.J)GO TO 1060
      S1=((-1.)**(J-I)-1.)/XN*2.*SIN(THMU(J))/
     1  (COS(THMU(J))-COS(THNU(I)))**2
      S2=-2.*(-1.)**(J-I)*SIN(THMU(J))/(SIN(THNU(I))*
     1  (COS(THMU(J))-COS(THNU(I))))
      GO TO 1070
 1060 S1=XN/SIN(THNU(I))
      S2=COS(THNU(I))/(SIN(THNU(I)))**2
 1070 YS1=S1*ZTNU(J)
      YS2=S2*ZTNU(J)
      ST2NU(I)=ST2NU(I)+YS2
      ST1NU(I)=ST1NU(I)+YS1
 1082 CONTINUE
 1080 CONTINUE
      ST1NU(N)=ST1NU(N)+XN*A0
C
C***    ST3NU DEFINES THE VORTEX DISTRIBUTION IN NORMAL FLOW
C
      DO 1110 I=1,N
      ST3NU(I)=0.0
      DO 1112 J=1,L
      IF(I.EQ.J) GO TO 1090
      S3=((-1.)**(J-I)-1.)/XN*2.*SIN(THMU(J))/
     1   (COS(THMU(J))-COS(THNU(I)))**2+2./XN*
     2   (1.-(-1.)**(J-I))/(SIN(THMU(J))*
     3   (COS(THMU(J))-COS(THNU(I))))
      GO TO 1100
 1090 S3=XN/SIN(THNU(I))
 1100 YS3=S3*ZTNU(J)
      ST3NU(I)=ST3NU(I)+YS3
 1112 CONTINUE
 1110 CONTINUE
      DO 1140 I=1,N
      IF(I.EQ.N)GO TO 1120
      S3=((-1.)**I-1.)/(XN*(1.+COS(THNU(I))))
      GO TO 1130
 1120 S3=0.0
 1130 Y=S3*SQRT(RHO/2.)
      IF(I.NE.N) GO TO 1140
      Y=XN/2.*A0
      ST3NU(I)=ST3NU(I)+Y
 1140 CONTINUE
C
C*** OBTAIN MEAN LINE DISTRIBUTION AT CALCULATED X(I) FOR WEBER ANALYSIS
C
      DO 1150 I=1,N
      CALL POLINT(X0,CAM,M,X(I),ZCNU(I),DYDX)
 1150 CONTINUE
C
C***    ST4NU DEFINES THE VORTEX DISTRIBUTION DUE TO CAMBER
C***    ST5NU DEFINES THE SLOPE OF THE MEAN LINE
C
      DO 1180 I=1,N
      ST4NU(I)=0.0
      ST5NU(I)=0.0
      DO 1182 J=1,L
      IF(I.EQ.J)GO TO 1160
      S4=2.*((-1.)**(J-I)-1.)/(XN*SIN(THNU(I)))*(1.-COS(THMU(J))*
     1  COS(THNU(I)))/(COS(THNU(I))-COS(THMU(J)))**2-2.*((-1.)**J-1.)/
     2  (XN*SIN(THNU(I))*(1.-COS(THMU(J))))
      S5=-2.*(-1.)**(J-I)/(COS(THMU(J))-COS(THNU(I)))
      GO TO 1170
 1160 S4=XN/SIN(THNU(I))-2.*((-1.)**J-1.)/(XN*SIN(THNU(I))*
     1  (1.-COS(THNU(I))))
      S5=-COS(THNU(I))/(SIN(THNU(I)))**2
 1170 YS4=S4*ZCNU(J)
      YS5=S5*ZCNU(J)
      ST4NU(I)=ST4NU(I)+YS4
      ST5NU(I)=ST5NU(I)+YS5
 1182 CONTINUE
 1180 CONTINUE
C
C***  GET PLANFORM IDEAL PARAMETERS
C
      JN=N-4
      ZC95=ZCNU(4)
      ZC05=ZCNU(JN)
      DAI05= .3739*ZC05+.04745*ST5NU(N-1)
      DAI95=-.3739*ZC95+.04745*ST5NU(1)
      DA95=-.7834*ZC95+.09518*ST5NU(1)
      T1=.5*(-ZC95*COS(THNU(4))/(SIN(THNU(4))/2.)**2-ZC05*
     1  COS(THNU(JN))/(SIN(THNU(JN))/2.)**2)
      T2=.5*ZC95/(1.-COS(THNU(4)))
      NM5=N-5
      SUMAL=0.0
      SUMAI=0.0
      DO 1200 I=5,L
      IF(I .GT. NM5) GO TO 1190
      T3=-ZCNU(I)*COS(THNU(I))/(SIN(THNU(I))/2.)**2
      SUMAI=SUMAI+T3
 1190 T4=ZCNU(I)/(1.-COS(THNU(I)))
      SUMAL=SUMAL+T4
 1200 CONTINUE
C
C***  IDEAL ANGLE OF ATTACK
C
      DAI=.5/XN*(T1+SUMAI)
      AI=RAD*(DAI05+DAI+DAI95)
C
C***  ANGLE OF ATTACK FOR ZERO LIFT
C
      DALO=-2./XN*(T2+SUMAL)
      ALO=RAD*(DA95+DALO)
C
C***  IDEAL LIFT COEFFICIENT
C
      CLI=2.*PI/RAD*(AI-ALO)
 1210 CONTINUE
C
C***  PITCHING MOMENT ABOUT THE QUARTER CHORD
C
      SUMCM=0.0
      DO 1220 I=1,L
      T5=ZCNU(I)*COS(THNU(I))
      SUMCM=SUMCM+T5
 1220 CONTINUE
      XMUO=SUMCM*(-PI/XN)
      CMCO4=2.*XMUO+PI/2.*ALO/RAD
C
      RETURN
      END
