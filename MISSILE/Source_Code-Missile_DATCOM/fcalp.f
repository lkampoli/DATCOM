      SUBROUTINE FCALP(NALP,ALPHA,CN,CAO,SW,NPAN,AR,SREF,
     1   MACH,REN,RLE,SWEEPI,TAPER,ALPI,CADL)
C
C***  SUBROUTINE TO COMPUTE FIN ALONE AXIAL FORCE AT ANGLE OF ATTACK
C
C  MODIFIED BY W. BLAKE, WL/FIGC
C
C     CADL IS CALCULATED FROM THE RELATION
C        CA = (CADL+CAO) = (CAO+CDL)*COS(ALPHA)-CL*SIN(ALPHA)
C
C     THE CAO*COS(ALPHA) TERM IS ADDED IN ROUTINE FINXCA
C        WHERE "ALPHA" IS THE BODY TOTAL ANGLE OF ATTACK.
C
C     THIS ROUTINE CALCULATES A TABLE OF VALUES FOR OBTAINING
C     THE CDL*COS(ALPHA)-CL*SIN(ALPHA) TERM
C        THIS IS APPLIED IN ROUTINE FINXCA AT THE WHERE "ALPHA"
C        IS THE FIN EQUIVALENT ANGLE OF ATTACK
C
C  THE FOLLOWING METHODS ARE USED FOR CDL:
C     FOR SHARP LEADING EDGES AT M < 1 AND ALL SECTIONS AT M > 1
C        CDL = CL*TAN(ALPHA) 
C     FOR ROUND LEADING EDGES AT SUBSONIC SPEEDS
C        CDL = CL**2/(PI*AR*E)   ALPHA < 8
C        CUBIC FAIRING       8 < ALPHA < 16  
C        CDL = CL*TAN(ALPHA)     ALPHA > 16
C
C***  INPUTS
C
C   NALP -- NUMBER OF ANGLES AF ATTACK IN FIN CN TABLE
C  ALPHA -- ANGLES OF ATTACK, DEGREES
C     CN -- NORMAL FORCE COEFFICIENTS
C    CAO -- AXIAL FORCE COEFFICIENT AT ZERO NORMAL FORCE
C     SW -- WING AREA
C   NPAN -- NUMBER OF PANELS IN SET
C     AR -- FIN ASPECT RATIO
C   SREF -- REFERENCE AREA
C   MACH -- FREESTREAM MACH NUMBER
C    REN -- FREESTREAM REYNOLDS NUMBER/FT
C    RLE -- LEADING EDGE RADIUS
C SWEEPI -- LEADING EDGE SWEEP, DEG.
C  TAPER -- TAPER RATIO
C   ALPI -- ANGLE OF ATTACK OF INTEREST, DEGREES
C
C***  OUTPUT
C
C   CADL -- AXIAL FORCE AT ANGLE OF ATTACK (EXCLUDING CAO)
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      REAL MACH,NPAN
C
      DIMENSION IROUT(2),ALPHA(20),CN(20)
      DIMENSION RLETAB(9),RTAB(9)
C
      DATA RLETAB/0.,1.1E3,1.E4,2.E4,4.E4,6.E4,8.E4,1.E5,1.E9/
      DATA RTAB/0.,0.,0.6,0.73,0.81,0.84,0.85,0.865,0.865/
C
C   USE TOTAL PANEL RESULTS REFERENCED TO PANEL AREA
C
      CAOT=2.*CAO*SREF/SW
      CALL LNTRP(ALPHA,CN,NALP,ALPI,CNH)
      CNTP=CNH*SREF/SW
      CADL=0.
      IF(MACH.GE.1.0 .OR. RLE.LE.UNUSED) GO TO 1050
C
C***  ROUND NOSE AIRFOIL CALCULATIONS, DATCOM SEC. 4.1.5.2
C
      SWEEP=ABS(SWEEPI)
      FACT=0.
      IF(SWEEP .NE. 0. .AND. MACH .LE. .8 .AND. RLE .NE. UNUSED)
     1 FACT=REN*RLE/TAN(SWEEP/RAD)*SQRT(1.-MACH**2*COS(SWEEP/RAD)**2)
      IF(SWEEP .EQ. 0. .AND. MACH .LE. .8 .AND. RLE .NE. UNUSED)
     1 FACT=1.E9
      CALL LNTRP(RLETAB,RTAB,9,FACT,RLS)
      FACT1=AR*TAPER/COS(SWEEP/RAD)
      ADD=.011*FACT1
      IF(ADD .GT. .11)ADD=.11
      IF(FACT .GE. 1.E3)RLS=RLS+ADD
C
      CALL LNTRP(ALPHA,CN,NALP,0.,CNA0)
      CALL LNTRP(ALPHA,CN,NALP,1.,CNA1)
      CLA=(CNA1-CNA0)*SREF/SW*RAD-CAOT
      E=1.1*CLA/AR/(RLS*CLA/AR+(1.-RLS)*PI)
C
C*** CALCULATE CL FROM CN AND CAO
C
      IF(ABS(ALPI).LT.0.01.OR.ABS(ALPI).GT.45.) THEN
         CL=CNTP
      ELSE
         TERM1=PI*E*AR/(2.*TAN(ALPI/RAD))
         TERM2=(CNTP/COS(ALPI/RAD)-CAOT*TAN(ALPI/RAD))
         CL=TERM1*(-1.+SQRT(1.+2./TERM1*TERM2))
      ENDIF
C
      CADLS=CL**2*COS(ALPI/RAD)/(PI*AR*E)-CL*SIN(ALPI/RAD)
      ATEM=ABS(ALPI)-8.
      IF(ATEM.GT.0. .AND. ATEM.LT.8.)
     1   CADLS=CADLS*(ATEM**3-12.*ATEM**2+256.)/256.
      IF(ATEM.GE.8.)  CADLS=0.
C
C   FINAL RESULT, REFERENCE TO SREF
C
      CADL=CADLS
 1050 CONTINUE
      CADL=CADL*SW/SREF
C
      RETURN
      END

