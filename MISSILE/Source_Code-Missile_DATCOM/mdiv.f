      SUBROUTINE MDIV(FRNOSE,NX,R,X,LNOSE,MSUB)
C
C     THIS SUBROUTINE DETERMINES CPmin BASED ON A CORRELATION
C     OF RESULTS GATHERED FROM A LINE SINGULARITY CODE WRITTEN
C     BY DR. MIKE HEMSCH OF NASA/LANGLEY.  CPmin IS THEN USED
C     TO PREDICT THE ONSET OF THE TRANSONIC DRAG RISE. THE DRAG
C     DIVERGENCE MACH NUMBER IS PASSED TO THE CALLING PROGRAM.
C
C  WRITTEN BY A. JENN, MCDONNELL DOUGLAS
C
      DIMENSION R(50),X(50)
      REAL MSUB,NX,LNOSE
C
C     ASSIGN NX AS AN INTEGER
      INX = NX
C
C     ONLY THE NOSE COORDINATES ARE NEEDED
      JNX = 0
      DO 50 I=1,INX
         JNX = JNX + 1
         IF (X(I) .EQ. X(1)+LNOSE) GOTO 75
         IF (X(I) .GT. X(1)+LNOSE) THEN
            JNX = JNX - 1
            GOTO 75
         ENDIF
   50 CONTINUE
C
   75 CONTINUE
C           
C     DETERMINE THE TERMINAL ANGLE OF A CONE OF
C     FINENESS RATIO FRNOSE
      THETAC = ATAN(1./(2.*FRNOSE))
C
C     CALCULATE CPPAN AS IF THE WHOLE NOSE WERE OF THE SHAPE
C     OF THIS PANEL
      THETA = ATAN((R(JNX) - R(JNX-1))/(X(JNX) - X(JNX-1)))
      IF (THETA.GT.THETAC) THETA=THETAC
      SLOPE = 0.22 - 0.13*(ABS(THETA/THETAC))**0.75
      Y2 = FRNOSE*SLOPE
      CPMIN = 1./(10.*Y2)**1.25
C
C     CALCULATE MSUB FOR THE SPECIFIC VALUE OF CPMIN
      MSUB = EXP(-0.521*CPMIN**0.645)
C
      RETURN
      END
