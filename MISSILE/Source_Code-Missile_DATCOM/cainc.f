      SUBROUTINE CAINC(ICASE,CAS,ICON,DUMMY)
C      
C     SUBROUTINE TO DETERMINE CA INCREMENTED TABLE AND INCREMENT CA
C      
C***  INPUT
C      
C     ICASE = CASE NUMBER OF A RUN
C     CAS   = ARRAY CONTAINING CA VALUES FROM SYNTHS
C     DUMMY = ARRAY THAT CONTAINS EXPERIMENTAL COEFFICIENTS AND WILL
C             CONTAIN UPDATED COEFFICIENTS CA = DUMMY 41 - 60
C     ICON  = CONFIGURATION TO BE ICREMENTED
C             ICON = 1     BODY ALONE
C             ICON = 2     BODY + 1 FIN SET
C             ICON = 3     BODY + 2 FIN SETS
C             ICON = 4     BODY + 3 FIN SETS
C             ICON = 5     BODY + 4 FIN SETS
C      
C***  OUTPUT
C      
C     CA = INCREMENTED CA VS. ALPHA ARRAY EQUIVALENT TO DUMMY 41 - 60
C      
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /FLC   / NA,ALPHA(20),FLDUM(123)
      COMMON /INC   / DUM(44),C(20),DIP(64),ZIP,ALP(20),NAP
      DIMENSION CA(20),CATH(20),CAEXP(20),DUMMY(120),CAS(20,5)
      REAL NA,NAP
C
C **  INITIALIZE WORKING ARRAYS
C      
      DO 1000 I=1,20
         CA(I)=UNUSED
         CATH(I)=UNUSED
         CAEXP(I)=UNUSED
 1000 CONTINUE
C      
      NAP1 = NAP + .5
      NALPHA= NA + .5
C      
C       DETERMINE WHICH CONFIGURATION IS TO BE INCREMENTED AND FILL
C         THE WORKING ARRAYS
C      
      DO 1010 I=1,NALPHA
         IF(ICON .EQ. 1)CATH(I) = CAS(I,5)
         IF(ICON .GT. 1)CATH(I) = CAS(I,ICON-1)
 1010 CONTINUE
      IF (ICASE .NE. 1)GO TO 1030
      IF (ZIP .EQ. 1.)GO TO 1030
C      
C       FILL CAEXP ARRAY WITH EXPERIMENTAL DATA BEING HELD IN
C            ARRAY DUMMY
C      
      DO 1020 I=1,NALPHA
         CAEXP(I) = DUMMY(I+40)
 1020 CONTINUE
C      
C       CALL CALIB TO GET A TABLE OF INCREMENTAL MULTIPLIERS
C      
      CALL CALIB(2,NALPHA,ALPHA,CAEXP,NALPHA,ALPHA,CATH,C)
 1030 CONTINUE
C      
C***  DETERMINE INCREMENTED CA VALUE USING INCREMENTALS IN ARRAY C
C       NEED TO INTERPOLATE FOR ALPHA OF INTEREST BECAUSE INCREMENTALS
C       WERE CALCULATED AT INITIAL CASE ALPHA'S
C      
      DO 1040 I=1,NALPHA
         CALL LNTRP(ALP,C,NAP1,ALPHA(I),CI)
         CA(I) = CI * CATH(I)
 1040 CONTINUE
C
C     PUT INCREMENTED CA INTO ARRAY DUMMY
C
      DO 1050 I=1,NALPHA
         DUMMY(I+40)=CA(I)
 1050 CONTINUE
      RETURN
      END
