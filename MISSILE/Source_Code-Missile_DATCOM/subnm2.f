      SUBROUTINE SUBNM2(ISEQ,CARD,NAMES,MAXLET,NNAM,NLET,VALUES,MAXLV,
     1 LENV,SUBED)
C
C***  SUBROUTINE TO SUBSTITUTE NUMERIC VALUES FOR NAMES IN PROTUBERANCE
C***  DRAG INPUTS IN AN INPUT CARD
C
C***  INPUTS
C
C   CARD -- THE INPUT CARD (80 COLUMNS)
C  NAMES -- THE NAMES TO BE CHANGED (A TWO-DIMENSIONAL ARRAY)
C MAXLET -- MAXIMUM NUMBER OF LETTERS IN ANY ONE NAME
C   NNAM -- NUMBER OF NAMES TO BE CHECKED
C   NLET -- NUMBER OF LETTERS IN EACH NAME (AN ARRAY)
C VALUES -- VALUES TO BE SUBSTITUTED (A TWO-DIMENSIONAL ARRAY)
C  MAXLV -- MAXIMUM NUMBER OF LETTERS IN SUBSTITUTION VALUES
C   LENV -- NUMBER OF CHARACTERS IN EACH SUBSTITUTED VALUE (AN ARRAY)
C
C***  OUTPUT
C
C   CARD -- THE NEW CARD IMAGE
C
C***  OTHER ROUTINES CALLED -- NONE
C
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      CHARACTER*4 IBLNK,CARD,VALUES,ICOL,NAMES
C
      DIMENSION CARD(80),VALUES(MAXLV,NNAM)
      LOGICAL SKIPPR,SUBED
      DIMENSION ICOL(80),NAMES(MAXLET,NNAM),NLET(NNAM),LENV(NNAM)
      DATA IBLNK / '    ' /
C
C***  FILL THE NEW CARD WITH BLANKS
C
      DO 1000 I=1,80
         ICOL(I)=IBLNK
 1000 CONTINUE
      INUM=0
      ISKIP=0
      SKIPPR=.FALSE.
C
C***  MOVE ALONG THE CARD BY COLUMNS
C
      DO 1060 I=1,80
         IM1=I-1
         IF(I.LE.ISKIP)GO TO 1060
         INUM=INUM+1
         ICOL(INUM)=CARD(I)
C
C***  LOOK FOR NON-BLANK CHARACTER
C
         IF(CARD(I).EQ.IBLNK)GO TO 1060
C
C***  CHECK FOR NAME
C
C***  CHECK EACH NAME INPUT
C
         DO 1050 N=1,NNAM
            NN=N
            KLEN=NLET(N)
C
C***  CHECK EACH LETTER IN THE NAME
C
            DO 1020 K=1,KLEN
                KPIM1=K+IM1
                IF(CARD(KPIM1).NE.NAMES(K,N))GO TO 1050
 1020       CONTINUE
C
C***  NAME FOUND, SUBSTITUTE NEW STRING AND SKIP REST OF CHARACTERS
C
            MAXK=LENV(NN)
            K1=IM1+1
            K2=KLEN+IM1
            IF(SKIPPR)GO TO 1030
            IF(PARTS(17))WRITE(6,1090)ISEQ,(CARD(K),K=1,80),
     1      (CARD(K),K=K1,K2)
 1030       IF(SKIPPR.AND.PARTS(17))WRITE(6,1100)(CARD(K),K=K1,K2)
            SKIPPR=.TRUE.
            INUMM1=INUM-1
            DO 1040 K=1,MAXK
               KK=K+INUMM1
               ICOL(KK)=VALUES(K,NN)
 1040       CONTINUE
            INUM=INUM+MAXK-1
            ISKIP=I+KLEN-1
            GO TO 1060
 1050    CONTINUE
 1060 CONTINUE
C
C***  IF ISKIP .EQ. 0, THEN NO SUBSTITUTION HAS BEEN MADE -- EXIT
C
      IF(ISKIP.EQ.0)GO TO 1080
C
C***  PACK NEW CARD IMAGE INTO OLD
C
      DO 1070 I=1,80
         CARD(I)=ICOL(I)
 1070 CONTINUE
      SUBED=.TRUE.
 1080 CONTINUE
 1090 FORMAT(1X,I3,1X,80A1,/,14X,' ** SUBSTITUTING NUMERIC FOR NAME ',
     1 10A1)
 1100 FORMAT(14X,' ** SUBSTITUTING NUMERIC FOR NAME ',10A1)
      RETURN
      END
