      SUBROUTINE CORDSP(X,XU,XL,YUU,YLL,YU,YL,L,I,J,K,II,JJ,KK,III,JJJ,
     1 KKK,LLL,TOC,XT,KSHARP)
C
C*****
C***** CALCULATE SUPERSONIC AIRFOIL COORDINATES
C*****
C
C  ROUTINE ADAPTED FROM DIGITAL DATCOM
C  ORIGINAL ROUTINE WRITTEN BY D. KINSEY, D. BOWERS, AFFDL
C  MODIFIED BY S.R. VUKELICH, MCDONNELL DOUGLAS
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      DIMENSION X(50),XU(50),XL(50),YUU(50),YLL(50),YU(50),YL(50)
C
      REAL KSHARP
      XT=(100.*J+10.*K+II)/1000.
      TOC=(100.*JJ+10.*KK+III)/1000.
      XF=(100.*JJJ+10.*KKK+LLL)/1000.
      GO TO (1020,1000,1050), I
 1000 CONTINUE
C
C**** HERE FOR BICONVEX (CIRCULAR ARC)
C
      XT=0.50
      KSHARP=16./3.
      RC=(TOC**2+1.)/(4.*TOC)
      DO 1010 IL=1,L
      XU(IL)=X(IL)
      XL(IL)=X(IL)
      YU(IL)=TOC/2.-RC+SQRT(RC**2-(X(IL)-.5)**2)
      YL(IL)=-YU(IL)
 1010 CONTINUE
      GO TO 1090
 1020 CONTINUE
C
C**** HERE FOR DOUBLE WEDGE
C
      KSHARP=16./3.
      IF(XT .NE. 1.)KSHARP=(1./XT)/(1.-XT)
      DO 1040 IL=1,L
      XU(IL)=X(IL)
      XL(IL)=X(IL)
      IF(X(IL).GT.XT)GO TO 1030
      YU(IL)=X(IL)*TOC/(2.*XT)
      YL(IL)=-YU(IL)
      GO TO 1040
 1030 YU(IL)=TOC/2.-(X(IL)-XT)*TOC/(2.*(1.-XT))
      YL(IL)=-YU(IL)
 1040 CONTINUE
      GO TO 1090
 1050 CONTINUE
C
C**** HERE FOR HEXAGONAL
C
      OMTMF=1.-XT-XF
      KSHARP=16./3.
      IF(OMTMF .GT. 0.)KSHARP=(1.-XF)/(OMTMF*XT)
      DO 1080 IL=1,L
      XU(IL)=X(IL)
      XL(IL)=X(IL)
      IF(X(IL).GT.(XT+XF))GO TO 1070
      IF(X(IL).GE.(XT   ))GO TO 1060
      YU(IL)=X(IL)*TOC/(2.*XT)
      YL(IL)=-YU(IL)
      GO TO 1080
 1060 YU(IL)=TOC/2.
      YL(IL)=-YU(IL)
      GO TO 1080
 1070 IF(OMTMF .LE. 0.)GO TO 1060
      YU(IL)=TOC/2.-(X(IL)-XT-XF)*TOC/(2.*OMTMF)
      YL(IL)=-YU(IL)
 1080 CONTINUE
 1090 CONTINUE
      IF(ABS(YU(L)) .LT. 0.00001)YU(L)=0.
      IF(ABS(YL(L)) .LT. 0.00001)YL(L)=0.
      DO 1100 IL=1,L
      YUU(IL)=YU(IL)
      YLL(IL)=YL(IL)
 1100 CONTINUE
      RETURN
      END
