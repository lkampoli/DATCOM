      SUBROUTINE THEORY(NACA,NM,MACH,REN,IFIN,RHO,TMAX,DELTAY,CBARR,
     1 X0,THN,CAM,CLA,XAC,CMCO4,ALPHAI,ALPHAO,CLIDL,CLM)
C      
C***  MAIN ROUTINE FOR CALCULATING AIRFOIL SECTION AERODYNAMICS
C***  METHOD USES A WEBER ANALYSIS OF THE CROSS-SECTION AT SUBSONIC
C***  SPEEDS TO THE POINT THAT A SHOCK IS FORMED ON THE SURFACE
C
C  ROUTINE ADAPTED FROM DIGITAL DATCOM
C  ORIGINAL ROUTINE WRITTEN BY D. KINSEY, D. BOWERS, AFFDL
C  MODIFIED BY S.R. VUKELICH, MCDONNELL DOUGLAS
C  FURTHER MODIFIED BY W. BLAKE, WL/FIGC
C      
C***  INPUTS
C      
C   NACA -- NACA CONTROL CARD FOR FOIL
C     NM -- NUMBER OF MACH NUMBERS
C   MACH -- ARRAY OF MACH NUMBERS
C    REN -- ARRAY OF REYNOLDS NUMBERS PER FOOT
C   IFIN -- FIN SET BEING ANALYIZED
C    RHO -- FIN LEADING EDGE RADIUS
C   TMAX -- FIN MAXIMUM THICKNESS
C DELTAY -- FIN DELTA-Y PARAMETER
C     X0 -- ARRAY OF CHORD STATIONS DEFINING THE FOIL
C    THN -- ARRAY OF THICKNESSES DEFINING THE FOIL
C    CAM -- ARRAY OF CAMBER LINE STATIONS DEFINING THE FOIL
C ALPHAI -- IDEAL ANGLE OF ATTACK FROM NACA DESIGNATION
C ALPHAO -- ZERO-LIFT ANGLE OF ATTACK
C  CLIDL -- IDEAL LIFT COEFFICIENT FROM NACA DESIGNATION
C    CLM -- MAXIMUM LIFT COEFFICIENT
C      
C***  OUTPUTS
C      
C    CLA -- SECTION LIFT CURVE SLOPE, PER DEGREE
C    XAC -- SECTION AERODYNAMIC CENTER
C  CMCO4 -- FOIL CMO ABOUT THE QUARTER-CHORD
C      
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      COMMON /ASMVAR/ CBAR,A0,XC,MCC,CLCC,AI,ALO,CLI,ASEP,CLA0,
     1                M,ZTNU(32),ZCNU(32),ST1NU(32),ST2NU(32),
     2                ST3NU(32),ST4NU(32),ST5NU(32),X(32),THNU(32),
     3                THMU(32),VXOVU(32),VXOVL(32),CPUM(32),
     4                CPLM(32),BO(32),CPI(32)
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      DIMENSION NACA(80),MACH(20),REN(20),CLA(20),XAC(20),X0(50),
     1 THN(50),CAM(50),IROUT(2),CMCO4(20),CLM(20)
      CHARACTER*4 NACA
      LOGICAL CRIT,FLAG,SKIP
      REAL NM,MACH,MCC
C      
      AI=ALPHAI
      ALO=ALPHAO
      CLI=CLIDL
      CMCO4O=0.
      CLA0=2.*PI/RAD
C      
      CBAR=CBARR
      RHOOC=RHO/CBAR
C
      NMACH=NM + 0.5
      DO 1000 I=1,NMACH
         CLA(I)=2.*PI/RAD
         XAC(I)=0.25
         CMCO4(I)=0.
 1000 CONTINUE
C      
      DO 1010 I=2,50
         IF(X0(I) .LE. X0(I-1))GO TO 1020
         IF(THN(I).GT.THN(I-1)) XTMAX=X0(I)
         M=I
 1010 CONTINUE
C      
 1020 CONTINUE
C      
C  REV 1/96 UPDATE FOR BLUNTED T.E. SECTION
C
      IF(PARTS(IFIN+1))CALL HEADR9
      IF(PARTS(IFIN+1))WRITE(6,1060)IFIN
      SKIP=.FALSE.
      IF(THN(M) .NE. 0.)SKIP=.TRUE.
      IF(SKIP)GO TO 1050
C      
C***  SHARP TRAILING EDGES - WEBER'S METHOD REF AFFDL-TR-71-87
C
C***  CALCULATE THE SECTION AERODYNAMICS AT ZERO MACH
C      
      CALL IDEAL(NACA,RHOOC,X0,THN,CAM)
      AMACH = 0.0
      RENN = 0.0
      CALL SLOPE(RHOOC,AMACH,CLA0,RENN,XACO,CMCO4O)
      IF(PARTS(IFIN+1))WRITE(6,1070)AI,ALO,CLI,CMCO4O,CLA0,
     1     RHOOC,TMAX,DELTAY
C      
C***  CALCULATE THE LIFT CURVE SLOPE, CMO AND XAC AT MACH NUMBER
C      
      CRIT=.FALSE.
      DO 1040 I=1,NMACH
         AMACH=MACH(I)
         RENN=REN(I)*CBAR
         IF(AMACH .GE. MCC) CRIT=.TRUE.
         IF(AMACH .GE. MCC) CLA(I)=CLCC
         IF(CRIT) GO TO 1040
         CALL SLOPE(RHOOC,AMACH,CLA(I),RENN,XAC(I),CMCO4(I))
         IF(PARTS(IFIN+1))WRITE(6,1080) AMACH,CLA(I),XAC(I),CLM(I)
 1040 CONTINUE
      IF(CRIT.AND.PARTS(IFIN+1))WRITE(6,1090) MCC,XC,CLCC
      GO TO 1200
C
C  REV 1/96 UPDATE BLUNT TRAILING EDGES
C  EMPIRICAL METHOD FOR MCC,CLA,XAC WITH PRANDTL-GLAUERT MACH EFFECT
C
 1050 CONTINUE
      IF(PARTS(IFIN+1))WRITE(6,1070)AI,ALO,CLI,CMCO4O,CLA0,
     1                 RHOOC,TMAX,DELTAY
      CRIT=.FALSE.
      DO 1100 I=1,NMACH
         AMACH=MACH(I)
         TMCC=TMAX
         IF(TMCC.LT.0.02) TMCC=0.02
         MCC=1.-(SQRT(TMCC)+0.262*(SQRT(TMCC)-SQRT(0.06)))
         CLAT=2.*PI/RAD
         IF(AMACH .LT. MCC) THEN
            CLA(I)=CLAT/SQRT(1.-AMACH**2)
            XAC(I)=0.25
            IF(PARTS(IFIN+1)) WRITE(6,1080) AMACH,CLA(I),XAC(I),CLM(I)
         ELSE
            CRIT=.TRUE.
            IC=I
            CLA(I)=CLAT/SQRT(1.-MCC**2)
            XAC(I)=0.25
         ENDIF
 1100 CONTINUE
         IF(CRIT.AND.PARTS(IFIN+1)) WRITE(6,1090) MCC,XTMAX,CLA(IC)
C
 1200 CONTINUE
C
         ALPHAO=ALO
         ALPHAI=AI
         CLIDL=CLI
      RETURN
C
 1060 FORMAT(4X,23X,'FIN SET ',I1,' SECTION AERODYNAMICS',/)
 1070 FORMAT(
     1 4X,15X,'     IDEAL ANGLE OF ATTACK =',F10.4,' DEG.',/,
     1 4X,15X,' ZERO LIFT ANGLE OF ATTACK =',F10.4,' DEG.',/,
     2 4X,15X,'    IDEAL LIFT COEFFICIENT =',F10.4,/,
     3 4X,4X,'ZERO LIFT PITCHING MOMENT COEFFICIENT =',F10.4,/,
     4 4X,15X,'MACH ZERO LIFT-CURVE-SLOPE =',F10.4,' /DEG.',/,
     5 4X,15X,'       LEADING EDGE RADIUS =',F10.4,' FRACTION CHORD',/,
     6 4X,15X,' MAXIMUM AIRFOIL THICKNESS =',F10.4,' FRACTION CHORD',/,
     7 4X,15X,'                   DELTA-Y =',F10.4,' PERCENT CHORD',//)
 1080 FORMAT(4X,'MACH =',F6.3,3X,'CL-ALPHA =',F7.4,
     1   ' /DEG.',3X,'XAC =',F7.4,3X,'CL MAX =',F7.4,/)
 1090 FORMAT(4X,15X,'*** CREST CRITICAL MACH NUMBER EXCEEDED ***',//,
     1 4X,15X,'        CREST CRITICAL MACH =',F10.4,/,
     2 4X,15X,'                   LOCATION =',F10.4,' FRACTION CHORD',/,
     3 4X,15X,'           LIFT-CURVE-SLOPE =',F10.4,' /DEG.')
      END
