      SUBROUTINE FCNAA(MACH, AR, DELY, SLE, STE, TAPER, ALPHA, CNA,
     1                  B, SECCLA, SPLAN, SREF, SCLM, CNAA)
C ***
C *** COMPUTES NON-LINEAR NORMAL FORCE PER SINE(ALPHA)**2
C ***
C *** REFERENCE   DATCOM SECTION 4.1.3.3
C ***
C *** LIMITATIONS   ALPHA .LT. 90 DEG
C ***               AR .LE. 4.0/((C1+1)*COS(SLE))
C ***               STRAIGHT TAPERED FINS
C ***               NO CAMBER
C ***
C *** INPUT   MACH    MACH NUMBER
C ***         AR      EXPOSED FIN ASPECT RATIO FOR TWO PANELS
C ***         DELY    DIFFERENCE IN AIRFOIL ORDINATE BETWEEN
C ***                 .15% AND 6% CHORD,PER CENT CHORD LENGTH
C ***         SLE     LEADING EDGE SWEEP ANGLE, DEG
C ***         STE     TRAILING EDGE SWEEP ANGLE, DEG
C ***         TAPER   EXPOSED FIN TAPER RATIO
C ***         ALPHA   ANGLE-OF-ATTACK, DEG
C ***         CNA     LINEAR CN, CN PER ALPHA AT ALPHA = 0, 1/DEG,
C ***                 REFERENCED TO SREF
C ***         B       FIN EXPOSED SPAN, TWO PANELS
C ***         SECCLA  SECTION LIFT CURVE SLOPE
C ***         SPLAN   EXPOSED PLANFORM AREA OF TWO PANELS
C ***         SREF    REFERENCE AREA
C ***         SCLM    AIRFOIL SECTION MAX LIFT
C ***
C *** OUTPUT  CNAA    CN PER SIN(ALPHA)**2, REFERENCED TO SREF
C ***
C ***
      REAL MACH
C
C ***
C *** SUBSONIC   (FCNAAS)  MACH .LE. 0.60
C *** TRANSONIC  (FCNAAT)  0.60 .LT. MACH .LT. 1.40
C *** SUPERSONIC (FCNAAH)  MACH .GE. 1.40
C ***
      CNAW = CNA*SREF/SPLAN
C ***
      SWP=SLE
      IF(SLE .LT. 0.)SWP=-STE
C ***
      IF(MACH .LE. 0.60)
     * CALL FCNAAS(MACH, AR, DELY, SWP, TAPER, ALPHA, CNAW, SCLM, CNAA)
C ***
      IF((MACH .GT. 0.60) .AND. (MACH .LT. 1.40))
     * CALL FCNAAT(MACH, AR, DELY, SWP, TAPER, ALPHA, CNAW, B, SECCLA,
     *             SCLM, CNAA)
C ***
      IF(MACH .GE. 1.40)
     * CALL FCNAAH(MACH, DELY, SWP, ALPHA, CNAW, CNAA)
C ***
      CNAA = CNAA*SPLAN/SREF
C ***
      RETURN
      END
