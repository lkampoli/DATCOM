      SUBROUTINE SUPBOD(IM)
C
C***  TOP LEVEL EXECUTIVE FOR BODY ALONE AT SUPERSONIC SPEEDS
C
C  MODIFIED BY W. BLAKE, WL/FIGC
C
C***  IM IS THE CURRENT MACH NUMBER INDEX
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     &                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
C
      COMMON /TOTALC/ BALPHA(20),BBETA(20),BPHI(20),ALPTOT(20)
C
      COMMON /REFQN/ SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,
     1                RHR
C
      COMMON /ABODIN/ NX,XO,X(50),R(50),TNOSE,LNOSE,DNOSE,BNOSE,
     &                TRUNC,LCENTR,DCENTR,TAFT,LAFT,DAFT,POWER,
     &                DISCON(20),ELLDAT(103),DEXIT,BASE,BETAN,
     &                JMACH(20),PRAT(20),TRAT(20),PROTUB,ADUM(581)
C
      COMMON /GEOBOD/ SPNOSE,SWNOSE,VOLNOS,XCVNOS,XCPNOS,FRNOSE,
     &  THEOLN,THEOFN,SPCENT,SWCENT,VOLCEN,XCVCEN,XCPCEN,FRCENT,
     &  THEOLC,THEOFC,SPAFT ,SWAFT ,VOLAFT,XCVAFT,XCPAFT,FRAFT ,
     &  THEOLA,THEOFA,SPLAN ,SWET  ,VOL   ,XCENTV,XCENTP,FR    ,
     &                DBASE ,SBASE ,DMAX  ,SMAX  ,LTOTAL,GDUM(12)
C
      COMMON /SBODY/ CNB(20),CMB(20),CAB(20),CYB(20),CLNB(20),CLLB(20),
     &               CNAB(20),CMAB(20),CYBB(20),CNBB(20),CLBB(20)
C
      COMMON /BDWORK/  CNP(20),CMP(20),CNVIS(20),CMVIS(20),CAPR(20),
     &                 CAF(20),CABASE(20),ETA,CDC(20),CAPROT(20),
     &                 BOTDCA(20),BOTDCN(20),BOTDCM(20)
C
      COMMON /PARTF/ PART(19)
      LOGICAL PART
C
      COMMON /FSET1/ FIN1(399)
      COMMON /FSET2/ FIN2(399)
      COMMON /FSET3/ FIN3(399)
      COMMON /FSET4/ FIN4(399)
C
      REAL NALPHA,NMACH,MACH
      REAL LREF,LATREF
      REAL NX,LNOSE,LCENTR,LAFT
      REAL LTOTAL
      REAL FIN(4,399),JMACH
C
      LOGICAL TRUNC,BASE,NA,NM,ND,PROTUB
      LOGICAL IWARN1,IWARN2,IWARN3,IWARN4,IWARN5,
     &        IWARN6,IWARN7,IWARN8,IWARN9,IWAR10,
     &        IWAR11,IWAR12,IWAR13,IWAR14,IWARN
C
      DIMENSION PHIFIN(8)
      DIMENSION CPBASE(20),CABAS(20),PBASE(20),TBASE(20),
     &          XSEP(8),THSEP(8),SEPX(20,8),SEPTH(20,8)
      DIMENSION CAOPI(11),CAOM(11)
C
C***  CAO FOR BLUNT NOSED BODIES, FROM HOERNER
C***  REFERENCE AFFDL-TR-76-55, MARCH 1979, P. 84
C
      DATA CAOM / 0.,.6,.8,1.,1.2,1.4,1.6,1.8,2.2,2.6,3.0/
      DATA CAOPI/ .86,.86,.96,1.21,1.39,1.53,1.62,4*1.68/
C
C***  SET UP FIN ARRAY
      DO 20 I=1,4
        DO 10 J=1,399
          IF (I.EQ.1) FIN(I,J)=FIN1(J)
          IF (I.EQ.2) FIN(I,J)=FIN2(J)
          IF (I.EQ.3) FIN(I,J)=FIN3(J)
          IF (I.EQ.4) FIN(I,J)=FIN4(J)
 10     CONTINUE
 20   CONTINUE
C
C***  INITIALIZE WARNING FLAGS
      IWARN1=.FALSE.
      IWARN2=.FALSE.
      IWARN3=.FALSE.
      IWARN4=.FALSE.
      IWARN5=.FALSE.
      IWARN6=.FALSE.
      IWARN7=.FALSE.
      IWARN8=.FALSE.
      IWARN9=.FALSE.
      IWAR10=.FALSE.
      IWAR11=.FALSE.
      IWAR12=.FALSE.
      IWAR13=.FALSE.
      IWAR14=.FALSE.
      IWARN=.FALSE.
C
C***  CROSS FLOW DRAG PROPORTIONALITY FACTOR IS UNITY
C
      ETA=1.
C
C***  OBTAIN THE POTENTIAL AERODYNAMIC CHARACTERISTICS
C
      CALL SUPPOT(IM,CAP,CNALP,CMALP,CAAA)
C
      CALL CAFRIC(0.,MACH(IM),REN(IM),LTOTAL,ROUGH,BLAYER,SWET,SREF,
     1            1.1547,1.022,CAFO)
C
C*** PROTUBERANCE DRAG CALCULATION AT ZERO AOA
      CPRTZ=0.0
      IF (PROTUB) THEN
        IF (PART(6)) THEN
          CALL HEADR9
          WRITE(6,1230)
          CALL PRFLC9(IM,NA,NM,ND)
        ENDIF
        CALL CDPROT(MACH(IM),REN(IM),SREF,CPRTZ)
      END IF
C
C***  ANGLE OF ATTACK LOOP
C
      ALPLAS=0.
      NALP=NALPHA+0.5
      DFWD=DCENTR
      IF(LCENTR .LE. UNUSED)DFWD=DNOSE
C
C***  SET AXIAL LOCATION AT START OF BOATTAIL
      XB=LNOSE+LCENTR
C
C***     FIN LOCATIONS NEAR THE BASE
      NFIN=0
      DO 40 ISET=1,4
        IF ((FIN(ISET,102)+FIN(ISET,52)).GT.XB) THEN
          NFIN=INT(FIN(ISET,112)+0.5)
          DO 30 IFIN=1,NFIN
            PHIFIN(IFIN)=FIN(ISET,381+IFIN)
 30       CONTINUE
        END IF
 40   CONTINUE
C
      IF (BASE.AND.PART(6)) THEN
C
C***    OUTPUT HEADER
      CALL HEADR9
        WRITE(6,1000)
        CALL PRFLC9(IM,NA,NM,ND)
      END IF
C
      IAOA1=20
      IAOA2=0
      IERR=0
C
C***     BASE DRAG AT ZERO ANGLE OF ATTACK
C
         CALL BDCAB(DFWD,DBASE,DEXIT,LAFT,SREF,MACH(IM),CABASZ)
C
      DO 70 I=1,NALP
C
C***     FRICTION DRAG AT ANGLE OF ATTACK
C
C  REV 3/99  EXTRANEOUS FRICTION DRAG CALCULATION DELETED
C        CALL CAFRIC(ALPTOT(I),MACH(IM),REN(IM),LTOTAL,ROUGH,
C    1               BLAYER,SWET,SREF,1.1547,1.022,CAF(I))
C
C***     INVISCID CN AND CM
C
         CALL BDCNP(CNALP,ALPTOT(I),CNP(I))
         CALL BDCMP(CMALP,ALPTOT(I),CMP(I))
C
C***     VISCOUS CROSS FLOW DRAG COEFFICIENT
C
         CALL CDCS(ABS(ALPTOT(I)),REN(IM),DMAX,MACH(IM),CDC(I))
C
C***     VISCOUS NORMAL FORCE AND PITCHING MOMENT
C
         CALL BDCNV(ALPTOT(I),CDC(I),ETA,SPLAN,SREF,CNVIS(I))
         CALL BDCMV(ALPTOT(I),CDC(I),ETA,SPLAN,XCG-XO,XCENTP,SREF,
     1              LREF,CMVIS(I))
C
C***     BOATTAIL-JET PLUME INTERACTION, FLOW SEPARATION EFFECTS 
C
         BOTDCM(I)=0.0
         BOTDCN(I)=0.0
         BOTDCA(I)=0.0
C
C***     DETERMINE IF BASPRS IS TO BE USED
         IF (BASE) THEN
C
C***     CHECK VALIDITY OF INPUT CONDITIONS
         IF (IERR.EQ.1) GOTO 60
         IF (MACH(IM).LT.2.0) THEN
           IWARN1=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (MACH(IM).GT.5.0) THEN
           IWARN2=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (ALPTOT(I).LT.0.0) THEN
           IWARN3=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (ALPTOT(I).GT.8.0) THEN
           IWARN4=.TRUE.
           IWARN=.TRUE.
         END IF
         IF ((PRAT(IM).LT.0.).OR.(PRAT(IM).GT.10.)) THEN
           IWARN5=.TRUE.
           IWARN=.TRUE.
         END IF
         IF ((TRAT(IM).LT.4.).OR.(TRAT(IM).GT.10.)) THEN
           IWARN6=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (JMACH(IM).LT.(MACH(IM)-1.)) THEN
           IWARN7=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (JMACH(IM).GT.(MACH(IM)+1.)) THEN
           IWARN8=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (BETAN.LT.5.) THEN
           IWARN9=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (BETAN.GT.25.) THEN
           IWAR10=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (LAFT/DFWD.GT.2.0) THEN
           IWAR11=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (LAFT.LE.UNUSED) THEN
           IF (ATAN((DFWD-DAFT)/LAFT)*RAD.GT.12.0) THEN
             IWAR12=.TRUE.
             IWARN=.TRUE.
           END IF
         END IF           
         IF (DAFT.GE.DFWD) THEN
           IF (PART(6)) WRITE(6,1130)
           IERR=1
           GOTO 60
         END IF
         IF (DEXIT.LE.UNUSED) THEN
           IF (PART(6)) WRITE(6,1140)
           IERR=1
           GOTO 60
         END IF
C
C***     DETERMINE BOATTAIL SHAPE OR CYLINDER SHAPE
         IF (LAFT.LE.UNUSED) THEN
           ISHAPE=1
           BASDIA=DFWD
         ELSE
           ISHAPE=INT(TAFT+2.5)
           BASDIA=DAFT
         END IF
         IF (DEXIT/BASDIA.LT.0.8) THEN
           IWAR13=.TRUE.
           IWARN=.TRUE.
         END IF
         IF (DEXIT/BASDIA.GT.0.95) THEN
           IWAR14=.TRUE.
           IWARN=.TRUE.
         END IF
         IF ((ISHAPE.NE.1).AND.(ISHAPE.NE.2).AND.(ISHAPE.NE.3)) THEN
           IF (PART(6)) WRITE(6,1210)
           IERR=1
           GOTO 60
         END IF
C
         IF (I.LT.IAOA1) IAOA1=I
         IF (I.GT.IAOA2) IAOA2=I
         CALL BASPRS(ISHAPE,XO,LAFT,DFWD,BASDIA,DEXIT,PRAT(IM),
     &        MACH(IM),JMACH(IM),BETAN,TRAT(IM),ALPTOT(I),XB,REN(IM),
     &        NFIN,PHIFIN,CPBASE(I),CABAS(I),PBASE(I),TBASE(I),
     &        XSEP,THSEP,BOTDCN(I),BOTDCM(I),BOTDCA(I))
C
         DO 50 IFIN=1,NFIN
           SEPX(I,IFIN)=XSEP(IFIN)
           SEPTH(I,IFIN)=THSEP(IFIN)
 50      CONTINUE
         END IF
C
 60      CONTINUE
C
C***     TOTAL FORCES AND MOMENT
C
C  REV 1/96  AXIAL FORCE SUMMATION MODIFIED FOR HIGH AOA
C
         ABSALP=ABS(ALPTOT(I))
         CAF(I)=CAFO*COS(ALPTOT(I)/RAD)*ABS(COS(ALPTOT(I)/RAD))
         CABASE(I)=CABASZ*COS(ALPTOT(I)/RAD)
         CAPROT(I)=CPRTZ*COS(ALPTOT(I)/RAD)*ABS(COS(ALPTOT(I)/RAD))
         CAPR(I)=CAP+CAAA*SIN(ALPTOT(I)/RAD)**2
C
C***  HIGH ANGLE OF ATTACK CORRECTION TO REMOVE PREDICTION DISCONTINUITY
C
         CAPR(I)=CAPR(I)*SQRT(ABS(COS(ALPTOT(I)/RAD)))
         CAB(I)=CAPR(I)+CAF(I)+BOTDCA(I)+CAPROT(I)
         IF(DEXIT .NE. UNUSED)CAB(I)=CAB(I)+CABASE(I)
C
C***  FOR ALPHAS GREATER THAN 90 DEGREES USE JORGENSEN
C***  BUT USE CAO FROM BLUNT-NOSED BODY DATA
C     
C  REV 1/96  CODE ADDED FOR FORWARD FACING BASE IF ABS(AOA) > 90
C***  ESTIMATE CAO AT ALPHA=180 DEGREES
C
         CALL LNTRP(CAOM,CAOPI,11,MACH(IM),CAPI)
         CAPI=CAPI*PI*DMAX**2/4./SREF
C
         IF(ABSALP.GE.90.) CAB(I)=CAPI*COS(ALPTOT(I)/RAD)*
     1                                 ABS(COS(ALPTOT(I)/RAD))
C   
C  REV 1/96   ALPHA > 90 AXIAL FORCE CHARGED TO BASE DRAG
C
         IF(ABSALP.GE.90.) CABASE(I)=CAB(I)-CAF(I)-CAPR(I)-
     1                               CAPROT(I)-BOTDCA(I)
C
         CNB(I)=(CNP(I)+CNVIS(I)+BOTDCN(I))*COS(BPHI(I)/RAD)
         CYB(I)=-(CNP(I)+CNVIS(I))*SIN(BPHI(I)/RAD)
         CMB(I)=(CMP(I)+CMVIS(I)+BOTDCM(I))*COS(BPHI(I)/RAD)
         CLNB(I)=-(CMP(I)+CMVIS(I))*SIN(BPHI(I)/RAD)*LREF/LATREF
C
 70   CONTINUE
C
      IF ((BASE).AND.(IERR.NE.1).AND.PART(6)) THEN
C***  OUTPUT BOATTAIL-JET PLUME INTERACTION FLOW PARAMETERS
C
C     WRITE WARNING MESSAGES
      IF (IWARN) WRITE(6,1220)
      IF (IWARN1) WRITE(6,1090)
      IF (IWARN2) WRITE(6,1100)
      IF (IWARN3) WRITE(6,1110)
      IF (IWARN4) WRITE(6,1120)
      IF (IWARN5) WRITE(6,1150)
      IF (IWARN6) WRITE(6,1160)
      IF (IWARN7) WRITE(6,1170)
      IF (IWARN8) WRITE(6,1180)
      IF (IWARN9) WRITE(6,1190)
      IF (IWAR10) WRITE(6,1200)
      IF (IWAR11) WRITE(6,1240)
      IF (IWAR12) WRITE(6,1250)
      IF (IWAR13) WRITE(6,1260)
      IF (IWAR14) WRITE(6,1270)
C
      WRITE(6,1010)
      DO 80 I=IAOA1,IAOA2
        WRITE(6,1020) ALPTOT(I),CPBASE(I),CABAS(I),TBASE(I),PBASE(I),
     &                BOTDCN(I),BOTDCM(I),BOTDCA(I)
 80   CONTINUE
C
C***  OUTPUT SEPARATION PARAMETER DATA IF NECESSARY
      IF (NFIN .GT. 0) THEN
      CALL HEADR9
        WRITE(6,1000)
        CALL PRFLC9(IM,NA,NM,ND)
        WRITE(6,1030)
      IF (NFIN .EQ. 1) THEN
        WRITE(6,1040) (IFIN,PHIFIN(IFIN),IFIN=1,1)
        DO 90 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),SEPX(I,1),SEPTH(I,1)
 90     CONTINUE
      ELSEIF (NFIN .EQ. 2) THEN
        WRITE(6,1060) (IFIN,PHIFIN(IFIN),IFIN=1,2)
        DO 100 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=1,2)
 100    CONTINUE
      ELSEIF (NFIN .EQ. 3) THEN
        WRITE(6,1070) (IFIN,PHIFIN(IFIN),IFIN=1,3)
        DO 110 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=1,3)
 110    CONTINUE
      ELSEIF (NFIN .EQ. 4) THEN
        WRITE(6,1080) (IFIN,PHIFIN(IFIN),IFIN=1,4)
        DO 120 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=1,4)
 120    CONTINUE
      ELSEIF (NFIN .EQ. 5) THEN
        WRITE(6,1080) (IFIN,PHIFIN(IFIN),IFIN=1,4)
        DO 130 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=1,4)
 130    CONTINUE
        WRITE(6,1040) (IFIN,PHIFIN(IFIN),IFIN=5,5)
        DO 140 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),SEPX(I,5),SEPTH(I,5)
 140    CONTINUE
      ELSEIF (NFIN .EQ. 6) THEN
        WRITE(6,1080) (IFIN,PHIFIN(IFIN),IFIN=1,4)
        DO 150 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=1,4)
 150    CONTINUE
        WRITE(6,1060) (IFIN,PHIFIN(IFIN),IFIN=5,6)
        DO 160 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=5,6)
 160    CONTINUE
      ELSEIF (NFIN .EQ. 7) THEN
        WRITE(6,1080) (IFIN,PHIFIN(IFIN),IFIN=1,4)
        DO 170 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=1,4)
 170    CONTINUE
        WRITE(6,1070) (IFIN,PHIFIN(IFIN),IFIN=5,7)
        DO 180 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=5,7)
 180    CONTINUE
      ELSE
        WRITE(6,1080) (IFIN,PHIFIN(IFIN),IFIN=1,4)
        DO 190 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=1,4)
 190    CONTINUE
        WRITE(6,1080) (IFIN,PHIFIN(IFIN),IFIN=5,8)
        DO 200 I=IAOA1,IAOA2
          WRITE(6,1050) ALPTOT(I),(SEPX(I,IFIN),SEPTH(I,IFIN),IFIN=5,8)
 200    CONTINUE
C
      END IF
      END IF
      END IF
C
C***  FORMAT STATEMENTS
 1000 FORMAT(33X,'BASE-JET PLUME INTERACTION FLOW PARAMETERS',//)
 1010 FORMAT(//,15X,'----- BASE FLOW PARAMETERS -----',4X,
     &       '---- INCREMENTAL  DATA ----',/
     &       5X,'ALPHA',5X,'CP-BASE',2X,'CA-BASE',2X,'TB/TINF',2X,
     &       'PB/PINF',4X,'DEL CN',3X,'DEL CM',3X,'DEL CA',/)      
 1020 FORMAT(4X,F6.2,2X,4(F9.4),2X,3(F9.4))
 1030 FORMAT(10X,'--------- BOATTAIL SEPARATION PARAMETERS ',
     &              '---------')
 1040 FORMAT(/,12X,1('PANEL',I2,' PHI=',F5.1,4X),/,13X,1('X-SEP',2X,
     &       'MACH CONE',5X),/,' ALPHA',8X,1('(FT)',4X,'ANGLE',8X),/) 
 1050 FORMAT(F6.2,4(5X,F7.3,2X,F7.3))
 1060 FORMAT(/,12X,2('PANEL',I2,' PHI=',F5.1,4X),/,13X,2('X-SEP',2X,
     &       'MACH CONE',5X),/,' ALPHA',8X,2('(FT)',4X,'ANGLE',8X),/) 
 1070 FORMAT(/,12X,3('PANEL',I2,' PHI=',F5.1,4X),/,13X,3('X-SEP',2X,
     &       'MACH CONE',5X),/,' ALPHA',8X,3('(FT)',4X,'ANGLE',8X),/) 
 1080 FORMAT(/,12X,4('PANEL',I2,' PHI=',F5.1,4X),/,13X,4('X-SEP',2X,
     &       'MACH CONE',5X),/,' ALPHA',8X,4('(FT)',4X,'ANGLE',8X),/)
 1090 FORMAT(10X,'* FREESTREAM MACH NUMBER LESS THAN 2.0')
 1100 FORMAT(10X,'* FREESTREAM MACH NUMBER GREATER THAN 5.0')
 1110 FORMAT(10X,'* ANGLE OF ATTACK LESS THAN 0.0')
 1120 FORMAT(10X,'* ANGLE OF ATTACK GREATER THAN 8.0')
 1130 FORMAT(//,10X,'BASE-JET PLUME INTERACTION CANNOT BE CALCULATED',
     &       /,10X,'AFT BODY IS A FLARE')
 1140 FORMAT(//,10X,'BASE-JET PLUME INTERACTION CANNOT BE CALCULATED',
     &       /,10X,'JET EXIT DIAMETER IS NOT SPECIFIED')
 1150 FORMAT(10X,'* STATIC JET TO FREESTREAM PRESSURE RATIO OUTSIDE',
     &           ' THE 0 TO 10 RANGE')
 1160 FORMAT(10X,'* STAGNATION JET TO FREESTREAM TEMPERATURE RATIO',
     &       ' OUTSIDE THE 4 TO 10 RANGE')
 1170 FORMAT(10X,'* JET MACH NUMBER LESS THAN THE FREESTREAM',
     &           ' MACH NUMBER - 1.0') 
 1180 FORMAT(10X,'* JET MACH NUMBER GREATER THAN THE FREESTREAM',
     &           ' MACH NUMBER + 1.0')
 1190 FORMAT(10X,'* JET NOZZLE EXIT ANGLE LESS THAN 5.0')
 1200 FORMAT(10X,'* JET NOZZLE EXIT ANGLE GREATER THAN 25.0')
 1210 FORMAT(//,10X,'BASE-JET PLUME INTERACTION CANNOT BE CALCULATED',
     &       /,10X,'AFT BODY IS NOT CYLINDRICAL, CONICAL, OR',
     &      ' A TANGENT OGIVE')
 1220 FORMAT(/,10X,'WARNING EXTRAPOLATION WILL BE REQUIRED FOR THE ',
     &       'FOLLOWING CONDITIONS:',/) 
 1230 FORMAT(18X,'PROTUBERANCE OUTPUT',/)
 1240 FORMAT(10X,'* BOATTAIL FINENESS RATIO GREATER THAN 2.0')
 1250 FORMAT(10X,'* BOATTAIL TERMINAL ANGLE GREATER THAN 12.0')
 1260 FORMAT(10X,'* NOZZLE EXIT TO BASE DIAMETER RATIO ',
     &              'LESS THAN 0.8')
 1270 FORMAT(10X,'* NOZZLE EXIT TO BASE DIAMETER RATIO ',
     &              'GREATER THAN 0.95')
C
      RETURN
      END
