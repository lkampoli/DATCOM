      SUBROUTINE FCAWT(M,SEXP,TOC,AR,SWEP25,SREF,CAWT)
C
C***  COMPUTE FIN ALONE TRANSONIC WAVE DRAG INCREMENT
C***  METHOD FROM DATCOM, SECTION 4.1.5.1
C
C***  INPUTS
C
C      M -- MACH NUMBER
C   SEXP -- EXPOSED PANEL AREA
C    TOC -- THICKNESS RATIO T/C
C     AR -- EXPOSED PANEL ASPECT RATIO
C SWEP25 -- PANEL QUARTER CHORD SWEEP ANGLE, DEG.
C   SREF -- REFERENCE AREA
C
C***  OUTPUT
C
C   CAWT -- PRESSURE DRAG COEFFICIENT
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      REAL M
C
      DIMENSION TQ(7),TPX(15),TXKP(105),IROUT(2)
C
C***  TABLE OF ASPECT*(T/C)**(1/3)
C
      DATA TQ/ 0.,.5,1.,1.5,2.,3.,4./
C
C***  TABLE OF BETA/((T/C)**(5/3))
C
      DATA TPX/-1.4,-1.2,-1.0,-.8,-.6,-.4,-.2,
     1          .0,.2,.4,.6,.8,1.0,1.2,1.4/
C
C***  TABLE OF VALUES CAWT/(T/C)**(5/3)
C
      DATA TXKP/
     1 0.00, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00,
     2 0.00, 0.00, 0.00, 0.00, 0.00, 0.00, 0.00,
     3 0.00,  .10,  .20,  .40,  .55,  .70,  .80,  .90,
     4 1.00, 1.10, 1.15, 1.20, 1.25, 1.30, 1.35,
     5 0.00,  .20,  .50, 1.05, 1.70, 2.05, 2.20, 2.35,
     6 2.45, 2.55, 2.65, 2.70, 2.70, 2.70, 2.80,
     7 0.00,  .30, 1.00, 1.95, 2.70, 2.90, 3.00, 3.00,
     8 3.05, 3.10, 3.15, 3.15, 3.15, 3.15, 3.10,
     9 0.00,  .40, 1.30, 2.50, 3.10, 3.25, 3.30, 3.30,
     A 3.30, 3.30, 3.30, 3.30, 3.30, 3.30, 3.25,
     B 0.00,  .50, 1.60, 2.90, 3.40, 3.45, 3.48, 3.50,
     C 3.50, 3.50, 3.48, 3.45, 3.42, 3.40, 3.35,
     D 0.00,  .60, 1.80, 3.20, 3.55, 3.60, 3.60, 3.65,
     E 3.65, 3.65, 3.60, 3.60, 3.57, 3.55, 3.50  /
C
C***  SWEEP ANGLE CORRECTION TO DATCOM CHART
C
      CCO4=COS(SWEP25/RAD)
      XM=M*SQRT(CCO4)
      IF(XM.LT.1.0 .AND. XM.GT.0.99) XM=0.99
      IF(XM.GE.1.0 .AND. XM.LT.1.01) XM=1.01
C
C***  PX IS RESCALED X AXIS OF THE PLOT
C
      PX=SQRT(ABS(XM**2-1.))/TOC**(1./3.)
      PX=PX*(XM**2-1.)/ABS(XM**2-1.)
      Q=AR*TOC**(1./3.)
C
      CAWT=0.
      IF(PX.LT.-1.4)GO TO 1000
C
C***  LOOKUP WAVE DRAG INCREMENT FROM CHART
C
      CALL MVLOOK(1,7,15,105,1.,TQ,TPX,TXKP,PX,Q,1.,XKP)
      CAWT=XKP*TOC**(5./3.)*CCO4**2.5*SEXP/SREF
C
 1000 CONTINUE
C
      RETURN
      END
