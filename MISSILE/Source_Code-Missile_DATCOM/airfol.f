      SUBROUTINE AIRFOL(IFIN,NACA,X,YUN,YLN,THN,CAM,CBAR,RHO,T,DELTAY,
     1    KSHARP,ALPI,ALPO,CLIDL)
C
C***  CALCULATE NACA DESIGNATED AIRFOIL COORDINATES
C
C***  FOLLOWING ARE THE DEFINITIONS OF EACH VARIABLE USED
C
C   NACA -- NACA AIRFOIL CONTROL CARD AS INPUT BY THE USER
C           NACA-X-Y-N
C               X=FIN NUMBER
C               Y=NACA SERIES (1, 4, 5, 6, OR S)
C               N=DESIGNATION
C      X -- X/C STATIONS ALONG AIRFOIL CHORD
C     XU -- X/C STATION OF UPPER SURFACE
C     XL -- X/C STATION OF LOWER SURFACE
C    YUN -- Y/C OF SECTION AT XU
C    YLN -- Y/C OF SECTION AT XL
C    YUU -- Y/C OF SUPERSONIC AIRFOILS, UPPER SURFACE
C    YLL -- Y/C OF SUPERSONIC AIRFOILS, LOWER SURFACE
C    THN -- THICKNESS/C AT X
C    CAM -- CAMBER/C AT X
C      L -- NUMBER OF AIRFOIL POINTS
C  I-LLL -- DECODED AIRFOIL DESIGNATION
C ALPHAI -- IDEAL ANGLE OF ATTACK
C ALPHAO -- ZERO-LIFT ANGLE OF ATTACK
C    AII -- IDEAL LIFT COEFFICIENT, FROM NACA DESIGNATION
C   CBAR -- MEAN GEOMETRIC CHORD OF PANEL
C    RHO -- LEADING EDGE RADIUS
C      T -- MAXIMUM AIRFOIL THICKNESS, FROM NACA DESIGNATION
C KSHARP -- SHAPE FACTOR FOR SUPERSONIC AIRFOILS
C   XOVC -- MAXIMUM THICKNESS CHORD STATION
C
      COMMON /SECVAR/ XU(50),XL(50),YU(50),YL(50),YUU(50),YLL(50),
     1                L,I,J,K,II,JJ,KK,III,JJJ,KKK,LLL,
     2                ALPHAI,ALPHAO,AII,XOVC
      COMMON /PARTF/  PARTS(19)
      LOGICAL PARTS,PARTT
      DIMENSION THN2(50),NACA(80),IUM(10),X(50),YUN(50),YLN(50),
     1 THN(50),CAM(50)
      REAL KSHARP
      CHARACTER*4 NACA
      EQUIVALENCE (IUM(1),I)
C
      PARTT=PARTS(IFIN+1)
      KSHARP=16./3.
      ALPHAI=0.
      ALPHAO=0.
      AII=0.
      IF(PARTT)CALL HEADR9
      IF(PARTT)WRITE(6,1230)IFIN
C
C  DECODE THE USERS INPUT NACA DESIGNATION
C
      CALL DECODE(NACA,NA,X,L,IUM)
C
C  CALCULATE THE AIRFOIL COORDINATES
C
      IF(NA .LT. 1 .OR. NA .GT. 7)WRITE(6,1240)IFIN,(NACA(I),I=1,80)
      IF(NA .LT. 1 .OR. NA .GT. 7)STOP
C
      GO TO (1000,1010,1020,1030,1040,1050,1060), NA
C
C NACA 4 SERIES
C
 1000 IF(PARTT)WRITE(6,1100)I,J,K,II
      CALL COORD4(X,XU,XL,YUN,YLN,L,I,J,K,II,JJ,KK,III,JJJ,
     1 THN,CAM,RHO,T)
      GO TO 1080
C
C NACA 4 MODIFIED SERIES
C
 1010 IF(PARTT)WRITE(6,1110)I,J,K,II,KK,III
      CALL CORD4M(X,XU,XL,YUN,YLN,L,I,J,K,II,JJ,KK,III,JJJ,
     1 THN,CAM,RHO,T)
      GO TO 1080
C
C NACA 5 SERIES
C
 1020 IF(PARTT)WRITE(6,1120)I,J,K,II,JJ
      CALL COORD5(X,XU,XL,YUN,YLN,L,I,J,K,II,JJ,KK,III,JJJ,
     1 THN,CAM,RHO,T)
      GO TO 1080
C
C NACA 5 MODIFIED SERIES
C
 1030 IF(PARTT)WRITE(6,1130)I,J,K,II,JJ,III,JJJ
      CALL CORD5M(X,XU,XL,YUN,YLN,L,I,J,K,II,JJ,KK,III,JJJ,
     1 THN,CAM,RHO,T)
      GO TO 1080
C
C NACA 1 SERIES
C
 1040 IF(PARTT)WRITE(6,1140)I,J,II,JJ,KK
      CALL COORD1(X,XU,XL,YUN,YLN,L,I,J,K,II,JJ,KK,III,JJJ,
     1 THN,CAM,ALPHAI,ALPHAO,AII,RHO,T)
      GO TO 1080
C
C NACA 6 SERIES
C
 1050 IF(K.EQ.0.AND.II.EQ.0.AND.PARTT)WRITE(6,1150)I,J,JJ,KK,III
      IF(K.NE.0.AND.II.EQ.0.AND.PARTT)WRITE(6,1160)I,J,K,JJ,KK,III
      IF(K.EQ.0.AND.II.NE.0.AND.PARTT)WRITE(6,1170)I,J,JJ,KK,III
      IF(K.NE.0.AND.II.NE.0.AND.PARTT)WRITE(6,1180)I,J,K,JJ,KK,III
      IF(JJJ.GE.1.AND.PARTT)WRITE(6,1190)JJJ
      CALL COORD6(X,XU,XL,YUN,YLN,L,I,J,K,II,JJ,KK,III,JJJ,
     1 THN,CAM,ALPHAI,ALPHAO,AII,RHO,T)
      GO TO 1080
C
C SUPERSONIC SERIES
C
 1060 IF(I.NE.2)GO TO 1070
      J=5
      K=0
      II=0
 1070 IF(PARTT)WRITE(6,1220)I,J,K,II,JJ,KK,III,JJJ,KKK,LLL
      CALL CORDSP(X,XU,XL,YUU,YLL,YU,YL,L,I,J,K,II,JJ,KK,III,JJJ,
     1 KKK,LLL,T,XOVC,KSHARP)
      CALL SUPXY(THN,CAM,X,XU,XL,YU,YL,YUN,YLN,L)
 1080 ALPI=ALPHAI
      ALPO=ALPHAO
      CLIDL=AII
      IF(PARTT)WRITE(6,1200)
C
C SET TOTAL AIRFOIL THICKNESS FOR OUTPUT
C
      DO 1090 M=1,L
      THN2(M)=2.*THN(M)
 1090 CONTINUE
C
C PRINT AIRFOIL COORDINATES
C
      IF(PARTT)WRITE(6,1210)(X(M),XU(M),YUN(M),XL(M),YLN(M),CAM(M),
     1 THN2(M),M=1,L)
C
C***  COMPUTE DELTA-Y
C
      CALL LNTRP(X,THN,L,.0015,YU15)
      CALL LNTRP(X,THN,L,.0600,YU60)
      DELTAY=(YU60-YU15)*100.
C
      RHO=RHO*CBAR
C
 1100 FORMAT(34X,'NACA ',4I1)
 1110 FORMAT(34X,'NACA ',4I1,' - ',2I1)
 1120 FORMAT(34X,'NACA ',5I1)
 1130 FORMAT(34X,'NACA ',5I1,' - ',2I1)
 1140 FORMAT(34X,'NACA ',2I1,' - ',3I1)
 1150 FORMAT(34X,'NACA ',2I1,'-',3I1)
 1160 FORMAT(34X,'NACA ',2I1,',',I1,'-',3I1)
 1170 FORMAT(34X,'NACA ',2I1,'A',3I1)
 1180 FORMAT(34X,'NACA ',2I1,',',I1,'A',3I1)
 1190 FORMAT(34X,'A=0.',I1)
 1200 FORMAT(/,4X,5X,'X/C',5X,'X-UPPER',3X,'Y-UPPER',3X,
     1  'X-LOWER',3X,'Y-LOWER',2X,'MEAN LINE',2X,'THICKNESS',/)
 1210 FORMAT(4X,7F10.5)
 1220 FORMAT(34X,'NACA S-',I1,'-',2I1,'.',I1,'-',2I1,'.',I1,
     1 '-',2I1,'.',I1)
 1230 FORMAT(4X,21X,'FIN SET NUMBER',I2,' AIRFOIL SECTION',/)
 1240 FORMAT(4X,'LIFTING SURFACE DESIGNATION INCORRECT',
     1 ' FOR FIN',I2,'.   EXECUTION ABORTED.',/,
     2 4X,'DESIGNATION IS ',80A1)
C
      RETURN
      END
