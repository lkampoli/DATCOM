      SUBROUTINE USERS
C
C***  SUBROUTINE TO COMPUTE GEOMETRY CHARACTERISTICS FOR USER
C***  DEFINED SHAPES
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /ABODIN/ NX,XO,X(50),R(50),TNOSE,LNOSE,DNOSE,BNOSE,
     1                TRUNC,LCENTR,DCENTR,TAFT,LAFT,DAFT,
     2                POWER,DISCON(20),ADUM(748)
      COMMON /XRBLNT/ XTEMP(50),RTEMP(50),NXTEMP,VNTEMP,SWTEMP,ITYPE
      COMMON /GEOBOD/ SPNOSE,SWNOSE,VOLNOS,XCVNOS,XCPNOS,FRNOSE,
     1  THEOLN,THEOFN,SPCENT,SWCENT,VOLCEN,XCVCEN,XCPCEN,FRCENT,
     2  THEOLC,THEOFC,SPAFT ,SWAFT ,VOLAFT,XCVAFT,XCPAFT,FRAFT ,
     3  THEOLA,THEOFA,SPLAN ,SWET  ,VOL   ,XCENTV,XCENTP,FR    ,
     4                DBASE ,SBASE ,DMAX  ,SMAX  ,LTOTAL,GDUM(12)
      COMMON /THERY/ LSOSE,PRESUR,LHYBRD,LHYPER
C
      DIMENSION XX(50),RR(50),DUM(8),DUMMY(20),IROUT(2)
      REAL NXTEMP
      REAL NX,LNOSE,LCENTR,LAFT,LTOTAL
      LOGICAL LSOSE,TRUNC
C
      LSOSE=.TRUE.
C
      NNX=NX+0.5
      DO 1000 I=1,NNX
         X(I)=X(I)-XO
         XX(I)=X(I)
         RR(I)=R(I)
 1000 CONTINUE
C
C***  TOTAL BODY GEOMETRY CHARACTERISTICS
C
      LTOTAL=X(NNX)
C
      SPLAN=DSPLAN(NNX,X,R)
      SWET=DSWET(NNX,X,R)
      VOL=DVOL(NNX,X,R)
      XCENTV=DXCENV(NNX,X,R)
      XCENTP=DXCENP(NNX,X,R)
C
      INOSE=0
      ICENTR=0
      IAFT=0
C
C***  INSERT COORDINATES IF REQUIRED
C
      IF(LNOSE .LE. UNUSED)GO TO 1050
C
      CALL LNTRP(XX,RR,NNX,LNOSE,RNOSE)
C
      INSERT=0
      DO 1010 I=1,NNX
         IF(ABS(LNOSE - XX(I)) .LT. 0.00001)INOSE=I
         IF(ABS(LNOSE - XX(I)) .LT. 0.00001)GO TO 1050
         IF(LNOSE .LT. XX(I))INSERT=I
         IF(LNOSE .LT. XX(I))GO TO 1020
 1010 CONTINUE
C
      IF(INSERT .EQ. 0)GO TO 1050
C
 1020 INOSE=INSERT
      DO 1030 I=INSERT,NNX
         XX(NNX+1-I+INSERT)=XX(NNX-I+INSERT)
         RR(NNX+1-I+INSERT)=RR(NNX-I+INSERT)
 1030 CONTINUE
      XX(INSERT)=LNOSE
      RR(INSERT)=RNOSE
      IF(DNOSE .LE. UNUSED)DNOSE=2.*RNOSE
      NNX=NNX+1
      DO 1040 I=1,20
         IF(DISCON(I) .GE. FLOAT(INSERT))DISCON(I)=DISCON(I)+1.
 1040 CONTINUE
C
 1050 CONTINUE
      IF(INOSE .EQ. NNX)GO TO 1110
C
      IF(LCENTR .LE. UNUSED)GO TO 1100
C
      CALL LNTRP(XX,RR,NNX,LNOSE+LCENTR,RCENTR)
C
      INSERT=0
      DO 1060 I=1,NNX
         IF(ABS(LCENTR - (XX(I)-XX(INOSE))) .LT. 0.00001)ICENTR=I
         IF(ABS(LCENTR - (XX(I)-XX(INOSE))) .LT. 0.00001)GO TO 1100
         IF(LCENTR .LT. (XX(I)-XX(INOSE)))INSERT=I
         IF(LCENTR .LT. (XX(I)-XX(INOSE)))GO TO 1070
 1060 CONTINUE
C
      IF(INSERT .EQ. 0)GO TO 1100
C
 1070 ICENTR=INSERT
      DO 1080 I=INSERT,NNX
         XX(NNX+1-I+INSERT)=XX(NNX-I+INSERT)
         RR(NNX+1-I+INSERT)=RR(NNX-I+INSERT)
 1080 CONTINUE
      XX(INSERT)=LNOSE+LCENTR
      RR(INSERT)=RCENTR
      IF(DCENTR .LE. UNUSED)DCENTR=2.*RCENTR
      NNX=NNX+1
      DO 1090 I=1,20
         IF(DISCON(I) .GE. FLOAT(INSERT))DISCON(I)=DISCON(I)+1.
 1090 CONTINUE
C
 1100 CONTINUE
      IF((INOSE+ICENTR) .EQ. NNX)GO TO 1110
C
      IAFT=NNX
C
 1110 CONTINUE
C
      DO 1120 I=1,NNX
         X(I)=XX(I)
         R(I)=RR(I)
 1120 CONTINUE
C
      NX=FLOAT(NNX)
C
      IF(LNOSE .GT. UNUSED)GO TO 1150
C
C***  DEFINE NOSE
C
      DO 1130 I=2,NNX
         IF(R(I) .LE. R(I-1))GO TO 1140
         INOSE=I
 1130 CONTINUE
C
 1140 IF(INOSE .LT. 1)GO TO 1150
      LNOSE=X(INOSE)
      DNOSE=2.*R(INOSE)
C
C***  DEFINE CENTERBODY
C
 1150 IF(LCENTR .GT. UNUSED)GO TO 1180
      INP1=INOSE+1
      IF(INP1 .LE. 1)INP1=2
      DO 1160 I=INP1,NNX
         IF(R(I) .NE. R(I-1))GO TO 1170
         ICENTR=I
 1160 CONTINUE
C
 1170 IF(ICENTR .LT. 1)GO TO 1180
      LCENTR=X(ICENTR)
      IF(INOSE .GT. 0)LCENTR=X(ICENTR)-X(INOSE)
      DCENTR=2.*R(ICENTR)
C
C***  DEFINE AFT BODY
C
 1180 IF(LAFT .GT. UNUSED)GO TO 1190
      IF(LNOSE .EQ. LTOTAL .OR. (LNOSE+LCENTR) .EQ. LTOTAL)
     1   GO TO 1190
      IAFT=NNX
      LAFT=LTOTAL-LNOSE-LCENTR
      DAFT=2.*R(NNX)
C
 1190 IF(LNOSE .LE. UNUSED)GO TO 1200
      IF(INOSE .LE. 1)GO TO 1200
C
C***  DEFINE GEOMETRY FOR A SHARP NOSE TO BE USED 
C***  IN TRANSONIC WAVE DRAG CALCULATIONS.
C***  THIS DATA IS STORED IN XRBLNT COMMON BLOCK
      IF (BNOSE.GT.UNUSED) THEN
        NXTEMP=UNUSED
        CALL CONIC(DUM,NXTEMP,XTEMP,RTEMP,UNUSED,.FALSE.,
     &             LNOSE,DNOSE,DUMMY)
        VNTEMP=DUM(3)
        DO 1195 I=INOSE+1,NNX
          NXTEMP=NXTEMP+1.0
          NTMP=NXTEMP+0.5
          XTEMP(NTMP)=X(I)
          RTEMP(NTMP)=R(I)
 1195   CONTINUE
C
C***    TOTAL WETTED AREA FOR SHARP NOSE
        SWTEMP=DSWET(NTMP,XTEMP,RTEMP)
      ENDIF
C
      SPNOSE=DSPLAN(INOSE,X,R)
      SWNOSE=DSWET(INOSE,X,R)
      VOLNOS=DVOL(INOSE,X,R)
      XCVNOS=DXCENV(INOSE,X,R)
      XCPNOS=DXCENP(INOSE,X,R)
      FRNOSE=LNOSE/DNOSE
      THEOLN=LNOSE
      THEOFN=FRNOSE
C
 1200 IF(LCENTR .LE. UNUSED)GO TO 1210
      NPTCEN=ICENTR
      IF(INOSE .GT. 1)NPTCEN=ICENTR-INOSE+1
      IF(NPTCEN .LE. 1)GO TO 1210
C
      SPCENT=DSPLAN(NPTCEN,X(INOSE),R(INOSE))
      SWCENT=DSWET(NPTCEN,X(INOSE),R(INOSE))
      VOLCEN=DVOL(NPTCEN,X(INOSE),R(INOSE))
      XCVCEN=DXCENV(NPTCEN,X(INOSE),R(INOSE))
      XCPCEN=DXCENP(NPTCEN,X(INOSE),R(INOSE))
      FRCENT=LCENTR/DCENTR
      THEOLC=LCENTR
      THEOFC=FRCENT
C
 1210 IF(LAFT .LE. UNUSED)GO TO 1220
      IFWD=ICENTR
      IF(IFWD .LE. 1)IFWD=INOSE
      NPTAFT=NNX-IFWD+1
      IF(IFWD .EQ. 0)NPTAFT=NNX
      IF(NPTAFT .LE. 1)GO TO 1220
C
      SPAFT=DSPLAN(NPTAFT,X(IFWD),R(IFWD))
      SWAFT=DSWET(NPTAFT,X(IFWD),R(IFWD))
      VOLAFT=DVOL(NPTAFT,X(IFWD),R(IFWD))
      XCVAFT=DXCENV(NPTAFT,X(IFWD),R(IFWD))
      XCPAFT=DXCENP(NPTAFT,X(IFWD),R(IFWD))
      IF(DAFT .GT. DCENTR)FRAFT=LAFT/DAFT
      THEOLA=LAFT
      IF(DAFT .LT. DCENTR)
     1 THEOLA=X(IFWD+NPTAFT-1)-R(IFWD+NPTAFT-1)*
     2 (X(IFWD)-X(IFWD+NPTAFT-1))/(R(IFWD)-R(IFWD+NPTAFT-1))
      IF(DAFT .LE. DCENTR)FRAFT=LAFT/DCENTR
      IF(DAFT .LT. DCENTR)THEOFA=THEOLA/DCENTR
C
 1220 CONTINUE
C
      DO 1230 I=1,NNX
         X(I)=X(I)+XO
 1230 CONTINUE
C
      DO 1240 I=1,20
         II=I
         IF(DISCON(I) .LE. UNUSED)GO TO 1250
 1240 CONTINUE
C
 1250 IF(II .GT. 1 .AND. DISCON(II-1) .NE. NX)DISCON(II)=NX
      IF(II .EQ. 1)DISCON(II)=NX
C
      DMAX=R(1)*2.
      DO 1260 I=2,NNX
         IF(R(I) .GT. R(I-1))DMAX=R(I)*2.
 1260 CONTINUE
C
      SMAX=PI*DMAX**2/4.
C
      DBASE=2.*R(NNX)
C
      SBASE=PI*DBASE**2/4.
C
      FR=LTOTAL/DMAX
C
      RETURN
      END
