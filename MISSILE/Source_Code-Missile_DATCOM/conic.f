      SUBROUTINE CONIC(AA,NX,X,R,BNOSE,TRUNC,LENGTH,DIAM,DISCON)
C
C***  COMPUTE THE CHARACTERISTICS OF A CONIC SEGMENT
C
C***  INPUTS
C
C     NX -- NUMBER OF INPUT POINTS
C      X -- X STATIONS
C      R -- RADII AT EACH X STATION
C  BNOSE -- FORWARD RADIUS OF SECTION
C  TRUNC -- TRUNCATED FLAG (.TRUE. IF TRUNCATED NOT BLUNTED)
C LENGTH -- SECTION LENGTH
C   DIAM -- SECTION BASE DIAMETER
C
C***  OUTPUTS
C
C     AA -- ARRAY OF CHARACTERISTICS TO BE COMPUTED
C           AA(1)=PLANFORM AREA
C           AA(2)=WETTED AREA
C           AA(3)=VOLUME
C           AA(4)=VOLUME CENTROID
C           AA(5)=PLANFORM CENTROID
C           AA(6)=FINENESS RATIO
C           AA(7)=THEORETICAL LENGTH
C           AA(8)=THEORETICAL FINENESS RATIO
C DISCON -- ELEMENTS WHICH HAVE DISCONTINUOUS SLOPES
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      DIMENSION AA(8),X(50),R(50),XX(101),RR(101),DISCON(20)
C
      REAL NX,LENGTH
      LOGICAL TRUNC
C
C***  SET DISCONTINUOUS POINT FOR FIRST POINT IF NOSE IS TRUNCATED
C
      IF (TRUNC.AND.INT(NX+0.5).EQ.0) DISCON(1)=1.0
C
C***  SKIP PRIMARY CALCULATIONS FOR HEMISPHERICAL CAP
C
      IF(2.*BNOSE .GE. DIAM .AND. .NOT. TRUNC)GO TO 1070
C
C***  COMPUTE GEOMETRY AT 101 POINTS
C
      NPTS=101
      IF(.NOT. TRUNC .AND. BNOSE .GT. UNUSED)NPTS=51
C
      FR=LENGTH/DIAM
      IF(DIAM .LE. 2.*BNOSE)FR=LENGTH/(2.*BNOSE)
C
C  DO NOT DUPLICATE LAST POINT FOR THIS SEGMENT
C
      IF(NX .GT. UNUSED)NX=NX-1.
      NNX=NX+0.5
      XO=0.
      IF(NX .GT. UNUSED)XO=X(NNX+1)
C
      XT=0.
      YT=BNOSE
C
C***  SURFACE X AND R COORDINATES
C
C  BLUNTED NOSE
C
      ILAST=0
      IF(TRUNC .OR. BNOSE .LE. UNUSED)GO TO 1010
C
C  POINT OF TANGENCY OF BLUNTED NOSE
C
C  LENGTH FROM BASE TO CAP CENTER
C
      AL1=LENGTH-BNOSE
C
C  SLANT LENGTH FROM BASE RADIUS TO CAP CENTER
C
      ALD=SQRT(AL1**2+(DIAM/2.)**2)
C
C  SLANT LENGTH FROM BASE RADIUS TO CAP RADIUS
C
      ALS=SQRT(AL1**2+(DIAM/2.)**2-BNOSE**2)
C
C  ANGLE BETWEEN CAP CENTER AND BASE RADIUS
C
      PHI=ATAN(DIAM/(2.*AL1))
C
C  ANGLE BETWEEN ALD AND ALS
C
      DELTA=ASIN(BNOSE/ALD)
C
C  ANGLE OF CONICAL SURFACE (0=CYLINDER)
C
      THETA=PHI-DELTA
C
C  SLOPE OF CONICAL SURFACE
C
      SLOPE=TAN(THETA)
      IF(BNOSE .GT. UNUSED .AND. TRUNC)
     1   SLOPE=(DIAM-2.*BNOSE)/2./LENGTH
C
C  DISTANCE FROM ACTUAL NOSE TIP TO TANGENCY
C
      XT=BNOSE-BNOSE*SIN(THETA)
C
C  RADIUS AT TANGENCY
C
      YT=BNOSE*COS(THETA)
C
C  GET THEORETICAL LENGTH
C
      IF(SLOPE .NE. 0.)THEOL=ABS(DIAM/2./SLOPE)
      IF(SLOPE .NE. 0. .AND. BNOSE .GT. DIAM/2.)THEOL=ABS(BNOSE/SLOPE)
      IF(SLOPE .EQ. 0.)THEOL=LENGTH
C
      DO 1000 I=1,NPTS
         XX(I)=FLOAT(I-1)*XT/FLOAT(NPTS-1)
         RR(I)=SQRT(BNOSE**2-(XX(I)-BNOSE)**2)
         XX(I)=XX(I)+THEOL-LENGTH
 1000 CONTINUE
C
      XO=XX(NPTS)
C
      ILAST=NPTS-1
C
 1010 CONTINUE
C
C  THEORETICAL LENGTH OF SHARP CONIC
C
      SLOPE=(DIAM/2.-YT)/(LENGTH-XT)
      IF(BNOSE .GT. UNUSED .AND. TRUNC)
     1   SLOPE=(DIAM-2.*BNOSE)/2./LENGTH
      IF(SLOPE .NE. 0.)THEOL=ABS(DIAM/2./SLOPE)
      IF(SLOPE .NE. 0. .AND. BNOSE .GT. DIAM/2.)THEOL=ABS(BNOSE/SLOPE)
      IF(SLOPE .EQ. 0.)THEOL=LENGTH
C
C  THEORETICAL FINENESS RATIO
C
      FF=THEOL/DIAM
      IF(DIAM .LE. 2.*BNOSE)FF=THEOL/(2.*BNOSE)
C
C  ON SHAPED SEGMENT
C
      DO 1020 I=1,NPTS
         L=I+ILAST
         XX(L)=(LENGTH-XT)/FLOAT(NPTS-1)*FLOAT(I-1)
         RR(L)=YT+SLOPE*XX(L)
         XX(L)=XX(L)+XO
 1020 CONTINUE
C
C***  EXTRACT EVERY TENTH POINT FOR OUTPUT AND SUBSEQUENT CALCS.
C
      II=0
      DO 1030 I=1,101,10
         II=II+1
         L=II+NNX
         X(L)=XX(I)
         R(L)=RR(I)
 1030 CONTINUE
C
C  UPDATE POINT TOTAL
C
      NX=NX+FLOAT(II)
C
C  PLANFORM AREA
C
      SP=DSPLAN(101,XX,RR)
C
C  WETTED AREA
C
      SW=DSWET(101,XX,RR)
C
C  VOLUME
C
      VOL=DVOL(101,XX,RR)
C
C  VOLUME CENTROID
C
      XCENV=DXCENV(101,XX,RR)
C
C  PLANFORM AREA CENTROID
C
      XCENP=DXCENP(101,XX,RR)
C
C  STORE FOR RETURN TO CALLING ROUTINE
C
      AA(1)=SP
      AA(2)=SW
      AA(3)=VOL
      AA(4)=XCENV
      AA(5)=XCENP
      AA(6)=FR
      AA(7)=THEOL
      AA(8)=FF
C
C  SET DISCONTINUOUS POINTS
C
      DO 1040 I=1,20
         JJ=I
         IF(DISCON(I).EQ.UNUSED)GO TO 1050
 1040 CONTINUE
      GO TO 1060
C
C  END OF SEGMENT IS DISCONTINUOUS
C
 1050    DISCON(JJ)=NX
 1060 CONTINUE
C
      GO TO 1080
C
C***  HERE FOR SPHERICAL CAP
C
 1070 CONTINUE
C
      IF(LENGTH .GT. BNOSE)LENGTH=BNOSE
C
      CALL SPHERE(AA,NX,X,R,DIAM)
C
 1080 CONTINUE
C
      RETURN
      END
