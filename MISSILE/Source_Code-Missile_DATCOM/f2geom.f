      SUBROUTINE F2GEOM
C
C***  SUBROUTINE TO COMPUTE THE GEOMETRIC CHARACTERISTICS OF A
C***  FIN
C
      COMMON /FSET2/  FDATA(399)
C
      COMMON /GEOFS2/ GFIN(188)
C
      DIMENSION SSPAN(10),LMAXU(10),LFLATU(10),LMAXL(10),
     1          LFLATL(10),CHORD(10),THICKU(10),THICKL(10),
     2          SWEEP(10),STA(10),XLE(10),
     3          XCORD(50),MEAN(50),THICK(50),YUPPER(50),
     4          YLOWER(50),LER(10)
C
      REAL LER,LMAXU,LMAXL,LFLATU,LFLATL,NPANEL,MEAN
C
      EQUIVALENCE (FDATA(1),SECTYP),(FDATA(2),SSPAN(1)),
     1            (FDATA(12),LMAXU(1)),(FDATA(22),LFLATU(1)),
     2            (FDATA(32),LMAXL(1)),(FDATA(42),LFLATL(1)),
     3            (FDATA(52),CHORD(1)),(FDATA(62),THICKU(1)),
     4            (FDATA(72),THICKL(1)),(FDATA(82),SWEEP(1)),
     5            (FDATA(92),STA(1)),(FDATA(102),XLE(1)),
     6            (FDATA(112),NPANEL),(FDATA(113),XCORD(1)),
     7            (FDATA(163),MEAN(1)),(FDATA(213),THICK(1)),
     8            (FDATA(263),YUPPER(1)),(FDATA(313),YLOWER(1)),
     9            (FDATA(363),FINPHI),(FDATA(364),LER(1))
C
      DIMENSION SPLANF(10),CBAR(10),SWEPLE(10),SWEP25(10),
     1          SWEP50(10),SWEP75(10),SWEPTE(10),
     2          XMGCLE(10),XMGC25(10),
     3          XMGC50(10),XMGC75(10),XMGCTE(10),YMEANC(10),
     4          ASPCT(10),TAPER(10),TOVERC(10),XCENT(10)
C
      EQUIVALENCE (GFIN(1),SPLANF(1)),(GFIN(11),CBAR(1)),
     1            (GFIN(21),SWEPLE(1)),(GFIN(31),SWEP25(1)),
     2            (GFIN(41),SWEP50(1)),(GFIN(51),SWEP75(1)),
     3            (GFIN(61),SWEPTE(1)),(GFIN(71),XMGCLE(1)),
     4            (GFIN(81),XMGC25(1)),(GFIN(91),XMGC50(1)),
     5            (GFIN(101),XMGC75(1)),(GFIN(111),XMGCTE(1)),
     6            (GFIN(121),YMEANC(1)),(GFIN(131),ASPCT(1)),
     7            (GFIN(141),TAPER(1)),(GFIN(151),TOVERC(1)),
     8            (GFIN(161),XCENT(1)),(GFIN(171),FINAR),
     9            (GFIN(172),FINTPR),(GFIN(173),FINSPN),
     A            (GFIN(174),FINSP),(GFIN(175),FINWET),
     B            (GFIN(176),TCEFF),(GFIN(177),FINXGC),
     C            (GFIN(178),FINYGC),(GFIN(179),SWAVLE),
     D            (GFIN(180),SWAV25),(GFIN(181),SWAV50),
     E            (GFIN(182),SWAV75),(GFIN(183),SWAVTE),
     F            (GFIN(184),FINCBR),(GFIN(185),DELTAY),
     G            (GFIN(186),DELTAD),(GFIN(187),XCENF),(GFIN(188),XOVC)
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
C***  CALCULATE CHARACTERISTICS USING COMMON FUNCTIONS
C
C***  DO ONLY FOR SEGMENTS WHERE SPAN STATIONS ARE NOT ZERO OR
C***  UNUSED
C
C***  SET DEFAULTS
C
      IF(SECTYP.EQ.UNUSED .OR. SECTYP.LT.0. .OR. SECTYP.GT.3.)SECTYP=0.
C
      IF(NPANEL .LE. UNUSED)NPANEL=4.
      IF(FINPHI .EQ. UNUSED)FINPHI=0.
C
      DO 1000 I=1,10
         IF(I .GT. 1 .AND. SSPAN(I) .LE. UNUSED)GO TO 1010
         IF(STA(I) .EQ. UNUSED)STA(I)=1.
         IF(THICKU(I) .EQ. UNUSED)THICKU(I)=0.025
         IF(THICKL(I) .EQ. UNUSED)THICKL(I)=THICKU(I)
         IF(NPANEL .EQ. UNUSED)NPANEL=2.
         IF(LMAXU(I) .EQ. UNUSED)LMAXU(I)=0.5
         IF(LMAXL(I) .EQ. UNUSED)LMAXL(I)=LMAXU(I)
         IF(LFLATU(I) .EQ. UNUSED)LFLATU(I)=0.
         IF(LFLATL(I) .EQ. UNUSED)LFLATL(I)=LFLATU(I)
         IF(LER(I) .EQ. UNUSED)LER(I)=0.
C
 1000 CONTINUE
C
C
 1010 CONTINUE
      FINSP=0.0
      FINSPN=0.0
      FINAR=0.0
      FINTPR=0.0
      FINWET=0.0
      SWAVLE=0.0
      SWAV25=0.0
      SWAV50=0.0
      SWAV75=0.0
      SWAVTE=0.0
      XCENF=0.0
      FINXGC=0.0
      FINYGC=0.0
      FINCBR=0.0
C
      DO 1020 I=1,9
C
         II=I
C
         TOVERC(I)=THICKU(I)+THICKL(I)
C
         IF(SSPAN(I+1) .EQ. UNUSED .OR. SSPAN(I+1) .EQ. 0.)GO TO 1030
C
            SPAN=2.*(SSPAN(I+1)-SSPAN(I))
C
            TAPER(I)=CHORD(I+1)/CHORD(I)
C
            IF(SWEEP(I) .EQ. UNUSED)SWEEP(I)=ATAN((XLE(I+1)-
     1         XLE(I) + CHORD(I+1) - CHORD(I) )/(SPAN/2.))*RAD
C
            ASPCT(I)=ASPECT(SPAN,CHORD(I),TAPER(I))/2.
C
            SWEPLE(I)=SWEEPN(STA(I),SWEEP(I),0.00,SPAN,CHORD(I),
     1                       TAPER(I))
C
            SWEP25(I)=SWEEPN(STA(I),SWEEP(I),0.25,SPAN,CHORD(I),
     1                       TAPER(I))
C
            SWEP50(I)=SWEEPN(STA(I),SWEEP(I),0.50,SPAN,CHORD(I),
     1                       TAPER(I))
C
            SWEP75(I)=SWEEPN(STA(I),SWEEP(I),0.75,SPAN,CHORD(I),
     1                       TAPER(I))
C
            SWEPTE(I)=SWEEPN(STA(I),SWEEP(I),1.00,SPAN,CHORD(I),
     1                       TAPER(I))
C
            SPLANF(I)=FINPLN(SPAN,CHORD(I),TAPER(I))/2.
C
            CBAR(I)=FINMGC(CHORD(I),TAPER(I))
C
            YMEANC(I)=YMGC(SPAN,CHORD(I),TAPER(I))
C
            IF(I .GT. 1 .AND. XLE(I) .EQ. UNUSED)XLE(I)=XLE(I-1)+
     1        (SSPAN(I)-SSPAN(I-1))*TAN(SWEPLE(I-1)/RAD)
C
            XMGCLE(I)=XLE(I)-XLE(1)+YMEANC(I)*TAN(SWEPLE(I)/RAD)
C
            XMGC25(I)=XMGCLE(I)+0.25*CBAR(I)
C
            XMGC50(I)=XMGCLE(I)+0.50*CBAR(I)
C
            XMGC75(I)=XMGCLE(I)+0.75*CBAR(I)
C
            XMGCTE(I)=XMGCLE(I)+CBAR(I)
C
            YMEANC(I)=YMEANC(I)+SSPAN(I)
C
            XCENT(I)=XLE(I)-XLE(1)+FINCEN(SPAN,SWEPLE(I),CHORD(I),
     1               TAPER(I))
C
            FINSPN=FINSPN+SPAN/2.
C
            FINSP=FINSP+SPLANF(I)
C
 1020 CONTINUE
C
 1030 CONTINUE
C
C***  TOTAL FIN RESULTS
C
      FINAR=(2.*FINSPN)**2/(2.*FINSP)/2.
C
      FINTPR=CHORD(II)/CHORD(1)
C
      FINWET=2.*FINSP
C
C
      DO 1040 I=1,II
C
         SWAVLE=SWAVLE+SWEPLE(I)*SPLANF(I)/FINSP
C
         SWAV25=SWAV25+SWEP25(I)*SPLANF(I)/FINSP
C
         SWAV50=SWAV50+SWEP50(I)*SPLANF(I)/FINSP
C
         SWAV75=SWAV75+SWEP75(I)*SPLANF(I)/FINSP
C
         SWAVTE=SWAVTE+SWEPTE(I)*SPLANF(I)/FINSP
C
         FINXGC=FINXGC+XMGCLE(I)*SPLANF(I)/FINSP
C
         FINYGC=FINYGC+YMEANC(I)*SPLANF(I)/FINSP
C
         FINCBR=FINCBR+CBAR(I)*SPLANF(I)/FINSP
C
         XCENF=XCENF+XCENT(I)*SPLANF(I)/FINSP
C
 1040 CONTINUE
C
      TCEFF=TCEFFT(II,SSPAN,CHORD,TOVERC,FINSP)
C
      RETURN
      END
