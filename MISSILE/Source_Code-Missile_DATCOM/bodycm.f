      SUBROUTINE BODYCM(IM,CNAN,CNAC,CNABT)
C
C***  SUBROUTINE TO COMPUTE THE PITCHING MOMENT OF BODIES
C***  ABOUT THE USER SPECIFIED CENTER OF GRAVITY
C
C***  INPUTS
C
C     IM -- MACH NUMBER INDEX
C   CNAN -- NOSE NORMAL FORCE SLOPE, PER RADIAN
C   CNAC -- CENTERBODY NORMAL FORCE SLOPE, PER RADIAN
C  CNABT -- BOATTAIL/FLARE NORMAL FORCE SLOPE, PER RADIAN
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      REAL NALPHA,NMACH,MACH
C
      COMMON /TOTALC/ BALPHA(20),BBETA(20),BPHI(20),ALPTOT(20)
C
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      REAL LREF,LATREF
C
      COMMON /GEOBOD/ SPNOSE,SWNOSE,VOLNOS,XCVNOS,XCPNOS,FRNOSE,
     1  THEOLN,THEOFN,SPCENT,SWCENT,VOLCEN,XCVCEN,XCPCEN,FRCENT,
     2  THEOLC,THEOFC,SPAFT ,SWAFT ,VOLAFT,XCVAFT,XCPAFT,FRAFT ,
     3  THEOLA,THEOFA,SPLAN ,SWET  ,VOL   ,XCENTV,XCENTP,FR    ,
     4                DBASE ,SBASE ,DMAX  ,SMAX  ,LTOTAL,GDUM(12)
      REAL LTOTAL
C
      COMMON /ABODIN/ NX,XO,X(50),R(50),TNOSE,LNOSE,DNOSE,BNOSE,
     1                TRUNC,LCENTR,DCENTR,TAFT,LAFT,DAFT,
     2                POWER,DISCON(20),ADUM(748)
      REAL NX,LNOSE,LCENTR,LAFT
      LOGICAL TRUNC
C
      COMMON /SBODY/ CNB(20),CMB(20),CAB(20),CYB(20),CLNB(20),CLLB(20),
     1               CNAB(20),CMAB(20),CYBB(20),CNBB(20),CLBB(20)
C
      COMMON /BDWORK/  CNP(20),CMP(20),CNVIS(20),CMVIS(20),CAPR(20),
     &                 CAF(20),CABASE(20),ETA,CDC(20),CAPROT(20),
     &                 BOTDCA(20),BOTDCN(20),BOTDCM(20)
      COMMON /XRBLNT/ XTEMP(50),RTEMP(50),NXTEMP,VNTEMP,SWTEMP,ITYPE
C
      XCPN=0.
      XCPC=0.
      XCPB=0.
      IF(ITYPE.EQ.1) GO TO 500
C
C***  POTENTIAL PITCHING MOMENT SLOPE
C
C ... NOSE/CENTERBODY CENTER OF PRESSURE
C
      CALL BDXCPN(TNOSE,MACH(IM),THEOFN,FRCENT+FRAFT,XCPN)
      XCPN=XCPN*DNOSE/LREF
C
C ... BOATTAIL/FLARE CENTER OF PRESSURE
C
C     AREA AT FORWARD END OF AFTER BODY
C
      SFWD=PI*DCENTR**2/4.
      DFWD=DCENTR
      IF(LCENTR .LE. UNUSED)SFWD=PI*DNOSE**2/4.
      IF(LCENTR .LE. UNUSED)DFWD=DNOSE
      IF(LAFT .GT. UNUSED .AND. DAFT .LT. DFWD)
     1 CALL BDXCPB(LTOTAL,LAFT,VOLAFT,DAFT,SFWD,XCPB)
      IF(LAFT .GT. UNUSED .AND. DAFT .GT. DFWD)
     1 CALL BDXCPF(DFWD,DAFT,LNOSE+LCENTR,LAFT,XCPB)
      XCPB=XCPB*DFWD/LREF
C
C ... PITCHING MOMENT SLOPE ABOUT NOSE
C
      CMNCB=-XCPN*CNAN-XCPC*CNAC-XCPB*CNABT
      CNALP=CNAN+CNAC+CNABT
      GO TO 600
  500 CONTINUE
C
C  FOR USER DEFINED BODIES (X,R) CALCULATE POTENTIAL CM-ALPHA
C
      CALL SUBPTS(NX,X,R,MACH(IM),CNALP,CMNCB)
C
C ... PITCHING MOMENT SLOPE ABOUT C. G.
C
  600 CONTINUE
      CMALP=CMNCB+CNALP*(XCG-XO)/LREF
C
C***  ANGLE OF ATTACK LOOP
C
      NALP=NALPHA+0.5
      DO 1000 I=1,NALP
C
C***     POTENTIAL CM
C
         CALL BDCMP(CMALP,ALPTOT(I),CMP(I))
C
C***     VISCOUS CM
C
         CALL BDCMV(ALPTOT(I),CDC(I),ETA,SPLAN,XCG-XO,XCENTP,SREF,
     1              LREF,CMVIS(I))
C
C***     TOTAL PITCHING MOMENT
C
      CMB(I)=(CMP(I)+CMVIS(I))*COS(BPHI(I)/RAD)
      CLNB(I)=-(CMP(I)+CMVIS(I))*SIN(BPHI(I)/RAD)*LREF/LATREF
C
 1000 CONTINUE
C
      RETURN
      END
