      SUBROUTINE CARRYO(MACH,RADIUS,SSPAN,LESWP,TESWP,CR,TAPER,AR,CLA,
     1 DX,KWB,KBW,KKWB,KKBW,XCPBW,XCPWB)
C
C***  ROUTINE TO COMPUTE THE VALUES OF THE FIN-BODY AND BODY-FIN
C***  CARRY-OVER FACTORS DUE TO ANGLE OF ATTACK AND
C***  INCIDENCE.  THE FIN ALONE CLA MUST BE INPUT PER RADIAN.
C***  IT IS ASSUMED THAT TWO PANELS (ON OPPOSITE SIDES OF THE BODY)
C***  ARE PRESENT.
C
C***  INPUTS
C
C   MACH - MACH NUMBER
C RADIUS - BODY RADIUS AT LIFTING SURFACE
C  SSPAN - PANEL SEMI-SPAN (EXCLUDES BODY)
C  LESWP - PANEL LEADING EDGE SWEEP, DEG.
C  TESWP - PANEL TRAILING EDGE SWEEP, DEG.
C     CR - PANEL EXPOSED ROOT CHORD
C  TAPER - PANEL EXPOSED TAPER RATIO
C     AR - PANEL EXPOSED ASPECT RATIO
C    CLA - PANEL LIFT CURVE SLOPE, PER RADIAN
C     DX - DISTANCE FROM PANEL ROOT TRAILING EDGE TO BODY BASE
C
C***  OUTPUTS
C
C    KWB - CARRY-OVER TERM DUE TO ALPHA, KW(B)
C    KBW - CARRY-OVER TERM DUE TO ALPHA, KB(W)
C   KKWB - CARRY-OVER TERM DUE TO DEFLECTION, KKW(B)
C   KKBW - CARRY-OVER TERM DUE TO DEFLECTION, KKB(W)
C  XCPBW - CARRY-OVER CENTER OF PRESSURE, FRAC. ROOT CHORDS AFT
C                OF FIN ROOT L.E.
C  XCPWB - CARRY-OVER CENTER OF PRESSURE, FRAC. ROOT CHORDS AFT
C                OF FIN ROOT L.E. (FROM SLENDER BODY THEORY)
C
C***  REFERENCE - NACA REPORT 1307 (OTHER REFERENCES IN ROUTINE CARRYS)
C
      REAL M,MACH,LESWP,KWB,KBW,KKWB,KKBW,KWBI,KKWBI
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      ACOSH(U)=ALOG(U+SQRT(U*U-1.))
      ATANH(U)=0.5*(ALOG(1.+U)-ALOG(1.-U))
C
      IF(LESWP .NE. 0.)M=1./TAN(LESWP/RAD)
      IF(LESWP .EQ. 0.)M=1./UNUSED
      BETA=SQRT(ABS(MACH**2-1.))
      DOB=RADIUS/(SSPAN+RADIUS)
C
C***  XCPWB SAME AS FOR WING ALONE
C
      IF(LESWP .GE. 0.)
     * CALL FALCP(MACH,AR,LESWP,TAPER,XCPWB)
      IF(LESWP .LT. 0.)
     * CALL FWDXAC(MACH,AR,LESWP,TAPER,XCPWB)
C
C  XCPBW FROM DATCOM EQ. 4.3.2.2-C EXTENDED TO DOB = 1
C
      TSWPQR=TAN(LESWP/RAD)-(1-TAPER)/(AR*(1+TAPER))    
      T1=2.*PI*DOB**2+4.*DOB**2-PI*DOB-6.*DOB+2.
      IF(DOB.LT.0.5) T2=SQRT(1.-2.*DOB)*
     1            LOG( (SQRT(1.-2.*DOB)-DOB+1)/DOB )
      IF(DOB.GT.0.5) T2=-SQRT(2.*DOB-1.)*ACOS((1.-DOB)/DOB)
      IF(DOB.NE.0.5) BWFACT=-2.*DOB*((DOB**2+2.*DOB-1.)*T2+T1)/
     1                       (2.*DOB**2*T2+T1)
      IF(DOB.EQ.0.5) BWFACT=(PI-3.)/(4.-PI) 
      XCPBW=(CR/4.+(SSPAN+RADIUS)*TSWPQR*BWFACT)/CR
C***
C***  COMPUTE KWB AND KKWB FROM THE SLENDER BODY EQUATIONS
C***
      KWB=2.
      KKWB=1.
      IF(DOB.EQ.1.)GO TO 1000
      KWB=1.
      IF(DOB.EQ.0.)GO TO 1000
      T=1./DOB
      KWB=(PI/2.*((T**2-1.)/T)**2+((T**2+1.)/T)**2*
     1 ASIN((T**2-1.)/(T**2+1.))-2.*(T**2-1.)/T)/(PI*(T-1.)**2)
      KKWB=(PI**2/4.*((T+1.)/T)**2+PI*((T**2+1.)/(T*(T-1.)))**2*
     1 ASIN((T**2-1.)/(T**2+1.))-2.*PI*(T+1.)/(T*(T-1.))+
     2 (T**2+1.)**2/(T**2*(T-1.)**2)*(ASIN((T**2-1.)/(T**2+1.)))**2
     3 -4.*(T+1.)/(T*(T-1.))*ASIN((T**2-1.)/(T**2+1.))+
     4 8./(T-1.)**2*ALOG((T+1./T)/2.))/PI**2
 1000 CONTINUE
C***
C***  COMPUTE KBW SUBSONIC OR SUPERSONIC, WITH OR WITHOUT AFTERBODY
C***
      KBW=(1.+DOB)**2-KWB
      IF(KBW .EQ. 0.)KBW=UNUSED
      IF(DOB .EQ. 0.)GO TO 1010
      IF(MACH .LE. 1.0) GO TO 1010
C
C ... IF LEADING EDGE SWEEP IS ZERO, ADJUST TO NEAR ZERO
C
      ALESWP=LESWP
      IF(ABS(LESWP) .LE. UNUSED)ALESWP=0.01
C
C ... ASSURE TIP MACH LINE IS NOT ON ROOT CHORD
C
      FACTOR=SQRT(MACH**2-1.)*AR*(1.+TAPER)*(1.+1./(SQRT(MACH**2-1.)
     1       /TAN(ALESWP/RAD)))
      IF(FACTOR .LE. 4.)GO TO 1010
C***
C***  SUPERSONIC CARRY-OVER, USE ROUTINE CARRYS
C***
      CALL CARRYS(MACH,ALESWP,2.*RADIUS,CR,DX,ZKBW,XCPBW)
C
C ... COMPLETE THE CALCULATION FOR KBW
C
      KBW=ZKBW/(BETA*CLA*(1.+TAPER)*(1./DOB-1.))
C***
C***  COMPUTE KKBW
C***
 1010 KKBW=KWB-KKWB
C
      IF(TESWP.LE.0.)GO TO 1020
C***
C***  CORRECT THE LIFT FACTORS IF PANEL HAS TRAILING EDGE SWEEP
C***  METHOD FROM J. AIRCRAFT, VOL. 12, OCTOBER 1975
C***
      CRI=CR+SSPAN*TAN(TESWP/RAD)
      KBW=KBW*CR/CRI
      KWBI=1.+(KWB-1.)*CR/CRI
      KKWBI=1.+(KKWB-1.)*CR/CRI
      KKBW=(KWBI-KKWBI)*CR/CRI
      KWB=KWBI
      KKWB=KKWBI
C
 1020 CONTINUE
C
      RETURN
      END
