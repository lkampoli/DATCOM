      SUBROUTINE FCALE(MACH,SWEP00,LER,SSPAN,SREF,CALE)
C
C***  COMPUTE THE DRAG INCREMENT DUE TO FIN LEADING EDGE BLUNTNESS
C***  METHOD FROM DATCOM, SECTION 4.1.5.1
C
C***  INPUTS
C
C   MACH -- MACH NUMBER
C SWEP00 -- LEADING EDGE SWEEP ANGLE, DEG.
C    LER -- FIN LEADING EDGE RADIUS
C  SSPAN -- FIN PANEL SPAN (SEMI-SPAN OF TWO EXPOSED PANELS)
C   SREF -- REFERENCE AREA
C
C***  OUTPUT
C
C   CALE -- AXIAL FORCE INCREMENT DUE TO FIN L.E. BLUNTNESS
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      REAL MACH,LER
C
C ... MACH NUMBER SHOULD BE IN THE RANGE 0.5 TO 20.
C
      XMACH = MACH
      IF(MACH .LT. 1.05) XMACH = 1.05
C
      CALE=1.28*XMACH**3*COS(SWEP00/RAD)**6*2.*LER*SSPAN/
     1 (1.+XMACH**3*COS(SWEP00/RAD)**3)/(SREF*COS(SWEP00/RAD))
C
      IF(MACH .LT. 1.05) CALE = CALE*(1.0-4.0*(1.05-MACH))
      IF(MACH .LT. 0.80) CALE = 0.0
C
      RETURN
      END
