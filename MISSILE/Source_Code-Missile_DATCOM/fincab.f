      SUBROUTINE FINCAB(MACH,SBASE,SREF,CABASE)
C
C***  COMPUTE THE BASE DRAG OF THE FIN TRAILING EDGE
C
C***  METHOD FROM MOORE, NWC TR-2796
C***  USES EMPIRICAL DATA FROM MACH 0. TO 3.1
C***       FROM MACH 0. TO 1.2, USE 3-D DATA
C***       FROM MACH 1.2 TO 3.1 USE 2-D DATA
C***       FOR MACH GREATER THAN 4.0, USE 1/MACH**2
C
C
C***  INPUTS
C
C   MACH -- MACH NUMBER
C  SBASE -- BASE AREA, SQ. FT.
C   SREF -- REFERENCE AREA, SQ. FT.
C
C***  OUTPUT
C
C CABASE -- BASE INCREMENT TO AXIAL FORCE
C
      DIMENSION CP2D(20),TMACH(20),IROUT(2)
      REAL MACH
C
      DATA TMACH/0.,.5,.7,.8,.85,.9,1.,1.05,1.1,1.2,1.3,1.5,1.7,
     1 1.9,2.1,2.3,2.5,2.8,3.1,4.0/
      DATA CP2D/.280,.293,.299,.310,.325,.345,.460,.466,.465,
     1 .440,.405,.337,.285,.245,.213,.186,.165,.135,.115,.0625/
C
C ... INTERPOLATE DUE TO MACH, IF LESS THAN 4.
C
      IF(MACH .LE. 4.)CALL LNTRP(TMACH,CP2D,20,MACH,CPBASE)
C
C ... USE 1/MACH**2 IF MACH GREATER THAN 4.
C
      IF(MACH .GT. 4.)CPBASE=1./MACH**2
C
C ... BASE AXIAL FORCE INCREMENT
C
      CABASE=CPBASE*SBASE/SREF
C
      RETURN
      END
