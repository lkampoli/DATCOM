      SUBROUTINE ILTVIS(IMACH)
C
C     THIS SUBROUTINE DETERMINES THE NONLINEAR AERODYNAMICS OF
C     AN INLET AND DIVERTER BY INCREMENTING THE LINEAR AERODYNAMICS
C     USING CROSSFLOW DRAG COEFFICIENTS. THE AERODYNAMICS ARE
C     COMPUTED AT EACH COMBINATION OF FLIGHT CONDITIONS SPECIFIED
C     IN THE INPUT FILE.
C
C  WRITTEN BY M. MOORE, MCDONNELL DOUGLAS
C
C                  *********************
C                  * INPUT/OUTPUT LIST *
C                  *********************
C
C
C     ***** INPUTS *****          ***** OUTPUTS *****
C           MACH                        CNCF
C           ALPHA                       CMCF
C           BETA                        CYCF
C                                       CLNCF
C
C
C     SUBROUTINES USED: ILTCFD,DIVCFD,BODCFD
C
C
      COMMON /INLETN/ NIN,INTYPE,XINLT,XDIV,HDIV,LDIV,PHI(20),
     1                XI(5),HI(5),WI(5),ICOVER,RAMANG,IAD,MFR(20)
      COMMON /ILTGEO/ LENGTH(5),LLNGTH(5),ALPEFF(5),SI(5),
     1                CRXLEN(5),DEQI(5),XMIDI(5),XMIDD(3),
     2                XD(3),HD(3),WD(3),DEQD(3),
     3                RARC(5),DLEN(3),KWA(20),KWB(20),
     4                RA2(20),RB2(20),AREAI(5),AREAD(3)
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHIROL,NMACH,MACH(20),
     1                ALT(20),REN(20),FLDUM(60)
      COMMON /REFQN/  SREF,LREF,BREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      COMMON /CONST/  PI,RAD,UNUSED,KAND
      COMMON /POTENT/ CNP(20),CYP(20),CMP(20),CLNP(20)
      COMMON / MOMEN/ CNMOM(20),CYMOM(20)
      COMMON /LINEAR/ ARA,ARB,CNLIN(20),CMLIN(20),
     1                CYLIN(20),CLNLIN(20)
      COMMON /CFFLOW/ CNCF(20),CYCF(20),
     1                CMCF(20),CLNCF(20)
      COMMON /ILTVSC/ THETAI(5,20),PHIR(20),CRXIWD(5,20),
     1                CRXIHT(5,20),CNNIZP(5,20),
     2                CNNIZN(5,20),CNNIYP(5,20),
     3                CNNIYN(5,20),PANILN(5,5,20),
     4                CNCIRN,FRI,RARX(3),
     5                DLCNAP(3,20),DLCNAN(3,20),
     6                DLCYAP(3,20),DLCYAN(3,20)
      COMMON /DIVVSC/ THETAD(5,20),CRXDWD(3,20),
     1                CRXDHT(3,20),CNNDZP(3,20),
     2                CNNDZN(3,20),CNNDYP(3,20),
     3                CNNDYN(3,20),PANDLN(5,20),FRD
      COMMON /BODVSC/ DLCNBP(5,20),DLCNBN(5,20),
     1                DLCYBP(5,20),DLCYBN(5,20),FRB
C
      REAL NIN,INTYPE,LENGTH,LLNGTH,LREF,LDIV,MACH,KWA,KWB
      REAL NALPHA,NMACH
      LOGICAL ICOVER
C
      NUMFLT=NALPHA+0.5
      NUMMCH=NMACH+0.5
C
      XMACH  = MACH(IMACH)
C     BEGIN LOOP FOR FLIGHT CONDITIONS
      BETAR  = BETA/RAD
      DO 100 IFLT = 1, NUMFLT
C
C       COMPUTE ALPHA IN RADIANS
C
        ALPHAR = ALPHA(IFLT) / RAD
C
C       COMPUTE AERO CONTRIBUTION DUE TO CROSSFLOW
C
        CALL ILTCFD(ALPHAR,BETAR,XMACH,CNIVIS,CMIVIS,CYIVIS,CLNIVS)
        IF(HDIV .GT. 0.0)THEN
          CALL DIVCFD(ALPHAR,BETAR,XMACH,CNDVIS,CMDVIS,CYDVIS,CLNDVS)
        ELSE
          CNDVIS = 0.0
          CMDVIS = 0.0
          CYDVIS = 0.0
          CLNDVS = 0.0
        ENDIF
        CALL BODCFD(ALPHAR,BETAR,XMACH,CNBVIS,CMBVIS,CYBVIS,CLNBVS)
        CNCF(IFLT)=CNIVIS+CNDVIS+CNBVIS
        CMCF(IFLT)=CMIVIS+CMDVIS+CMBVIS
        CYCF(IFLT)=CYIVIS+CYDVIS+CYBVIS
        CLNCF(IFLT)=CLNIVS+CLNDVS+CLNBVS
  100 CONTINUE
C
      RETURN
      END
