      SUBROUTINE BODCFD(ALPHAR,BETAR,XMACH,CNVIS,CMVIS,CYVIS,CLNVIS)
C
C     THIS SUBROUTINE DETERMINES THE CROSSFLOW DRAG COEFFICIENT
C     FOR THE BODY AT EACH AXIAL SECTION OF THE DIVERTER AND THEN
C     INCREMENTS THE TOTAL VEHICLE FORCE AND MOMENT COEFFICIENTS
C     ACCORDINGLY.  THIS ELIMINATES DOUBLE FORCE ACCOUNTING.
C
C  WRITTEN BY M. MOORE, MCDONNELL DOUGLAS
C  MODIFIED BY K. BURNS, MCDONNELL DOUGLAS
C
C                  *********************
C                  * INPUT/OUTPUT LIST *
C                  *********************
C
C     ***** INPUTS *****          ***** OUTPUTS *****
C           ALPHAR                      CNVIS
C           BETAR                       CMVIS
C           XMACH                       CYVIS
C
C     SUBROUTINES USED: ILTCDC
C
      COMMON /INLETN/ NIN,INTYPE,XINLT,XDIV,HDIV,LDIV,PHI(20),
     1                XI(5),HI(5),WI(5),ICOVER,RAMANG,IAD,MFR(20)
      COMMON /ILTGEO/ LENGTH(5),LLNGTH(5),ALPEFF(5),SI(5),
     1                CRXLEN(5),DEQI(5),XMIDI(5),XMIDD(3),
     2                XD(3),HD(3),WD(3),DEQD(3),
     3                RARC(5),DLEN(3),KWA(20),KWB(20),
     4                RA2(20),RB2(20),AREAI(5),AREAD(3)
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHIROL,NMACH,MACH(20),
     1                ALT(20),REN(20),FLDUM(60)
      COMMON /REFQN/  SREF,LREF,BREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      COMMON /CONST/  PI,RAD,UNUSED,KAND
      COMMON /POTENT/ CNP(20),CYP(20),CMP(20),CLNP(20)
      COMMON / MOMEN/ CNMOM(20),CYMOM(20)
      COMMON /LINEAR/ ARA,ARB,CNLIN(20),CMLIN(20),
     1                CYLIN(20),CLNLIN(20)
      COMMON /CFFLOW/ CNCF(20),CYCF(20),
     1                CMCF(20),CLNCF(20)
      COMMON /ILTVSC/ THETAI(5,20),PHIR(20),CRXIWD(5,20),
     1                CRXIHT(5,20),CNNIZP(5,20),
     2                CNNIZN(5,20),CNNIYP(5,20),
     3                CNNIYN(5,20),PANILN(5,5,20),
     4                CNCIRN,FRI,RARX(3),
     5                DLCNAP(3,20),DLCNAN(3,20),
     6                DLCYAP(3,20),DLCYAN(3,20)
      COMMON /DIVVSC/ THETAD(5,20),CRXDWD(3,20),
     1                CRXDHT(3,20),CNNDZP(3,20),
     2                CNNDZN(3,20),CNNDYP(3,20),
     3                CNNDYN(3,20),PANDLN(5,20),FRD
      COMMON /BODVSC/ DLCNBP(5,20),DLCNBN(5,20),
     1                DLCYBP(5,20),DLCYBN(5,20),FRB
C
      REAL NIN,INTYPE,LENGTH,LLNGTH,LREF,LDIV,MACH,KWA,KWB
      REAL NALPHA,NMACH
      LOGICAL ICOVER
      COMMON /GEOBOD/ DUM(32),DMAX,SMAX,LBOD,GDUM(12)
      REAL LBOD
      RBOD=DMAX/2.0
C
      CNVIS  = 0.0
      CMVIS  = 0.0
      CYVIS  = 0.0
      CLNVIS = 0.0
C
      NUMI=NIN+0.5
      DO 100 INLET = 1, NUMI
C
C       INITIALIZE SUMMING VARIABLES
        CNSUM  = 0.0
        CMSUM  = 0.0
        CYSUM  = 0.0
        CLNSUM = 0.0
C
C       BEGIN LOOP FOR DIVERTER OR INLET SECTIONS
C
C       NCRX IS SET ACCORDING TO WHETHER BODY/DIVERTER OR BODY/INLET
C       INTERFERENCE IS BEING DETERMINED
        IF(HDIV .GT. 0.0)NCRX=2
        IF(HDIV .LE. 0.0)NCRX=4
        DO 110 IDCRX = 1, NCRX
          IDCRX1 = IDCRX + 1
C
C         DETERMINE CROSSFLOW DRAG COEFFICIENTS
          CALL ILTCDC(ALPHAR,XMACH,FRB,CDCA)
          CALL ILTCDC(BETAR,XMACH,FRB,CDCB)
C
C         COMPUTE THE AVERAGE NEWTONIAN CORRECTION FACTOR BETWEEN
C         THE TWO BOUNDING CROSS SECTIONS
C
C         CHECK FOR POSITIVE OR NEGATIVE ANGLE OF ATTACK
          IF (ALPHAR .GE. 0.0) THEN
            CNZAVE = -(DLCNBP(IDCRX,INLET) + DLCNBP(IDCRX1,INLET)) / 2.0
          ELSE
            CNZAVE = -(DLCNBN(IDCRX,INLET) + DLCNBN(IDCRX1,INLET)) / 2.0
          END IF
C
C         CHECK FOR POSITIVE OR NEGATIVE SIDESLIP ANGLE
          IF (BETAR .GE. 0.0) THEN
            CNYAVE = -(DLCYBP(IDCRX,INLET) + DLCYBP(IDCRX1,INLET)) / 2.0
          ELSE
            CNYAVE = -(DLCYBN(IDCRX,INLET) + DLCYBN(IDCRX1,INLET)) / 2.0
          END IF
C
C         INCREMENT AERO COEFFICIENTS FOR 2-D INLETS
          TEMPA  = CDCA * 2. * RBOD * DLEN(IDCRX) * CNZAVE / CNCIRN
          TEMPB  = CDCB * 2. * RBOD * DLEN(IDCRX) * CNYAVE / CNCIRN
          CNSUM  = CNSUM  + TEMPA
          CMSUM  = CMSUM  - TEMPA * XMIDD(IDCRX)
          CYSUM  = CYSUM  - TEMPB
          CLNSUM = CLNSUM + TEMPB * XMIDD(IDCRX)
C
  110   CONTINUE
C
C       COMPUTE THE DYNAMIC PRESSURE RATIO
        F3A = SIN(ALPHAR) * ABS(SIN(ALPHAR))
        F3B = SIN(BETAR)  * ABS(SIN(BETAR))
C
C       COMPLETE CROSSFLOW AERO CALCULATION
        CNVIS  = CNVIS  + CNSUM  * F3A /  SREF
        CMVIS  = CMVIS  + CMSUM  * F3A / (SREF * LREF)
        CYVIS  = CYVIS  + CYSUM  * F3B /  SREF
        CLNVIS = CLNVIS + CLNSUM * F3B / (SREF * BREF)
C
  100 CONTINUE
C
      RETURN
      END
