      SUBROUTINE SUBPTS(NX,X,R,MACH,CNALP,CMNCB)
C
C  THIS ROUTINE CALCULATES THE POTENTIAL NORMAL FORCE
C  AND PITCHING MOMENT SLOPE ABOUT THE NOSE AT
C  AT SUB/TRANSONIC SPEEDS FOR ARBITRARY BODIES (X,R)
C
C  CNSUM:  NORMAL FORCE DERIVATIVE WITH ANGLE OF ATTACK
C  CMSUM:  PITCHING MOMENT DERIVATIVE WITH ANGLE OF ATTACK
C          REFERENCED TO NOSE
C  FVOL:   VOLUME OF CONICAL FRUSTUM BETWEEN X-STATIONS
C  SBFACT: SLENDER BODY THEORY NORMAL FORCE SLOPE CORRECTED
C          FOR COMPRESSIBILITY (NOMINAL VALUE=2)
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
C
      REAL MACH,LREF,LMAX,NX
      DIMENSION X(50),R(50),S(50)
C
      NNX=NX+0.5
      DMAX=0.
      LMAX=X(NNX)-X(1)
      DO 500 I=1,NNX
         IF(2.*R(I).GT.DMAX) DMAX=2.*R(I)
         S(I)=PI*R(I)**2
  500 CONTINUE
C
C TRANSONIC SLENDER BODY THEORY CORRECTION FACTORS (EMPIRICAL)
C
      IF(MACH.LT.0.6) FM=1.0
      IF(MACH.GE.0.6) FM=1.+0.2*(MACH-0.6)
     1           +1.2562*(MACH-0.6)**2-1.74305*(MACH-0.6)**3
      FL=2.5+5./(6.*PI)*ATAN((LMAX/DMAX-15.)/3.)
C
C  INTEGRATE ALONG BODY
C
      CNSUM=0.
      CMSUM=0.
      SBFACT=2.
C     SBFACT=FL*FM
      DO 600 I=1,NNX-1
         CNSUM=CNSUM+SBFACT*(S(I+1)-S(I))/SREF
         FVOL=(X(I+1)-X(I))*(S(I+1)+S(I)+PI*R(I+1)*R(I))/3.
         CMSUM=CMSUM-SBFACT*(X(I+1)*S(I+1)-X(I)*S(I)-FVOL)/(LREF*SREF)
  600 CONTINUE
C
      CNALP=CNSUM
      CMNCB=CMSUM
      RETURN
      END
