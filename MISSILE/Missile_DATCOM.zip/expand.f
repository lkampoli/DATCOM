      SUBROUTINE EXPAND(GAM,TH,AM1,P1,AM2,P2,ICOMP)
C
C  SUBROUTINE TO CALCULATE THE FLOW CONDITIONS AFTER AN EXPANSION
C
C  INPUTS
C
C    GAM -- RATIO OF SPECIFIC HEATS FOR A GAS
C     TH -- THE TURNING ANGLE IN RADIANS
C    AM1 -- MACH NUMBER UPSTREAM OF THE CORNER
C     P1 -- PRESSURE UPSTREAM OF THE CORNER
C
C  OUTPUTS
C
C    AM2 -- EXPANSION MACH NUMBER
C     P2 -- EXPANSION PRESSURE
C  ICOMP -- RETURN A VALUE OF 1 IF COMPRESSIVE FLOW
C
C  SUBROUTINES CALLED -- NONE
C
C
C FUNCTION DESCRIBING THE PRANDTL-MEYER EXPANSION ANGLE
C
      AN(AS)=SQRT((GAM+1.)/(GAM-1.))*ATAN(SQRT((AS-1.)*(GAM-1.)
     1       /(GAM+1.)))-ATAN(SQRT(AS-1.))
      ICOMP=1
      AS=AM1**2
      AN1=AN(AS)
      F=-TH
C
C  GET MACH NUMBER FROM EXPANSION ANGLE THROUGH ITERATION
C
      DO 1000 JK=1,10
         DER=SQRT(AS-1.)/(2.*AS*(1.+(GAM-1.)/2.*AS))
         DEA=-F/DER
         AS=AS+DEA
         IF(AS.LT.1.0)GO TO 1020
         F=AN(AS)-AN1-TH
         IF(ABS(F).LT.1.E-4)GO TO 1010
 1000 CONTINUE
C
C  FINALLY GET EXPANSION MACH NUMBER
C
 1010 AM2=SQRT(AS)
C
C  GET PRESSURE AFTER EXPANSION
C
      P2=P1*((1.+(GAM-1.)/2.*AM1**2)/(1.+(GAM-1.)/2.*AM2**2))
     1   **(GAM/(GAM-1.))
      ICOMP=0
 1020 CONTINUE
      RETURN
      END
