      SUBROUTINE ILTARO(IM,BCN,BCM,BCA,BCY,BCLN,BCLL)
C**********************************************************************
C
C     THIS PROGRAM CALCULATES AERODYNAMICS FOR INLETS AT SUBSONIC 
C     AND SUPERSONIC SPEEDS.  THE CODE CALCULATES CONTRIBUTIONS TO 
C     NORMAL FORCE AND SIDE FORCE AERODYNAMICS USING A COMBINATION OF 
C     POTENTIAL FLOW AND VISCOUS CROSS FLOW THEORIES.  THE CONTRIBUTION 
C     TO AXIAL FORCE IS CALCULATED USING A COMBINATION OF THE WAVE 
C     DRAG AREA RULE AND CLASSICAL SKIN FRICTION METHODS.
C
C***********************************************************************
C
C  WRITTEN BY M. MOORE, MCDONNELL DOUGLAS
C  MODIFIED BY K. BURNS, MCDONNELL DOUGLAS
C  FURTHER MODIFIED BY W. BLAKE, WL/FIGC
C
      COMMON /INLETN/ NIN,INTYPE,XINLT,XDIV,HDIV,LDIV,PHI(20),
     1                XI(5),HI(5),WI(5),ICOVER,RAMANG,IADD,MFR(20)
      COMMON /ILTGEO/ LENGTH(5),LLNGTH(5),ALPEFF(5),SI(5),
     1                CRXLEN(5),DEQI(5),XMIDI(5),XMIDD(3),
     2                XD(3),HD(3),WD(3),DEQD(3),
     3                RARC(5),DLEN(3),KWA(20),KWB(20),
     4                RA2(20),RB2(20),AREAI(5),AREAD(3)
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHIROL,NMACH,MACH(20),
     1                ALT(20),REN(20),FLDUM(60)
      COMMON /REFQN/  SREF,LREF,BREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      COMMON /CONST/  PI,RAD,UNUSED,KAND
      COMMON /POTENT/ CNP(20),CYP(20),CMP(20),CLNP(20)
      COMMON / MOMEN/ CNMOM(20),CYMOM(20)
      COMMON /LINEAR/ ARA,ARB,CNLIN(20),CMLIN(20),
     1                CYLIN(20),CLNLIN(20)
      COMMON /CFFLOW/ CNCF(20),CYCF(20),
     1                CMCF(20),CLNCF(20)
      COMMON /ILTVSC/ THETAI(5,20),PHIR(20),CRXIWD(5,20),
     1                CRXIHT(5,20),CNNIZP(5,20),
     2                CNNIZN(5,20),CNNIYP(5,20),
     3                CNNIYN(5,20),PANILN(5,5,20),
     4                CNCIRN,FRI,RARX(3),
     5                DLCNAP(3,20),DLCNAN(3,20),
     6                DLCYAP(3,20),DLCYAN(3,20)
      COMMON /DIVVSC/ THETAD(5,20),CRXDWD(3,20),
     1                CRXDHT(3,20),CNNDZP(3,20),
     2                CNNDZN(3,20),CNNDYP(3,20),
     3                CNNDYN(3,20),PANDLN(5,20),FRD
      COMMON /BODVSC/ DLCNBP(5,20),DLCNBN(5,20),
     1                DLCYBP(5,20),DLCYBN(5,20),FRB
      COMMON /GEOBOD/ GDUM(32),DMAX,SMAX,LBOD,GDUM1(12)
C
      REAL NIN,INTYPE,LENGTH,LLNGTH,LREF,LDIV,MACH,KWA,KWB
      REAL NALPHA,NMACH,MFR,MFRM
      LOGICAL ICOVER,IADD
      COMMON /ABODIN/ DUM,XO,DUM2(879)
      COMMON /PARTF/ PARTS(19)
      COMMON /INLTD/ CNINLT(20),CMINLT(20),CAINLT(20),
     1               CYINLT(20),CLNILT(20),CLLILT(20)
C
      LOGICAL PARTS
      DIMENSION X(50),R(50),CMMOM(20),CLNMOM(20)
      DIMENSION BCN(20),BCM(20),BCA(20),BCY(20),BCLN(20),BCLL(20)
C
      REAL LINLET,LNOSE,NX
C
      NUMFLT=NALPHA+0.5
      NUMMCH=NMACH+0.5
      NUMIN=NIN+0.5
C
      ALPHA1 = 1.0
      BETA1  = 1.0
      IF(INTYPE.EQ.1.)THEN
        CALL POTAR1(ALPHA1,BETA1,CNP1,CYP1,CMP1,CLNP1)
      ELSEIF(INTYPE.EQ.2.)THEN
        CALL POTAR2(ALPHA1,BETA1,CNP1,CYP1,CMP1,CLNP1)
      ELSEIF(INTYPE.EQ.3.)THEN
        CALL POTAR3(ALPHA1,BETA1,CNP1,CYP1,CMP1,CLNP1)
      ENDIF
      CNA = CNP1*RAD
      CMA = CMP1*RAD
      CYB = CYP1*RAD
      CLNB= CLNP1*RAD
      BETAR  = BETA/RAD
      DO 10 IFLT = 1, NUMFLT
        ALPHAR = ALPHA(IFLT)/RAD
        CNP(IFLT)=CNA*COS(ALPHAR)*SIN(ALPHAR)
        CMP(IFLT)=CMA*COS(ALPHAR)*SIN(ALPHAR)
        IF(ABS(CNP(IFLT)).GT.0.00001)THEN
          XXCPA=ABS(CMP(IFLT)/CNP(IFLT)*LREF)+XINLT
          CMP(IFLT)=CNP(IFLT)*((XCG-XO)-XXCPA)/LREF
        ENDIF
        CYP(IFLT)=CYB*COS(BETAR)*SIN(BETAR)
        CLNP(IFLT)=CLNB*COS(BETAR)*SIN(BETAR)
        IF(ABS(CYP(IFLT)).GT.0.00001)THEN
          XXCPB=ABS(CLNP(IFLT)/CYP(IFLT)*BREF)+XINLT
          CLNP(IFLT)=CYP(IFLT)*((XCG-XO)-XXCPB)/BREF
        ENDIF
C
   10 CONTINUE
C
C     CALCULATION OF FORCE INCREMENT DUE TO FLOW TURNING
      DO 20 IFLT = 1, NUMFLT
        IF (ICOVER) THEN
          CNMOM(IFLT)=0.0
          CYMOM(IFLT)=0.0
          CMMOM(IFLT)=0.0
          CLNMOM(IFLT)=0.0
        ELSE
          ALPHAR = ALPHA(IFLT)/RAD
          CALL MOMFOR(ALPHAR,BETAR,CNMOM(IFLT),CYMOM(IFLT))
          CMMOM(IFLT)=CNMOM(IFLT)*((XCG-XO)-XINLT)/LREF
          CLNMOM(IFLT)=CYMOM(IFLT)*((XCG-XO)-XINLT)/BREF
        ENDIF
   20 CONTINUE    
C
C     CALCULATION OF LINEAR AERODYNAMICS BASED ON PLANFORM ASPECT
C     RATIO
      CALL LINFOR(CNALIN,CYBLIN)
C
      CALL LINXCP(IM,XCPA,XCPB)
      DO 30 IFLT = 1, NUMFLT
        ALPHAR = ALPHA(IFLT)/RAD
        CNLIN(IFLT) = CNALIN * COS(ALPHAR) * SIN(ALPHAR)
        XXCPA = XCPA + XINLT
        CMLIN(IFLT) = CNLIN(IFLT)*((XCG-XO)-XXCPA)/LREF
        CYLIN(IFLT) = -CYBLIN * COS(BETAR) * SIN(BETAR)
        XXCPB = XCPB + XINLT
        CLNLIN(IFLT) = CYLIN(IFLT)*((XCG-XO)-XXCPB)/BREF
   30 CONTINUE
C
C     CALL VISCOUS CROSS FLOW CALCULATIONS
      CALL ILTVIS(IM)
C
      DO 50 IFLT = 1, NUMFLT
        IF(ABS(CNCF(IFLT)).GT.0.00001)THEN
          XXCPA=ABS(CMCF(IFLT)/CNCF(IFLT)*LREF)+XINLT
          CMCF(IFLT)=CNCF(IFLT)*((XCG-XO)-XXCPA)/LREF
        ENDIF
        IF(ABS(CYCF(IFLT)).GT.0.00001)THEN
          XXCPB=ABS(CLNCF(IFLT)/CYCF(IFLT)*BREF)+XINLT
          CLNCF(IFLT)=CYCF(IFLT)*((XCG-XO)-XXCPB)/BREF
        ENDIF
   50 CONTINUE
C
      DO 70 IFLT = 1, NUMFLT
        CNINLT(IFLT) = CNP(IFLT) + 0.*CNLIN(IFLT)
     1                        + CNCF(IFLT) + CNMOM(IFLT)
        CMINLT(IFLT) = CMP(IFLT) + 0.*CMLIN(IFLT)
     1                        + CMCF(IFLT) + CMMOM(IFLT)
        CYINLT(IFLT) = CYP(IFLT) + 0.*CYLIN(IFLT)
     1                        + CYCF(IFLT) + CYMOM(IFLT)
        CLNILT(IFLT) = CLNP(IFLT) + 0.*CLNLIN(IFLT)
     1                        + CLNCF(IFLT) + CLNMOM(IFLT)
   70 CONTINUE
C
C     AXIAL FORCE CALCULATIONS
C
      CALL ILTRVX(NSTEP,X,R)
      CALL ILTSWT(SWET)
C
C     FIND MAXIMUM DIAMETER OF EQUIVALENT RADIUS
      RMAX=R(1)
      INDEX=1
      DO 80 I=2,NSTEP
        IF (R(I).LE.RMAX) THEN
C
C         SPECIFY LENGTH, FINENESS RATIO, AND VOLUME OF THE NOSE
          LNOSE=X(INDEX)
          FRNOSE=LNOSE/(2.*RMAX)
          VOLNOS=DVOL(INDEX,X,R)
          GOTO 90
        ENDIF
        RMAX=R(I)
        INDEX=I
   80 CONTINUE
   90 CONTINUE
C
      LINLET = XI(5)
      FR=LINLET/(2.*RMAX)
      NX=FLOAT(NSTEP)
C
C***  CHECK TO SEE IF CRITICAL MACH NUMBER HAS BEEN REACHED
      CALL MDIV(FRNOSE,NX,R,X,LNOSE,CRMACH)
      IF (MACH(IM).LE.CRMACH) THEN
C
C***    SUBSONIC SKIN FRICTION DRAG
        CALL CAFRIC(0.,MACH(IM),REN(IM),LINLET,ROUGH,
     &            BLAYER,SWET,SREF,1.1547,1.022,CAF)
C
C***    SUBSONIC PRESSURE DRAG 
        CALL BDCAPR(FR,CAF,CAPR)
C
C***    CORRECT FOR COMPRESSIBILITY EFFECTS
        IF (MACH(IM) .LT. 1.) CAPR=CAPR/SQRT(1.-MACH(IM)**2)
C
      ELSE
C
C***    SUPERSONIC SKIN FRICTION DRAG
        CALL SKINF(MACH(IM),REN(IM),LINLET,ROUGH,BLAYER,SWET,SREF,
     &                     1.0,1.1547,1.022,CAF)
C
C***    CALCULATE SUBSONIC PRESSURE DRAG FOR WAVE DRAG CALCULATIONS
        CALL CAFRIC(0.,CRMACH,REN(IM),LINLET,ROUGH,BLAYER,SWET,
     &         SREF,1.1547,1.022,CAFO)
        CALL BDCAPR(FR,CAFO,CDPO)
C
C***    CALCULATE TRANSONIC/SUPERSONIC WAVE DRAG
        IF(MACH(IM).GT.1.2) THEN
          CALL CDPRES(NX,X,R,SREF,1.2,FRNOSE,
     $              VOLNOS,LNOSE,CRMACH,CDPO,CAPR12)
          CAPR=CAPR12*.66332/SQRT(MACH(IM)**2-1.)
        ELSE
          CALL CDPRES(NX,X,R,SREF,MACH(IM),FRNOSE,
     &              VOLNOS,LNOSE,CRMACH,CDPO,CAPR)
        ENDIF
      ENDIF
C
        CA=NIN*(CAF+CAPR)
C
C***  CALCULATE INLET ADDITIVE DRAG
      CXADD=0.0
      IF ((IADD).AND.(.NOT.ICOVER).AND.(MACH(IM).GE.1.0)) THEN
        IF (INTYPE.EQ.3.) THEN
          CALL IADAXI(MACH(IM),RAMANG,XI(2),DEQI(2)/2.,
     &                MFR(IM),SREF,CXADD,MFRM)
        ELSE
          CALL IAD2D(MACH(IM),RAMANG,XI(2),HI(2),WI(2),
     &               MFR(IM),SREF,CXADD,MFRM)
        ENDIF
        CXADD=CXADD*NIN
      ENDIF
C
C  REV 1/96 UPDATE
C
C*** CALCULATE ROLLING MOMENT DUE TO INLETS
C*** CALCULATE AXIAL FORCE CONTRIBUTION TO PITCHING AND YAWING MOMENT
C
        IF(INTYPE.LT.1.5) ARM=DMAX/2.+HDIV+HI(3)*4./(3.*PI)
        IF(INTYPE.GT.1.5) ARM=DMAX/2.+HDIV+WI(3)*4./(3.*PI)
        DO 92 I=1,NUMFLT
          CLLILT(I)=0.
          DO 95 J=1,NUMIN
            TCN=CNINLT(I)/NIN*(ARM/BREF)
            TCY=CYINLT(I)/NIN*(ARM/BREF)
            TCA=(CA+CXADD)/NIN*(ARM/BREF)
            CLLILT(I)=CLLILT(I)-TCN*SIN(PHI(J)/RAD)+TCY*COS(PHI(J)/RAD)
            CMINLT(I)=CMINLT(I)+TCA*COS(PHI(J)/RAD)*BREF/LREF
            CLNILT(I)=CLNILT(I)+TCA*SIN(PHI(J)/RAD)
   95     CONTINUE
   92   CONTINUE
C
C***  ADD INLET INCREMENTS TO BODY ALONE COEFFICIENTS
      DO 100 I=1,NUMFLT
        BCN(I)=CNINLT(I)+BCN(I)
        BCM(I)=CMINLT(I)+BCM(I)
        BCA(I)=CA+CXADD+BCA(I)
        CAINLT(I)=CA
        BCY(I)=CYINLT(I)+BCY(I)
        BCLN(I)=CLNILT(I)+BCLN(I)
        BCLL(I)=BCLL(I)+CLLILT(I)
  100 CONTINUE
C
C***  PRINT INLET AERODYNAMICS
      IF (PARTS(16)) CALL INLETA(IM,CXADD,MFRM)
C
      RETURN
      END
