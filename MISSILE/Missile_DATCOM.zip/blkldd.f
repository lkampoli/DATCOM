      SUBROUTINE BLKLDD(IFLAGA)
C      
C***     THIS SUBROUTINE BLANKS OUT LATERAL DIRECTIONAL DERIVATIVES IF
C         EXPERIMENTAL INPUTS HAVE BEEN USED IN THE FOLLOWING MANNER:
C           IF CY  HAS BEEN INPUT CYB  IS SET TO -UNUSED
C           IF CSN HAS BEEN INPUT CLNB IS SET TO -UNUSED
C           IF CSL HAS BEEN INPUT CLLB IS SET TO -UNUSED
C        THIS IS DONE FOR EACH DERIVATIVE BEGINNING WITH THE MOST
C         SIMPLE CONFIGURATION THAT EXPERIMENTAL DATA IS INPUT FOR
C         THROUGH THE MOST COMPLEX CONFIGURATION BEING RUN
C           -EXAMPLE-
C             IF EXPERIMENTAL DATA IS INPUT FOR CY OF THE BF1
C             CONFIGURATION THEN IN THE SAME CASE FOR THE BF12
C             CONFIGURATION AND ANY OTHER MORE COMPLEX CONFIGURATIONS
C             CYB IS SET TO -UNUSED
C      
C***  INPUT
C      
C     IFLAGA = A 2 DIMENSIONAL ARRAY IN WHICH THE FIRST VALUE (I)
C              REPRESENTS THE COEFFICIENT BEING CHECKED AND THE SECOND
C              VALUE (J) REPRESENTS THE CONFIGURATION BEING CHECKED IF
C              THE ARRAY ELEMENT EQUALS 1 THERE HAS BEEN EXPERIMENTAL
C              INPUT FOR THAT COEFFICIENT AND THAT CONFIGURATION
C              I = 1 FOR CY      J = 1 FOR BF1
C              I = 2 FOR CSN     J = 2 FOR BF12
C              I = 3 FOR CSL     J = 3 FOR BF123
C                                J = 4 FOR BF1234
C        -EXAMPLE-
C           IFLAGA(3,2)=1  MEANS EXPERIMENTAL VALUES OF CSL HAVE BEEN
C                          INPUT FOR A BF12 CONFIGURATION
C      
      COMMON /SBODY / DUM1(160),BCYB(20), BCLNB(20), BCLLB(20)
      COMMON /SB1   / DUM2(160),CYB1(20), CLNB1(20), CLLB1(20)
      COMMON /SB12  / DUM3(160),CYB12(20),CLNB12(20),CLLB12(20)
      COMMON /SB123 / DUM4(160),CYB13(20),CLNB13(20),CLLB13(20)
      COMMON /SB1234/ DUM5(160),CYB14(20),CLNB14(20),CLLB14(20)
      COMMON /CONST / PI,RAD,UNUSED,KAND
      DIMENSION IFLAGA(3,4)
C      
C         DETERMINE WHICH CONFIGURATIONS HAVE INPUT FOR CY
C         AND SET CYB TO -UNUSED
C      
      IF( BCYB(1) .EQ. -UNUSED )IFLAGA(1,1)=1
      IF(IFLAGA(1,1) .NE. 1)GO TO 1010
      DO 1000 K1=1,20
         CYB1(K1) = -UNUSED
         CYB12(K1)= -UNUSED
         CYB13(K1)= -UNUSED
         CYB14(K1)= -UNUSED
 1000 CONTINUE
      GO TO 1070
 1010 IF( IFLAGA(1,2) .NE. 1)GO TO 1030
      DO 1020 K2=1,20
         CYB12(K2)= -UNUSED
         CYB13(K2)= -UNUSED
         CYB14(K2)= -UNUSED
 1020 CONTINUE
      GO TO 1070
 1030 IF( IFLAGA(1,3) .NE. 1)GO TO 1050
      DO 1040 K3=1,20
         CYB13(K3)= -UNUSED
         CYB14(K3)= -UNUSED
 1040 CONTINUE
      GO TO 1070
 1050 IF( IFLAGA(1,4) .NE. 1)GO TO 1070
      DO 1060 K4=1,20
         CYB14(K4)= -UNUSED
 1060 CONTINUE
C      
C         DETERMINE WHICH CONFIGURATIONS HAVE INPUT FOR CSN
C         AND SET CLNB TO -UNUSED
C      
 1070 IF( BCLNB(1) .EQ. -UNUSED)IFLAGA(2,1)=1
      IF(IFLAGA(2,1) .NE. 1)GO TO 1090
      DO 1080 L1=1,20
         CLNB1(L1) = -UNUSED
         CLNB12(L1)= -UNUSED
         CLNB13(L1)= -UNUSED
         CLNB14(L1)= -UNUSED
 1080 CONTINUE
      GO TO 1150
 1090 IF( IFLAGA(2,2) .NE. 1)GO TO 1110
      DO 1100 L2=1,20
         CLNB12(L2)= -UNUSED
         CLNB13(L2)= -UNUSED
         CLNB14(L2)= -UNUSED
 1100 CONTINUE
      GO TO 1150
 1110 IF( IFLAGA(2,3) .NE. 1)GO TO 1130
      DO 1120 L3=1,20
         CLNB13(L3) = -UNUSED
         CLNB14(L3) = -UNUSED
 1120 CONTINUE
      GO TO 1150
 1130 IF( IFLAGA(2,4) .NE. 1)GO TO 1150
      DO 1140 L4=1,20
         CLNB14(L4) = -UNUSED
 1140 CONTINUE
C      
C         DETERMINE WHICH CONFIGURATIONS HAVE INPUT FOR CSB
C         AND SET CLLB TO -UNUSED
C      
 1150 IF( BCLLB(1) .EQ. -UNUSED)IFLAGA(3,1)=1
      IF(IFLAGA(3,1) .NE. 1)GO TO 1170
      DO 1160 M1=1,20
         CLLB1(M1) = -UNUSED
         CLLB12(M1)= -UNUSED
         CLLB13(M1)= -UNUSED
         CLLB14(M1)= -UNUSED
 1160 CONTINUE
      GO TO 1230
 1170 IF( IFLAGA(3,2) .NE. 1)GO TO 1190
      DO 1180 M2=1,20
         CLLB12(M2)= -UNUSED
         CLLB13(M2)= -UNUSED
         CLLB14(M2)= -UNUSED
 1180 CONTINUE
      GO TO 1230
 1190 IF( IFLAGA(3,3) .NE. 1)GO TO 1210
      DO 1200 M3=1,20
         CLLB13(M3) = -UNUSED
         CLLB14(M3) = -UNUSED
 1200 CONTINUE
      GO TO 1230
 1210 IF( IFLAGA(3,4) .NE. 1)GO TO 1230
      DO 1220 M4=1,20
         CLLB14(M4) = -UNUSED
 1220 CONTINUE
 1230 CONTINUE
      RETURN
      END
