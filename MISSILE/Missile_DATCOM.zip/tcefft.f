      FUNCTION TCEFFT(NPTS,SSPAN,CHORD,TOVERC,FINSP)
C
C***  FUNCTION TO COMPUTE THE EFFECTIVE THICKNESS-TO-CHORD RATIO
C***  FOR A FIN PANEL
C
C***  INPUTS
C
C   NPTS -- NUMBER OF INPUT SPAN STATIONS
C  SSPAN -- SEMISPAN STATIONS
C  CHORD -- LOCAL CHORD AT EACH SEMISPAN STATION
C TOVERC -- THICKNESS-TO-CHORD AT EACH SEMISPAN STATION
C  FINSP -- FIN PANEL AREA
C
C***  OUTPUT
C
C TCEFFT -- EFFECTIVE THICKNESS-TO-CHORD RATIO
C
      DIMENSION SSPAN(10),CHORD(10),TOVERC(10),T(10)
C
C*** FUNCTIONS TO INTEGRATE C*(T/C)**2
C
C  IF CHORD LENGTH IS CONSTANT
C
      TOCF1(YY)=(A**2*YY**3+3.*A*B*YY**2+3.*B**2*YY)/(3.*D)
C
C  IF CHORD LENGTH VARIES LINEARLY BUT DOES NOT GO TO ZERO
C
      TOCF2(YY)=(A**2*.5*YY**2/C)+((2.*A*B/C)-(A**2*D/C**2))*YY+
     1((B**2/C)+(A**2*D**2/C**3)-(2.*A*B*D/C**2))*ALOG(C*YY+D)
C
C  IF CHORD LENGTH VARIES LINEARLY AND DOES GO TO ZERO
C
      TOCF3(YY)=(A**2/C)*(YY**2/2+D/C*YY)
C
      TCEFFT=0.0
C
C*** THICKNESS AT EACH SEMISPAN STATION
C
      DO 1000 I=1,NPTS
         T(I)=TOVERC(I)*CHORD(I)
 1000 CONTINUE
C
C*** WORK ACROSS THE SPAN
C
      DO 1020 I=2,NPTS
         II=I
C
C***     ASSUMING A LINEAR VARIATION OF THICKNESS AND CHORD OVER SEMISPA
C
C        THICKNESS =A*Y+B
C
         A=(T(II)-T(II-1))/(SSPAN(II)-SSPAN(II-1))
         B=T(II-1)-A*SSPAN(II-1)
C
C        CHORD=C*Y+B
C
         C=(CHORD(II)-CHORD(II-1))/(SSPAN(II)-SSPAN(II-1))
         D=CHORD(II-1)-C*SSPAN(II-1)
C
C***     CHECK IF CHORD LENGTH GOES TO ZERO
C
         IF(CHORD(II).EQ.0.0.OR.CHORD(II-1).EQ.0.0)GO TO 1010
C
C***     INTEGRATION OF THIS SECTION FOR CHORD NOT GOING TO ZERO
C
C     IF CHORD LENGTH IS CONSTANT
C
         IF(C.EQ.0.)TCEFFT=TCEFFT+TOCF1(SSPAN(II))-TOCF1(SSPAN(II-1))
C
C     IF CHORD LENGTH VARIES LINEARLY BUT DOES NOT GO TO ZERO
C
         IF(C.NE.0.)TCEFFT=TCEFFT+TOCF2(SSPAN(II))-TOCF2(SSPAN(II-1))
         GO TO 1020
C
C***     INTEGRATION OF THIS SECTION FOR CHORD THAT DOES GO TO ZERO
C
C     IF CHORD LENGTH VARIES LINEARLY AND DOES GO TO ZERO
C
 1010    CONTINUE
         TCEFFT=TCEFFT+TOCF3(SSPAN(II))-TOCF3(SSPAN(II-1))
C
 1020 CONTINUE
C
C***  FINALIZE THE CALCULATION
C
      TCEFFT=SQRT(TCEFFT/FINSP)
C
      RETURN
      END
