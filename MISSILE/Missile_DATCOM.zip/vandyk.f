      SUBROUTINE VANDYK(IPRINT,BETA,RN,DIA,RP,R,N,NSHAPE,N2,NN1,NN2,
     1  NN3,NN4,NFL,NBLUNT,NN,NNI,VOVS,AL,XM,XINT,RR,IERROR,
     2  RREF,CAW,CNW,CMW)
C
C  ROUTINE ADAPTED FROM NSWC AEROPREDICTION CODE
C  ORIGINAL ROUTINE WRITTEN BY F. MOORE, NSWC
C  MODIFIED BY J. JENKINS, WL/FIGC
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      COMMON /VDARY/ XB(220),RB(220),RBP(220),C(220),C1(220),RB1(220),
     1  RBP1(220),B(220),PSI(220),ZE0X(220),PSIR(220),PHIX(220)
      COMMON /VANVAR/PHIR(220),ZE1(220),ZE1X(220),ZE1R(220),
     1              T(100),AK(100),AE(100),C3
      DIMENSION RP(6),R(50)
      DIMENSION CPV(220,7)
      DIMENSION THET(7)
      DIMENSION ZE0PX(220)
      EQUIVALENCE (ZE0PX(1),ZE0X(1))
C
C     THIS ROUTINE COMPUTES THE SECOND ORDER AXIAL AND FIRST
C       ORDER CROSS FLOW PERTURBATION VELOCITY COMPONENTS. THESE
C        COMPONENTS ARE THEN COMBINED TO YIELD A HYBRID SOLUTION.
C
      IKK=1
      IK=1
      THET(1)=0.
      DO 1000 IJ=2,7
         THET(IJ)=THET(IJ-1)+30./RAD
 1000 CONTINUE
      TA=RP(1)
      TA2=TA**2
C
C ... J.A.S. EQUATION 23B
C
      BETATA=BETA*TA
      IF(BETATA.GE.1.) BETATA=0.9999
      C(1)=TA2/(SQRT(1.-BETA**2*TA2)+TA2*ARSECH(BETATA))
C
C
C........  EXACT SECOND ORDER SOLUTION FOR TANGENT CONE
C
      TAU=BETA*TA
      IF(TAU.GE.1.) TAU=0.9999
      TAU2=TAU*TAU
      F1=ARSECH(TAU)
      F2=SQRT(1.-TAU2)
C
C........  J.A.S. EQUATION 20B
C
      AN=1.2*VOVS**2/BETA**2
C
C........  CALCULATE BRACKETED TERMS ON R.H.S. OF J.A.S. EQUATION
C........  24B & 24C
C
      B1=F1**2-(AN-1.)*F1/F2-(AN+1.)-.75*BETA**2*C(1)*F2/TAU2
      B2=-2.*F2*F1/TAU+(AN+1.)/TAU+(AN-1.)*TAU*F1/F2+.25*BETA**2
     1 *C(1)*(2.+TAU2)*F2/TAU**3
C
C........  SOLVE FOR CONSTANT IN CHI0 FROM J.A.S. EQUATIONS
C........  24B, 24C, & 18C
C
      C1(1)=C(1)*(1.+(C(1)*VOVS)**2*B1)-BETA*(C(1)*VOVS)**2*C(1)
     1 *B2/RBP(1)
C
C........  CALCULATE PHIX AND PHIR FROM J.A.S. EQUATIONS 24B & 24C
C
      PHIX(1)=-C1(1)*F1+(C(1)*VOVS)**2*B1
      PHIR(1)=C1(1)*F2/RBP(1)+BETA*(C(1)*VOVS)**2*B2
C
C     FIRST ORDER AXIAL FLOW
C
      DO 1080 I=2,NN
      CALL DELV(1,I,BETA,XXI,TAU,F1,F2,PSIX,PSIR,C,SUM1,SUM2,
     1   SUM3,SUM4,SUM5,SUM6,NN1,NN2,NN3,IKK,JL,TTX,TTR,TEMP)
      IF(I .LE. JL) GO TO 1080
      D=ABS(RBP(JL+1)-RBP(JL))
      IF(D.GE.0.0001) GO TO 1070
      C(JL+1)=C(JL)
      IKK=IKK+1
      IF(NSHAPE .EQ. 3)IKK=IKK+1
      GO TO 1080
C
C ... J.A.S. EQUATION 27B
C
 1070 XI=XB(JL-1)-BETA*RB(JL-1)
      TAU=BETA*RB(JL)/(XB(JL)-XI)
      IF(TAU.GE.1.) TAU=0.9999
      F1=ARSECH(TAU)
      C(JL+1)=(RBP(JL+1)-RBP(JL))*(1.+TEMP-F1*C(1))/(RBP(JL+1)+BETA)
      IKK=IKK+1
      IF(NSHAPE .EQ. 3)IKK=IKK+1
 1080 CONTINUE
C
C     I=2 IS 2ND POINT ON SURFACE
C
      DO 1250 I=2,NN
      CALL SUMPOT(1,BETA,I,C,ZE0,ZE0X,ZE0R,ZE0XX,ZE0XR,ZE0RR,
     1   NN1,NN2,NN3)
      PHIX(I)=ZE0X(I)
      PHIR(I)=ZE0R
      IF(NSHAPE.LT.4)GO TO 1120
      IF(NSHAPE.NE.4) GO TO 1110
      IF(R(N2+1).GT.R(N2).AND.I.GT.NN1)GO TO 1120
      IF(R(N2+1).NE.R(N2).AND.I.GT.NN1)GO TO 1240
      IF(I.GT.NN2.AND.R(N).LT.R(N2+1))GO TO 1240
      GO TO 1120
 1110 IF(I.GT.NN3) GO TO 1240
 1120 CONTINUE
C
C   SECOND ORDER AXIAL SOLUTION.
C     A. PARTICULAR SOLUTION
C
C ... J.A.S. EQUATION 20A
C
      PSI(I)=VOVS**2*(ZE0X(I)*(ZE0+AN*RB(I)*ZE0R)-0.25*RB(I)
     1*ZE0R**3)
C
C ... J.A.S. EQUATION 20C
C
      PSIX=VOVS**2*((ZE0 +AN*RB(I)*ZE0R)*ZE0XX + ZE0X(I)*(
     1ZE0X(I)+AN*RB(I)*ZE0XR)-0.75*RB(I)*ZE0R**2*ZE0XR)
C
C ... J.A.S. EQUATION 20D
C
      PSIR(I)=VOVS**2*((ZE0+AN*RB(I)*ZE0R)*ZE0XR+ZE0X(I)*((AN
     1+1.)*ZE0R+AN*RB(I)*ZE0RR)-0.25*ZE0R**2*(ZE0R+3.*RB(I)
     2*ZE0RR))
C
C     B. COMPLIMENTARY SOLUTION
C
 1130 CONTINUE
      CALL DELV(2,I,BETA,XXI,TAU,F1,F2,PSIX,PSIR,C1,SUM1,SUM2,
     1   SUM3,SUM4,SUM5,SUM6,NN1,NN2,NN3,IK,JL,TTX,TTR,TEMP)
      IF(I .LE. JL) GO TO 1210
      D=ABS(RBP(JL+1)-RBP(JL))
      IF(D.LE.0.0001) GO TO 1200
      D1=PSI(JL+1)-PSI(JL)
      C3=-D1
      TAU1=BETA*RB(JL+1)/(XB(JL+1)-XB(1)+BETA*RB(1))
      IF(TAU1.GE.1.) TAU1=0.9999
      TTX=-C1(1)*ARSECH(TAU1)
      TTR=C1(1)*BETA*SQRT(1.-TAU1**2)/TAU1
      IF(TAU.GE.1.) TAU=0.9999
      F1=ARSECH(TAU)
      F2=SQRT(1.-TAU**2)
      C1(JL+1)=(RBP(JL+1)*(1.+PSIX+TTX+SUM2)-TTR-SUM3-PSIR(JL+1)+
     1 3.*C3/(8.*RB(JL+1)))/BETA
      IK=IK+1
      IF(NSHAPE .EQ. 3)IK=IK+1
      GO TO 1210
 1200 C1(JL+1)=(RBP(I)*(1.+PSIX+TTX+SUM2)-TTR-SUM3-PSIR(I))/BETA
      IK=IK+1
      IF(NSHAPE .EQ. 3)IK=IK+1
 1210 CONTINUE
      CALL SUMPOT(2,BETA,I,C1,ZE0P,ZE0PX,ZE0PR,ZE0PXX,ZE0PXR,ZE0PRR,
     1   NN1,NN2,NN3)
C
C  TOTAL 2ND ORDER SOLN.=PARTICULAR PLUS COMPLIMENTARY.
C
      PHIX(I)=PSIX+ZE0PX(I)
      PHIR(I)=PSIR(I)+ZE0PR
 1240 CONTINUE
 1250 CONTINUE
 1260 CONTINUE
      J=1
C
C ... BOUNDARY LAYER DISPLACEMENT THICKNESS INCLUDED FOR CROSSFLOW SOL.
C
      PQ=(1.+.0092*VOVS**2)**0.88
      RND=RN*DIA
      XCRIT=500000./RND
      DELI=0.125*PQ*5.*SQRT(XCRIT/RND)
      DEL1=(5.*SQRT(XCRIT/RND))**1.25
      DEL2=0.
      DRBP=0.
      DO 1320 I=2,NN
      RB1(I)=RB(I)
      RBP1(I)=RBP(I)
 1320 CONTINUE
      RB1(1)=RB(1)
      RBP1(1)=RBP(1)
      TA=RBP1(1)
      TA2=TA**2
      B(1)=BETA*TA2
      BETATA=BETA*TA
      IF(BETATA.GE.1.) BETATA=0.9999
      DEN=(1.+2.*TA2)*SQRT(1.-(BETATA)**2)+(BETATA)**2*ARSECH(BETATA)
      B(1)=B(1)/DEN
      I=1
      TAU=BETA*TA
      IF(TAU.GE.1.) TAU=0.9999
      F1=ARSECH(TAU)
      F2=SQRT(1.-TAU**2)
      ZE1(1)=B(1)*(F2/TAU-TAU*F1)
      ZE1X(1)=2.*B(1)*F2/TAU
      ZE1R(1)=-B(1)*BETA*(F2/TAU**2+F1)
      DO 1330 IJ=1,7
C
C ... J.A.S. EQUATIONS 2B, 4 AND 5
C
      UB=COS(AL)*(1.+PHIX(I))+SIN(AL)*COS(THET(IJ))*ZE1X(I)
      VB=COS(AL)*PHIR(I)+SIN(AL)*COS(THET(IJ))*(1.+ZE1R(I))
      WB=-SIN(AL)*SIN(THET(IJ))*(1.+ZE1(I)/TA)
      QB=UB**2+VB**2+WB**2
      QQ=1.+0.2*VOVS**2*(1.-QB)
      IF(QQ .LT. 0. .AND. PARTS(18))WRITE(6,1540)
      IF(QQ .LT. 0.)IERROR=1
      IF(QQ .LT. 0.)GO TO 1490
      CPV(1,IJ)=2./(1.4*VOVS**2)*((1.+0.2*VOVS**2*(1.-QB))**3.5-1.)
 1330 CONTINUE
      IF(NN.NE.2)GO TO 1350
      DO 1340 IJ=1,7
      CPV(2,IJ)=CPV(1,IJ)
 1340 CONTINUE
      GO TO 1480
 1350 J5=NN
      DO 1470 I=2,J5
      SUM2=0.
      SUM3=0.
      J6=I-1
      DO 1390 J=1,J6
      IF(J.GT.1) GO TO 1360
      TAU=BETA*RB1(I)/(XB(I)-XB(1)+BETA*RB(1))
      IF(TAU.GE.1.) TAU=0.9999
      GO TO 1370
 1360 TAU=BETA*RB1(I)/(XB(I)-XB(J-1)+BETA*RB1(J-1))
      IF(TAU.GE.1.) TAU=0.9999
 1370 F1=ARSECH(TAU)
      F2=SQRT(1.-TAU**2)
      IF(J.EQ.1) GO TO 1380
      D=ABS(XB(J)-XB(J-1))
      IF(D.LT.0.000001) GO TO 1390
 1380 SUM2=SUM2+2.*B(J)*F2/TAU
      SUM3=SUM3-B(J)*BETA*(F2/(TAU**2)+F1)
 1390 CONTINUE
      TAU=BETA*RB1(I)/(XB(I)-XB(I-1)+BETA*RB1(I-1))
      IF(TAU.GE.1.) TAU=0.9999
      F1=ARSECH(TAU)
      F2=SQRT(1.-TAU*TAU)
      D=ABS(XB(I)-XB(I-1))
      IF(D.LT.0.000001) GO TO 1400
      B(I)=(1.+SUM3-RBP1(I)*SUM2)*TAU**2/BETA
      B(I)=B(I)/(F2*(1.+2.*RBP1(I)*TAU/BETA)+TAU**2*F1)
      GO TO 1410
 1400 B(I)=0.
 1410 SUM1=0.
      SUM2=0.
      SUM3=0.
      DO 1440 J=1,I
      IF(J.GT.1) GO TO 1420
      TAU=BETA*RB1(I)/(XB(I)-XB(1)+BETA*RB(1))
      IF(TAU.GE.1.) TAU=0.9999
      XXI=XB(I)-XB(1)+BETA*RB(1)
      GO TO 1430
 1420 XXI=XB(I)-XB(J-1)+BETA*RB1(J-1)
      TAU=BETA*RB1(I)/XXI
      IF(TAU.GE.1.) TAU=0.9999
 1430 F1=ARSECH(TAU)
      F2=SQRT(1.-TAU**2)
      SUM1=SUM1+B(J)*XXI*(F2/TAU-TAU*F1)
      SUM2=SUM2+2.*B(J)*F2/TAU
      SUM3=SUM3-B(J)*BETA*(F2/TAU**2+F1)
      IF(I.EQ.1) GO TO 1450
 1440 CONTINUE
 1450 ZE1(I)=SUM1
      ZE1X(I)=SUM2
      ZE1R(I)=SUM3
C
C  HYBRID THEORY
C
      DO 1460 IJ=1,7
C
C ... J.A.S. EQUATIONS 2B, 4 AND 5
C
      RREF = 0.5
      UB=COS(AL)*(1.+PHIX(I))+SIN(AL)*COS(THET(IJ))*ZE1X(I)
      VB=COS(AL)*PHIR(I)+SIN(AL)*COS(THET(IJ))*(1.+ZE1R(I))
      WB=-SIN(AL)*SIN(THET(IJ))*(1.+ZE1(I)/RB1(I))
      QB=UB**2+VB**2+WB**2
      QQ=1.+0.2*VOVS**2*(1.-QB)
      IF(QQ .LT. 0.)WRITE(6,1540)
      IF(QQ .LT. 0.)IERROR=1
      IF(QQ .LT. 0.)GO TO 1490
      CPV(I,IJ)=2./(1.4*VOVS**2)*((1.+0.2*VOVS**2*(1.-QB))**3.5-1.)
 1460 CONTINUE
 1470 CONTINUE
 1480 IF(NBLUNT .NE. 0) CALL NEWT(CPV,R,NN1,NFL,NNI,VOVS,AL,XM,
     1  XINT,RR,RREF,CABL,CNBL,CMBL)
C
C ... FINAL RESULTS
C
      CALL WAVE(CPV,NN1,NN2,NN3,NN4,NBLUNT,NN,NNI,RR,RREF,
     1 SUM1,SUM2,SUM3,CABL,CNBL,CMBL,CAW,CNW,CMW)
C
      IF(IPRINT .NE. 1)GO TO 1490
C
C  PRINT PRESSURE DISTRIBUTION ABOUT THE BODY
C
C     CALL HEADR9
      ALD=AL*RAD
C     WRITE(6,1510)VOVS,ALD
C     WRITE(6,1500)
      WRITE(10,1520)(XB(J),(CPV(J,K),K=1,7),J=1,J5)
C     WRITE(6,1530)
 1490 CONTINUE
C
C  PRINT BODY COORDINATES TO TAPE UNIT 9
C
C  FOLLOWING CARD REMOVED FOR TECPLOT FORMAT
C
C     IF(IPRINT.EQ.2) WRITE(9,1600)
      IF(IPRINT.EQ.2) WRITE(9,1610) (DIA*XB(J),DIA*RB(J),J=1,J5)
C
C1500 FORMAT(//,52X,21HPRESSURE COEFFICIENTS,//,7X,10H  X/DMAX  ,9X,
C    15HPHI=0,8X,6HPHI=30,8X,6HPHI=60,8X,6HPHI=90,8X,7HPHI=120,7X,
C    27HPHI=150,7X,7HPHI=180)
C1510 FORMAT(51X,26HBODY ALONE PRESSURE OUTPUT//
C    1 43X,6HMACH =,F5.2,2X,17HANGLE OF ATTACK =,F7.2,5H DEG.)
 1520 FORMAT(F14.6,7F10.5)
C1530 FORMAT(49H0 NOTE - PHI=  0 IS TOP VERTICAL CENTER (LEEWARD) /
C    1       53H         PHI=180 IS BOTTOM VERTICAL CENTER (WINDWARD) )
 1540 FORMAT('*** HYBRID ANALYSIS FAILING - CHANGING TO ',
     1 ' SECOND-ORDER SHOCK EXPANSION FOR BODY ALONE')
C
C  BODY COORDINATE COMMENT REMOVED FOR TECPLOT FORMAT
C
C1600 FORMAT(3X,39HVAN DYKE HYBRID THEORY BODY COORDINATES)
 1610 FORMAT(3X,2F14.6)
C
      RETURN
      END
