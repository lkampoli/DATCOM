      SUBROUTINE CNNEWT(A,B,PHI,CNCNO)
C
C ** NEWTONIAN CORRECTION FACTOR FOR ELLIPTICAL BODY
C ** CORRECTION DUE TO EXPERIMENT BASED UPON NACA TN D-7228, TABLE 1
C
C ** INPUTS
C
C       A -- MAJOR AXIS
C       B -- MINOR AXIS
C     PHI -- BODY ROLL ANGLE, DEGREES
C            PHI= 0 IF MAJOR AXIS PERPENDICULAR TO CROSS FLOW
C            PHI=90 IF MINOR AXIS PERPENDICULAR TO CROSS FLOW
C
C ** OUTPUT
C
C   CNCNO -- CORRECTION FACTOR FOR ELLIPTICAL SEGMENT
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      DIMENSION ABTAB(7),RATIO(7),IROUT(2)
C
      DATA ABTAB / 0.,.5,1.,2.,4.,10.,20. /
      DATA RATIO / 1.11,1.08,1.00,.82,.682,.60,.60 /
C
      CNCNO=1.
      CNX=UNUSED
      CNY=UNUSED
      IF(A .EQ. 0. .OR. B .EQ. 0.)GO TO 1010
      IF(A .EQ. B)GO TO 1010
C
      AOB=A/B
      BOA=B/A
C
      CALL LNTRP(ABTAB,RATIO,7,AOB,CORR1)
      CALL LNTRP(ABTAB,RATIO,7,BOA,CORR2)
C
      IF(BOA .GT. 1.)GO TO 1000
C
C***  MAJOR AXIS PERPENDICULAR TO CROSS FLOW
C
      CNCNO=CORR1*1.5*SQRT(AOB)*(-BOA**2/(1.-BOA**2)**1.5*
     1 ALOG(AOB*(1.+SQRT(1.-BOA**2)))+1./(1.-BOA**2))
      CNX=CNCNO
C
C***  MINOR AXIS PERPENDICULAR TO CROSS FLOW
C
 1000 IF(AOB .LT. 1.)GO TO 1010
      CNCNO=CORR2*1.5*SQRT(BOA)*(AOB**2/(AOB**2-1.)**1.5*
     1 ATAN(SQRT(AOB**2-1.))-1./(AOB**2-1.))
      CNY=CNCNO
C
C***  VARY BETWEEN TWO CASES DEPENDING ON PHI
C
      IF(CNX .NE. UNUSED .AND. CNY .NE. UNUSED)
     1   CNCNO=CNX*COS(PHI/RAD)**2+CNY*SIN(PHI/RAD)**2
C
 1010 CONTINUE
C
      RETURN
      END
