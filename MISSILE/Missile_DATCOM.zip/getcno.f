      SUBROUTINE GETCNO(NX,X,WIDTH,HEIGHT,CNSB,CNNEW,CMSBT,CMNEWT)
C
C***  COMPUTE THE INTEGRALS FOR THE LINEAR AND NON-LINEAR
C***  FACTORS FOR JORGENSEN CN AND CM EQUATIONS FOR ELLIPTICAL
C***  BODIES
C
C***  INPUTS
C
C     NX - NUMBER OF INPUT POINTS (REAL NUMBER)
C      X - LONGITUDINAL STATIONS
C  WIDTH - BODY HALF-WIDTHS
C HEIGHT - BODY HALF-HEIGHTS
C
C***  OUTPUTS
C
C   CNSB - INTEGRAL OF CN/CNO*DADX
C  CNNEW - INTEGRAL OF CN/CNO*R
C  CMSBT - INTEGRAL OF CN/CNO*DADX*(XM-X)
C CMNEWT - INTEGRAL OF CN/CNO*R*(XM-X)
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
C
      DIMENSION CNOS(101),DADX(101),R(101),XMX(101)
      DIMENSION WIDTH(50),HEIGHT(50),X(50)
C
      REAL NX
C
C***  COMPUTE CN/CNO AT 101 STATIONS ALONG THE BODY
C
      NNX=NX+.5
C
      DX=(X(NNX)-X(1))/100.
C
      XX=X(1)-DX
C
C***  COMPUTE SLENDER BODY THEORY VALUES
C
      DO 1000 I=1,101
         XX=XX+DX
         CALL LNTRP(X,WIDTH,NNX,XX,WW)
         CALL LNTRP(X,HEIGHT,NNX,XX,HH)
C
         AMAJOR=WW
         IF(HH .GT. WW)AMAJOR=HH
         IF(AMAJOR .EQ. WW)AMINOR=HH
         IF(AMAJOR .EQ. HH)AMINOR=WW
         IF(AMAJOR .EQ. WW)PHI=0.
         IF(AMINOR .EQ. WW)PHI=90.
C
         CALL CNSBT(AMAJOR,AMINOR,PHI,CNOS(I))
C
         XMX(I)=XCG-XX
         R(I)=SQRT(WW*HH)
         IF(I .EQ. 1)GO TO 1000
         DADX(I)=PI*(R(I)**2-R(I-1)**2)/DX
 1000 CONTINUE
C
      DADX(1)=DADX(2)
C
C ... COMPUTE INTEGRAL CN/CNO*DADX
C
      DO 1010 I=1,101
         CNOS(I)=CNOS(I)*DADX(I)
 1010 CONTINUE
C
      CALL QUAD1(CNOS,101,DX,CNSB)
C
C ... COMPUTE INTEGRAL CN/CNO*DADX*(XM-X)
C
      DO 1020 I=1,101
         CNOS(I)=CNOS(I)*XMX(I)
 1020 CONTINUE
C
      CALL QUAD1(CNOS,101,DX,CMSBT)
C
C***  REPEAT FOR NEWTONIAN VALUES
C
      XX=X(1)-DX
C
      DO 1030 I=1,101
         XX=XX+DX
         CALL LNTRP(X,WIDTH,NNX,XX,WW)
         CALL LNTRP(X,HEIGHT,NNX,XX,HH)
C
         AMAJOR=WW
         IF(HH .GT. WW)AMAJOR=HH
         IF(AMAJOR .EQ. WW)AMINOR=HH
         IF(AMAJOR .EQ. HH)AMINOR=WW
         IF(AMAJOR .EQ. WW)PHI=0.
         IF(AMINOR .EQ. WW)PHI=90.
C
         CALL CNNEWT(AMAJOR,AMINOR,PHI,CNOS(I))
C
 1030 CONTINUE
C
C ... COMPUTE INTEGRAL CN/CNO*R
C
      DO 1040 I=1,101
         CNOS(I)=CNOS(I)*R(I)
 1040 CONTINUE
C
      CALL QUAD1(CNOS,101,DX,CNNEW)
C
C ... COMPUTE INTEGRAL CN/CNO*R*(XM-X)
C
      DO 1050 I=1,101
         CNOS(I)=CNOS(I)*XMX(I)
 1050 CONTINUE
C
      CALL QUAD1(CNOS,101,DX,CMNEWT)
C
      RETURN
      END
