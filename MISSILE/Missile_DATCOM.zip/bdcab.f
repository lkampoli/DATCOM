      SUBROUTINE BDCAB(DFWD,DBASE,DEXIT,LAFT,SREF,MACH,CABASE)
C
C***  SUBROUTINE TO COMPUTE THE BASE DRAG FOR BODIES AT ALL SPEEDS
C
C  MODIFIED BY K. BURNS, MCDONNELL DOUGLAS, W. BLAKE, WL/FIGC
C
C***  METHOD FROM EMPIRICAL DATA OF NASA TR R-100
CC***  BOATTAIL/FLARE CORRECTION FROM AMC PAMPHLET AMCP 706-280
C
C***  INPUTS
C
C   DFWD - DIAMETER OF THE BODY AHEAD OF THE AFTERBODY
C  DBASE - DIAMETER OF THE BODY BASE
C  DEXIT - DIAMETER OF THE NOZZLE EXIT
C   LAFT - LENGTH OF THE AFTERBODY
C   SREF - REFERENCE AREA FOR CA
C   MACH - FREESTREAM MACH NUMBER
C
C***  OUTPUT
C
C CABASE - INCREMENT IN AXIAL FORCE DUE TO BASE DRAG AT ALPHA=0.
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      REAL MACH,LAFT
C
C  REV 3/99 CORRECTED BASE DRAG TABLE ARRAY DIMENSIONS 
C
      DIMENSION AM(24),CP(24),SBSC(13),PCPB(13),IROUT(2)
C
C.....NSWC METHOD ADDED 2/17/93 TO IMPROVE FLARE BASE PRESSURE PREDICTIONS
C
C.....ADDITIONAL HIGH MACH (M=5-9) POINTS ADDED 3/13/95
C
      DIMENSION AMF(22),CPF(110),FLARE(5),
     1          CPF1(22),CPF2(22),CPF3(22),CPF4(22),CPF5(22)
      EQUIVALENCE (CPF(1),CPF1(1)),(CPF(23),CPF2(1)),
     1            (CPF(45),CPF3(1)),(CPF(67),CPF4(1)),
     2            (CPF(89),CPF5(1))
      DATA AMF / 0.0,.2,.4,.6,.8,.9,1.0,1.1,1.2,1.4,1.6,2.0,2.4,
     1           3.0,3.5,4.0,5.,6.,7.,8.,9.,10.0/
      DATA FLARE / 0.0,5.,10.,15.,90./
      DATA CPF1 / 0.137,
     1     .137,.137,.136,.134,.132,.1825,.203,.193,.1834,.1727,
     2     .147,.123,.095,.078,.065,
     3     .04,.0278,.0204,.0156,.0123,.01/
      DATA CPF2 / 0.188,
     1     .190,.194,.196,.208,.225,.267,.276,.273,.235,.210,.165,
     2     .133,.098,.082,.065,
     3     .04,.0278,.0204,.0156,.0123,.01/
      DATA CPF3 / 0.220,
     1     .222,.224,.225,.237,.273,.339,.350,.343,.288,.239,.180,
     2     .143,.104,.084,.065,
     3     .04,.0278,.0204,.0156,.0123,.01/
      DATA CPF4 / 0.288,
     1     .290,.292,.295,.314,.367,.457,.470,.459,.371,.280,.192,
     2     .153,.114,.086,.065,
     3     .04,.0278,.0204,.0156,.0123,.01/
      DATA CPF5 / 0.288,
     1     .290,.292,.295,.314,.367,.457,.470,.459,.371,.280,.192,
     2     .153,.114,.086,.065,
     3     .04,.0278,.0204,.0156,.0123,.01/
C
      DATA AM / 0.,.5,.8,.9,.95,1.05,1.10,1.15,1.25,1.5,1.8,2.0,
     1          2.2,2.5,2.8,3.0,3.5,4.0,5.,6.,7.,8.,9.,10.0/
      DATA CP / .137,.137,.134,.132,.155,.210,.203,.196,.190,
     1     .179,.160,.147,.135,.117,.104,.095,.078,.065,
     2     .04,.0278,.0204,.0156,.0123,.01/
C
      CABASE=0.
      IF(DFWD .LE. 0. .OR. DBASE .LE. 0.)GO TO 1000
C
      IF(MACH .LE. 10.)CALL LNTRP(AM,CP,24,MACH,CPBASE)
      IF(MACH .GT. 10.)CPBASE=1./MACH**2
C
C... BOATTAIL CORRECTION
C
      CORR=DBASE/DFWD
C
C... FLARE CPB WITHOUT EXTRAPOLATION ON FLARE ANGLE (15 DEG MAX)
C
      IF(CORR.GT.1.) THEN
        CORR=1.
        FLRANG=ATAN2((DBASE-DFWD)/2.,LAFT)*RAD
        CALL MVLOOK(1,5,22,110,1,FLARE,AMF,CPF,MACH,FLRANG,1,CPBASE)
      ENDIF
C
      ANNULR=DBASE**2-DEXIT**2
      IF(ANNULR .LT. 0.)ANNULR=0.
      CABASE=CORR*CPBASE*PI*ANNULR/4./SREF
C
 1000 CONTINUE
C
      RETURN
      END
