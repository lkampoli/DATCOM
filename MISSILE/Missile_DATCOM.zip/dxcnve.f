      FUNCTION DXCNVE(NPTS,X,A,B)
C
C** FUNCTION TO COMPUTE THE VOLUME CENTROID OF AN ELLIPTICAL SEGMENT
C
C** INPUTS
C
C   NPTS -- NUMBER OF INPUT POINTS
C      X -- X STATION
C      A -- HORIZONTAL AXIS AT EACH X STATION
C      B -- VERTICAL AXIS AT EACH X STATION
C
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      DIMENSION TA(100), TB(100), TX(100), XMAJOR(50), XMINOR(50)
      DIMENSION X(NPTS),A(NPTS),B(NPTS),XSTA(50)
      LOGICAL SWITCH
C
C** STORE INPUT PARAMETERS TO BE MANIPULATED
C
      SWITCH=.FALSE.
C
      DO 1000 II=1,NPTS
         IF(A(II) .EQ. B(II))GO TO 1000
         IF(A(II) .LT. B(II))SWITCH=.TRUE.
         GO TO 1010
 1000 CONTINUE
C
 1010 CONTINUE
C
      DO 1020 II=1,NPTS
         IN=II*2-1
         TA(IN)=A(II)
         TB(IN)=B(II)
         IF(SWITCH)TA(IN)=B(II)
         IF(SWITCH)TB(IN)=A(II)
         TX(IN)=X(II)
         TA(IN+1)=-9999.
         TB(IN+1)=-9999.
         TX(IN+1)=-9999.
 1020 CONTINUE
      NPTS2=NPTS*2-1
C
C** LOOK FOR POSSIBLE MAJOR AXIS SHIFT (EX: HORIZ. AXIS TO VERT. AXIS)
C
      DO 1050 J=3,NPTS2,2
         IF(TB(J) .LE. TA(J)) GO TO 1050
C
C** COMPUTE A=B POINT AND PUT IN A DUMMY ARGUMENT (-9999.) LOCATION
C
 1030    SLOPE=(TB(J)-TB(J-2))/(TA(J)-TA(J-2))
         YINT=TB(J)-SLOPE*TA(J)
         XPOS=YINT/(1.-SLOPE)
         TA(J-1)=XPOS
         TB(J-1)=XPOS
         TX(J-1)=TX(J-2)+(TX(J)-TX(J-2))*ABS((TA(J-1)-TA(J-2))/
     1           (TA(J)-TA(J-2)))
C
C** SWITCH REST OF TA & TB PARAMETERS TO REFLECT AXIS SHIFT
C
        DO 1040 K=J,NPTS2,2
           TEMP=TA(K)
           TA(K)=TB(K)
           TB(K)=TEMP
 1040   CONTINUE
 1050 CONTINUE
C
C** REMOVE DUMMY ARRAY ELEMENTS (-9999.)
C
      NN=0
      DO 1060 M=1,NPTS2
         IF(TA(M) .EQ. -9999.) GO TO 1060
         NN=NN+1
         XMAJOR(NN)=TA(M)
         XMINOR(NN)=TB(M)
         XSTA(NN)=TX(M)
 1060 CONTINUE
C
C
      VOLTOT=0.0
      DXCNVE=0.0
      DCNV=0.0
C
      DO 1070 I=2,NN
         DVOLM=PI*(XSTA(I)-XSTA(I-1))*(XMAJOR(I)*XMINOR(I)
     1         -.5*((XMINOR(I)-XMINOR(I-1))*XMAJOR(I)+(XMAJOR(I)-
     2         XMAJOR(I-1))*XMINOR(I))+1./3.*(XMINOR(I)-XMINOR(I-1))*
     3         (XMAJOR(I)-XMAJOR(I-1)))
         VOLTOT=VOLTOT+DVOLM
         DCNV=PI/12.*(XSTA(I)-XSTA(I-1))**2*
     1        (6.*XMAJOR(I)*XMINOR(I)-2.*XMINOR(I)*
     2        (XMAJOR(I)-XMAJOR(I-1))-2.*XMAJOR(I)*
     3        (XMINOR(I)-XMINOR(I-1))+(XMAJOR(I)-XMAJOR(I-1))*
     4        (XMINOR(I)-XMINOR(I-1)))
         DXCNVE=DXCNVE+DCNV+DVOLM*XSTA(I-1)
 1070 CONTINUE
      DXCNVE=DXCNVE/VOLTOT
      RETURN
      END
