      SUBROUTINE BDCNAB(MACH,DBASE,DMAX,CNAB)
C
C***  SUBROUTINE TO COMPUTE THE INCREMENTAL NORMAL FORCE SLOPE
C***  DUE TO A CONICAL BOATTAIL ON AXISYMMETRIC BODIES
C***  METHOD FROM NSWC-TR-81-156, P.110 (EMPIRICAL CORRELATION)
C
C***  INPUTS
C
C   MACH -- FREE STREAM MACH NUMBER
C  DBASE -- BODY BASE DIAMETER
C   DMAX -- MAXIMUM BODY DIAMETER
C
C***  OUTPUT
C
C   CNAB -- INCREMENTAL NORMAL FORCE SLOPE, PER RADIAN
C
      DIMENSION TCNASB(10),TCNASP(10),BETASB(10),BETASP(10)
C
      REAL MACH
C
      DATA TCNASB /3.35,3.48,3.60,3.65,3.50,2.60,1.75,1.46,
     1             1.35,1.28/
      DATA TCNASP /3.35,3.00,2.56,2.25,1.83,1.30,1.02,0.95,
     1             0.85,0.75/
      DATA BETASB/0.,.10,.20,.30,.45,.60,.75,.85,.925,1.00/
      DATA BETASP/0.,.25,.50,.65,.82,1.00,1.25,1.50,2.00,2.50/
C
C***    COMPUTE BETA (MACH SIMILARITY PARAMETER)
C
        BETA=SQRT(ABS(MACH**2-1.))
C
C***    SELECT SUBSONIC OR SUPERSONIC TABLE
C
C     * IF THE MACH NUMBER IS GREATER THAN UNITY
           IF(MACH .GT. 1.)GO TO 1000
           GO TO 1010
C       THEN LINEARILY INTERPOLATE SUPERSONIC TABLE
 1000      CONTINUE
           CALL LNTRP(BETASP,TCNASP,10,BETA,CNAF)
           GO TO 1020
C       ELSE LINEARILY INTERPOLATE SUBSONIC TABLE
 1010      CONTINUE
           CALL LNTRP(BETASB,TCNASB,10,BETA,CNAF)
 1020   CONTINUE
C     * ENDIF
C
C***    MULTIPLY BY -(1.-(DBASE/DMAX)**2) TO GET CNAB
C
        CNAB=CNAF*((DBASE/DMAX)**2-1.)
C
      RETURN
      END
