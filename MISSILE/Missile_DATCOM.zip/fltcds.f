      SUBROUTINE FLTCDS
C
C***  TOP LEVEL ROUTINE TO COMPUTE THE REQUIRED FLIGHT CONDITIONS
C
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      COMMON /TOTALC/ BALPHA(20),BBETA(20),BPHI(20),ALPTOT(20)
      COMMON /CONST/  PI,RAD,UNUSED,KAND
C
      REAL NALPHA,NMACH,MACH
C
      DIMENSION ARRAY(9)
C
      DATA RCON/1716.0/
      DATA GAMMA/1.400/
      DATA RNCON/1.2527E6/
C
C***  COMPUTE FLIGHT CONDITIONS FOR ALL MACH NUMBERS
C
      NNMACH=NMACH+0.5
C
      DO 1030 I=1,NNMACH
C
C***  CHECK FOR MACH AND REYNOLDS NUMBER INPUTS, SKIP IF PRESENT
C
         IF(MACH(I).NE.UNUSED .AND. REN(I).NE.UNUSED)GO TO 1030
C
C  IS ALTITUDE INPUT
C
         IF(ALT(I) .EQ. UNUSED)GO TO 1010
C
C  ALTITUDE INPUT, IS MACH NUMBER
C
         IF(MACH(I) .EQ. UNUSED)GO TO 1000
C
C***  BOTH ALTITUDE AND MACH NUMBER INPUT
C
         CALL ATMOS(ALT(I),ARRAY)
         IF(TINF(I) .EQ. UNUSED)TINF(I)=ARRAY(8)
         IF(PINF(I) .EQ. UNUSED)PINF(I)=ARRAY(4)
         VINF(I)=MACH(I)*SQRT(GAMMA*RCON*TINF(I))
         REN(I)=RNCON*PINF(I)*MACH(I)*(TINF(I)+198.6)/TINF(I)**2
      GO TO 1030
C
 1000 CONTINUE
C
C  IS VELOCITY INPUT
C
         IF(VINF(I) .EQ. UNUSED)GO TO 1030
C
C***  BOTH VELOCITY AND ALTITUDE INPUT
C
         CALL ATMOS(ALT(I),ARRAY)
         IF(TINF(I) .EQ. UNUSED)TINF(I)=ARRAY(8)
         IF(PINF(I) .EQ. UNUSED)PINF(I)=ARRAY(4)
         MACH(I)=VINF(I)/SQRT(GAMMA*RCON*TINF(I))
         REN(I)=RNCON*PINF(I)*MACH(I)*(TINF(I)+198.6)/TINF(I)**2
      GO TO 1030
C
 1010 CONTINUE
C
C  ALTITUDE NOT INPUT, IS FREESTREAM PRESSURE AND TEMPERATURE
C
         IF(PINF(I) .EQ. UNUSED .OR. TINF(I) .EQ. UNUSED)GO TO 1030
C
C  IS MACH NUMBER INPUT
C
         IF(MACH(I) .EQ. UNUSED)GO TO 1020
C
C***  MACH NUMBER, TEMPERATURE AND PRESSURE INPUT
C
         VINF(I)=MACH(I)*SQRT(GAMMA*RCON*TINF(I))
         REN(I)=RNCON*PINF(I)*MACH(I)*(TINF(I)+198.6)/TINF(I)**2
      GO TO 1030
C
 1020 CONTINUE
C
C  IS FREESTREAM VELOCITY INPUT
C
         IF(VINF(I) .EQ. UNUSED)GO TO 1030
C
C***  VELOCITY, TEMPERATURE AND PRESSURE INPUT
C
         MACH(I)=VINF(I)/SQRT(GAMMA*RCON*TINF(I))
 1030 CONTINUE
C
C***  COMPUTE TOTAL ANGLE OF ATTACK, OR ALPHA/BETA
C
      NALP=NALPHA+0.5
      DO 1040 I=1,NALP
         BALPHA(I)=ALPHA(I)
         ALPTOT(I)=ALPHA(I)
         BBETA(I)=BETA
         BPHI(I)=PHI
 1040 CONTINUE
C
      IF(ABS(BETA).LE.UNUSED .AND. ABS(PHI).LE.UNUSED)GO TO 1060
      DO 1050 I=1,NALP
         IF(ABS(PHI) .GT. UNUSED)CALL ALPBET(BALPHA(I),ALPHA(I),
     1      BBETA(I),PHI)
         IF(ABS(PHI) .LE. UNUSED)CALL ALPBET(ALPHA(I),ALPTOT(I),BETA,
     1           BPHI(I))
 1050 CONTINUE
C
 1060 CONTINUE
C
      RETURN
      END
