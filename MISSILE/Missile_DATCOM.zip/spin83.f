      SUBROUTINE SPIN83(LN,BL,LAFT,D,FM,VCG,OR,SREF,LREF)
C
C***  SUBROUTINE TO COMPUTE DERIVATIVES FOR SPIN STABILIZED BODIES
C
C  MODIFIED BY F. SAWYER, WL/FIGC
C
C***  REFERENCE: ARDC SPINNER CODE
C
C***  NOTE:  CNP/SIN(ALPHA)=CNP1+CNP3*(SIN(ALPHA)**2)+CNP5*
C            (SIN(ALPHA)**4)
C
C***  CAUTION:  USE OF THIS POLYNOMIAL MUCH BEYOND THE  FIRST POSITIVE
C     EXTREMUM USUALLY BECOMES UNPHYSICAL - CNP FOR MOST PROJECTILES
C     IS EXPECTED TO REMAIN POSITIVE AT LARGER ALPHA.
C
C***  INPUT PARAMETERS
C       LN    - THE NOSE LENGTH (FT)
C       D     - BODY CYLINDER DIAMETER (FT)
C       BL    - TOTAL BODY LENGTH (FT)
C       LAFT  - BOATTAIL LENGTH (FT)
C       FM    - FREE STREAM MACH NUMBER
C       VCG   - DISTANCE FROM THE BODY VERTEX TO THE CENTER OF
C               GRAVITY (FT)
C       OR    - OGIVE FRUSTRUM RADIUS (CAL)
C       SREF  - USER DEFINED REFERENCE AREA (FT)
C       LREF  - USER DEFINED REFERENCE LENGTH (FT)
C
C***  OUTPUT VARIABLES
C       CLLP  - ROLLING MOMENT DUE TO ROLL RATE (ROLL DAMPING) (PER RAD)
C       CYP   - SIDE FORCE DUE TO ROLL RATE (PER RADIAN)
C       CNP1  - ZERO YAW MAGNUS COEFFFICIENT DERIVATIVE PER SIN ALPHA
C       CNP3  - CUBIC MAGNUS MOMENT COEFFICIENT PER (SIN ALPHA)**3
C       CNP5  - QUINTIC MAGNUS MOMENT COEFFICIENT PER (SIN ALPHA)**5
C       CNPY5 - 5 DEGREE SECANT SLOPE OF MAGNUS MOMENT COEFFICENT
C               DERIVATIVE (AT FIVE DEGREE YAW) PER SIN ALPHA
C
C***  CALLING ROUTINES
C       BDAMP
C
      COMMON /DBODY/ DUM(360),CLLP,CYP,CNP1,CNP3,CNP5,CNPY5,DUM1(40)
      REAL LAFT,LREF,LN
      DIMENSION CMACH(17)
      DIMENSION XE1(17),XE2(17),XE3(17),XE4(17)
      DIMENSION XE5(17),XG1(17)
      DIMENSION XG2(17),XG3(17),XG4(17),XG5(17),XG6(17)
      DIMENSION CYPA(17),CNPA1(17),CNPA3(17),CNPA5(17),CLP(17),
     1          CNPAA5(17),IROUT(2)
C
      DATA CMACH / 0.01,0.6,0.8,0.9,0.95,1.0,1.05,1.1,1.2,1.35,1.5,1.75,
     1   1          2.0,2.5,3.0,4.0,5.0 /
      DATA XE1 /-.16,-.16,-.16,-.18,-.23,-.21,-.19,-.18,-.16,-.16,-.16,
     1          -.16,-.16,-.16,-.16,-.16,-.16/
      DATA XE2 /1.8 ,1.8 ,2.0 ,2.4 ,2.7 ,2.8 ,2.9 ,
     1          2.95,2.98,3.00,3.01,3.02,3.03,3.04,3.05,3.05,3.05/
      DATA XE3 /2.5 ,2.5 ,2.6 ,2.75,2.9 ,3.0 ,3.06,
     1          3.07,3.08,3.08,3.08,3.09,3.09,3.09,3.09,3.09,3.09/
      DATA XE4 /2.9,2.9,3.0,3.05,3.1,3.1,3.1,
     1          3.1,3.1,3.1,3.10,3.1,3.1,3.1,3.1,3.1,3.1/
      DATA XE5 /0.0,0.0,0.0,0.0,0.0,0.0,0.0,0.0,0.0,0.0,0.1,0.2,
     1          0.3,0.3,0.27,0.25,0.20/
      DATA XG1/-.0328,-.0327,-.0311,-.0295,-.02885,-.0277,-.0272,-.0267,
     1   -.0264,-.0254,-.0244,-.0238,-.0232,-.0225,-.0219,-.0216,-.0213/
      DATA XG2/-.0063,-.0063,-.0063,-.0063,-.00638,-.00594,-.00593,
     1  -.00593,-.00591,-.00557,-.00522,-.00455,-.00404,-.00404,-.00404,
     2  -.00404,-.00404/
      DATA XG3/.01229,.01229,.01229,.01229,.01229,.01097,.01039,.01039,
     1 .01036,.00968,.00898,.00814,.00717,.00688,.00688,.00688,.00688/
      DATA XG4/-.004,-.004,-.004,-.004,-.004,-.004,-.004,-.004,-.004,
     1   -.004,-.004,-.004,-.004,-.004,-.004,-.004,-.004/
      DATA XG5/-.2335,-.2335,-.2335,-.2132,-.2030,-.2130,-.2112,-.2112,
     1  -.1927,-.1722,-.1527,-.1131,-.0827,-.0827,-.0827,-.0827,-.0827/
      DATA XG6/.01556,.01556,.01556,.01418,.01349,.01279,.01298,.01298,
     1  .01131,.00918,.00705,.00352,.00305,.002,.001,0.0,0.0/
C
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C     INITIALIZE DERIVATIVES TO ZERO
C
      CLLP=0.
      CYP=0.
      CNP1=0.
      CPN3=0.
      CPN5=0.
      CNPY5=0.
C
C***  SETUP CONSTANTS
C
      VCG=VCG/D
      VN=LN/D
      VL=BL/D
      VB=LAFT/D
      IF(VL .LT. 2.5) VL=2.5
      IF(VL .GT. 10.) VL=10.
      IF(VN .LT. 1.2) VN=1.2
      IF(VN .GT. 5.5) VN=5.5
      IF(VB .GT. 2.0) VB=2.0
      BD=1.
      AR=D*D*0.7854
      CVL=VL
      CXCL=VL-VN-VB-1.5
      CVN=VN-2.5
      CVB=VB
C
C***  SET UP TABLES OF CYPA, CNPA1, CNPA3, CNPA5, CNPAA5, CLP
C
      DO 1040 J=1,17
C
C***  COMPUTE CYPA
C
        CYP=XE1(J)*CVL
        CYPA(J)=CYP-0.1*CVB
C
C***  COMPUTE CPF AND CNPA AT A=1.0
C
        DCPF=0.0
        IF(VL .LE. 6.0) GO TO 1020
          DCPF=(VL-6.0)*XE5(J)
 1020   CONTINUE
        CNPAN=-CYP*(XE2(J)+0.55*CXCL+0.8*CVN)+1.0*CVL/4.7*CVB
        CPF=-CNPAN/CYPA(J)
        CPF=CPF+DCPF
        CNPA=(VCG-CPF )*CYPA(J)
C
C***  COMPUTE CPF AND CNPA AT A=2.0
C
        CNPAN=-CYP*(XE3(J)+0.55*CXCL+0.8*CVN)+1.0*CVL/4.7*CVB
        CPFA2=-CNPAN/CYPA(J)
        CPFA2=CPFA2+DCPF
        CNPAA2=(VCG-CPFA2)*CYPA(J)
C
C***  COMPUTE CPF AND CNPA AT A=5.0
C
        CNPAN=-CYP*(XE4(J)+0.55*CXCL+0.8*CVN)+1.0*CVL/4.7*CVB
        CPFA5=-CNPAN/CYPA(J)
        CPFA5=CPFA5+DCPF
        CNPAA5(J)=(VCG-CPFA5)*CYPA(J)
C
C***  COMPUTE CLP
C
        VLCLP=VL-3.49
        VNCLP=VN-1.71
        IF(VN .LT. 1.1) VNCLP=-.61
        IF(VN .GT. 3.5) VNCLP=1.79
        VBCLP=VB
        IF(VB .GT. 0.7) VBCLP=0.7
        VOR=VN*VN/OR-0.5
        VORCLP=VOR
        VBDCLP=BD-1.04
        CLP(J)=XG1(J)+XG2(J)*VLCLP+XG3(J)*VNCLP+XG4(J)*VBCLP+XG5(J)*
     1  VBDCLP+XG6(J)*VORCLP
        XMAG1=CNPAA5(J)-CNPA
        CNPA5(J)=2.1502E4*(CNPAA5(J)-7.9834*CNPAA2+6.9835*CNPA)
        CNPA3(J)=137.15*XMAG1-CNPA5(J)*7.9009E-3
        CNPA1(J)=CNPA-3.046E-4*CNPA3(J)-9.2773E-8*CNPA5(J)
 1040 CONTINUE
C
C***  USE THE VARIABLE TABLES GENERATED TO DO LINEAR INTERPOLATION
C     AT THE MACH NUMBER UNDER CONSIDERATION
C
      CALL LNTRP (CMACH,CLP,17,FM,CLLP)
      CALL LNTRP (CMACH,CYPA,17,FM,CYP)
      CALL LNTRP (CMACH,CNPA1,17,FM,CNP1)
      CALL LNTRP (CMACH,CNPA3,17,FM,CNP3)
      CALL LNTRP (CMACH,CNPA5,17,FM,CNP5)
      CALL LNTRP (CMACH,CNPAA5,17,FM,CNPY5)
C
C***  CHANGE REFERENCE QUANTITIES TO RATE*(LREF/V)
C
      CLLP=CLLP*.5*(AR*D*D)/(SREF*LREF*LREF)
      CYP=CYP*.5*(AR*D)/(SREF*LREF)
C
C***  POLYNOMIAL FIT TO SECANT SLOPE OF SIDE MOMENT SPIN DERIVATIVE
C     FOLLOWING CODE NOT PRESENTLY USED
C     CNP/SIN(ALPHA)=CNP1+CNP3*(SIN(ALPHA))**2+CNP5*(SIN(ALPHA))**4
C
      CNP1=CNP1*.5*(AR*D*D)/(SREF*LREF*LREF)
      CNP3=CNP3*.5*(AR*D*D)/(SREF*LREF*LREF)
      CNP5=CNP5*.5*(AR*D*D)/(SREF*LREF*LREF)
      CNPY5=CNPY5*.5*(AR*D*D)/(SREF*LREF*LREF)
C
      RETURN
      END
