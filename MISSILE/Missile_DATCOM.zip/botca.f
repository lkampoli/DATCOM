      SUBROUTINE BOTCA(ISHAPE, SREF, BOTLEN, MISDIA, BASDIA, FMACH,
     1                 XSEPA0, PBASE, BOTDCA)
C
C     THIS SUBROUTINE CALCULATES THE INCREMENTAL AXIAL FORCE ON A
C     BOATTAIL DUE TO FLOW SEPARATION.  PRANDTL-MEYER EXPANSION IS
C     USED TO DETERMINE THE SURFACE PRESSURE AHEAD OF SEPARATION,
C     AND THE BASE PRESSURE IS USED BEHIND SEPARATION.
C
C  WRITTEN BY A. JENN, MCDONNELL DOUGLAS
C
      REAL PMACH(41), PMU(41), MISDIA
C
      PARAMETER (GAMMA = 1.4)
C
C     DATA STATEMENTS FOR PRANDTL MEYER FUNCTION
      DATA PMACH / 2.00,  2.10,  2.20,  2.30,  2.40,  2.50,  2.60,
     1             2.70,  2.80,  2.90,  3.00,  3.10,  3.20,  3.30,
     2             3.40,  3.50,  3.60,  3.70,  3.80,  3.90,  4.00,
     3             4.10,  4.20,  4.30,  4.40,  4.50,  4.60,  4.70,
     4             4.80,  4.90,  5.00,  5.10,  5.20,  5.30,  5.40,
     5             5.50,  5.60,  5.70,  5.80,  5.90,  6.00 /
      DATA PMU   /26.38, 29.10, 31.73, 34.28, 36.75, 39.12, 41.41,
     1            43.62, 45.75, 47.79, 49.76, 51.65, 53.47, 55.22,
     2            56.91, 58.53, 60.09, 61.60, 63.04, 64.44, 65.78,
     3            67.08, 68.33, 69.54, 70.71, 71.83, 72.92, 73.97,
     4            74.99, 75.97, 76.92, 77.84, 78.73, 79.60, 80.43,
     5            81.24, 82.03, 82.80, 83.54, 84.26, 84.96 /
C
C     DETERMINE VALUE OF PI
      PI = ACOS(-1.0)
C
C     CALCULATE THE MISSILE CROSS SECTIONAL AREA
      ARAMIS = PI * MISDIA**2 / 4.0
C
C     CHECK FOR BODY SHAPE
      IF (ISHAPE .EQ. 1 .OR. BASDIA .GE. MISDIA) THEN
C
C        CYLINDRICAL BOATTAIL
C
C        NO AXIAL FORCE CONTRIBUTION
         BOTDCA = 0.0
C
      ELSE IF (ISHAPE .EQ. 2) THEN
C
C        CONICAL BOATTAIL
C
C        CALCULATE BOATTAIL DIAMETER AT SEPARATION
         DBSEP = BASDIA + (MISDIA - BASDIA) * XSEPA0 / BOTLEN
C
C        CALCULATE AXIAL PROJECTION SEPARATED SURFACE AREA
         ARASEP = PI * (DBSEP**2 - BASDIA**2) / 4.0
C
C        CALCULATE THE FREESTREAM PRANDTL MEYER ANGLE
         CALL TABLOK(1,41,41,41,PMACH,PMU,FMACH,FMU,0,IERR)
C
C        CALCULATE THE BOATTAIL ANGLE
         DR = (MISDIA - BASDIA) / 2.0
         BETAM = ATAN(DR / BOTLEN) * 180.0 / PI
C                               
C        CALCULATE THE PRANDTL MEYER ANGLE ON THE BOATTAIL
         BMU = FMU + BETAM
C
C        DETERMINE THE MACH NUMBER ON THE BOATTAIL
         CALL TABLOK(1,41,41,41,PMU,PMACH,BMU,BMACH,0,IERR)
C
C        CALCULATE THE BOATTAIL TO FREESTREAM STATIC PRESSURE RATIO
         PTOPF = (1.0 + (GAMMA - 1.0) / 2.0 * FMACH**2)**(GAMMA / 
     1           (GAMMA - 1.0))
         PTOPB = (1.0 + (GAMMA - 1.0) / 2.0 * BMACH**2)**(GAMMA / 
     1           (GAMMA - 1.0))
         PBOAT = PTOPF / PTOPB
C
C        CALCULATE CP ON THE BOATTAIL
         CPBOAT = 2.0 / (GAMMA * FMACH**2) * (PBOAT - 1.0)
C
C        CALCULATE CP ON THE BASE
         CPBASE = 2.0 / (GAMMA * FMACH**2) * (PBASE - 1.0)
C
C        CALCULATE UNSEPARATED CA FOR THE SEPARATED REGION
         CABU = -CPBOAT * ARASEP / ARAMIS
         CABU=CABU*ARAMIS/SREF
C
C        CALCULATE SEPARATED CA FOR THE SEPARATED REGION
         CABS = -CPBASE * ARASEP / ARAMIS
         CABS=CABS*ARAMIS/SREF
C
C        CALCULATE INCREMENTAL CA DUE TO SEPARATION
         BOTDCA = CABS - CABU
C
      ELSE
C
C        OGIVE BOATTAIL
C
C        CALCULATE BOATTAIL RADIUS OF CURVATURE
         DR = (MISDIA - BASDIA) / 2.0
         RCUR = (DR**2 + BOTLEN**2) / (2.0 * DR)
C
C        DETERMINE LENGTH OF UNSEPARATED BOATTAIL
         BOTLS = BOTLEN - XSEPA0
C
C        BOATTAIL DIAMETER AT SEPARATION
         P = SQRT(RCUR**2 - BOTLEN**2)
         DBSEP = BASDIA + 2.0 * (SQRT(RCUR**2 - BOTLS**2) - P)
C
C        CALCULATE AXIAL PROJECTION SEPARATED SURFACE AREA
         ARASEP = PI * (DBSEP**2 - BASDIA**2) / 4.0
C
C        CALCULATE THE FREESTREAM PRANDTL MEYER ANGLE
         CALL TABLOK(1,41,41,41,PMACH,PMU,FMACH,FMU,0,IERR)
C
C        DEFINE PANEL LENGTH FOR SEPARATED REGION OF BOATTAIL
         PERSEP = XSEPA0 / BOTLEN       
         NPAN   = NINT(20.0 * PERSEP + 1.0)  
C
C        DETERMINE INCREMENTAL PANEL LENGTH
         DXP = XSEPA0 / NPAN
C
C        INITIALIZE VARIABLES FOR AXIAL FORCE SUMMATION
         XLAS = BOTLS
         RLAS = DBSEP / 2.0
         CABU = 0.0
C
C        LOOP FOR EACH UNSEPARATED PANEL ON THE BOATTAIL
         DO 100 IPAN = 1, NPAN
C
C           DETERMINE THE PANEL MID AND END POINT
            XEND = XLAS + DXP
            XMID = (XEND + XLAS) / 2.0
C
C           DETERMINE THE ANNUAR RADIUS AND AXIAL PROJECTION
            REND = BASDIA / 2.0 + (SQRT(RCUR**2 - XEND**2) - P)
            ARARNG = PI * (RLAS**2 - REND**2)
C
C           SAVE PANEL AXIAL LOCATION AND RADIUS FOR NEXT PANEL
            RLAS = REND
            XLAS = XEND
C
C           DETERMINE THE ANGLE AT THE PANEL MIDPOINT
            BETAM = ATAN(XEND / SQRT(RCUR**2 - XEND**2)) * 180.0 / PI
C
C           CALCULATE THE PRANDTL MEYER ANGLE ON THE PANEL
            BMU = FMU + BETAM
C
C           DETERMINE THE MACH NUMBER ON THE PANEL
            CALL TABLOK(1,41,41,41,PMU,PMACH,BMU,BMACH,0,IERR)
C
C           CALCULATE THE PANEL TO FREESTREAM STATIC PRESSURE RATIO
            PTOPF = (1.0 + (GAMMA - 1.0) / 2.0 * FMACH**2)**(GAMMA / 
     1              (GAMMA - 1.0))
            PTOPP = (1.0 + (GAMMA - 1.0) / 2.0 * BMACH**2)**(GAMMA / 
     1              (GAMMA - 1.0))
            PPAN = PTOPF / PTOPP
C
C           CALCULATE CP ON THE PANEL
            CPPAN = 2.0 / (GAMMA * FMACH**2) * (PPAN - 1.0)
C
C           INCREMENT THE AXIAL FORCE COEFFICIENT
            CABU = CABU - CPPAN * ARARNG / ARAMIS
C
  100    CONTINUE
C
         CABU=CABU*ARAMIS/SREF
C
C        CALCULATE CP ON THE BASE
         CPBASE = 2.0 / (GAMMA * FMACH**2) * (PBASE - 1.0)
C
C        CALCULATE SEPARATED CA FOR THE SEPARATED REGION
         CABS = -CPBASE * ARASEP / ARAMIS
         CABS=CABS*ARAMIS/SREF
C
C        CALCULATE INCREMENTAL CA DUE TO SEPARATION
         BOTDCA = CABS - CABU
C
      END IF
C
      RETURN
      END
