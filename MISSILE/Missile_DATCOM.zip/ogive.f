      SUBROUTINE OGIVE(AA,NX,X,R,BNOSE,TRUNC,LENGTH,DIAM,DISCON)
C
C***  COMPUTE THE CHARACTERISTICS OF A OGIVE SEGMENT
C
C***  INPUTS
C
C     NX -- NUMBER OF INPUT POINTS
C      X -- X STATIONS
C      R -- RADII AT EACH X STATION
C  BNOSE -- FORWARD RADIUS OF SECTION
C  TRUNC -- TRUNCATED FLAG (.TRUE. IF TRUNCATED NOT BLUNTED)
C LENGTH -- SECTION LENGTH
C   DIAM -- SECTION BASE DIAMETER
C
C***  OUTPUTS
C
C     AA -- ARRAY OF CHARACTERISTICS TO BE COMPUTED
C           AA(1)=PLANFORM AREA
C           AA(2)=WETTED AREA
C           AA(3)=VOLUME
C           AA(4)=VOLUME CENTROID
C           AA(5)=PLANFORM CENTROID
C           AA(6)=FINENESS RATIO
C           AA(7)=THEORETICAL LENGTH
C           AA(8)=THEORETICAL FINENESS RATIO
C DISCON -- ELEMENTS WHICH HAVE DISCONTINUOUS SLOPES
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      DIMENSION AA(8),X(50),R(50),XX(101),RR(101),DISCON(20)
C
      REAL NX,LENGTH
      LOGICAL TRUNC
C
C***  SKIP PRIMARY CALCULATIONS FOR HEMISPHERICAL CAP
C
      IF(2.*BNOSE .GE. DIAM .AND. .NOT. TRUNC)GO TO 1090
C
C***  COMPUTE GEOMETRY AT 101 POINTS
C
      NPTS=101
      IF(.NOT. TRUNC .AND. BNOSE .GT. UNUSED)NPTS=51
C
      IF(DIAM .GT. 2.*BNOSE)FR=LENGTH/DIAM
      IF(DIAM .LE. 2.*BNOSE)FR=LENGTH/(2.*BNOSE)
C
C  DO NOT DUPLICATE LAST POINT FOR THIS SEGMENT
C
      IF(NX .GT. UNUSED)NX=NX-1.
      NNX=NX+0.5
      XO=0.
      IF(NX .GT. UNUSED)XO=X(NNX+1)
C 
C  INITIALIZE THE X POSITION OF THE BLUNTED NOSE (XT) AND THE START OF THE 
C  FLARE SECTION (XF)
C
      XT=0.
      XF=0.
C
C  OGIVAL ARC RADIUS
C
      ETA=ABS((LENGTH*(LENGTH-2.*BNOSE)+(DIAM/2.)**2)/(DIAM-2.*BNOSE))
C
C  COMPUTE ARC IF TRUNCATED
C
      ABSDMB=ABS(DIAM/2.-BNOSE)
      IF(TRUNC)ETA=(LENGTH**2+ABSDMB**2)/(2.*ABSDMB)
C
C***  SURFACE X AND R COORDINATES
C
C  BLUNTED NOSE
C
      ILAST=0
C
      IF(TRUNC .OR. BNOSE .LE. UNUSED)GO TO 1010
C
C  POINT OF TANGENCY OF BLUNTED NOSE
C
      XT=BNOSE*(ETA-LENGTH)/(ETA-BNOSE)
      YT=SQRT(XT*(2.*BNOSE-XT))
C
      IF(DIAM .GT. 2.*BNOSE)THEOL=DIAM*SQRT(ETA/DIAM-0.25)
      IF(DIAM .LE. 2.*BNOSE)THEOL=2.*BNOSE*SQRT(ETA/2./BNOSE-0.25)
C
      DO 1000 I=1,NPTS
         XX(I)=FLOAT(I-1)*XT/FLOAT(NPTS-1)
         RR(I)=SQRT(BNOSE**2-(XX(I)-BNOSE)**2)
         XX(I)=XX(I)+THEOL-LENGTH
 1000 CONTINUE
C
      ILAST=NPTS-1
C
 1010 CONTINUE
C
C  THEORETICAL LENGTH OF SHARP OGIVE
C
      IF(DIAM .GT. 2.*BNOSE)THEOL=DIAM*SQRT(ETA/DIAM-0.25)
      IF(DIAM .LE. 2.*BNOSE)THEOL=2.*BNOSE*SQRT(ETA/2./BNOSE-0.25)
C
      IF(DIAM .GT. 2.*BNOSE)FF=THEOL/DIAM
      IF(DIAM .LE. 2.*BNOSE)FF=THEOL/(2.*BNOSE)
C
C  ON SHAPED SEGMENT
C
C     COMPUTE THE DISTANCE FROM THJE THEORETICAL NOSE TIP TO THE START
C     OF THE FLARE
C
      IF (TRUNC .AND. BNOSE .GT. UNUSED) XF=THEOL-LENGTH
      DO 1040 I=1,NPTS
         L=I+ILAST
         IF(BNOSE .GT. DIAM/2.)GO TO 1020
C
C  HERE FOR NOSES OR FLARES
C
         XX(L)=THEOL-LENGTH+XT+(LENGTH-XT)/FLOAT(NPTS-1)*FLOAT(I-1)
C
         RR(L)=DIAM/2.*(1.+2.*(SQRT((FF**2-.25)**2+FF**2*(2.*XX(L)/
     1        THEOL-XX(L)**2/THEOL**2))-(FF**2+.25)))
C
C  IF CHECK IS TO PREVENT THE USE OF THIS CORRECTION FOR NOSES SINCE
C  IT IS INTENDED FOR USE WITH FLARES
C
         IF (NX .GT. UNUSED) XX(L)=XX(L)-XF
C
         GO TO 1030
C
 1020    CONTINUE
C
C  HERE FOR BOATTAILS
C
         XX(L)=LENGTH/FLOAT(NPTS-1)*FLOAT(I-1)
         RR(L)=SQRT(ETA**2-XX(L)**2)-(ETA-BNOSE)
C
 1030    CONTINUE
C
         XX(L)=XX(L)+XO
C
 1040 CONTINUE
C
C***  EXTRACT EVERY TENTH POINT FOR OUTPUT AND SUBSEQUENT CALCS.
C
      II=0
      DO 1050 I=1,101,10
         II=II+1
         L=II+NNX
         X(L)=XX(I)
         R(L)=RR(I)
 1050 CONTINUE
C
C  UPDATE POINT TOTAL
C
      NX=NX+FLOAT(II)
C
C  PLANFORM AREA
C
      SP=DSPLAN(101,XX,RR)
C
C  WETTED AREA
C
      SW=DSWET(101,XX,RR)
C
C  VOLUME
C
      VOL=DVOL(101,XX,RR)
C
C  VOLUME CENTROID
C
      XCENV=DXCENV(101,XX,RR)
C
C  PLANFORM AREA CENTROID
C
      XCENP=DXCENP(101,XX,RR)
C
C  STORE FOR RETURN TO CALLING ROUTINE
C
      AA(1)=SP
      AA(2)=SW
      AA(3)=VOL
      AA(4)=XCENV
      AA(5)=XCENP
      AA(6)=FR
      AA(7)=THEOL
      AA(8)=FF
C
C  SET DISCONTINUOUS POINTS
C
      DO 1060 I=1,20
         JJ=I
         IF(DISCON(I).EQ.UNUSED)GO TO 1070
 1060 CONTINUE
      GO TO 1080
C
C  END OF SEGMENT IS DISCONTINUOUS IF BNOSE .GT. DIAM/2. OR
C  FIRST OF SEGMENT IS DISCONTINUOUS IF TRUNCATED
C
 1070    IF(TRUNC .AND. 2.*BNOSE .LE. DIAM)DISCON(JJ)=NX-FLOAT(II)+1
         NNN=0
         IF(TRUNC .AND. 2.*BNOSE .LE. DIAM)NNN=1
         IF(BNOSE .GT. DIAM/2.)DISCON(JJ+NNN)=NX
 1080 CONTINUE
C
      GO TO 1100
C
C***  HERE FOR SPHERICAL CAP
C
 1090 CONTINUE
C
      IF(LENGTH .GT. BNOSE)LENGTH=BNOSE
C
      CALL SPHERE(AA,NX,X,R,DIAM)
C
 1100 CONTINUE
C
      RETURN
      END
