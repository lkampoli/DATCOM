      SUBROUTINE GEOAXI
C
C***  SUBROUTINE TO CALCULATE THE GEOMETRIC PARAMETERS
C***  OF AXISYMMETRIC BODIES
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      REAL LREF,LATREF
C
C
      COMMON /GEOBOD/ SPNOSE,SWNOSE,VOLNOS,XCVNOS,XCPNOS,FRNOSE,
     1  THEOLN,THEOFN,SPCENT,SWCENT,VOLCEN,XCVCEN,XCPCEN,FRCENT,
     2  THEOLC,THEOFC,SPAFT ,SWAFT ,VOLAFT,XCVAFT,XCPAFT,FRAFT ,
     3  THEOLA,THEOFA,SPLAN ,SWET  ,VOL   ,XCENTV,XCENTP,FR    ,
     4                DBASE ,SBASE ,DMAX  ,SMAX  ,LTOTAL,GDUM(12)
C
      COMMON /ABODIN/ NX,XO,X(50),R(50),TNOSE,LNOSE,DNOSE,BNOSE,
     1                TRUNC,LCENTR,DCENTR,TAFT,LAFT,DAFT,
     2                POWER,DISCON(20),ADUM(748)
      REAL NX,LNOSE,LCENTR,LAFT,LTOTAL
C
      COMMON /XRBLNT/ XTEMP(50),RTEMP(50),NXTEMP,VNTEMP,SWTEMP,ITYPE
      REAL NXTEMP
C
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      COMMON /LOGIC/ LFLAGS(18),LDIMIN,LDIMFT,LDIMCM,LDIMM,LDUM2(6)
      LOGICAL        LFLAGS    ,LDIMIN,LDIMFT,LDIMCM,LDIMM
C
      DIMENSION GEOB(35),DUMMY(20)
      EQUIVALENCE (GEOB(1),SPNOSE)
C
      LOGICAL TRUNC
C
C***  SCALE THE INPUT GEOMETRY
C
      IF(SCALE .EQ. UNUSED .OR. SCALE .EQ. 1.)GO TO 1010
      IF(XO .NE. UNUSED)XO=XO*SCALE
         DO 1000 I=1,50
            IF(X(I) .NE. UNUSED)X(I)=X(I)*SCALE
            IF(R(I) .NE. UNUSED)R(I)=R(I)*SCALE
 1000    CONTINUE
C
         IF(LNOSE .NE. UNUSED)LNOSE=LNOSE*SCALE
         IF(BNOSE .NE. UNUSED)BNOSE=BNOSE*SCALE
         IF(DNOSE .NE. UNUSED)DNOSE=DNOSE*SCALE
         IF(LCENTR .NE. UNUSED)LCENTR=LCENTR*SCALE
         IF(DCENTR .NE. UNUSED)DCENTR=DCENTR*SCALE
         IF(LAFT .NE. UNUSED)LAFT=LAFT*SCALE
         IF(DAFT .NE. UNUSED)DAFT=DAFT*SCALE
C
 1010 CONTINUE
C
      NXTEMP=UNUSED
C
C  CHECK FOR INPUT OPTION
C
      IF(NX .GT. UNUSED)GO TO 1120
C
C***  HERE FOR OPTION 1 (SHAPES DEFINED)
C
C DEFAULTS ...
C
      IF(LNOSE .NE. UNUSED .AND. TNOSE .EQ. UNUSED)TNOSE=1.
      IF(LAFT .NE. UNUSED .AND. TAFT .EQ. UNUSED)TAFT=0.
      IF(LNOSE .NE. UNUSED .AND. TNOSE .GT. 4.)TNOSE=0.
      IF(LAFT  .NE. UNUSED .AND. TAFT  .GT. 1.)TAFT=0.
C
      IF(LNOSE .LE. UNUSED)GO TO 1080
C
C  CHECK FOR PROPER SHAPE DEFINED
C
      IF(TNOSE .NE. 0. .AND. LNOSE .LT. DNOSE/2. .AND. PARTS(18))
     1 WRITE(6,1190)
      IF(TNOSE .NE. 0. .AND. LNOSE .LT. DNOSE/2.)TNOSE=0.
C
C  SET NOSE TYPE
C
      JUMP=TNOSE+1.5
C
C  COMPUTE THE APPROPIATE SHAPE
C  THE FIRST CALL IS TO CALCULATE GEOMETRY FOR A SHARP NOSE MISSILE,
C  WHICH IS USED IN LATER CALCULATIONS TO CALCULATE WAVE DRAG.
C  THE SECOND CALL IS TO CALCULATE THE ACTUAL GEOMETRY OF THE MISSILE,
C  BLUNTED NOSE, TRUNCATED NOSE, OR SHARP NOSE.
C
      GO TO (1030,1040,1050,1060,1060),JUMP
C
 1030 IF (BNOSE.GT.UNUSED) THEN
        CALL CONIC(GEOB(1),NXTEMP,XTEMP,RTEMP,UNUSED,.FALSE.,
     &             LNOSE,DNOSE,DUMMY)
        SWNTMP=GEOB(2)
        VNTEMP=GEOB(3)
      ENDIF
      CALL CONIC(GEOB(1),NX,X,R,BNOSE,TRUNC,LNOSE,DNOSE,DISCON)
      GO TO 1080
C
 1040 IF (BNOSE.GT.UNUSED) THEN
        CALL OGIVE(GEOB(1),NXTEMP,XTEMP,RTEMP,UNUSED,.FALSE.,
     &             LNOSE,DNOSE,DUMMY)
        SWNTMP=GEOB(2)
        VNTEMP=GEOB(3)
      ENDIF
      CALL OGIVE(GEOB(1),NX,X,R,BNOSE,TRUNC,LNOSE,DNOSE,DISCON)
      GO TO 1080
C
 1050 IF (BNOSE.GT.UNUSED) THEN
        CALL POWR(GEOB(1),NXTEMP,XTEMP,RTEMP,UNUSED,.FALSE.,
     &            LNOSE,DNOSE,POWER,DUMMY)
        SWNTMP=GEOB(2)
        VNTEMP=GEOB(3)
      ENDIF
      CALL POWR(GEOB(1),NX,X,R,BNOSE,TRUNC,LNOSE,DNOSE,POWER,DISCON)
      GO TO 1080
C
 1060 IF (BNOSE.GT.UNUSED) THEN
        CALL HAACK(XO,TNOSE,DNOSE,UNUSED,.FALSE.,LNOSE,DUMMY,
     &             XTEMP,RTEMP,NXTEMP,GEOB(1))
        SWNTMP=GEOB(2)
        VNTEMP=GEOB(3)
      ENDIF
      CALL HAACK(XO,TNOSE,DNOSE,BNOSE,TRUNC,LNOSE,
     &           DISCON,X,R,NX,GEOB(1))
C
 1080 CONTINUE
C
C  NOW COMPUTE CENTERBODY DATA IF DEFINED
C
      IF(LCENTR .LE. UNUSED)GO TO 1090
      RFWD=DNOSE/2.
      IF (BNOSE.GT.UNUSED) 
     & CALL CONIC(GEOB(9),NXTEMP,XTEMP,RTEMP,RFWD,.TRUE.,
     &            LCENTR,DCENTR,DUMMY)
      CALL CONIC(GEOB(9),NX,X,R,RFWD,.TRUE.,LCENTR,DCENTR,DISCON)
C
C  NOW THE AFTERBODY
C
 1090 CONTINUE
      IF(LAFT .LE. UNUSED)GO TO 1140
C
      RFWD=DCENTR/2.
      IF(LCENTR .LE. UNUSED)RFWD=DNOSE/2.
C
C  CHECK FOR PROPER SHAPE DEFINED
C
      IF(TAFT .NE. 0. .AND. LAFT .LT. RFWD .AND. PARTS(18))WRITE(6,1200)
      IF(TAFT .NE. 0. .AND. LAFT .LT. RFWD)TAFT=0.
C
C  SET AFTERBODY TYPE
C
      JUMP=TAFT+1.5
C
C  COMPUTE THE APPROPIATE SHAPE
C
      GO TO (1100,1110),JUMP
C
 1100 IF (BNOSE.GT.UNUSED) 
     & CALL CONIC(GEOB(17),NXTEMP,XTEMP,RTEMP,RFWD,.TRUE.,
     &            LAFT,DAFT,DUMMY)
      CALL CONIC(GEOB(17),NX,X,R,RFWD,.TRUE.,LAFT,DAFT,DISCON)
      GO TO 1140
C
 1110 IF (BNOSE.GT.UNUSED) 
     & CALL OGIVE(GEOB(17),NXTEMP,XTEMP,RTEMP,RFWD,.TRUE.,
     &            LAFT,DAFT,DUMMY)
      CALL OGIVE(GEOB(17),NX,X,R,RFWD,.TRUE.,LAFT,DAFT,DISCON)
      GO TO 1140
C
 1120 CONTINUE
C
C***  HERE FOR OPTION 2 (COORDINATES DEFINED)
C
      NNX=NX+0.5
      ITYPE=1
C
      CALL USERS
C
      GO TO 1160
C
 1140 CONTINUE
C
C  GET TOTALS FOR OPTION 1 INPUTS
C
      ITYPE=2
      SPLAN=SPNOSE+SPCENT+SPAFT
      SWET =SWNOSE+SWCENT+SWAFT
C
C***  FIND TOTAL WETTED AREA FOR A SHARP NOSE
      IF (BNOSE.GT.UNUSED) SWTEMP=SWNTMP+SWCENT+SWAFT
C
      VOL  =VOLNOS+VOLCEN+VOLAFT
      LTOTAL=LNOSE+LCENTR+LAFT
      XCENTV=(XCVNOS*VOLNOS+XCVCEN*VOLCEN+XCVAFT*VOLAFT)/VOL
      XCENTP=(XCPNOS*SPNOSE+XCPCEN*SPCENT+XCPAFT*SPAFT)/SPLAN
C
C  CORRECT FOR NOSE APEX COORDINATE
C
      NNX=NX+0.5
      X1=X(1)
      DMAX=2.*R(1)
      DO 1150 I=1,NNX
         X(I)=X(I)-X1+XO
         IF(2.*R(I) .GT. DMAX)DMAX=2.*R(I)
 1150 CONTINUE
C
      SMAX=PI*DMAX**2/4.
      DBASE=2.*R(NNX)
      SBASE=PI*DBASE**2/4.
      FR=LTOTAL/DMAX
      IF(XCVNOS .NE. UNUSED)XCVNOS=XCVNOS-X1
      IF(XCPNOS .NE. UNUSED)XCPNOS=XCPNOS-X1
      IF(XCVCEN .NE. UNUSED)XCVCEN=XCVCEN-X1
      IF(XCPCEN .NE. UNUSED)XCPCEN=XCPCEN-X1
      IF(XCVAFT .NE. UNUSED)XCVAFT=XCVAFT-X1
      IF(XCPAFT .NE. UNUSED)XCPAFT=XCPAFT-X1
      IF(XCENTV .NE. UNUSED)XCENTV=XCENTV-X1
      IF(XCENTP .NE. UNUSED)XCENTP=XCENTP-X1
      IF(R(1) .LE. UNUSED)R(1)=0.
C
 1160 CONTINUE
C
C***  CONVERT RESULTS TO INPUT UNITS FOR OUTPUT
C
      IF(.NOT.(LDIMIN .OR. LDIMCM .OR. LDIMM))GO TO 1170
C
         CALL CVRTFT
C
 1170 CONTINUE
C
      IF(PARTS(1))CALL PRIAXI(ITYPE)
C
C***  CONVERT RESULTS TO UNITS OF FEET FOR PROGRAM CALCULATIONS
C
      IF(.NOT.(LDIMIN .OR. LDIMCM .OR. LDIMM))GO TO 1180
C
         CALL CVRTUS
C
 1180 CONTINUE
C
C***  SET DEFAULT REFERENCE QUANTITIES
C
      IF(SREF .EQ. 0. .OR. SREF .EQ. UNUSED)SREF=SMAX
      IF(LREF .EQ. 0. .OR. LREF .EQ. UNUSED)LREF=DMAX
      IF(LATREF .EQ. 0. .OR. LATREF .EQ. UNUSED)LATREF=LREF
C
 1190 FORMAT(4X,'* WARNING * THE NOSE IS TOO SHORT',
     1 ' TO USE THE SHAPE SPECIFIED.',/,16X,'A CONICAL SEGMENT',
     2 ' HAS BEEN ASSUMED.')
 1200 FORMAT(4X,'* WARNING * THE AFTERBODY IS TOO SHORT',
     1 ' TO USE THE SHAPE SPECIFIED.',/,16X,'A CONICAL SEGMENT',
     2 ' HAS BEEN ASSUMED.')
C
      RETURN
      END
