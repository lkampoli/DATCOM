      SUBROUTINE READNL(KOUNT,NAMORD)
C      
C***  SUBROUTINE TO READ NAMELIST INPUTS FROM TAPE UNIT 2
C      
C***  KOUNT IS THE NUMBER OF NAMELISTS TO BE READ
C***  NAMORD CONTAINS NAMELIST ORDER
C      
      COMMON /ABODIN/ ABOD(881)
      COMMON /INCID/  INCI(40)
      COMMON /FLC/    FLTC(144)
      COMMON /REFQN/  REFQ(9)
      COMMON /TRIMIN/ TRIM(19)
      COMMON /INLETN/ INLE(64)
      COMMON /FSET1/  FIN1(399)
      COMMON /FSET2/  FIN2(399)
      COMMON /FSET3/  FIN3(399)
      COMMON /FSET4/  FIN4(399)
C      
      COMMON /LOGIC/ LDMPCS,LDAMP,LBUILD,LNACA,LDERDG,
     1               LDERRD,LPART,LNAME,LPLOT,
     2               LFLT,LREFQ,LAXIS,LFIN1,LFIN2,LFIN3,LFIN4,
     3               LDEFL,LTRIM,LDIMIN,LDIMFT,LDIMCM,LDIMM,LELLB,
     4               LINLET,LEXPR,LICRMT,LSPIN,LARBOD
      LOGICAL        LDMPCS,LDAMP,LBUILD,LNACA,LDERDG,
     1               LDERRD,LPART,LNAME,LPLOT,
     2               LFLT,LREFQ,LAXIS,LFIN1,LFIN2,LFIN3,LFIN4,
     3               LDEFL,LTRIM,LDIMIN,LDIMFT,LDIMCM,LDIMM,LELLB,
     4               LINLET,LEXPR,LICRMT,LSPIN,LARBOD
C      
      REAL INLE,INCI
      DIMENSION NAMORD(300)
      REWIND 2
C      
C***  NAMORD ARRAY CONTAINS NAMELIST ORDER
C      
      DO 1120 J=1,KOUNT
         JUMP=NAMORD(J)
         NSET=0
 1000       IF(JUMP.LT.100)GO TO 1010
C      
C***  DETERMINE FIN SET (SUBTRACT 100 FROM JUMP)
C      
               NSET=NSET+1
               JUMP=JUMP-100
               GO TO 1000
 1010       CONTINUE
C      
C***  IF NSET .GT. 4 (800 ADDED TO JUMP), THEN DELETE NAMELIST
C      
         IF(NSET .GT. 4)CALL DREAD(NSET,JUMP)
         IF(NSET .GT. 4)GO TO 1120
C      
C***  EXPERIMENTAL DATA NOT READ HERE (JUMP=9)
C      
         IF(JUMP.LE.0 .OR. JUMP.GT.10)GO TO 1120
         IF(JUMP.EQ.4 .AND. (NSET.EQ.0 .OR. NSET.GT.4))GO TO 1110
         GO TO (1020,1030,1040,1050,1060,1070,1080,1090,1120,1100),JUMP
C      
C***  READ FLTCON NAMELIST
C      
 1020       CALL RFLT(LFLT,FLTC)
         GO TO 1110
C      
C***  READ REFQ NAMELIST
C      
 1030       CALL RREFQ(LREFQ,REFQ)
         GO TO 1110
C      
C***  READ AXIBOD NAMELIST
C      
 1040       CALL RAXIS(LAXIS,ABOD)
         GO TO 1110
C      
C***  READ FINSET NAMELIST
C      
 1050       CALL RFINS(NSET,LFIN1,LFIN2,LFIN3,LFIN4,
     1                 FIN1,FIN2,FIN3,FIN4)
         GO TO 1110
C      
C***  READ DEFLCT NAMELIST
C      
 1060       CALL RDEFL(LDEFL,INCI)
         GO TO 1110
C      
C***  READ TRIM NAMELIST
C      
 1070       CALL RTRIM(LTRIM,TRIM)
         GO TO 1110
C      
C***  READ ELLIPTICAL BODY NAMELIST
C      
 1080       CALL RELLB(LELLB,ABOD)
      GO TO 1110
C      
C***  READ INLET NAMELIST
C      
 1090       CALL RINLET(LINLET,INLE)
      GO TO 1110
C      
C***  READ ARBITRARY BODY NAMELIST
C      
 1100       CALL RARBOD(LARBOD,ABOD)
C      
 1110    CONTINUE
 1120 CONTINUE
C      
      RETURN
      END
