      SUBROUTINE SKINF(M,REN,LCFT,K,BLAYER,SWET,SREF,FORM,
     1    LAM3D,TUR3D,CDF)
C
C***  COMPUTES SKIN FRICTION DRAG USING VAN DRIEST METHOD II
C***  FOR TURBULENT FLOW AND BLASIUS FOR LAMINAR FLOW
C***  EMPIRICAL CORRELATIONS ARE USED FOR THE TRANSITION
C***  AND SURFACE ROUGHNESS CALCULATIONS
C***
C***  REFERENCES -
C***  (1) MDAC-WEST AERODYNAMIC HANDBOOK(M 8.080-CD) -- VAN DRIEST II
C***      TURBULENT FRICTION COEFFICIENT, FLAT INSULATED PLATE,
C***      INCLUDES PARTIALLY LAMINAR FLOW WITH NATURAL TRANSITION POINT
C***
C***  (2) USAF DATCOM -- EFFECT OF UNIFORMLY DISTRIBUTED SURFACE ROUGH-
C***      NESS (SECTION 4.1.5.1)
C***
C***  (3) FLUID-DYNAMIC DRAG(HOERNER) -- LAMINAR FRICTION COEFFICIENT,
C***      COMPRESSIBILITY
C***
C***  LIMITATIONS -
C***      MACH NO. MUST FALL BETWEEN 0.0-10.0
C***      REYNOLDS NO. MUST FALL BETWEEN 1E4-1E9
C***
C***  NOTE -  INPUT UNITS ON SWET AND SREF MUST BE CONSISTENT.
C***
C***   INPUTS
C***         M    - MACH NO.
C***         REN  - REYNOLDS NUMBER PER FOOT
C***         LCFT - CHARACTERISTIC LENGTH, FEET
C***         K    - ROUGHNESS HEIGHT, INCHES
C***        BLAYER- BOUNDARY LAYER TYPE
C***                0.=TURBULENT, 1.=NATURAL TRANSITIONING
C***         SWET - WETTED SURFACE AREA
C***         SREF - REFERENCE AREA
C***         FORM - SHAPE CORRECTION FACTOR
C***        LAM3D - 3-D LAMINAR FLOW CORRECTION FACTOR
C***        TUR3D - 3-D TURBULENT CORRECTION FACTOR
C***
C***   OUTPUT
C***         CDF  - SKIN FRICTION DRAG BASED ON SREF
C
      REAL K,LCFT,L,M,LAM3D
C
      DATA RTREF /1.E6/
C
C***  DEFINE REYNOLDS NUMBERS
C
      L=LCFT
      RNU=REN
      RX=RNU*L
C
C***  INITIALIZE
C
      CFLAM=0.
      CFTURB=0.
      IF(RNU .LE. 0.1 .OR. RX .LT. 0.1)GO TO 1060
C
C***  CHECK FOR FULLY TURBULENT FLOW
C***   (0.=TURBULENT,1.=NATURAL)
C
      IF(BLAYER .EQ. 0.)GO TO 1010
C
C     **********************************************
C     *  NATURAL TRANSITION - COMPUTE FLOW REGIONS *
C     *        (REF 1 - PG 2-129)                  *
C     **********************************************
C
      RTRAN=RTREF*EXP(.22*M)
      XTRAN=RTRAN/RNU
      A=.036-.00128*M-.00072*M**2
      IF(M .GT. 3.) A=.02568
      IF(M .LT. 1.) A=.034
      RLAM=RTRAN*(1.-(.67/A)**1.25/RTRAN**.375)
      XLAM=RLAM/RNU
      XTURB= L-XTRAN
      DXTRAN=XTRAN-XLAM
      XPTURB=L-XLAM
C
C     ************************************************
C     *  COMPUTE LAMINAR FRICTION COEFFICIENT        *
C     *    (REF 3 - BLASIUS-EQUA 23, PG 2-4)         *
C     *    (REF 3 - COMPRESSIBILITY-EQUA 12, PG 17-4)*
C     ************************************************
C
      CFLAMI=1.328/SQRT(RX)
      CFLAM=CFLAMI/(1.+.045*M**2)**.25
C
C***  FULLY LAMINAR FLOW
C
      IF(L.GT.XLAM) GO TO 1000
      CFTURB=0.
      GO TO 1060
C
C***  PARTIAL TRANSITION
C
 1000 CONTINUE
      IF(L.GT.XTRAN) GO TO 1010
         XTURB=0.
         DXTRAN=L-XLAM
C
C     ********************************************
C     *  COMPUTE TURBULENT FRICTION COEFFICIENT  *
C     ********************************************
C
 1010 CONTINUE
      IF(BLAYER .EQ. 1.)GO TO 1020
         XLAM=0.0
         XPTURB=L
         XTURB=L
         DXTRAN=0.0
         CFLAM=0.
 1020 CONTINUE
C
C***  ADMISSABLE ROUGHNESS HEIGHT
C***  (REF (2) - FIGURE 4.1.5.1-27)
C
      IF(K.EQ.0.) GO TO 1030
         A=3.60679+.2018*M+.12045*M**2
         AK=12.*L/EXP((ALOG(RX)-A)/1.0483)
         IF(K.GE.AK) RX=EXP(A+1.0483*ALOG(12.*L/K))
C
C***  VAN DRIEST II (REF 1 - PG 2-130)
C
 1030 CONTINUE
      E=.2*M**2
      TWTD=1.+.89*E
      A=E/TWTD
      B=-1.+(1.+E)/TWTD
      AL=(2.*A-B)/SQRT(4.*A+B*B)
      BE=B/SQRT(4.*A+B*B)
      C=0.5*(1.+2.*.76)*ALOG10(TWTD)
      D=.242*(ASIN(AL)+ASIN(BE))/SQRT(E)
C
C ... USING SCHONHERR AS FIRST APPROXIMATION
C
      CFI=.455/(ALOG10(RX))**2.58
C
C ... ITERATE UNTIL CONVERGENCE
C
 1040 CONTINUE
         CF=(D/(ALOG10(CFI*RX)-C))**2
         ERR=CF-CFI
         IF(ABS(ERR).LT.1.E-5) GO TO 1050
         CFI=CFI+ERR/2.0
      GO TO 1040
C
C     ********************************************
C     *  COMPUTE SKIN FRICTION DRAG COEFFICIENT  *
C     ********************************************
C
 1050 CONTINUE
      CFTURB=CF*(XTURB+DXTRAN)/L
      CFLAM=CFLAM*XLAM/L
C
C***  TOTAL CF
C
 1060 CONTINUE
      CFTOT=LAM3D*CFLAM+TUR3D*CFTURB
C
C***  TOTAL CDF BASED ON REFERENCE AREA
C
      CDF=CFTOT*FORM*SWET/SREF
C
      RETURN
      END
