      SUBROUTINE PANLCN(ALPHA,MACH,CDC,PHIB,PHIW,GAM,ALO,NTHEO,ATHEO,
     1   CNTHEO,XW,SPAN,AR,TAPER,CR,LODN,RADN,RADF,DREF,DELT,NSURF,
     2   DAQVW,KW,KKW,BLUNT,SWEEP0,AREA,YCPFIN,
     3   CN,CNWING,CYWING,CL,CLWING,WAQ,FACTH,FACTV,KSET,DFLAG)
C      
C  THIS SUBROUTINE COMPUTES THE TOTAL FIN NORMAL FORCE OF
C  A CRUCIFORM FIN SET USING THE CONCEPT OF EQUIVALENT
C  ANGLE OF ATTACK SUGGESTED BY NIELSEN (AIAA PAPER 77-1153)
C
C  MODIFIED BY W. BLAKE, WL/FIGC
C      
C  HERE THE TERM WING MEANS TWO EXPOSED FIN PANELS JOINED TOGETHER
C  AT THE ROOT.  FIN IMPLIES A SINGLE FIN PANEL.
C      
C *** INPUTS ***
C      
C  ALPHA - BODY TOTAL ANGLE OF ATTACK, DEG.
C   MACH - FREESTREAM MACH NUMBER
C    CDC - CROSS FLOW DRAG COEFFICIENT
C   PHIB - BODY ROLL ANGLE, CLOCKWISE FROM VERTICAL
C   PHIW - PANEL ROLL ANGLES, CLOCKWISE FROM VERTICAL
C    GAM - PANEL ROLL ANGLE FROM ROOT CHORD ROLL POSITION, DEG.
C          (POSITIVE CLOCKWISE)
C  NTHEO - NUMBER OF INPUT POINTS IN WING THEORETICAL CN TABLE
C  ATHEO - ANGLE OF ATTACK CORRESPONDING TO CNTHEO, DEG.
C CNTHEO - WING THEORETICAL NORMAL FORCE AS A FUNCTION OF
C          ATHEO; BASED UPON THE PLANFORM AREA
C     XW - DISTANCE FROM THE NOSE APEX TO THE FIN EXPOSED ROOT
C          CHORD LEADING EDGE
C   SPAN - WING SPAN, MEASURED TIP TO TIP (INCLUDES BODY)
C     AR - FIN EXPOSED ASPECT RATIO (WING: TWO FINS JOINED AT ROOT)
C  TAPER - FIN EXPOSED TAPER RATIO
C     CR - FIN EXPOSED ROOT CHORD
C   LODN - OGIVE NOSE FINENESS RATIO
C   RADN - BODY RADIUS AT NOSE/CENTERBODY JUNCTURE
C   RADF - BODY RADIUS AT FIN
C   DREF - REFERENCE DIAMETER (LENGTH)
C   DELT - ARRAY OF DEFLECTION ANGLES
C  NSURF - NUMBER OF SURFACES
C  DAQVW - EQUIV. ALPHA DUE TO VORTICES
C     KW - WING DUE TO BODY CARRY OVER INTERFERENCE FACTOR (ALPHA)
C    KKW - WING DUE TO BODY CARRY OVER INTERFERENCE FACTOR (DELTA)
C  BLUNT - BODY NOSE TIP RADIUS TO BODY RADIUS (BLUNTNESS RATIO)
C SWEEP0 - L.E. SWEEP ANGLE OF THE PANEL, DEG.
C   AREA - FIN PANEL ALONE PLANFORM AREA
C YCPFIN - FIN LATERAL CENTER OF PRESSURE
C FACTH  - CORRECTION TO PANEL LIFT DUE TO ELLIPTICITY - ELLIPSE IN HORIZONTAL
C          POSITION (WIDTH .GT. HEIGHT) EQUALS 1. FOR AXISYMETRIC BODY
C FACTV  - CORRECTION TO PANEL LIFT DUE TO ELLIPTICITY - ELLIPSE IN VERTCIAL
C          POSITON
C KSET   - INDEX OF FIN SET BEING EVALUATED
C DFLAG  - FLAG FOR AXIS OF ROTATION, STATIC: 0., PITCH: 1., YAW: 2.,
C          ROLL: 3.
C      
C *** OUTPUTS ***
C      
C     CN - PANEL NORMAL FORCE COEFFICIENT, FOR ALL FIN PANELS
C CNWING - TOTAL CN DUE TO WING SURFACES IN THE PRESENCE OF THE BODY
C CYWING - TOTAL CY DUE TO WING SURFACES IN THE PRESENCE OF THE BODY
C     CL - PANEL ROLLING MOMENT COEFFICIENT, FOR ALL PANELS
C CLWING - TOTAL WING ROLLING MOMENT COEFFICIENT
C    WAQ - ALPHA EQUIV. FOR ALL PANELS
C      
C  NOTE- REFER TO SUBROUTINES ALPEQ AND CLVR FOR OTHER DEFINITIONS
C        AND USE RESTRICTIONS
C      
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /FFINDL/ DUM(32),CLD(4)
      COMMON /FSET1/  D1,SPAN1(10),D2(251),YUP1(50),D3(77),CFOC1(10)
      COMMON /FSET2/  D4,SPAN2(10),D5(251),YUP2(50),D6(77),CFOC2(10)
      COMMON /FSET3/  D7,SPAN3(10),D8(251),YUP3(50),D9(77),CFOC3(10)
      COMMON /FSET4/  DX,SPAN4(10),DY(251),YUP4(50),DZ(77),CFOC4(10)
C      
      DIMENSION PHIW(8),GAM(8)
      DIMENSION ATHEO(NTHEO),CNTHEO(NTHEO)
      DIMENSION DELT(8),DAQVW(8),CN(8),CL(8),WAQ(8),DELTMP(4)
C
      REAL MACH,LODN,L12,L24,KW,KKW
C      
      DOB  = 2.*RADF/SPAN
C      
C***  DETERMINE FIN/FIN INTERFERENCE FACTORS
C      
      IF(NSURF .LE. 2)ISURF=1
      IF(NSURF .GT. 2)ISURF=2
C      
      CALL FINFIN(MACH,AR,CR,SPAN,TAPER,RADF,ISURF,A14,A24,A34,A44,
     1            B12,B24)
      L12=B12
      L24=B24
C      
C***  COMPUTE PANEL LOADS
C      
      CNWING=0.
      CYWING=0.
      CLWING=0.
C      
      DO 900 I=1,NSURF
        IF (PHIW(I).GT.360.) PHIW(I)=PHIW(I)-360.
        IF (PHIW(I).LT.0.0) PHIW(I)=PHIW(I)+360.
 900  CONTINUE
      IF (PHIW(NSURF).EQ.0.) PHIW(NSURF)=360.
C
C***  SET UP PANEL ROLL POSITION AND DEFLECTION ANGLE
C      
      DO 1140 I=1,NSURF
         PHI=PHIB+PHIW(I)+GAM(I)
         IF(PHI .GE. 360.) PHI=PHI-360.
         DEL=DELT(I)/RAD
C      
C***  ZERO OUT THE TEMPORARY ARRAY FOR PANEL DEFLECTIONS
C      
         DO 1000 J=1,4
              DELTMP(J)=0.0
 1000 CONTINUE
C      
C***  SET UP THE DEFLECTION ANGLES FOR AN EQUIVALENT CRUCIFORM PANEL
C     ARRANGEMENT TO BE USED TO COMPUTE THE PANEL TO PANEL INTERFERENCE
C     THE PANEL WHOSE EQUIVALENT ANGLE OF ATTACK IS BEING EVALUATED
C     IS SET UP AS THE TOP PANEL (FIN 1) IN A CRUCIFORM FIN CONFIGURATIO
C     THE OTHER PANELS IN THE DUMMY FIN SET ARE DEFLECTED TO MODEL THE
C     INFLUENCE OF THE OTHER PANELS IN THE REAL FIN SET.  THEN THE WHOLE
C     DUMMY FIN SET IS ROLLED SO THAT THE PANEL BEING EVALUATED IS
C     POSITIONED IN ITS PROPER ROLL ATTITUDE.
C      
C      
C***  SET TEMPORARY DEFLECTION ANGLE ARRAY ELEMENT FOR PANEL BEING
C     EVALTUATED
C      
              DELTMP(1)=DELT(I)
C      
C***  EVALUATE INTERFERENCE IF CONFIGURATION ONLY HAS ONE PANEL
C      
      IF (NSURF .EQ. 1) GOTO 1080
C      
C***  HERE FOR TWO PANEL FIN SET
      IF (NSURF .NE. 2) GOTO 1020
C      
C*          FIRST PANEL
C      
         IF (I .EQ. 2)  GOTO 1010
              DELTMP(3)= DELT(2)
      GOTO 1080
 1010 CONTINUE
C      
C*          SECOND PANEL
C      
              DELTMP(3)= DELT(1)
      GOTO 1080
 1020 CONTINUE
C      
C*** HERE FOR THREE PANEL FIN SET
C      
      IF (NSURF .NE. 3) GOTO 1050
C      
C*        FIRST PANEL
C      
         IF (I .NE. 1)  GOTO 1030
         IF (ABS(PHIW(2)-PHIW(1)).GT.120.) THEN
           DELTMP(2)= 0.0
           DELTMP(3)= DELT(2)
           DELTMP(4)= DELT(3)
         ELSEIF (ABS(PHIW(3)-PHIW(2)).GT.120.) THEN
           DELTMP(2)= DELT(2)
           DELTMP(3)= 0.0
           DELTMP(4)= DELT(3)
         ELSEIF (ABS(PHIW(3)-(PHIW(1)+360.)).GT.120.) THEN
           DELTMP(2)= DELT(2)
           DELTMP(3)= DELT(3)
           DELTMP(4)= 0.0
         ELSEIF (DELT(1).EQ.0.0) THEN
           DELTMP(2)=DELT(2)
           DELTMP(3)=0.0
           DELTMP(4)=DELT(3)
         ELSEIF (DELT(2).EQ.0.0) THEN
           DELTMP(2)=DELT(2)
           DELTMP(3)=DELT(3)
           DELTMP(4)=0.0
         ELSE
           DELTMP(2)=0.0
           DELTMP(3)=DELT(2)
           DELTMP(4)=DELT(3)
         ENDIF 
      GOTO 1080
 1030 CONTINUE
C      
C*        SECOND PANEL
C      
         IF (I .NE. 2) GOTO 1040
         IF (ABS(PHIW(2)-PHIW(1)).GT.120.) THEN
           DELTMP(2)= DELT(3)
           DELTMP(3)= DELT(1)
           DELTMP(4)= 0.0
         ELSEIF (ABS(PHIW(3)-PHIW(2)).GT.120.) THEN
           DELTMP(2)= 0.0
           DELTMP(3)= DELT(3)
           DELTMP(4)= DELT(1)
         ELSEIF (ABS(PHIW(3)-(PHIW(1)+360.)).GT.120.) THEN
           DELTMP(2)= DELT(3)
           DELTMP(3)= 0.0
           DELTMP(4)= DELT(1)
         ELSEIF (DELT(1).EQ.0.0) THEN
           DELTMP(2)=0.0
           DELTMP(3)=DELT(3)
           DELTMP(4)=DELT(1)
         ELSEIF (DELT(2).EQ.0.0) THEN
           DELTMP(2)=DELT(3)
           DELTMP(3)=0.0
           DELTMP(4)=DELT(1)
         ELSE 
           DELTMP(2)=DELT(3)
           DELTMP(3)=DELT(1)
           DELTMP(4)=0.0
         ENDIF 
      GOTO 1080
 1040 CONTINUE
C      
C*        THIRD PANEL
C      
         IF (ABS(PHIW(2)-PHIW(1)).GT.120.) THEN
           DELTMP(2)= DELT(1)
           DELTMP(3)= 0.0
           DELTMP(4)= DELT(2)
         ELSEIF (ABS(PHIW(3)-PHIW(2)).GT.120.) THEN
           DELTMP(2)= DELT(1)
           DELTMP(3)= DELT(2)
           DELTMP(4)= 0.0
         ELSEIF (ABS(PHIW(3)-(PHIW(1)+360.)).GT.120.) THEN
           DELTMP(2)= 0.0
           DELTMP(3)= DELT(1)
           DELTMP(4)= DELT(2)
         ELSEIF (DELT(1).EQ.0.0) THEN
           DELTMP(2)=DELT(1)
           DELTMP(3)=DELT(2)
           DELTMP(4)=0.0
         ELSEIF (DELT(2).EQ.0.0) THEN
           DELTMP(2)=0.0
           DELTMP(3)=DELT(1)
           DELTMP(4)=DELT(2)
         ELSE 
           DELTMP(2)=DELT(1)
           DELTMP(3)=0.0
           DELTMP(4)=DELT(3)
         ENDIF 
      GOTO 1080
 1050 CONTINUE
      IF(NSURF.NE.4) GO TO 1055
C      
C*** HERE FOR FIN SETS WITH FOUR PANELS
C
C  REV 8/99
C  CORRECT ADJACENT PANEL NUMBERING FOR 4 PANEL SETS
C
C*    FIRST PANEL
      IF (I .EQ. 1) THEN
              DELTMP(4)= DELT(4)
              DELTMP(3)= DELT(3)
              DELTMP(2)= DELT(2)
C*    SECOND PANEL
      ELSEIF(I .EQ. 2) THEN
              DELTMP(4)= DELT(1)
              DELTMP(3)= DELT(4)
              DELTMP(2)= DELT(3)
C*    THIRD PANEL
      ELSEIF(I .EQ. 3) THEN
              DELTMP(4)= DELT(2)
              DELTMP(3)= DELT(1)
              DELTMP(2)= DELT(4)
C*    LAST PANEL
      ELSEIF(I .EQ. 4) THEN
              DELTMP(4)= DELT(3)
              DELTMP(3)= DELT(2)
              DELTMP(2)= DELT(1)
      ENDIF
      GO TO 1080
 1055 CONTINUE
C      
C*** HERE FOR FIN SETS WITH MORE THAN FOUR PANELS
C      
      IF (I .GT. 1) GOTO 1060
C      
C*        FIRST PANEL
C      
              DELTMP(4)= DELT(NSURF)
              DELTMP(2)= DELT(2)
      GOTO 1080
 1060 CONTINUE
      IF (I .EQ. NSURF) GOTO 1070
C      
C*       MIDDLE PANELS
C      
              DELTMP(4)= DELT(I-1)
              DELTMP(2)= DELT(I+1)
      GOTO 1080
 1070 CONTINUE
C      
C*       LAST PANEL
C      
              DELTMP(4)= DELT(NSURF-1)
              DELTMP(2)= DELT(1)
C
 1080 CONTINUE
C
C  CALCULATE QWB, THE KWB CORRECTION FOR PANEL DIHEDRAL
C
         QWBTOP=SIN(GAM(I)/RAD)*SIN(GAM(I)/RAD)
         QWBBOT=(0.7*SQRT(1.-ABS(SIN(GAM(I)/RAD)))+0.6)
     1          **(2./SQRT(SPAN/2./RADF))
         QWBBOT=QWBBOT*SPAN/2./RADF*LOG(SPAN/2./RADF)+1.
         QWB=1.-QWBTOP/QWBBOT
         PDKW=KW*QWB
C      
C***  COMPUTE ALPHA EQUIVALENT
C      
         CALL ALPEQ(DOB,ALPHA,AR,PHI,1,DELTMP,MACH,DAQVW(I),LODN,RADN,
     1     XW,RADF,TAPER,ISURF,A14,A24,A34,A44,L12,L24,AEQ,PDKW,KKW,
     2     CDC,BLUNT,SWEEP0,AREA,ALO,KSET,DFLAG)
         WAQ(I)=AEQ
C      
C***     SPLINE INPUT CURVE FOR FIN CN AT AEQ
C      
C***     DETERMINE IF CN IS POSITIVE OR NEGATIVE AND ADJUST SIGN
C        OF CN EQUATION APPROPRIATELY.
C      
         SIGN=1.0
         IF (AEQ .LT. 0.0) SIGN=-1.0
         ACORR=ABS(AEQ)
         CALL SPLINE(ATHEO,CNTHEO,NTHEO,2,2,0.,0.,ACORR,1,CN(I),CNA)
         CN(I)=SIGN*CN(I)/2.*(FACTV*COS(PHIW(I)/RAD)**2+
     &         FACTH*SIN(PHIW(I)/RAD)**2)
C
C*** COMPUTE FIN LATERAL CENTER OF PRESSURE
C
      CALL YCP(AR,TAPER,MACH,ABS(CN(I)),YCPFIN)
      ROS=RADF/(SPAN/2.)
      DYCP=(1.-ROS)**2*(9.*PI**2-64.)/(12.*PI*(4.*(1.-ROS)+3.*PI*ROS))
C
C  LATERAL CP FOR ROLLING FIN
C
      IF(DFLAG.EQ.3.) YCPFIN=YCPFIN+DYCP
      IF(ABS(DELT(I)).LE.UNUSED .OR. MACH.GT.0.8) GO TO 1200
C         IF(KSET.EQ.1 .AND. CFOC1(1).LT.1.) THEN
C            CALL YCPAIL(AR,TAPER,MACH,SWEEP0,SPAN1,CFOC1,
C     1                  YUP1,CLD(KSET),YCPFIN)
C         ELSEIF(KSET.EQ.2 .AND. CFOC2(1).LT.1.) THEN
C            CALL YCPAIL(AR,TAPER,MACH,SWEEP0,SPAN2,CFOC2,
C     2                  YUP2,CLD(KSET),YCPFIN)
C         ELSEIF(KSET.EQ.3 .AND. CFOC3(1).LT.1.) THEN
C            CALL YCPAIL(AR,TAPER,MACH,SWEEP0,SPAN3,CFOC3,
C     3                  YUP3,CLD(KSET),YCPFIN)
C         ELSEIF(KSET.EQ.4 .AND. CFOC4(1).LT.1.) THEN
C            CALL YCPAIL(AR,TAPER,MACH,SWEEP0,SPAN4,CFOC4,
C     4                  YUP4,CLD(KSET),YCPFIN)
C         ENDIF
 1200 CONTINUE
C
C*** COMPUTE CL FROM CN
C
         CL(I)=-CN(I)*(YCPFIN*(SPAN/2.-RADF)+RADF*COS(GAM(I)/RAD))
     1               *COS(DEL)/DREF
C      
C***     TOTAL TAIL RESULTS
C      
         PHIR=(PHI-PHIB)/RAD
C
C***     IN MISSILE DATCOM, FIN PANELS WITH POSITIVE CAMBER WHICH 
C        ARE LOCATED BETWEEN 180 AND 360 DEGREES (180<PHI<360) ARE 
C        ASSUMED TO HAVE POSITIVE CN. THE SIGN OF
C        CN IS ADJUSTED HERE BY MULTIPLYING BY SIN(PHIR).
C
         CNWING=CNWING+CN(I)*COS(DEL)*SIN(PHIR)
         CYWING=CYWING-CN(I)*COS(DEL)*COS(PHIR)
         CLWING=CLWING+CL(I)
 1140 CONTINUE
C      
      RETURN
      END
