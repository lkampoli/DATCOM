      SUBROUTINE VRINTS(HR,VR,PHI,RL,TAPER,SSPANW,VRINT)
C **
C **  COMPUTES INTERFERENCE FACTOR FOR VORTEX INTERACTION WITH
C **  LIFTING SURFACE. MODIFICATION OF SVRINT FOR ONE PANEL AND 
C **  ONE VORTEX WITH ITS IMAGE.
C **
C **  REFERENCE - NACA 1307, PITTS, NIELSEN, AND KAATARI (APPENDIX B) 
C **
C **  LIMITATIONS -  STRIP AND SLENDER-BODY THEORY. 
C **
C **  NOTE - UNITS ON HR,VR,SSPAN AND RL MUST BE CONSISTENT.
C **
C **  INPUT - 
C **        HR    = HORIZONTAL POSITION OF VORTEX MEASURED FROM 
C **                VEHICLE CENTERLINE
C **        VR    = VERTICAL POSITION OF VORTEX MEASURED FROM 
C **                VEHICLE CENTERLINE
C **        PHI   = ROLL ANGLE OF PANEL, DEG. FROM TOP VERTICAL CENTER
C **        RL    = LOCAL BODY RADIUS AT LIFTING SURFACE
C **        TAPER = TIP TO ROOT CHORD TAPER RATIO 
C **        SSPANW= EXPOSED SEMI-SPAN OF THE WING 
C **
C **  OUTPUT -
C **        VRINT = VORTEX INTERFERENCE FACTOR
C **
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C 
      REAL L
      DIMENSION L(2)
C **
      I=1
      DIST=0.310*(SSPANW+RL)
      VRINT=0.
      CPHI=COS(PHI/RAD)
      SPHI=SIN(PHI/RAD)
      S=RL+SSPANW
      SIGN=1.
      IF(HR .LT. 0.)SIGN=-1.
      HOR=HR
      VOR=VR
C **
C **  DELETE VORTEX INFLUENCE IF INSIDE BODY
C **
      IF(SQRT(HOR**2+VOR**2) .LE. RL)GO TO 1040
C **
C **  HORIZONTAL AND VERTICAL DISTANCES OF RIGHT HAND VORTEX
C **  FROM RIGHT HAND LIFTING SURFACE.
C **
C ** LOCATION OF PRIMARY VORTEX
C **
 1000 F=HOR*SPHI+VOR*CPHI
      H=VOR*SPHI-HOR*CPHI
C
C ... LIMIT VORTEX POSITION TO VORTEX CORE RADIUS FROM FIN
C ... METHOD FROM J.A.S. JANUARY, 1951, PAGE 23
C ... BY SPRIETER AND SACKS
C
      HSIGN=1.
      IF(H .LT. 0.)HSIGN=-1.
      IF(ABS(H) .LT. DIST)H=HSIGN*DIST
      GO TO 1020
 1010 F=HOR*SPHI+VOR*CPHI
      H=VOR*SPHI-HOR*CPHI
 1020 A=((S-RL*TAPER)-F*(1.-TAPER))/(2.*(S-RL))
      A=A*ALOG((H*H+(F-S)**2)/(H*H+(F-RL)**2))
      HH=H
      IF(H.EQ.0.0) HH=0.0001
      FS=(F-S)/HH
      FR=(F-RL)/HH
      AA=1.0
      IF(FS.LT.0.0) AA=-AA
      B=S-RL+H*ATAN(ABS(FS))*AA
      AA=1.0
      IF(FR.LT.0.0) AA=-AA
      B=B-H*ATAN(ABS(FR))*AA
      B=B*(1.-TAPER)/(S-RL)
      L(I)=A-B
      IF(I.EQ.2) GO TO 1030
C **
C **  LOCATION OF IMAGE VORTEX
C **
      Z2=VOR*VOR+HOR*HOR
      IF(Z2.EQ.0.0) Z2=.0001
      VOR=VOR*RL*RL/Z2
      HOR=HOR*RL*RL/Z2
      I=I+1
      GO TO 1010
 1030 VRINT=SIGN*(L(1)-L(2))*2./(1.+TAPER)
C
 1040 CONTINUE
      RETURN
      END
