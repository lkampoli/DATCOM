      SUBROUTINE ELLKWB(BEXP,TLES,TTES,CR,TAPER,A,B,PHI,DBE,
     1                  KEWCNL)
C **
C **  COMPUTES THE RATIO OF ELLIPTIC WING-BODY TO EQUIVALENT
C **  AREA CIRCULAR WING-BODY LINEAR FORCE COEFFICIENTS.
C **
C **  REFERENCE --
C **        JORGENSEN,NASA TN D-7228, EQUATIONS (13)-(18)
C **
C **  LIMITATIONS --
C **        SLENDER BODY THEORY-LINEAR CN
C **
C **  NOTE --
C **        INPUT UNITS ON BEXP,CR,A,B,DBE MUST BE CONSISTENT
C **
C **  INPUT --
C **        BEXP  = EXPOSED SPAN (2 PANELS)
C **        TLES  = TANGENT OF LEADING EDGE SWEEP ANGLE
C **        TTES  = TANGENT OF TRAILING EDGE SWEEP ANGLE
C **        CR    = ROOT CHORD AT WING-BODY JUNCTION
C **        TAPER = WING TAPER RATIO
C **        A     = BODY ELLIPSE MAJOR AXIS LENGTH
C **        B     = BODY ELLIPSE MINOR AXIS LENGTH
C **        PHI   = ELLIPSE ORIENTATION (BETWEEN 0 AND 90-DEG)
C **              = 0.0 FOR MAJOR AXIS PERPENDICULAR TO CROSSFLOW
C **                VELOCITY VECTOR
C **              = 90.0 FOR MAJOR AXIS PARALLEL TO CROSSFLOW
C **                VELOCITY VECTOR
C **        DBE   = EQUIVALENT CIRCULAR BODY DIAMETER
C **
C **  OUTPUT --
C **        KEWCNL  = ELLIPTIC BODY-WING LINEAR CN CORRECTION
C **                  FACTOR (FACTOR MULTIPLIES WING CN IN
C **                  PRESENCE OF EQUIVALENT CIRCULAR BODY)
C **
      REAL K,K12,K1,KEWCNL,K2,K22,INTRPL
C **
C ** FUNCTIONS
C **
      RATSCI(S,S2,R,R2)=(S*S2/(3.0*R2)-(R2/S)
     1-S+5.0*R/3)/(S-R)
      RATSBI(A,B,K,S,P)=((1./((S-A)*4.*K**4*A*B))*((K**4+(A+B)**4)
     1*(2./3.*(S**3-A**3)-P*S*K*K+P*A*K*K)+2.*(K**4-(A+B)**4)
     2*(1./3.*((S*S-P*K*K)**1.5-(A*A-P*K*K)**1.5))
     3-(2.*(A+B)**2*(S-A)*K**4)))+A/B
      INTRPL(X1,X2,Y1,Y2,X)=(Y2-Y1)/(X2-X1)*(X-X1)+Y1
      AVE(X1,X2,RAT1,RAT2)=(X1*RAT1+X2*RAT2)/(RAT1+RAT2)
      RATSBC(S2,R2)=S2/R2+R2/S2-1.0
      RATSB(A,B,K12,A2)=(K12+A2)/(A*B)
      AK(SIG1,A,B)=SIG1-(A+B)**2/(4.0*SIG1)
      SIGMA(S,S2,A2,B2)=0.5*(S+SQRT(S2-A2+B2))
C
      KEWCNL=1.
      IF(A .EQ. B .OR. (A. EQ. 0. .OR. B .EQ. 0.))GO TO 1060
C **
C **  SET UP VARIABLES FOR CALCULATIONS
C **
      RBE=0.5*DBE
      SBEXP=0.5*BEXP
      SA=A*0.5
      SB=B*0.5
      RBE2=RBE*RBE
      K=SQRT(SA*SA-SB*SB)
      A2=SA*SA
      B2=SB*SB
      RSBE9=0.0
      RSBEZ=0.0
      RSBE9I=0.0
      RSBEZI=0.0
C **
C **  COMPUTE WING CHORD SEGMENT LENGTHS
C **
      CLEAD=SBEXP*TLES
      CTRAIL=SBEXP*ABS(TTES)
      CMID=CR-CLEAD-CTRAIL
      IF(TTES.LE.0.0) GO TO 1010
C **
C **  HERE IF WING TRAILING EDGE SWEPT AFT
C **
      CT=TAPER*CR
      CMID=CT-CTRAIL
      IF(CMID.LE.0.0) GO TO 1000
C **
C **  HERE IF WING TRAILING EDGE ORIGIN IS BEHIND WING
C **  TIP ORIGIN.
C **
      CTRAIL=0.0
      GO TO 1010
 1000 CONTINUE
C **
C **  HERE IF WING TRAILING EDGE ORIGIN IS FORWARD OF
C **  WING TIP ORIGIN.
C **
      CMID=0.0
      CLEAD=CR
      CTRAIL=0.0
      SBEXP=CR/TLES
C **
 1010 CONTINUE
C **
      CX=CLEAD+CTRAIL
      S=SBEXP+SA
      S2=S*S
      SCIRC=RBE+SBEXP
      SCIRC2=SCIRC*SCIRC
      S90=SBEXP+SB
      S902=S90*S90
      APHI=ABS(PHI)
      ATAPER=ABS(TAPER)
      IF(ATAPER.LT.0.01) GO TO 1030
C **
C **  COMPUTE CONSTANT SPAN PORTION OF CN RATIO
C **
      RSBC=RATSBC(SCIRC2,RBE2)
      IF(APHI.LE.0.1) GO TO 1020
C **
C **  HERE IF PHI IS NOT ZERO
C **
      SIG2=SIGMA(S90,S902,B2,A2)
      K2=AK(SIG2,SB,SA)
      K22=K2*K2
      RSBE9=RATSB(SB,SA,K22,B2)/RSBC
      IF(APHI.GE.89.9) GO TO 1030
 1020 CONTINUE
C **
C **  HERE IF PHI IS NOT 90 DEG.
C **
      SIG1=SIGMA(S,S2,A2,B2)
      K1=AK(SIG1,SA,SB)
      K12=K1*K1
      RSBEZ=RATSB(SA,SB,K12,A2)/RSBC
 1030 CONTINUE
C **
C **  HERE IF WING IS SWEPT
C **
      RSBCI=RATSCI(SCIRC,SCIRC2,RBE,RBE2)
      IF(APHI.LE.0.1) GO TO 1040
C **
C **  HERE IF PHI IS NOT ZERO
C **
      RSBE9I=RATSBI(SB,SA,K,S90,-1.0)/RSBCI
      IF(APHI.GE.89.9) GO TO 1050
 1040 CONTINUE
C **
C **  HERE IF PHI IS NOT 90 DEG.
C **
      RSBEZI=RATSBI(SA,SB,K,S,1.0)/RSBCI
 1050 CONTINUE
C **
C **  COMBINE FACTORS FOR SWEPT AND CONSTANT SPAN PORTIONS
C **   OF WING
C **
      RSBE9=AVE(RSBE9,RSBE9I,CMID,CX)
      RSBEZ=AVE(RSBEZ,RSBEZI,CMID,CX)
C **
C **  COMBINE FACTORS FOR ORIENTATION OF ELLIPSE
C **
      KEWCNL=INTRPL(90.0,0.0,RSBE9,RSBEZ,PHI)
C **
 1060 CONTINUE
C
      RETURN
      END
