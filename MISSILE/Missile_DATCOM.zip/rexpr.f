      SUBROUTINE REXPR(RMACH,ICONF,IFOUND,XSREF,XLREF,XLATR,XXCG,
     1   XZCG,XNALPH,XALPHA,XCN,XCM,XCA,XCY,XCSN,XCSL)
C
C***  READ EXPERIMENTAL DATA FROM TAPE 2
C
C***  INPUTS
C
C   RMACH - MACH NUMBER DESIRED
C   ICONF - CODE OF CONFIGURATION DESIRED
C           1=BODY, 2=F1,   3=F2,    4=F3,    5=F4,
C           6=BF1,  7=BF12, 8=BF123, 9=BF1234
C
C***  OUTPUTS
C
C   IFOUND - ZERO IF DATA FOUND, ONE IF NOT FOUND
C    XSREF - REFERENCE AREA
C    XLREF - REFERENCE LENGTH
C    XLATR - LAT/DIR REFERENCE LENGTH
C     XXCG - LONGITUDINAL CG
C     XZCG - VERTICAL CG
C   XNALPH - NUMBER OF ANGLES OF ATTACK
C   XALPHA - ANGLES OF ATTACK
C      XCN - NORMAL FORCE COEFFICIENT
C      XCM - PITCHING MOMENT COEFFICIENT
C      XCA - AXIAL FORCE COEFFICIENT
C      XCY - SIDE FORCE COEFFICIENT
C     XCSN - YAWING MOMENT COEFFICIENT
C     XCSL - ROLLING MOMENT COEFFICIENT
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C      
      CHARACTER*4 KAND,NLNAME,IVNAME
C
      REAL MACH,NALPHA,LREF,LATREF
C
      LOGICAL IEOF
C
      DIMENSION ALPHA(20),CN(20),CM(20),CA(20),CY(20),CSN(20),CSL(20)
      DIMENSION XALPHA(20),XCN(20),XCM(20),XCA(20),XCY(20),XCSN(20),
     1 XCSL(20)
C
      DIMENSION NLNAME(4),IVNAME(53),LEN(15),IDIM(15),LOC(15),AA(148)
C
      EQUIVALENCE (MACH,AA(1)),(NALPHA,AA(2)),(ALPHA(1),AA(3)),
     1 (SREF,AA(23)),(LREF,AA(24)),(LATREF,AA(25)),(XCG,AA(26)),
     2 (ZCG,AA(27)),(CONF,AA(28)),(CN(1),AA(29)),(CM(1),AA(49)),
     3 (CA(1),AA(69)),(CY(1),AA(89)),(CSN(1),AA(109)),
     2 (CSL(1),AA(129))
C
      DATA NLNAME /  'E   ','X   ','P   ','R   '/
      DATA IVNAME /  'M   ','A   ','C   ','H   ','N   ','A   ','L   ',
     1 'P   ','H   ','A   ','A   ','L   ','P   ','H   ','A   ','S   ',
     2 'R   ','E   ','F   ','L   ','R   ','E   ','F   ','L   ','A   ',
     3 'T   ','R   ','E   ','F   ','X   ','C   ','G   ','Z   ','C   ',
     4 'G   ','C   ','O   ','N   ','F   ','C   ','N   ','C   ','M   ',
     5 'C   ','A   ','C   ','Y   ','C   ','S   ','N   ','C   ','S   ',
     6 'L   '/
      DATA IDIM   / 1,1,20,1,1,1,1,1,1,20,20,20,20,20,20 /
      DATA LEN    / 4,6,5,4,4,6,3,3,4,2,2,2,2,3,3 /
      DATA LOC    / 1,2,3,23,24,25,26,27,28,29,49,69,89,109,129 /
C
      IFOUND=1
C
 1000 MACH=UNUSED
      NALPHA=UNUSED
      SREF=UNUSED
      LREF=UNUSED
      LATREF=UNUSED
      XCG=UNUSED
      ZCG=UNUSED
      CONF=UNUSED
      DO 1010 I=1,20
         ALPHA(I)=UNUSED
         CN(I)=UNUSED
         CM(I)=UNUSED
         CA(I)=UNUSED
         CY(I)=UNUSED
         CSN(I)=UNUSED
         CSL(I)=UNUSED
 1010 CONTINUE
C
      CALL NAMER(KAND,2,NLNAME,4,IVNAME,53,LEN,15,IDIM,AA,148,LOC,IEOF)
      IF(IEOF)GO TO 1040
C
C***  CHECK FOR CORRECT MACH NUMBER
C***  NO INPUT MACH NUMBER IMPLIES CURRENT MACH NUMBER
C
 1020 IF(MACH .EQ. UNUSED)GO TO 1030
      IF(MACH .NE. RMACH) GO TO 1000
C
C***  CHECK FOR CORRECT CONFIGURATION
C
 1030 JCONF=CONF+.5
      IF(JCONF .NE. ICONF)GO TO 1000
C
      IFOUND=0
C
 1040 CONTINUE
C
      XSREF=SREF
      XLREF=LREF
      XLATR=LATREF
      XXCG=XCG
      XZCG=ZCG
      XNALPH=NALPHA
      DO 1050 I=1,20
         XALPHA(I)=ALPHA(I)
         XCN(I)=CN(I)
         XCM(I)=CM(I)
         XCA(I)=CA(I)
         XCY(I)=CY(I)
         XCSN(I)=CSN(I)
         XCSL(I)=CSL(I)
 1050 CONTINUE
C
      RETURN
      END
