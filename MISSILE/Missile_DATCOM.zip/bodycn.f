      SUBROUTINE BODYCN(IM,CNAN,CNAC,CNABT)
C
C***  EXECUTIVE SUBROUTINE TO COMPUTE THE NORMAL FORCE OF BODIES
C
C***  INPUT
C
C     IM -- MACH NUMBER INDEX
C
C***  OUTPUTS
C
C   CNAN -- NOSE NORMAL FORCE SLOPE, PER RADIAN
C   CNAC -- CENTERBODY NORMAL FORCE SLOPE, PER RADIAN
C  CNABT -- BOATTAIL/FLARE NORMAL FORCE SLOPE, PER RADIAN
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      REAL NALPHA,NMACH,MACH
C
      COMMON /TOTALC/ BALPHA(20),BBETA(20),BPHI(20),ALPTOT(20)
C
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      REAL LREF,LATREF
C
      COMMON /ABODIN/ NX,XO,X(50),R(50),TNOSE,LNOSE,DNOSE,BNOSE,
     1                TRUNC,LCENTR,DCENTR,TAFT,LAFT,DAFT,
     2                POWER,DISCON(20),ADUM(748)
      REAL NX,LNOSE,LCENTR,LAFT
      LOGICAL TRUNC
C
      COMMON /GEOBOD/ SPNOSE,SWNOSE,VOLNOS,XCVNOS,XCPNOS,FRNOSE,
     1  THEOLN,THEOFN,SPCENT,SWCENT,VOLCEN,XCVCEN,XCPCEN,FRCENT,
     2  THEOLC,THEOFC,SPAFT ,SWAFT ,VOLAFT,XCVAFT,XCPAFT,FRAFT ,
     3  THEOLA,THEOFA,SPLAN ,SWET  ,VOL   ,XCENTV,XCENTP,FR    ,
     4                DBASE ,SBASE ,DMAX  ,SMAX  ,LTOTAL,GDUM(12)
      REAL LTOTAL
C
      COMMON /SBODY/ CNB(20),CMB(20),CAB(20),CYB(20),CLNB(20),CLLB(20),
     1               CNAB(20),CMAB(20),CYBB(20),CNBB(20),CLBB(20)
C
      COMMON /BDWORK/  CNP(20),CMP(20),CNVIS(20),CMVIS(20),CAPR(20),
     &                 CAF(20),CABASE(20),ETA,CDC(20),CAPROT(20),
     &                 BOTDCA(20),BOTDCN(20),BOTDCM(20)
      COMMON /XRBLNT/ XTEMP(50),RTEMP(50),NXTEMP,VNTEMP,SWTEMP,ITYPE
C
      CNAN=0.
      CNAC=0.
      CNABT=0.
      IF(ITYPE.EQ.1) GO TO 500
C
C***  POTENTIAL CN SLOPE
C
C ... NOSE - CENTERBODY
C
      FRCNA = FRCENT + FRAFT
      CALL BDCNAN(TNOSE,MACH(IM),THEOFN,FRCNA,CNAN)
      CNAN=CNAN*PI*DNOSE**2/4./SREF
C
C ... BOATTAIL/FLARE INCREMENT
C
      DFWD=DNOSE
      IF(LCENTR .GT. UNUSED)DFWD=DCENTR
C
      IF(LAFT .GT. UNUSED .AND. DAFT .LT. DFWD)
     1 CALL BDCNAB(MACH(IM),DBASE,DFWD,CNABT)
      IF(LAFT .GT. UNUSED .AND. DAFT .GT. DFWD)
     1 CALL BDCNAF(DFWD,DAFT,CNABT)
      CNABT=CNABT*PI*DFWD**2/4./SREF
C
C ... TOTAL
C
      CNALP=CNAN+CNAC+CNABT
      IF(CNALP .LT. 0.) CNALP=0.
      GO TO 600
  500 CONTINUE
C
C  FOR USER DEFINED BODIES (X,R) CALCULATE POTENTIAL CN-ALPHA
C
      CALL SUBPTS(NX,X,R,MACH(IM),CNALP,CMNCB)
  600 CONTINUE
C
C***  COMPUTE CROSS FLOW DRAG PROPORTIONALITY FACTOR
C***  USING EFFECTIVE BODY FINENESS RATIO
C
      CALL GETETA(LTOTAL**2/SPLAN,MACH(IM),ETA)
C
C***  ANGLE OF ATTACK LOOP
C
      NALP=NALPHA+0.5
      DO 1000 I=1,NALP
C
C***     POTENTIAL CN
C
         CALL BDCNP(CNALP,ALPTOT(I),CNP(I))
C
C***     VISCOUS CN
C
C        COMPUTE CROSS FLOW DRAG COEFFICIENT
C        USING EFFECTIVE DIAMETER
C
         CALL CDCS(ALPTOT(I),REN(IM),SPLAN/LTOTAL,MACH(IM),CDC(I))
C
C        VISCOUS COMPONENT AT ANGLE OF ATTACK
C
         CALL BDCNV(ALPTOT(I),CDC(I),ETA,SPLAN,SREF,CNVIS(I))
C
C***     TOTAL NORMAL FORCE
C
         CNB(I)=(CNP(I)+CNVIS(I))*COS(BPHI(I)/RAD)
         CYB(I)=-(CNP(I)+CNVIS(I))*SIN(BPHI(I)/RAD)
C
 1000 CONTINUE
C
      RETURN
      END
