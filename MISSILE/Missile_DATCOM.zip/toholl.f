      SUBROUTINE TOHOLL(VALUE,NDEC,N,IHOLL)
C
C***  CONVERT A REAL NUMBER TO HOLLERITH
C
C***  INPUTS
C
C  VALUE -- REAL VALUE OF THE NUMBER
C   NDEC -- NUMBER OF DECIMAL PLACES TO BE RETAINED
C      N -- DIMENSION OF IHOLL ARRAY
C
C***  OUTPUT
C
C   IHOLL-- ARRAY FOR THE HOLLERITH RESULT
C           (RESULTS PUT IN LARGEST INDICES OF ARRAY)
C
      DIMENSION IHOLL(N),INUM(10)
      CHARACTER*4 INUM,IPOINT,IMINUS,IBLANK,ISTAR,IHOLL
      DATA INUM /'1   ','2   ','3   ','4   ','5   ','6   ',
     1           '7   ','8   ','9   ','0   '/
      DATA IPOINT /'.   '/
      DATA IMINUS /'-   '/
      DATA IBLANK /'    '/
      DATA ISTAR /'*   '/
C
C ... BLANK IHOLL ARRAY
C
      DO 1000 I=1,N
         IHOLL(I)=IBLANK
 1000 CONTINUE
C
C ... ROUND THE NUMBER TO NDEC DECIMAL DIGITS
C
      V=ABS(VALUE)*10**NDEC
C
C ... NUMBER OF DIGITS TO SEARCH + DECIMAL POINT
C
      NDIGTS=N-1
      IF(V .NE. 0.)NDIGTS=AINT(ALOG10(V))+1.5
      IF(NDIGTS .GT. N)NDIGTS=N
      IV=V+0.5
      IOFF=0
C
C ... LOOP ON NUMBER DIGITS
C
C  REV 3/99 MODIFICATION TO ALLOW <1% THICKNESS AIRFOIL 
C
      IF(NDIGTS.EQ.1) NDIGTS=2
      DO 1020 I=1,NDIGTS
         INDEX=N-I+1
         IF(I-1 .EQ. NDEC)IHOLL(INDEX)=IPOINT
         IF(I-1 .EQ. NDEC)IOFF=1
C
C ... EXTRACT DIGIT
C
         IDIGIT=10.*(FLOAT(IV)/10.**I-AINT(FLOAT(IV)/10.**I))+0.01
C
C ... FIND MATCH AND SET VALUE
C
         INDEX=N-I+1-IOFF
         IF(INDEX .LT. 1)INDEX=1
         IF(INDEX .EQ. 1 .AND. I .NE. NDIGTS)IHOLL(INDEX)=ISTAR
         IF(INDEX .EQ. 1 .AND. I .NE. NDIGTS)GO TO 1030
C
         DO 1010 J=1,10
            IF(J .LT. 10 .AND. IDIGIT .EQ. J)IHOLL(INDEX)=INUM(J)
            IF(J .EQ. 10 .AND. IDIGIT .EQ. 0)IHOLL(INDEX)=INUM(J)
 1010    CONTINUE
 1020 CONTINUE
C
C ... INSERT SIGN
C
      INDEX=N-NDIGTS-IOFF
      IF(VALUE .LT. 0.)IHOLL(INDEX)=IMINUS
C
 1030 CONTINUE
C
      RETURN
      END
