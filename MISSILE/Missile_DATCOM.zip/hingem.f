      SUBROUTINE HINGEM(IM,NSETS,NFIN,RADW,DELTAW,DREF,XCG,XHINGE,
     1                  SKEW,SSPAN,YCP,XCPWA)
C
C*** ROUTINE TO COMPUTE AND PRINT PANEL HINGE MOMENT DATA
C
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /PARTF/ PARTS(19)
C
      COMMON /PAERO/  AKBW(4),AKWB(4),AKKBW(4),AKKWB(4),XCPBW(4),
     1                XCPWB(4),DAQ(8,20,4),WAQ(8,20,4),
     2                CNW(8,20,4),CLW(8,20,4),CNWB(20,4),CMWB(20,4),
     3                CAWB(20,4),CYWB(20,4),CSNWB(20,4),CSLWB(20,4),
     4                CNBW(20,4),CMBW(20,4),CABW(20,4),CYBW(20,4),
     5                CSNBW(20,4),CSLBW(20,4),CN(20,4),CM(20,4),
     6                CA(20,4),CY(20,4),CSN(20,4),CSL(20,4)
C
      DIMENSION HING(8,20,4),RADW(4),DELTAW(8,4),XHINGE(4),NFIN(4),
     1   SKEW(4),SSPAN(4),YCP(20,4),XCPWA(4),BEND(8,20,4)
C
      REAL NALPHA,MACH
C
      LOGICAL NDUM,PARTS
C
      NDUM=.FALSE.
C
      NALP=NALPHA+.5
C
      IF(.NOT. PARTS(13))GO TO 1050
C***
C*** COMPUTE BENDING MOMENTS AND PRINT
C***
      DO 1020 ISET=1,NSETS
         NFINS=NFIN(ISET)
         DO 1010 IFIN=1,NFINS
            DO 1000 I=1,NALP
               BEND(IFIN,I,ISET)=0.
               IF(CNW(IFIN,I,ISET) .NE. 0.)
     1         BEND(IFIN,I,ISET)=YCP(I,ISET)*SSPAN(ISET)/DREF*
     2            CNW(IFIN,I,ISET)
 1000       CONTINUE
 1010    CONTINUE
 1020 CONTINUE
C
      DO 1040 ISET=1,NSETS
         NFINS=NFIN(ISET)
         CALL HEADR9
         WRITE(6,1120)ISET
         CALL PRFLC9(IM,NDUM,NDUM,NDUM)
         WRITE(6,1150)
         WRITE(6,1190)
C
         DO 1030 I=1,NALP
              WRITE(6,1170)ALPHA(I),(BEND(IFIN,I,ISET),IFIN=1,NFINS)
 1030    CONTINUE
 1040 CONTINUE
C
 1050 CONTINUE
      IF(.NOT. PARTS(14))GO TO 1110
C***
C*** COMPUTE HINGE MOMENTS AND PRINT
C***
      DO 1080 ISET=1,NSETS
         NFINS=NFIN(ISET)
         DO 1070 IFIN=1,NFINS
            DO 1060 I=1,NALP
C
C   REV 3/99 CALCULATE FIN C.P. FROM TRI-SERVICE DATA BASE
C   CORRELATIONS DEVELOPED BY NIELSEN ENGNEERING AND RESEARCH
C
            CALL HMCP(ISET,MACH(IM),
     1                ABS(CNW(IFIN,I,ISET)),XCPWA(ISET),XCPNR)
               ARM=(XHINGE(ISET)-XCPNR
     1             +YCP(1,ISET)*SSPAN(ISET)*SIN(SKEW(ISET)/RAD))
     1             *COS(SKEW(ISET)/RAD)
               HING(IFIN,I,ISET)=CNW(IFIN,I,ISET)*ARM/DREF
 1060       CONTINUE
 1070    CONTINUE
 1080 CONTINUE
C
      DO 1100 ISET=1,NSETS
         NFINS=NFIN(ISET)
         CALL HEADR9
         WRITE(6,1130)ISET
         CALL PRFLC9(IM,NDUM,NDUM,NDUM)
         WRITE(6,1150)
         WRITE(6,1190)
C
         DO 1090 I=1,NALP
            WRITE(6,1170)ALPHA(I),(HING(IFIN,I,ISET),IFIN=1,NFINS)
 1090    CONTINUE
 1100 CONTINUE
C
 1110 CONTINUE
C
      RETURN
C***
C***  FORMATS
C***
 1120 FORMAT(8X,'FIN SET ',I1,' PANEL BENDING MOMENTS (ABOUT',
     1 ' EXPOSED ROOT CHORD)',/)
 1130 FORMAT(12X,'FIN SET ',I1,' PANEL HINGE MOMENTS (ABOUT',
     1 ' HINGE LINE)',/)
 1150 FORMAT(3X,'ALPHA',3X,'PANL 1',3X,'PANL 2',3X,'PANL 3',3X,
     1 'PANL 4',3X,'PANL 5',3X,'PANL 6',3X,'PANL 7',3X,'PANL 8')
 1170 FORMAT(3X,F5.1,1P,8(1X,E9.2))
 1180 FORMAT(20X,F9.4,5X,1P,E12.5,4X,E12.5,4X,E12.5,4X,E12.5/
     1 (34X,E12.5,4X,E12.5,4X,E12.5,4X,E12.5))
 1190 FORMAT(' ')
      END
