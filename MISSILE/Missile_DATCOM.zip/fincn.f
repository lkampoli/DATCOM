      SUBROUTINE FINCN (IM,NFIN)
C      
C***  EXECUTIVE SUBROUTINE TO COMPUTE FIN ALONE NORMAL FORCE
C      
C***  REFERENCE      DATCOM EQUATION 4.1.3.3-A
C      
C***  INPUT          IM       MACH NUMBER INDEX
C***                 NFIN     FIN SET INDEX
C***  OUTPUT         CNFX     FIN ALONE NORMAL FORCE REFERENCED TO
C***                          SREF (LABELED COMMON SFINX)
C      
C  MODIFIED BY K. DETERS, MCDONNELL DOUGLAS
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C      
      COMMON /REFQN/ SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,
     1               RHR
      REAL LREF,LATREF
C      
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      REAL NALPHA,NMACH,MACH
C      
      COMMON /TOTALC/ BALPHA(20),BBETA(20),BPHI(20),ALPTOT(20)
C      
      COMMON /GEOFC/ FINAR,FINTPR,FINSPN,FINSP,FINWET,TCEFF,
     1               FINXGC,FINYGC,SWAVLE,SWAV25,SWAV50,SWAV75,
     2               SWAVTE,FINCBR,DELTAY,DELTAD,FINPHI,NPANEL,
     3               XLEF,XCENF,CROOT
      REAL NPANEL
C      
      COMMON /FSET1/  FSS1(373),GAM1(8),PHIF1(8),CFOC1(10)
      COMMON /FSET2/  FSS2(373),GAM2(8),PHIF2(8),CFOC2(10)
      COMMON /FSET3/  FSS3(373),GAM3(8),PHIF3(8),CFOC3(10)
      COMMON /FSET4/  FSS4(373),GAM4(8),PHIF4(8),CFOC4(10)
C      
      COMMON /F1WORK/ RHO1,TMAX1,KSHAR1,CCLA1(20),XAC1(20),CMCO41(20),
     1                CNALF1,CNAAF1(20),CNLF1(20),CNNLF1(20),CNF1AT(20),
     2                XCPL1,XCPNL1,CMFL1(20),CMFNL1(20),CMF1AT(20),
     3                CAOF1,CANLF1(20),ALPTF1(20),CNFIT1(20),AI1,ALO1,
     4                CLI1,CLM1(20)
      COMMON /F2WORK/ RHO2,TMAX2,KSHAR2,CCLA2(20),XAC2(20),CMCO42(20),
     1                CNALF2,CNAAF2(20),CNLF2(20),CNNLF2(20),CNF2AT(20),
     2                XCPL2,XCPNL2,CMFL2(20),CMFNL2(20),CMF2AT(20),
     3                CAOF2,CANLF2(20),ALPTF2(20),CNFIT2(20),AI2,ALO2,
     4                CLI2,CLM2(20)
      COMMON /F3WORK/ RHO3,TMAX3,KSHAR3,CCLA3(20),XAC3(20),CMCO43(20),
     1                CNALF3,CNAAF3(20),CNLF3(20),CNNLF3(20),CNF3AT(20),
     2                XCPL3,XCPNL3,CMFL3(20),CMFNL3(20),CMF3AT(20),
     3                CAOF3,CANLF3(20),ALPTF3(20),CNFIT3(20),AI3,ALO3,
     4                CLI3,CLM3(20)
      COMMON /F4WORK/ RHO4,TMAX4,KSHAR4,CCLA4(20),XAC4(20),CMCO44(20),
     1                CNALF4,CNAAF4(20),CNLF4(20),CNNLF4(20),CNF4AT(20),
     2                XCPL4,XCPNL4,CMFL4(20),CMFNL4(20),CMF4AT(20),
     3                CAOF4,CANLF4(20),ALPTF4(20),CNFIT4(20),AI4,ALO4,
     4                CLI4,CLM4(20)
C      
      COMMON /SFIN1/ CNF1(20),CMF1(20),CAF1(20),CYF1(20),CLNF1(20),
     1               CLLF1(20),CNAF1(20),CMAF1(20),CYBF1(20),
     2               CNBF1(20),CLBF1(20)
      COMMON /SFIN2/ CNF2(20),CMF2(20),CAF2(20),CYF2(20),CLNF2(20),
     1               CLLF2(20),CNAF2(20),CMAF2(20),CYBF2(20),
     2               CNBF2(20),CLBF2(20)
      COMMON /SFIN3/ CNF3(20),CMF3(20),CAF3(20),CYF3(20),CLNF3(20),
     1               CLLF3(20),CNAF3(20),CMAF3(20),CYBF3(20),
     2               CNBF3(20),CLBF3(20)
      COMMON /SFIN4/ CNF4(20),CMF4(20),CAF4(20),CYF4(20),CLNF4(20),
     1               CLLF4(20),CNAF4(20),CMAF4(20),CYBF4(20),
     2               CNBF4(20),CLBF4(20)
C      
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      COMMON /LOGIC/ LDMPCS,LDAMP,LBUILD,LNACA,LDERDG,
     1               LDERRD,LPART,LNAME,LPLOT,
     2               LFLT,LREFQ,LAXIS,LFIN1,LFIN2,LFIN3,LFIN4,
     3               LDEFL,LTRIM,LDIMIN,LDIMFT,LDIMCM,LDIMM,
     4               LELLB,LINLET,LEXPR,LICRMT,LSPIN,LARBOD
      LOGICAL        LDMPCS,LDAMP,LBUILD,LNACA,LDERDG,
     1               LDERRD,LPART,LNAME,LPLOT,
     2               LFLT,LREFQ,LAXIS,LFIN1,LFIN2,LFIN3,LFIN4,
     3               LDEFL,LTRIM,LDIMIN,LDIMFT,LDIMCM,LDIMM,
     4               LELLB,LINLET,LEXPR,LICRMT,LSPIN,LARBOD
C      
      DIMENSION IROUT(2),RTRANS(7),ALPTFX(20),PHIF(8)
C      
C***  GET APPROPRIATE SECTION CLA AND FOR SUBSONIC CASES GET ALO
C      
      GO TO (1000,1010,1020,1030), NFIN
C      
 1000 SECCLA = CCLA1(IM)*RAD
      CLM = CLM1(IM)
      ALO=0.0
      IF (MACH(IM) .LE. 0.8) ALO=ALO1
      GO TO 1040
 1010 SECCLA = CCLA2(IM)*RAD
      CLM = CLM2(IM)
      ALO=0.0
      IF (MACH(IM) .LE. 0.8) ALO=ALO2
      GO TO 1040
 1020 SECCLA = CCLA3(IM)*RAD
      CLM = CLM3(IM)
      ALO=0.0
      IF (MACH(IM) .LE. 0.8) ALO=ALO3
      GO TO 1040
 1030 SECCLA = CCLA4(IM)*RAD
      CLM = CLM4(IM)
      ALO=0.0
      IF (MACH(IM) .LE. 0.8) ALO=ALO4
C      
 1040 CONTINUE
C
C***  NORMAL FORCE CURVE SLOPE - CNA
C      
      CALL FCNA(MACH(IM),FINAR*2.,FINTPR,SWAVLE,SWAV50,SWAVTE,
     1          DELTAD,SECCLA,FINSP*2.,SREF,CNA)
C      
C***  STORE CNA IN WORK ARRAY
C      
      GO TO (1050,1070,1090,1110), NFIN
C      
 1050  CNALF1=CNA/2.
       DO 1060 IP=1,8
          PHIF(IP)=PHIF1(IP)
 1060  CONTINUE
       GO TO 1130
 1070  CNALF2=CNA/2.
       DO 1080 IP=1,8
          PHIF(IP)=PHIF2(IP)
 1080  CONTINUE
       GO TO 1130
 1090  CNALF3=CNA/2.
       DO 1100 IP=1,8
          PHIF(IP)=PHIF3(IP)
 1100  CONTINUE
       GO TO 1130
 1110  CNALF4=CNA/2.
       DO 1120 IP=1,8
          PHIF(IP)=PHIF4(IP)
 1120  CONTINUE
C      
 1130 CONTINUE
C
C***  
      XBETA=SQRT(ABS(MACH(IM)**2-1.))
      AR=FINAR*2.
C      
C***  ANGLE OF ATTACK LOOP
C      
      NALP=NALPHA+0.5
      NPAN = NPANEL + 0.5
      DO 1250 I=1,NALP
C      
C***     INITIALIZE PANEL SUMMATION TERMS
C      
       CNFAT=0.
       CNFI=0.
       CNLSUM=0.
       CNNLSM=0.
C      
C***     PANEL SUMMATION LOOP (FIN SET NORMAL FORCE
C***     IS SUM OF INDIVIDUAL PANEL CONTRIBUTIONS)
C      
C      
C***     CORRECT ALPTOT AND ALPEQ FOR ALO FOR SUBSONIC CASES
C      
         TEMP1=ALPTOT(I)
         ALPT=TEMP1-ALO
C      
         DO 1190 IPAN=1,NPAN
C      
         FPHI=PHIF(IPAN)
         FPHIC = FPHI
         IF(FPHIC.GT.180.0 .AND. FPHIC.LT.360.0) FPHIC=FPHIC-180.
         IF(FPHIC.LT.0.0 .AND. FPHIC.GT.-180.0)  FPHIC=ABS(FPHIC)
C      
C***     COMPUTE ALPHA EQUIVALENT (NO INTERFERENCE)
C      
C  REV 3/99 MODIFIED TO GIVE CORRECT RESULT FOR AOA>90 OR AOA<-90
C  ABS(COS(ALPT)) ADDED.
C
         ALPEQ = ATAN(SIN(ALPT/RAD)/ABS(COS(ALPT/RAD))
     1                *SIN(FPHIC/RAD))*RAD
         TEMP2=ALPEQ
C      
C***     COMPUTE THE FIN NORMAL FORCE FOR ALPTOT GREATER THAN ALO
C      
         IF (ALPT .LT. ALO) GO TO 1160
C      
C***     COMPUTE LINEAR CN TERM - CNL (1 PANEL)
C      
         CNL=(CNA/2.)*RAD/2.*SIN(2.*ALPEQ/RAD)
C      
C***     NONLINEAR COEFFICIENT - CNAA
C
         CALL FCNAA(MACH(IM),FINAR*2.,DELTAY,SWAVLE,SWAVTE,FINTPR,
     1              ALPEQ,CNA,FINSPN*2.,SECCLA,
     2              FINSP*2.,SREF,CLM,CNAA)
         CNAA=CNAA/2.
C      
         CALL FCNAA(MACH(IM),FINAR*2.,DELTAY,SWAVLE,SWAVTE,FINTPR,
     1              ALPT,CNA,FINSPN*2.,SECCLA,
     2              FINSP*2.,SREF,CLM,CNAAT)
         CNAAT=CNAAT/2.
C      
C***     COMPUTE NONLINEAR CN TERM - CNNL (1 PANEL)
C      
         CNNL=CNAA*SIN(ALPEQ/RAD)*ABS(SIN(ALPT/RAD))
C
         GO TO 1170
C      
 1160 CONTINUE
C      
C***     HERE FOR ALPTOT LESS THAN ALO
C      
         ALPEQ=ABS(ALPEQ)
         ALPT=ABS(ALPT)
C***     COMPUTE LINEAR CN TERM - CNL (1 PANEL)
C      
         CNL=-(CNA/2.)*RAD/2.*SIN(2.*ALPEQ/RAD)
C      
C***     NONLINEAR COEFFICIENT - CNAA
C      
         CALL FCNAA(MACH(IM),FINAR*2.,DELTAY,SWAVLE,SWAVTE,FINTPR,
     1              ALPEQ,CNA,FINSPN*2.,SECCLA,
     2              FINSP*2.,SREF,CLM,CNAA)
         CNAA=-CNAA/2.
C      
         CALL FCNAA(MACH(IM),FINAR*2.,DELTAY,SWAVLE,SWAVTE,FINTPR,
     1              ALPT,CNA,FINSPN*2.,SECCLA,
     2              FINSP*2.,SREF,CLM,CNAAT)
         CNAAT=-CNAAT/2.
C      
C***     COMPUTE NONLINEAR CN TERM - CNNL (1 PANEL)
C      
         CNNL=CNAA*SIN(ALPEQ/RAD)*ABS(SIN(ALPT/RAD))
         ALPT=TEMP1-ALO
 1170 CONTINUE
         ALPEQ=TEMP2
C
C***  LUCERO'S METHOD LIMITATIONS
      SUBLE=MACH(IM)*COS(SWAVLE/RAD)
        XSWP=0.0
        XRECT=0.0
        IF(FINTPR.GT.0.5 .OR.  SWAVLE.EQ.0.) XRECT=XBETA*AR
        IF(FINTPR.LE.0.5 .AND. SWAVLE.NE.0.) XSWP=XBETA/TAN(SWAVLE/RAD)
      IF(MACH(IM) .GE. 2.5 .AND. XRECT .LE. 1.35 .AND. 
     &  XSWP .LE. 1.35 .AND. SWAVLE .GE. 0. .AND. SUBLE
     &  .GT. 1.)THEN
C
C***    SUBROUTINE CALL STATEMENTS 
          IF(SWAVLE .EQ. 0.)THEN
          XTERM=1./(XBETA*AR)
          CALL SPLN2(XTERM,XMULT)
          ELSE IF(FINTPR .EQ. 0.)THEN
          XTERM=TAN(SWAVLE/RAD)/XBETA
          CALL SPLN2(XTERM,XMULT)
          ELSE
          XTERM1=1./(XBETA*AR)
          CALL SPLN2(XTERM1,XMULT1)
          XTERM2=TAN(SWAVLE/RAD)/XBETA
          CALL SPLN2(XTERM2,XMULT2)
          XMULT=XMULT1*FINTPR+XMULT2*(1.-FINTPR)
          ENDIF
       CNNL=CNNL*XMULT
       ENDIF
C      
C***     COMPUTE TOTAL PANEL NORMAL FORCE
C      
         CNFI   = (CNL + CNNL)*ABS(SIN(FPHIC/RAD))
C      
C***     SUM COMPONENTS (1 PANEL AT ALPHA TOTAL)
C      
         CNLSUM = CNLSUM + CNL*ABS(SIN(FPHIC/RAD))
         CNNLSM = CNNLSM + CNNL*ABS(SIN(FPHIC/RAD))
         CNFAT  = CNFAT + CNFI
C      
 1190  CONTINUE
C      
C***     STORE COEFFICIENTS IN WORK ARRAYS
C***     NOTE: STORED COEFF'S ARE SINGLE PANEL VALUES
C      
         GO TO (1210,1220,1230,1240),NFIN
C      
C           HERE IF FIN SET 1
C      
 1210    CNLF1(I)=CNLSUM
         CNAAF1(I)=CNAAT
         CNNLF1(I)=CNNLSM
         CNF1AT(I)=CNFAT
         GO TO 1250
C      
C           HERE IF FIN SET 2
C      
 1220    CNLF2(I)=CNLSUM
         CNAAF2(I)=CNAAT
         CNNLF2(I)=CNNLSM
         CNF2AT(I)=CNFAT
         GO TO 1250
C      
C           HERE IF FIN SET 3
C      
 1230    CNLF3(I)=CNLSUM
         CNAAF3(I)=CNAAT
         CNNLF3(I)=CNNLSM
         CNF3AT(I)=CNFAT
         GO TO 1250
C      
C           HERE IF FIN SET 4
C      
 1240    CNLF4(I)=CNLSUM
         CNAAF4(I)=CNAAT
         CNNLF4(I)=CNNLSM
         CNF4AT(I)=CNFAT
C      
 1250 CONTINUE
C      
C***  INTERPOLATE ALPHA TOTAL DERIVED CURVE AT BODY
C***  ANGLE OF ATTACK FOR STORAGE AND PRINTOUT
C      
      DO 1300 I=1,NALP
         GO TO (1260,1270,1280,1290),NFIN
 1260    CALL LNTRP(ALPTOT,CNF1AT,NALP,BALPHA(I),CNF1(I))
         GO TO 1300
 1270    CALL LNTRP(ALPTOT,CNF2AT,NALP,BALPHA(I),CNF2(I))
         GO TO 1300
 1280    CALL LNTRP(ALPTOT,CNF3AT,NALP,BALPHA(I),CNF3(I))
         GO TO 1300
 1290    CALL LNTRP(ALPTOT,CNF4AT,NALP,BALPHA(I),CNF4(I))
C      
 1300 CONTINUE
C      
C***  COMPUTE FIN PANEL LOADS AT FIXED RANGE OF ALPHAS
C      
      DO 1310 I=1,11
         ALPTFX(I)=FLOAT(I-1)*2.
 1310 CONTINUE
      DO 1320 I=12,20
         ALPTFX(I)=FLOAT(I-11)*4.+20.
 1320 CONTINUE
C      
      DO 1370 I=1,20
         CALL FCNAA(MACH(IM),FINAR*2.,DELTAY,SWAVLE,SWAVTE,FINTPR,
     1              ALPTFX(I),CNA,FINSPN*2.,SECCLA,
     2              FINSP*2.,SREF,CLM,CNAA)
         CNAA=CNAA/2.
C      
         CNL=(CNA/2.)*RAD/2.*SIN(2.*ALPTFX(I)/RAD)
C
         CNNL=CNAA*SIN(ALPTFX(I)/RAD)*ABS(SIN(ALPTFX(I)/RAD))    
C
C***  LUCERO'S METHOD LIMITATIONS
      SUBLE=MACH(IM)*COS(SWAVLE/RAD)
        XSWP=0.
        XRECT=0.
        IF(FINTPR.GT.0.5 .OR.  SWAVLE.EQ.0.) XRECT=XBETA*AR
        IF(FINTPR.LE.0.5 .AND. SWAVLE.NE.0.) XSWP=XBETA/TAN(SWAVLE/RAD)
      IF(MACH(IM) .GE. 2.5 .AND. XRECT .LE. 1.35 .AND. 
     &  XSWP .LE. 1.35 .AND. SWAVLE .GE. 0. .AND. SUBLE
     &  .GT. 1.)THEN
C
C***    SUBROUTINE CALL STATEMENTS 
          IF(SWAVLE .EQ. 0.)THEN
          XTERM=1./(XBETA*AR)
          CALL SPLN2(XTERM,XMULT)
          ELSE IF(FINTPR .EQ. 0.)THEN
          XTERM=TAN(SWAVLE/RAD)/XBETA
          CALL SPLN2(XTERM,XMULT)
          ELSE
          XTERM1=1./(XBETA*AR)
          CALL SPLN2(XTERM1,XMULT1)
          XTERM2=TAN(SWAVLE/RAD)/XBETA
          CALL SPLN2(XTERM2,XMULT2)
          XMULT=XMULT1*FINTPR+XMULT2*(1.-FINTPR)
          ENDIF
       CNNL=CNNL*XMULT
       ENDIF
C      
         CNFI   = CNL + CNNL
         GO TO (1330,1340,1350,1360),NFIN
 1330    ALPTF1(I)=ALPTFX(I)
         CNFIT1(I)=CNFI
         GO TO 1370
 1340    ALPTF2(I)=ALPTFX(I)
         CNFIT2(I)=CNFI
         GO TO 1370
 1350    ALPTF3(I)=ALPTFX(I)
         CNFIT3(I)=CNFI
         GO TO 1370
 1360    ALPTF4(I)=ALPTFX(I)
         CNFIT4(I)=CNFI
C      
 1370 CONTINUE
C      
      RETURN
      END
