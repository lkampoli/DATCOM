      SUBROUTINE SFWRW(M,AR,TAPER,LES,TES,FWRW)
C
C **  COMPUTES SURFACE VORTEX LATERAL POSITION FOR LIFTING
C **  SURFACE/BODY VORTEX INTERFERENCES.
C **  REFERENCE - NACA TR-1307, PITTS, NIELSEN, AND KAATARI.
C **  LIMITATIONS -  SLENDER BODY THEORY.  NO FORWARD SWEPT LEADING
C **                EDGE AND NO TRAILING EDGE SWEEP.  TAPER RATIO
C **                MUST FALL IN THE INTERVAL 0.-1.0.
C **  INPUT -
C **          M = MACH NO.
C **          AR = WING ALONE ASPECT RATIO
C **          TAPER = TIP TO ROOT CHORD TAPER RATIO
C **          LES = LEADING EDGE SWEEP ANGLE
C **          TES = TRAILING EDGE SWEEP ANGLE
C **  OUTPUT -
C **          FWRW = PARAMETER FOR LATERAL LOCATION OF SURFACE VORTEX.
C **               = (VORTEX LATERAL LOCATION-BODY RADIUS AT WING)/
C **                 (WING TOTAL SEMISPAN-BODY RADIUS AT WING)
C **               = (FW-RW)/(SW-RW)
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      REAL M,LES
      DIMENSION Z3(3),TX3(7),TY3(21),C(12),IROUT(2)
      DATA KG3,KC3,KP3,KD3/1,3,7,21/
      DATA G3/1./
C
C **  Z3 CORRESPONDS TO TAPER FROM CHART 6C.
C
      DATA Z3/0.,.5,1./
C
C **  TX3 CORRESPONDS TO BETA*AR FROM CHART 6C.
C
      DATA TX3/0.,1.,1.4,2.,4.,6.,8./
C
C **  TY3 - VALUES OF CHART 6C ORDINATE.
C **       COLUMNS CORRESPOND TO TX3.
C **       ROWS CORRESPOND TO Z3.
C
      DATA TY3/
     1.7844,.7891,.7893,.7913,.7934,.7022,.6608,
     2.7844,.7804,.7650,.8021,.8457,.8218,.8174,
     3.7844,.7891,.7782,.7543,.8891,.9326,.9478/
C
C **  COEFFICIENTS FOR CURVE FIT OF CHART 5C.
C
      DATA C/ .7874,-.0058,-.0020, .000110,
     1        -.0014,.0241,.0017,-.000029,
     2        -.0014,-.0140,.0027,-.000317/
      DATA AN/1.0/
C
      TLES=TAN(LES/RAD)
      TTES=TAN(TES/RAD)
      BETA=SQRT(ABS(M*M-1.0))
      BAR=BETA*AR
      IF(M.GT.0.8) GO TO 1000
C
C **  SUBSONIC FWRW FROM CHART 5C; NACA TR-1307.
C
      FWRW=FIT(C,BAR,TAPER,3,4)
      GO TO 1010
C
C **  SUPERSONIC FWRW FROM CHART 6C; NACA TR-1307.
C
 1000 CALL MVLOOK(KG3,KC3,KP3,KD3,G3,Z3,TX3,TY3,BAR,TAPER,AN,FWRW)
 1010 CONTINUE
C
      RETURN
      END
