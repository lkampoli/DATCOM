      SUBROUTINE FCNAAT(MACH, AR, DELY, SLE, TAPER, ALPHA, CNA,
     1                  B, SECCLA, SCLM, CNAA)
C ***
C *** COMPUTES TRANSONIC NON-LINEAR NORMAL FORCE PER SINE(ALPHA)**2
C ***
C *** REFERENCE   DATCOM SECTION 4.1.3.3
C ***
C *** LIMITATIONS   ALPHA .LT. 90 DEG
C ***               AR .LE. 4.0/((C1+1)*COS(SLE))
C ***               STRAIGHT TAPERED FINS
C ***               NO CAMBER
C ***
C *** INPUT   MACH    MACH NUMBER
C ***         AR      EXPOSED FIN ASPECT RATIO OF TWO PANELS
C ***         DELY    DIFFERENCE IN AIRFOIL ORDINATE BETWEEN
C ***                 .15% AND 6% CHORD,PER CENT CHORD LENGTH
C ***         SLE     LEADING EDGE SWEEP ANGLE, DEG
C ***         TAPER   EXPOSED FIN TAPER RATIO
C ***         ALPHA   ANGLE-OF-ATTACK, DEG
C ***         CNA     LINEAR CN, CN PER ALPHA AT ALPHA = 0, 1/DEG
C ***         B       FIN EXPOSED SPAN, TWO PANELS
C ***         SECCLA  SECTION LIFT CURVE SLOPE
C ***         SCLM    SECTION MAXIMUM LIFT
C ***
C *** OUTPUT  CNAA    CN PER SIN(ALPHA)**2,REFERENCED TO EXPOSED
C ***                 PLANFORM AREA OF TWO PANELS
C ***
C ***
      REAL MACH
C ***
C *** CONSTANT DATA
C ***
      COMMON /CONST/PI,RAD,UNUSED,KAND
C
C ***
C *** GET CNA AND CNAA AT MACH 0.60
C ***
      CR   = 2.0*B/(AR*(1.0+TAPER))
      CT   = CR*TAPER
      TLE  = TAN(SLE/RAD)
      TC2  = TLE+(CT-CR)/B
      SWEP50= ATAN(TC2)*RAD
      CALL FCNASB( SWEP50, AR, SECCLA, 0.60, CNA6)
      CALL FCNAAS(0.60, AR, DELY, SLE, TAPER, ALPHA, CNA6, SCLM, CNAA6)
      RC6 = CNAA6/CNA6
C ***
C *** GET CNA AND CNAA AT MACH 1.40
C ***
      DELN = RAD*ATAN(DELY/5.85)
      CALL FCNASP(1.40, TAPER, AR, SLE, DELN, CNA14)
      CALL FCNAAH(1.40, DELY, SLE, ALPHA, CNA14, CNAA14)
      RC14 = CNAA14/CNA14
C ***
C *** CALCULATE CNAA AT MACH
C ***
      RCM  = RC6+(RC14-RC6)*(MACH-0.60)/0.80
      CNAA = RCM*CNA
C ***
      RETURN
      END
