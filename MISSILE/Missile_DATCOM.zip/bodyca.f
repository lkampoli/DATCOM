      SUBROUTINE BODYCA(IM)
C
C***  SUBROUTINE TO COMPUTE THE BODY ALONE AXIAL FORCE COEFFICIENT
C
C  MODIFIED BY W. BLAKE, WL/FIGC
C
C***  IM IS THE CURRENT MACH NUMBER INDEX
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      REAL LREF,LATREF
C
      COMMON /ABODIN/ NX,XO,X(50),R(50),TNOSE,LNOSE,DNOSE,BNOSE,
     1              TRUNC,LCENTR,DCENTR,TAFT,LAFT,DAFT,POWER,
     2              DISCON(20),ELLDAT(103),DEXIT,DUM(62),PROTUB,
     3              ADUM(581)
      REAL NX,LNOSE,LCENTR,LAFT
      LOGICAL TRUNC,PROTUB,NA,NM,ND
C
      COMMON /XRBLNT/ XTEMP(50),RTEMP(50),NXTEMP,VNTEMP,SWTEMP,ITYPE
      REAL NXTEMP
C
      COMMON /GEOBOD/ SPNOSE,SWNOSE,VOLNOS,XCVNOS,XCPNOS,FRNOSE,
     1  THEOLN,THEOFN,SPCENT,SWCENT,VOLCEN,XCVCEN,XCPCEN,FRCENT,
     2  THEOLC,THEOFC,SPAFT ,SWAFT ,VOLAFT,XCVAFT,XCPAFT,FRAFT ,
     3  THEOLA,THEOFA,SPLAN ,SWET  ,VOL   ,XCENTV,XCENTP,FR    ,
     4                DBASE ,SBASE ,DMAX  ,SMAX  ,LTOTAL,GDUM(12)
      REAL LTOTAL
C
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      REAL NALPHA,NMACH,MACH
      COMMON /TOTALC/ BALPHA(20),BBETA(20),BPHI(20),ALPTOT(20)
C
      COMMON /SBODY/ CNB(20),CMB(20),CAB(20),CYB(20),CLNB(20),CLLB(20),
     1               CNAB(20),CMAB(20),CYBB(20),CNBB(20),CLBB(20)
C
      COMMON /BDWORK/  CNP(20),CMP(20),CNVIS(20),CMVIS(20),CAPR(20),
     &                 CAF(20),CABASE(20),ETA,CDC(20),CAPROT(20),
     &                 BOTDCA(20),BOTDCN(20),BOTDCM(20)
C
      COMMON /PARTF/ PART(19)
      LOGICAL PART
C
      DIMENSION CAOPI(11),CAOM(11)
C
C***  CAO FOR BLUNT NOSED BODIES, FROM HOERNER
C***  REFERENCE AFFDL-TR-76-55, MARCH 1979, P. 84
C
      DATA CAOM / 0.,.6,.8,1.,1.2,1.4,1.6,1.8,2.2,2.6,3.0/
      DATA CAOPI/ .86,.86,.96,1.21,1.39,1.53,1.62,4*1.68/
C
C***  PROTUBERANCE DRAG AT ZERO ANGLE OF ATTACK
      CPRTZ=0.0
      IF (PROTUB) CALL CDPROT(MACH(IM),REN(IM),SREF,CPRTZ)
C
C***  FRICTION DRAG AT ZERO ANGLE OF ATTACK
      CALL CAFRIC(0.,MACH(IM),REN(IM),LTOTAL,ROUGH,
     1            BLAYER,SWET,SREF,1.1547,1.022,CAFZ)
C
C***  SUBSONIC PRESSURE DRAG AT ZERO ANGLE OF ATTACK
      CALL BDCAPR(FR,CAFZ,CAPR(1))
C
C***  CORRECT FOR COMPRESSIBILITY EFFECTS
      IF (MACH(IM) .LT. 1.) CAPR(1)=CAPR(1)/SQRT(1.-MACH(IM)**2)
C
      IF (LNOSE.LE.UNUSED) GOTO 1000
C
C***  CHECK TO SEE IF THE CRITICAL MACH NUMBER HAS BEEN REACHED
      IF (BNOSE.GT.UNUSED) THEN
        CALL MDIV(FRNOSE,NXTEMP,RTEMP,XTEMP,LNOSE,CRMACH)
      ELSE
        CALL MDIV(FRNOSE,NX,R,X,LNOSE,CRMACH)
      ENDIF
C
C***  CALCULATE TRANSONIC WAVE DRAG
      IF (MACH(IM).GT.CRMACH) THEN
        IF (BNOSE.GT.UNUSED) THEN
C
C***      DETERMINE RATIO OF WAVE DRAG FOR BLUNTED BODY
C***      TO THAT OF A POINTED BODY.
          CALL BDCAWN(2.*BNOSE/DNOSE,FRNOSE,MACH(IM),CAWB)
          CALL BDCAWN(UNUSED,FRNOSE,MACH(IM),CAWS)
C
C VERSION 4/91A CORRECTION FOR NOSE BLUNTNESS
C
         CAPRAT=(CAWB-CAWS)*PI*DNOSE**2/4./SREF
C
C***      CALCULATE INCOMPRESSIBLE PRESSURE DRAG TO BE
C***      USED IN THE TRANSONIC WAVE DRAG CALCULATION
          CALL CAFRIC(0.,CRMACH,REN(IM),LTOTAL,ROUGH,BLAYER,SWTEMP,
     &            SREF,1.1547,1.022,CAFO)
          CALL BDCAPR(FR,CAFO,CDPO)
          CAPR(1)=0.0
C
C***      USE AREA RULE FORMULATION FOR WAVE DRAG
C***      CALCULATION OF SHARP NOSE
          CALL CDPRES(NXTEMP,XTEMP,RTEMP,SREF,MACH(IM),
     &           FRNOSE,VNTEMP,LNOSE,CRMACH,CDPO,CAPR(1))
C
C***      APPLY BLUNT NOSE CORRECTION FOR WAVE DRAG
          CAPR(1)=CAPR(1)+CAPRAT
        ELSE
C
C***      CALCULATE INCOMPRESSIBLE PRESSURE DRAG TO BE
C***      USED IN THE TRANSONIC WAVE DRAG CALCULATION
          CALL CAFRIC(0.,CRMACH,REN(IM),LTOTAL,ROUGH,BLAYER,SWET,
     &            SREF,1.1547,1.022,CAFO)
          CALL BDCAPR(FR,CAFO,CDPO)
          CAPR(1)=0.0
C
C***      USE AREA RULE FORMULATION FOR WAVE DRAG CALCULATION
          CALL CDPRES(NX,X,R,SREF,MACH(IM),FRNOSE,
     &                VOLNOS,LNOSE,CRMACH,CDPO,CAPR(1))
        ENDIF
      ENDIF
C
C***  BOATTAIL/FLARE PRESSURE/WAVE DRAG
 1000 DFWD=DCENTR
      IF(LCENTR .LE. UNUSED)DFWD=DNOSE
      CAPBT=0.
      CAWFL=0.
      IF(LAFT .GT. UNUSED .AND. DAFT .LT. DFWD)
     &    CALL BDCDRV(TAFT,DFWD,DAFT,LAFT,MACH(IM),THEOLA,CAPBT)
      IF(LAFT .GT. UNUSED .AND. DAFT .GT. DFWD)
     &    CALL BDCAWF(DFWD,DAFT,LAFT,MACH(IM),CAWFL)
C
C***  REFERENCE CAPBT AND CAWFL TO SREF
      CAPBT=CAPBT*PI*DFWD**2/4./SREF
      CAWFL=CAWFL*PI*DFWD**2/4./SREF
C
      CAPR(1)=CAPR(1)+CAPBT+CAWFL
C
C***  BASE DRAG AT ZERO ANGLE OF ATTACK
      CALL BDCAB(DFWD,DBASE,DEXIT,LAFT,SREF,MACH(IM),CABAS0)
C
C***  ESTIMATE CAO AT ALPHA=180 DEGREES
      CALL LNTRP(CAOM,CAOPI,11,MACH(IM),CAPI)
      CAPI=CAPI*PI*DMAX**2/4./SREF
      IF(TRUNC .OR. LNOSE.LE.UNUSED) CAPR(1)=CAPR(1)+
     &          CAPI*(2.*BNOSE/DMAX)**2
C
C***  AXIAL FORCE AT ZERO NORMAL FORCE (CAO)
      CAPRZ=CAPR(1)
      CAOB=CAFZ+CAPRZ+CPRTZ
C
C***  ANGLE OF ATTACK LOOP
      NALP=NALPHA+0.5
      DIABAS=DBASE
      IF(DBASE .LE. UNUSED)DIABAS=0.00001
      DO 1010 I=1,NALP
         ABSALP=ABS(ALPTOT(I))
C
C***  AXIAL FORCE VARIATION WITH ANGLE OF ATTACK
C
         CAF(I)=CAFZ*COS(ALPTOT(I)/RAD)*ABS(COS(ALPTOT(I)/RAD))
         CAPR(I)=CAPRZ*COS(ALPTOT(I)/RAD)*ABS(COS(ALPTOT(I)/RAD))
         CAPROT(I)=CPRTZ*COS(ALPTOT(I)/RAD)*ABS(COS(ALPTOT(I)/RAD))
C        CABASE(I)=(CABAS0*(SQRT(4.*SREF/PI)/DIABAS)**3-
C    1             (0.012-.0036*MACH(IM))*ABSALP/RAD*
C    2             (DIABAS/SQRT(4.*SREF/PI))**3
C   
C  REV 1/96  CORRECTED IMPLEMENTATION OF ANGLE OF ATTACK EFFECT
C
         CABASE(I)=CABAS0+(0.012-0.0036*MACH(IM))*ABSALP/RAD*
     1             (DIABAS/SQRT(4.*SREF/PI))**3
         CABASE(I)=CABASE(I)*COS(ALPTOT(I)/RAD)
C
C ...    SWITCH FROM ALLEN AND PERKINS FOR CA TO JORGENSEN
C ...    FOR ALPHAS GREATER THAN 30 DEGREES
C
         CNI=CNP(I)+CNVIS(I)
         IF(ABSALP .LE. 30.)CALL BDCALP(ALPTOT(I),CNI,CAOB,CADB)
C
C***     TOTAL AXIAL FORCE COEFFICIENT
C
C  REV 1/96  HIGH AOA AXIAL FORCE SUMMATION MODIFIED
C            PERKINS&ALLEN SWITCHOVER MOVED FROM 45 DEG AOA
C            TO 30 DEG AOA TO MINIMIZE DISCONTINUITY
C
         IF(ABSALP .LE. 30.)CAB(I)=CAOB+CADB
         IF(ABSALP .GT. 30.)CAB(I)=CAOB*COS(ALPTOT(I)/RAD)**2
         IF(DEXIT .NE. UNUSED)CAB(I)=CAB(I)+CABASE(I)
C
C ...    FOR ALPHAS GREATER THAN 90 DEGREES USE JORGENSEN
C ...    BUT USE CAO FROM BLUNT-NOSED BODY DATA
C
         IF(ABSALP .GE. 90.) CAB(I)=CAPI*COS(ALPTOT(I)/RAD)*
     1                              ABS(COS(ALPTOT(I)/RAD))
C
C  REV 1/96  ALPHA > 90 CA CHARGED TO BASE DRAG
C
         IF(ABSALP .GE. 90.) CABASE(I)=CAB(I)-CAF(I)-CAPR(I)-CAPROT(I)
C
 1010 CONTINUE
C
      RETURN
      END
