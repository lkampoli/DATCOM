      SUBROUTINE NEWTON(THETA,ALPHA,PHI1,CAF,CNF,CMF)
C
C***  COMPUTES THE PRESSURE-SURFACE-SLOPE INTEGRAL FACTORS
C***  FROM NASA TN D-176, EQUATIONS 13, 14, AND 15
C
C*** INPUTS
C
C  THETA - SURFACE SLOPE ANGLE, DEG.
C  ALPHA - ANGLE OF ATTACK, DEG.
C
C***  OUTPUTS
C
C   PHI1 - POLAR ANGLE TO EDGE OF SHADOWED REGION, DEG.
C          PHI1=0 AT TOP VERTICAL CENTER
C          IF PHI<PHI1 THEN THE LOCAL CP IS ZERO
C    CAF - INTEGRAL FACTOR FOR CA
C    CNF - INTGGRAL FACTOR FOR CN
C    CMF - INTEGRAL FACTOR FOR CM
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      EQN13(PHI)=-2.*CT*CT*SA*SA*SIN(PHI/RAD)*(COS(PHI/RAD)**2+2.)/3.
     1           +4.*ST*CT*CA*SA*(.5*SIN(PHI/RAD)*COS(PHI/RAD)+
     2           PHI/RAD/2.)-2.*ST*ST*CA*CA*SIN(PHI/RAD)
      EQN14(PHI)=2.*CT*ST*SA*SA*(.5*SIN(PHI/RAD)*COS(PHI/RAD)+PHI/RAD
     1           /2.)-4.*ST*ST*SA*CA*SIN(PHI/RAD)
     2           +2.*ST*ST*ST*CA*CA/CT*PHI/RAD
      EQN15(PHI)=-2.*CT*CT*SA*SA*SIN(PHI/RAD)*(COS(PHI/RAD)**2
     1           +2.)/3.+4.*ST*CT*CA*SA*(.5*SIN(PHI/RAD)*COS(PHI/RAD)
     2           +PHI/RAD/2.)-2.*ST*ST*CA*CA*SIN(PHI/RAD)
      ST=SIN(THETA/RAD)
      CT=COS(THETA/RAD)
      TT=ST/CT
      SA=SIN(ALPHA/RAD)
      CA=COS(ALPHA/RAD)
C
      IF(THETA .GT. 0.0) PHI1 = 0.0
      IF(THETA .LE. 0.0) PHI1 = PI
      IF(ALPHA .GT. ABS(THETA)) PHI1 = ACOS(TAN(THETA/RAD)/
     *TAN(ALPHA/RAD))
C
      CNF=EQN13(PI*RAD)-EQN13(PHI1*RAD)
      CAF=EQN14(PI*RAD)-EQN14(PHI1*RAD)
      CMF=EQN15(PI*RAD)-EQN15(PHI1*RAD)
C
      RETURN
      END
