      SUBROUTINE GEOELL
C
C***  COMPUTE GEOMETRY FOR ELLIPTICAL BODIES
C
C***  NOTE THAT THE ELLIPTICAL BODY DATA IS PLACED IN THE
C***  SAME ARRAY AS THE CIRCULAR BODY DATA IS PLACED
C***  THIS IS DONE TO CONSERVE CORE
C
      COMMON /ABODIN/ NX,XO,X(50),WIDTH(50),TNOSE,LNOSE,WNOSE,BNOSE,
     1                TRUNC,LCENTR,WCENTR,TAFT,LAFT,WAFT,
     2                POWER,DISCON(20),ELLIP(50),HEIGHT(50),ENOSE,
     3                ECENTR,EAFT,ADUM(645)
C
      COMMON /XRBLNT/ XTEMP(50),RTEMP(50),NXTEMP,VNTEMP,SWTEMP,ITYPE
      REAL NXTEMP,NXTMP
      REAL NX,LNOSE,LCENTR,LAFT,NCENT,NAFT
      LOGICAL TRUNC
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      REAL LREF,LATREF
C
      COMMON /GEOBOD/ SPNOSE,SWNOSE,VOLNOS,XCVNOS,XCPNOS,FRNOSE,
     1  THEOLN,THEOFN,SPCENT,SWCENT,VOLCEN,XCVCEN,XCPCEN,FRCENT,
     2  THEOLC,THEOFC,SPAFT ,SWAFT ,VOLAFT,XCVAFT,XCPAFT,FRAFT ,
     3  THEOLA,THEOFA,SPLAN ,SWET  ,VOL   ,XCENTV,XCENTP,FR    ,
     4                DBASE ,SBASE ,DMAX  ,SMAX  ,LTOTAL,
     5  BPNOSE,BCPNOS,BPCENT,BCPCEN,BPAFT,BCPAFT,BPLAN,
     6  BCENTP,ECSPN,ECSPC,ECSPA,ECSPT
C
      REAL LTOTAL
C
C            **NOTE**
C  ALL CALCULATIONS REFERENCED TO THE HORIZONTAL AXIS
C  ARE STORED IN GEOBOD 1-35 --- ALL VERTICAL AXIS
C  CALCULATIONS (DIFFERENT FROM HORIZONTAL CALCULATIONS)
C  ARE STORED IN GEOBOD 36-43
C
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      COMMON /LOGIC/ LFLAGS(18),LDIMIN,LDIMFT,LDIMCM,LDIMM,LDUM2(6)
      LOGICAL        LFLAGS    ,LDIMIN,LDIMFT,LDIMCM,LDIMM
C
      DIMENSION GEOB(43),GEOBEA(43),GEOBEB(43),TX(4),TE(4)
      DIMENSION REQ(50),DUMMY(20),WTEMP(50),HTEMP(50)
C
      EQUIVALENCE (GEOB(1),SPNOSE)
C
      X1V=0.
      X1H=0.
C
      NXTEMP=UNUSED
      DO 990 I=1,43
         GEOBEA(I)=UNUSED
         GEOBEB(I)=UNUSED
  990 CONTINUE
C
C***  DEFAULTS FOR ENOSE, ECENTR, AND EAFT
C
      IF(ENOSE .LE. UNUSED)ENOSE=1.
      IF(ECENTR .LE. UNUSED)ECENTR=1.
      IF(EAFT .LE. UNUSED)EAFT=1.
      IF(BNOSE .LE. UNUSED)BNOSE=0.
C
      HNOSE=UNUSED
      HCENTR=UNUSED
      HAFT=UNUSED
      IF(WNOSE .NE. UNUSED)HNOSE=ENOSE*WNOSE
      IF(WCENTR .NE. UNUSED)HCENTR=ECENTR*WCENTR
      IF(WAFT .NE. UNUSED)HAFT=EAFT*WAFT
C
      DO 1000 I=1,50
         IF(ELLIP(I) .EQ. UNUSED .AND. HEIGHT(I) .GT. UNUSED .AND.
     1      WIDTH(I) .GT. UNUSED)ELLIP(I)=HEIGHT(I)/WIDTH(I)
         IF(ELLIP(I) .EQ. UNUSED .AND. (HEIGHT(I) .LE. UNUSED .OR.
     1      WIDTH(I) .LE. UNUSED))ELLIP(I)=1.
         IF(ELLIP(I) .NE. UNUSED .AND. HEIGHT(I) .EQ. UNUSED)
     1    HEIGHT(I)=WIDTH(I)*ELLIP(I)
         IF(ELLIP(I) .NE. UNUSED .AND. WIDTH(I) .EQ. UNUSED)
     1    WIDTH(I)=HEIGHT(I)/ELLIP(I)
 1000 CONTINUE
C
C***  SCALE THE INPUT GEOMETRY
C
      IF(SCALE .EQ. UNUSED .OR. SCALE .EQ. 1.)GO TO 1020
      IF(XO .NE. UNUSED)XO=XO*SCALE
         DO 1010 I=1,50
            IF(X(I) .NE. UNUSED)X(I)=X(I)*SCALE
            IF(WIDTH(I) .NE. UNUSED)WIDTH(I)=WIDTH(I)*SCALE
            IF(HEIGHT(I) .NE. UNUSED)HEIGHT(I)=HEIGHT(I)*SCALE
 1010    CONTINUE
C
         IF(LNOSE .NE. UNUSED)LNOSE=LNOSE*SCALE
         IF(BNOSE .NE. UNUSED)BNOSE=BNOSE*SCALE
         IF(WNOSE .NE. UNUSED)WNOSE=WNOSE*SCALE
         IF(HNOSE .NE. UNUSED)HNOSE=HNOSE*SCALE
         IF(LCENTR .NE. UNUSED)LCENTR=LCENTR*SCALE
         IF(WCENTR .NE. UNUSED)WCENTR=WCENTR*SCALE
         IF(HCENTR .NE. UNUSED)HCENTR=HCENTR*SCALE
         IF(LAFT .NE. UNUSED)LAFT=LAFT*SCALE
         IF(WAFT .NE. UNUSED)WAFT=WAFT*SCALE
         IF(HAFT .NE. UNUSED)HAFT=HAFT*SCALE
C
 1020 CONTINUE
C
C  CHECK FOR INPUT OPTION
C
      IF(NX .GT. UNUSED)GO TO 1170
C
      ISEG=0
C
      BNOSEW=0.
      BNOSEH=0.
      IF(BNOSE .GT. UNUSED)BNOSEW=BNOSE
      IF(BNOSE .GT. UNUSED)BNOSEH=BNOSE*HNOSE/WNOSE
C
C***  HERE FOR OPTION 1 (SHAPES DEFINED)
C
C DEFAULTS ...
C
      IF(TAFT .LE. UNUSED)TAFT=0.
C
      IF(LNOSE .GT. UNUSED .AND. TNOSE .EQ. UNUSED)TNOSE=1.
      IF(LNOSE .GT. UNUSED .AND. TNOSE .GT. 4.)TNOSE=0.
      IF(LAFT  .GT. UNUSED .AND. TAFT  .GT. 1.)TAFT=0.
C
      IF(LNOSE .LE. UNUSED)GO TO 1100
C
      IF(TNOSE .NE. 0. .AND. LNOSE .LT. WNOSE/2.)TNOSE=0.
      IF(TNOSE .NE. 0. .AND. LNOSE .LT. HNOSE/2.)TNOSE=0.
      IF(TNOSE .NE. 0. .AND. LNOSE .LT. WNOSE/2. .AND. PARTS(18))
     1 WRITE(6,1240)
      IF(TNOSE .NE. 0. .AND. LNOSE .LT. HNOSE/2. .AND. PARTS(18))
     1 WRITE(6,1240)
C
C  SET NOSE TYPE
C
      JUMP=TNOSE+1.5
C
      IF(LNOSE .LE. UNUSED)GO TO 1100
C
C  COMPUTE THE APPROPIATE SHAPE
C
      GO TO (1030,1040,1050,1060,1060),JUMP
C
C            **NOTE**
C  GEOBEA -- GEOBOD PARAMETERS WITH RESPECT TO HORIZ.AXIS
C  GEOBEB -- GEOBOD PARAMETERS WITH RESPECT TO VERT. AXIS
C
 1030 IF (BNOSEW.GT.UNUSED)
     & CALL CONIC(GEOBEA,NXTEMP,XTEMP,WTEMP,UNUSED,.FALSE.,
     &            LNOSE,WNOSE,DUMMY)
      CALL CONIC(GEOBEA,NX,X,WIDTH,BNOSEW,TRUNC,LNOSE,WNOSE,DISCON)
      X1H=X(1)
      NX = 0.
      NXTEMP=0.0
      IF (BNOSEH.GT.UNUSED)
     & CALL CONIC(GEOBEB,NXTEMP,XTEMP,HTEMP,UNUSED,.FALSE.,
     &            LNOSE,HNOSE,DUMMY)
      CALL CONIC(GEOBEB,NX,X,HEIGHT,BNOSEH,TRUNC,LNOSE,HNOSE,DISCON)
      X1V=X(1)
      GO TO 1080
C
 1040 IF (BNOSEW.GT.UNUSED)
     & CALL OGIVE(GEOBEA,NXTEMP,XTEMP,WTEMP,UNUSED,.FALSE.,
     &            LNOSE,WNOSE,DUMMY)
      CALL OGIVE(GEOBEA,NX,X,WIDTH,BNOSEW,TRUNC,LNOSE,WNOSE,DISCON)
      X1H=X(1)
      NX = 0.
      NXTEMP=0.0
      IF (BNOSEH.GT.UNUSED)
     & CALL OGIVE(GEOBEB,NXTEMP,XTEMP,HTEMP,UNUSED,.FALSE.,
     &            LNOSE,HNOSE,DUMMY)
      CALL OGIVE(GEOBEB,NX,X,HEIGHT,BNOSEH,TRUNC,LNOSE,HNOSE,DISCON)
      X1V=X(1)
      GO TO 1080
C
 1050 IF (BNOSEW.GT.UNUSED)
     & CALL POWR(GEOBEA,NXTEMP,XTEMP,WTEMP,UNUSED,.FALSE.,
     &           LNOSE,WNOSE,POWER,DUMMY)
      CALL POWR(GEOBEA,NX,X,WIDTH,BNOSEW,TRUNC,LNOSE,WNOSE,POWER,
     1          DISCON)
      X1H=X(1)
      NX = 0.
      NXTEMP=0.0
      IF (BNOSEH.GT.UNUSED)
     & CALL POWR(GEOBEB,NXTEMP,XTEMP,HTEMP,UNUSED,.FALSE.,
     &           LNOSE,HNOSE,POWER,DUMMY)
      CALL POWR(GEOBEB,NX,X,HEIGHT,BNOSEH,TRUNC,LNOSE,HNOSE,POWER,
     1          DISCON)
      X1V=X(1)
      GO TO 1080
C
 1060 IF (BNOSEW.GT.UNUSED)
     & CALL HAACK(XO,TNOSE,WNOSE,UNUSED,.FALSE.,LNOSE,DUMMY,
     &            XTEMP,WTEMP,NXTEMP,GEOBEA)
      CALL HAACK(XO,TNOSE,WNOSE,BNOSEW,TRUNC,LNOSE,DISCON,
     &           X,WIDTH,NX,GEOBEA)
      X1H=X(1)
      NX = 0.
      NXTEMP=0.0
      IF (BNOSEH.GT.UNUSED)
     & CALL HAACK(XO,TNOSE,HNOSE,UNUSED,.FALSE.,LNOSE,DUMMY,
     &            XTEMP,HTEMP,NXTEMP,GEOBEB)
      CALL HAACK(XO,TNOSE,HNOSE,BNOSEH,TRUNC,LNOSE,DISCON,
     &           X,HEIGHT,NX,GEOBEB)
      X1V=X(1)
C
 1080 CONTINUE
      GEOB(1)=GEOBEA(1)
      GEOB(36)=GEOBEB(1)
      NNX=NX+.5
      GEOB(2)=DSWETE(NNX,X,WIDTH,HEIGHT)
      GEOB(3)=DVOLE(NNX,X,WIDTH,HEIGHT)
      GEOB(4)=DXCNVE(NNX,X,WIDTH,HEIGHT)
      GEOB(5)=GEOBEA(5)
      GEOB(37)=GEOBEB(5)
      GEOB(6)=LNOSE/(2.*SQRT((WNOSE/2.)*(HNOSE/2.)))
      GEOB(7)=GEOBEA(7)
      GEOB(8)=GEOB(7)/(2.*SQRT((WNOSE/2.)*(HNOSE/2.)))
C
      ISEG=ISEG+1
      TX(ISEG)=X(1)
      TE(ISEG)=ENOSE
      ISEG=ISEG+1
      TX(ISEG)=X(NNX)
      TE(ISEG)=ENOSE
      DO 1090 I=1,NNX
         REQ(I)=SQRT(WIDTH(I)*HEIGHT(I))
 1090 CONTINUE
C
      ECSPN=DSPLAN(NNX,X,REQ)
C
C***  FIND EQUIVALENT CIRCULAR RADIUS FOR SHARP NOSE (RTEMP)
C***  AND THE VOLUME FOR A SHARP AXISYMMETRIC NOSE (VNTEMP)
      IF (BNOSE.GT.UNUSED) THEN
        NTMP=NXTEMP+0.5
        DO 1095 I=1,NTMP
          RTEMP(I)=SQRT(WTEMP(I)*HTEMP(I))
 1095   CONTINUE
        VNTEMP=DVOL(NTMP,XTEMP,RTEMP)
      ENDIF
C
 1100 CONTINUE
C
C  NOW COMPUTE CENTERBODY DATA IF DEFINED
C
      IF(LCENTR .LE. UNUSED)GO TO 1120
      RFWDEA=WNOSE/2.
      RFWDEB=HNOSE/2.
C
      IF (BNOSE.GT.UNUSED) THEN
        NXTMP=NXTEMP
        CALL CONIC(GEOBEA(9),NXTEMP,XTEMP,WTEMP,RFWDEA,.TRUE.,
     &             LCENTR,WCENTR,DUMMY)
        NXTEMP=NXTMP
        CALL CONIC(GEOBEB(9),NXTEMP,XTEMP,HTEMP,RFWDEB,.TRUE.,
     &             LCENTR,WCENTR,DUMMY)
      ENDIF
C
      NCENT = NX
      CALL CONIC(GEOBEA(9),NX,X,WIDTH,RFWDEA,.TRUE.,LCENTR,WCENTR,
     1          DISCON)
      NX = NCENT
      CALL CONIC(GEOBEB(9),NX,X,HEIGHT,RFWDEB,.TRUE.,LCENTR,HCENTR,
     1          DISCON)
C
      GEOB(9)=GEOBEA(9)
      GEOB(38)=GEOBEB(9)
      NNX=NX+.5
      GEOB(10)=DSWETE(NNX,X,WIDTH,HEIGHT)-GEOB(2)
      GEOB(11)=DVOLE(NNX,X,WIDTH,HEIGHT)-GEOB(3)
      GEOB(12)=(DXCNVE(NNX,X,WIDTH,HEIGHT)*(GEOB(11)+GEOB(3))-
     1         GEOB(4)*GEOB(3))/GEOB(11)
      GEOB(13)=GEOBEA(13)
      GEOB(39)=GEOBEB(13)
      GEOB(14)=LCENTR/(2.*SQRT((WCENTR/2.)*(HCENTR/2.)))
      GEOB(15)=GEOBEA(15)
      GEOB(16)=GEOB(15)/(2.*SQRT((WCENTR/2.)*(HCENTR/2.)))
C
      DO 1110 I=1,NNX
         REQ(I)=SQRT(WIDTH(I)*HEIGHT(I))
 1110 CONTINUE
      ECSPC=DSPLAN(NNX,X,REQ)-ECSPN
      ISEG=ISEG+1
      TX(ISEG)=X(NNX)
      TE(ISEG)=ECENTR
C
C  NOW THE AFTERBODY
C
 1120 CONTINUE
      IF(LAFT .LE. UNUSED)GO TO 1180
      IF(LCENTR .NE. UNUSED)WC=WCENTR
      IF(LCENTR .NE. UNUSED)HC=HCENTR
      IF(LCENTR .LE. UNUSED)WC=WNOSE
      IF(LCENTR .LE. UNUSED)HC=HNOSE
C
C  SET AFTERBODY TYPE
C
      JUMP=TAFT+1.5
C
      RFWDEA=WC/2.
      RFWDEB=HC/2.
C
      IF(TAFT .NE. 0. .AND. LAFT .LT. RFWDEA)TAFT=0.
      IF(TAFT .NE. 0. .AND. LAFT .LT. RFWDEB)TAFT=0.
      IF(TAFT .NE. 0. .AND. LAFT .LT. RFWDEA .AND. PARTS(18))
     1 WRITE(6,1250)
      IF(TAFT .NE. 0. .AND. LAFT .LT. RFWDEB .AND. PARTS(18))
     1 WRITE(6,1250)
C
      NAFT = NX
      NXTMP=NXTEMP
C
C  COMPUTE THE APPROPIATE SHAPE
C
      GO TO (1130,1140),JUMP
C
 1130 IF (BNOSEW.GT.UNUSED)
     & CALL CONIC(GEOBEA(17),NXTEMP,XTEMP,WTEMP,RFWDEA,.TRUE.,
     &            LAFT,WAFT,DUMMY)
      CALL CONIC(GEOBEA(17),NX,X,WIDTH,RFWDEA,.TRUE.,LAFT,WAFT,DISCON)
      NX = NAFT
      NXTEMP=NXTMP
      IF (BNOSEH.GT.UNUSED)
     & CALL CONIC(GEOBEB(17),NXTEMP,XTEMP,HTEMP,RFWDEB,.TRUE.,
     &            LAFT,HAFT,DUMMY)
      CALL CONIC(GEOBEB(17),NX,X,HEIGHT,RFWDEB,.TRUE.,LAFT,HAFT,DISCON)
      GO TO 1150
C
 1140 IF (BNOSEW.GT.UNUSED)
     & CALL OGIVE(GEOBEA(17),NXTEMP,XTEMP,WTEMP,RFWDEA,.TRUE.,
     &            LAFT,WAFT,DUMMY)
      CALL OGIVE(GEOBEA(17),NX,X,WIDTH,RFWDEA,.TRUE.,LAFT,WAFT,DISCON)
      NX = NAFT
      NXTEMP=NXTMP
      IF (BNOSEH.GT.UNUSED)
     & CALL OGIVE(GEOBEB(17),NXTEMP,XTEMP,HTEMP,RFWDEB,.TRUE.,
     &            LAFT,HAFT,DUMMY)
      CALL OGIVE(GEOBEB(17),NX,X,HEIGHT,RFWDEB,.TRUE.,LAFT,HAFT,DISCON)
C
 1150 CONTINUE
      GEOB(17)=GEOBEA(17)
      GEOB(40)=GEOBEB(17)
      NNX=NX+.5
      GEOB(18)=DSWETE(NNX,X,WIDTH,HEIGHT)-GEOB(10)-GEOB(2)
      GEOB(19)=DVOLE(NNX,X,WIDTH,HEIGHT)-GEOB(11)-GEOB(3)
      GEOB(20)=(DXCNVE(NNX,X,WIDTH,HEIGHT)*(GEOB(19)+GEOB(11)+
     1         GEOB(3))-GEOB(4)*GEOB(3)-GEOB(11)*GEOB(12))/GEOB(19)
      GEOB(21)=GEOBEA(21)
      GEOB(41)=GEOBEB(21)
      GEOB(22)=LAFT/(2.*SQRT((WC/2.)*(HC/2.)))
      GEOB(23)=GEOBEA(23)
      GEOB(24)=GEOB(23)/(2.*SQRT((WC/2.)*(HC/2.)))
C
      DO 1160 I=1,NNX
         REQ(I)=SQRT(WIDTH(I)*HEIGHT(I))
 1160 CONTINUE
      ECSPA=DSPLAN(NNX,X,REQ)-ECSPN-ECSPC
      ISEG=ISEG+1
      TX(ISEG)=X(NNX)
      TE(ISEG)=EAFT
C
C***  EQUIVALENT CIRCULAR BODY RADIUS FOR SHARP NOSES (RTEMP)
      IF (BNOSE.GT.UNUSED) THEN
        NTMP=NXTEMP+0.5
        DO 1165 I=1,NTMP
          RTEMP(I)=SQRT(WTEMP(I)*HTEMP(I))
 1165   CONTINUE
      ENDIF
C
      GO TO 1180
C
 1170 CONTINUE
C
C***  HERE FOR OPTION 2 (COORDINATES DEFINED)
C
      NNX=NX+0.5
      ITYPE=1
C
      CALL USEREL(REQ)
C
      NNX=NX+0.5
C
      X1V=X(1)
      GO TO 1200
C
 1180 CONTINUE
C
C  GET TOTALS FOR OPTION 1 INPUTS
C
      ITYPE=2
      SPLAN=SPNOSE+SPCENT+SPAFT
      ECSPT=ECSPN+ECSPC+ECSPA
      BPLAN=BPNOSE+BPCENT+BPAFT
      SWET =SWNOSE+SWCENT+SWAFT
C
C***  FIND TOTAL WETTED AREA FOR A SHARP NOSE (SWTEMP)
      IF (BNOSE.GT.UNUSED) SWTEMP=DSWET(NTMP,XTEMP,RTEMP)
C
      VOL  =VOLNOS+VOLCEN+VOLAFT
      LTOTAL=LNOSE+LCENTR+LAFT
      XCENTV=(XCVNOS*VOLNOS+XCVCEN*VOLCEN+XCVAFT*VOLAFT)/VOL
      XCENTP=(XCPNOS*SPNOSE+XCPCEN*SPCENT+XCPAFT*SPAFT)/SPLAN
      BCENTP=(BCPNOS*BPNOSE+BCPCEN*BPCENT+BCPAFT*BPAFT)/BPLAN
C
C  FILL ELLIP ARRAY FOR BODY ELLIPTICITY
C
      NNX=NX+0.5
      IF(ISEG .LT. 1)GO TO 1200
      DO 1190 I=1,NNX
         CALL LNTRP(TX,TE,ISEG,X(I),ELLIP(I))
 1190 CONTINUE
C
C  CORRECT FOR NOSE APEX COORDINATE
C
 1200 CONTINUE
      DMAX=2.*REQ(1)
      DO 1210 I=1,NNX
         X(I)=X(I)-X1V+XO
         IF(2.*REQ(I) .GT. DMAX)DMAX=2.*REQ(I)
 1210 CONTINUE
      SMAX=PI*DMAX**2/4.
      DBASE=2.*REQ(NNX)
      SBASE=PI*DBASE**2/4.
      IF(LNOSE .GT. UNUSED)FRNOSE=LNOSE/DMAX
      THEOFN=FRNOSE
      IF(LCENTR .GT. UNUSED)FRCENT=LCENTR/DMAX
      THEOFC=FRCENT
      IF(LAFT .GT. UNUSED)FRAFT=LAFT/DMAX
      THEOFA=FRAFT
      IF(LTOTAL .GT. UNUSED)FR=LTOTAL/DMAX
C
      IF(XCVNOS .NE. UNUSED)XCVNOS=XCVNOS-X1V
      IF(XCPNOS .NE. UNUSED)XCPNOS=XCPNOS-X1H
      IF(BCPNOS .NE. UNUSED)BCPNOS=BCPNOS-X1V
      IF(XCVCEN .NE. UNUSED)XCVCEN=XCVCEN-X1V
      IF(XCPCEN .NE. UNUSED)XCPCEN=XCPCEN-X1H
      IF(BCPCEN .NE. UNUSED)BCPCEN=BCPCEN-X1V
      IF(XCVAFT .NE. UNUSED)XCVAFT=XCVAFT-X1V
      IF(XCPAFT .NE. UNUSED)XCPAFT=XCPAFT-X1H
      IF(BCPAFT .NE. UNUSED)BCPAFT=BCPAFT-X1V
      IF(XCENTV .NE. UNUSED)XCENTV=XCENTV-X1V
      IF(XCENTP .NE. UNUSED)XCENTP=XCENTP-X1H
      IF(BCENTP .NE. UNUSED)BCENTP=BCENTP-X1V
      IF(WIDTH(1) .LE. UNUSED)WIDTH(1)=0.
      IF(HEIGHT(1) .LE. UNUSED)HEIGHT(1)=0.
C
C***  CONVERT RESULTS TO INPUT UNITS FOR OUTPUT
C
      IF(.NOT.(LDIMIN .OR. LDIMCM .OR. LDIMM))GO TO 1220
C
         CALL CVRTFT
C
 1220 CONTINUE
C
      IF(PARTS(1))CALL PRIELL(ITYPE)
C
C***  CONVERT RESULTS TO UNITS OF FEET FOR PROGRAM CALCULATIONS
C
      IF(.NOT.(LDIMIN .OR. LDIMCM .OR. LDIMM))GO TO 1230
C
         CALL CVRTUS
C
 1230 CONTINUE
C
C***  SET DEFAULT REFERENCE QUANTITIES
C
      IF(SREF .EQ. 0. .OR. SREF .EQ. UNUSED)SREF=SMAX
      IF(LREF .EQ. 0. .OR. LREF .EQ. UNUSED)LREF=DMAX
      IF(LATREF .EQ. 0. .OR. LATREF .EQ. UNUSED)LATREF=LREF
C
 1240 FORMAT(4X,'* WARNING * THE NOSE IS TOO SHORT',
     1 ' TO USE THE SHAPE SPECIFIED.',/,16X,'A CONICAL SEGMENT',
     2 ' HAS BEEN ASSUMED.')
 1250 FORMAT(4X,'* WARNING * THE AFTERBODY IS TOO SHORT',
     1 ' TO USE THE SHAPE SPECIFIED.',/,16X,'A CONICAL SEGMENT',
     2 ' HAS BEEN ASSUMED.')
C
      RETURN
      END
