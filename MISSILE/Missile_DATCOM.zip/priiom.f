      SUBROUTINE PRIIOM(IC,IM,CN,CM,CA,CY,CLN,CLL,CNA,CMA,CYB,CNB,CLB)
C
C***  SUBROUTINE TO PRINT THE STATIC COEFFICIENTS USING SUBROUTINE SWRIT
C
C***  THE FINAL VALUES ARE OBTAINED FROM THE IOM
C
C***  INPUTS
C
C     IM -- CURRENT VALUE OF MACH NUMBER (INTEGER VALUE)
C     CN -- ARRAY OF NORMAL FORCE DATA
C     CM -- ARRAY OF PITCHING MOMENT DATA
C     CA -- ARRAY OF AXIAL FORCE DATA
C     CY -- ARRAY OF SIDE FORCE DATA
C    CLN -- ARRAY OF YAWING MOMENT DATA
C    CLL -- ARRAY OF ROLLING MOMENT DATA
C    CNA -- ARRAY OF NORMAL FORCE SLOPE DATA
C    CMA -- ARRAY OF PITCHING MOMENT SLOPE DATA
C    CYB -- ARRAY OF SIDE FORCE SLOPE DATA
C    CNB -- ARRAY OF YAW MOMENT SLOPE DATA
C    CLB -- ARRAY OF ROLL MOMENT SLOPE DATA
C
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      COMMON /REFQN/ SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      COMMON /CONST/  PI,RAD,UNUSED,KAND
      COMMON /LOGIC/ LDMPCS,LDAMP,LBUILD,LNACA,LDERDG,
     1               LDERRD,LPART,LNAME,LPLOT,
     2               LFLT,LREFQ,LAXIS,LFIN1,LFIN2,LFIN3,LFIN4,
     3               LDEFL,LTRIM,LDIMIN,LDIMFT,LDIMCM,LDIMM,
     4               LELLB,LINLET,LEXPR,LICRMT,LSPIN,LARBOD
      COMMON /INCID/ DELTA1(8),DELTA2(8),DELTA3(8),DELTA4(8),XHINGE(4),
     1               SKEW(4)
      COMMON /UDATA/ UCN(20,20,20),UCM(20,20,20),UCA(20,20,20),
     1               DEL(20,20)
      COMMON /CASEID/ IDCASE(74),KOUNT,NAMSV(100),CASE,NOEXTR,NOLAT,
     1               IR,IP
      COMMON /FSET1/ DATA1(381),PHIF1(8),CFOC1(10)
      COMMON /FSET2/ DATA2(381),PHIF2(8),CFOC2(10)
      COMMON /FSET3/ DATA3(381),PHIF3(8),CFOC3(10)
      COMMON /FSET4/ DATA4(381),PHIF4(8),CFOC4(10)
      LOGICAL        LDMPCS,LDAMP,LBUILD,LNACA,LDERDG,
     1               LDERRD,LPART,LNAME,LPLOT,
     2               LFLT,LREFQ,LAXIS,LFIN1,LFIN2,LFIN3,LFIN4,
     3               LDEFL,LTRIM,LDIMIN,LDIMFT,LDIMCM,LDIMM
C
      LOGICAL NMF,NAF,NDUM,NOLAT,NOEXTR
      INTEGER CASE
      CHARACTER*4 FOR1,NDM,NA,NDD
      REAL NALPHA,NMACH,MACH,LREF,LATREF
C
      DIMENSION CN(20),CM(20),CA(20),CY(20),CLN(20),CLL(20),
     1 CNA(20),CMA(20),CYB(20),CNB(20),CLB(20)
      DIMENSION CLARY(20),CDARY(20),CLCD(20),XCP(20)
      DIMENSION FOR1(49)
      DIMENSION JCONT(12)
C
      NALP=NALPHA+0.5
C
      NDUM=.FALSE.
      NMF=.FALSE.
      NAF=.FALSE.
C
C  CONVERT ALL DERIVATIVES TO PER RADIAN IF REQUIRED
C
      IF(.NOT. LDERRD)GO TO 1010
      DO 1000 I=1,20
         IF(ABS(CNA(I)) .NE. UNUSED)CNA(I)=CNA(I)*RAD
         IF(ABS(CMA(I)) .NE. UNUSED)CMA(I)=CMA(I)*RAD
         IF(ABS(CYB(I)) .NE. UNUSED)CYB(I)=CYB(I)*RAD
         IF(ABS(CNB(I)) .NE. UNUSED)CNB(I)=CNB(I)*RAD
         IF(ABS(CLB(I)) .NE. UNUSED)CLB(I)=CLB(I)*RAD
 1000 CONTINUE
C
 1010 CONTINUE
C
C  SKIP BODY AND BODY+FIN PRINTOUT FOR FIN ALONE
C
      IF(IC.GE.2.AND.IC.LE.5) GO TO 2000
C
C   WRITE COEFFICIENTS FOR BODY ALONE OR BODY PLUS FINS
C
      CALL HEADR9
      IF(IC.EQ.1) WRITE(6,1300)
      IF(IC.EQ.6) WRITE(6,1310)
      IF(IC.EQ.7) WRITE(6,1320)
      IF(IC.EQ.8) WRITE(6,1330)
      IF(IC.EQ.9) WRITE(6,1340)
      CALL PRFLC9(IM,NDUM,NMF,NAF)
      WRITE(6,1060)
C
      WRITE(6,1070)
      WRITE(6,1075)(ALPHA(I),CN(I),CM(I),CA(I),
     1   CY(I),CLN(I),CLL(I),I=1,NALP)
C
C  COMPUTE CL/CD AND XCP
C
      DO 1020 I=1,NALP
         CALL CLOCD(ALPHA(I),CN(I),CA(I),CLARY(I),CDARY(I),CLCD(I))
         IF(CN(I) .NE. 0.)XCP(I)=CM(I)/CN(I)
         IF(CN(I) .EQ. UNUSED)XCP(I)=1./UNUSED
         IF(CN(I) .EQ. 0. .AND. CNA(I) .EQ. 0.)XCP(I)=1./UNUSED
         IF(CN(I) .EQ. 0. .AND. CNA(I) .NE. 0.)XCP(I)=CMA(I)/CNA(I)
 1020 CONTINUE
C
      WRITE(6,1120)
      WRITE(6,1130)(ALPHA(I),CLARY(I),CDARY(I),CLCD(I),XCP(I),I=1,NALP)
      WRITE(6,1400)
C
C  WRITE DERIVATIVES FOR BODY ALONE OR BODY PLUS FINS
C
      CALL HEADR9
      IF(IC.EQ.1) WRITE(6,1300)
      IF(IC.EQ.6) WRITE(6,1310)
      IF(IC.EQ.7) WRITE(6,1320)
      IF(IC.EQ.8) WRITE(6,1330)
      IF(IC.EQ.9) WRITE(6,1340)
      CALL PRFLC9(IM,NDUM,NMF,NAF)
      IF(LDERRD) WRITE(6,1062)
      IF(.NOT.LDERRD) WRITE(6,1064)
C
      WRITE(6,1080)
      IF(.NOT.NOLAT) WRITE(6,1085)(ALPHA(I),CNA(I),CMA(I), 
     1              CYB(I),CNB(I),CLB(I),I=1,NALP)
      IF(NOLAT) WRITE(6,1087)(ALPHA(I),CNA(I),CMA(I),I=1,NALP)
      GO TO 2200
 2000 CONTINUE
C
C  WRITE COEFFICIENTS AND DERIVATIVES FOR FIN ALONE
C
      CALL HEADR9
      IF(IC.EQ.2) WRITE(6,1210)
      IF(IC.EQ.3) WRITE(6,1220)
      IF(IC.EQ.4) WRITE(6,1230)
      IF(IC.EQ.5) WRITE(6,1240)
      CALL PRFLC9(IM,NDUM,NMF,NAF)
      IF(LDERRD) WRITE(6,1066)
      IF(.NOT.LDERRD) WRITE(6,1068)
C
      WRITE(6,1090)
      WRITE(6,1095) (ALPHA(I),CN(I),CM(I),CA(I),CNA(I),CMA(I),I=1,NALP)
C
C  COMPUTE CL/CD AND XCP
C
      DO 1025 I=1,NALP
         CALL CLOCD(ALPHA(I),CN(I),CA(I),CLARY(I),CDARY(I),CLCD(I))
         IF(CN(I) .NE. 0.)XCP(I)=CM(I)/CN(I)
         IF(CN(I) .EQ. UNUSED)XCP(I)=1./UNUSED
         IF(CN(I) .EQ. 0. .AND. CNA(I) .EQ. 0.)XCP(I)=1./UNUSED
         IF(CN(I) .EQ. 0. .AND. CNA(I) .NE. 0.)XCP(I)=CMA(I)/CNA(I)
 1025 CONTINUE
C
      WRITE(6,1120)
      WRITE(6,1130)(ALPHA(I),CLARY(I),CDARY(I),CLCD(I),XCP(I),I=1,NALP)
      WRITE(6,1400)
C
 2200 CONTINUE
C
C  CONVERT ALL DERIVATIVES TO PER DEGREE IF REQUIRED
C
      IF(.NOT. LDERRD)GO TO 1040
      DO 1030 I=1,20
         IF(CNA(I) .NE. UNUSED)CNA(I)=CNA(I)/RAD
         IF(CMA(I) .NE. UNUSED)CMA(I)=CMA(I)/RAD
         IF(CYB(I) .NE. UNUSED)CYB(I)=CYB(I)/RAD
         IF(CNB(I) .NE. UNUSED)CNB(I)=CNB(I)/RAD
         IF(CLB(I) .NE. UNUSED)CLB(I)=CLB(I)/RAD
 1030 CONTINUE
C
 1040 CONTINUE
C
C***  WRITE PLOT DATA IF REQUESTED
C
      IF(LPLOT)CALL PLOT3(IC,IM,CN,CM,CA,CY,CLN,CLL)
C
C     SAVE DATA FOR THE CASE
C
      NC = CASE
      IF(.NOT. LDEFL) DEL(NC,IM) = 0.0
      IF (LDEFL) THEN
        IF (LFIN1) THEN
          DO 1600 I = 1, 4
            IF (PHIF1(I) .LT. 180. ) DEL(NC,IM) = DELTA1(I)
            IF (DEL(NC,IM) .NE. 0.0) GOTO 1950
 1600     CONTINUE
        ENDIF
        IF (LFIN2) THEN
          DO 1700 I = 1, 4
            IF (PHIF2(I) .LT. 180. ) DEL(NC,IM) = DELTA2(I)
            IF (DEL(NC,IM) .NE. 0.0) GOTO 1950
 1700     CONTINUE
        ENDIF
        IF (LFIN3) THEN
          DO 1800 I = 1, 4
            IF (PHIF3(I) .LT. 180. ) DEL(NC,IM) = DELTA3(I)
            IF (DEL(NC,IM) .NE. 0.0) GOTO 1950
 1800     CONTINUE
        ENDIF
        IF (LFIN4) THEN
          DO 1900 I = 1, 4
            IF (PHIF4(I) .LT. 180. ) DEL(NC,IM) = DELTA4(I)
            IF (DEL(NC,IM) .NE. 0.0) GOTO 1950
 1900     CONTINUE
        ENDIF
      ENDIF
 1950 DO 1500 I = 1,NALP
        UCN(I,NC,IM) = CN(I)
        UCM(I,NC,IM) = CM(I)
        UCA(I,NC,IM) = CA(I)
 1500 CONTINUE
C
 1060 FORMAT(4X,15X,'----- LONGITUDINAL -----',5X,
     1             '-- LATERAL DIRECTIONAL --')
 1062 FORMAT(4X,15X,'---------- DERIVATIVES (PER RADIAN) ----------')
 1064 FORMAT(4X,15X,'---------- DERIVATIVES (PER DEGREE) ----------')
 1066 FORMAT(4X,15X,'----- LONGITUDINAL -----',5X,
     1             '   DERIVATIVES (PER RADIAN)')
 1068 FORMAT(4X,15X,'----- LONGITUDINAL -----',5X,
     1             '   DERIVATIVES (PER DEGREE)')
 1070 FORMAT(4X,5X,'ALPHA',7X,'CN',8X,'CM',8X,'CA',8X,'CY',7X,
     1             'CLN',7X,'CLL',/)
 1075 FORMAT(4X,F10.2,6F10.3)
 1080 FORMAT(4X,5X,'ALPHA',7X,'CNA',9X,'CMA',9X,'CYB',9X,
     1             'CLNB',8X,'CLLB')
 1085 FORMAT(4X,F10.2,5F12.4)
 1087 FORMAT(4X,F10.2,2F12.4)
 1090 FORMAT(4X,5X,'ALPHA',7X,'CN',8X,'CM',8X,'CA',8X,4X,
     1             'CNA',9X,'CMA',/)
 1095 FORMAT(4X,F10.2,3F10.3,4X,2F12.4)
 1120 FORMAT(/,4X,5X,'ALPHA',7X,'CL',8X,'CD',6X,'CL/CD',5X,'X-C.P.',/)
 1130 FORMAT(4X,F10.2,4F10.3)
 1210 FORMAT(15X,'FIN 1 ALONE STATIC AERODYNAMIC CHARACTERISTICS'/)
 1220 FORMAT(15X,'FIN 2 ALONE STATIC AERODYNAMIC CHARACTERISTICS'/)
 1230 FORMAT(15X,'FIN 3 ALONE STATIC AERODYNAMIC CHARACTERISTICS'/)
 1240 FORMAT(15X,'FIN 4 ALONE STATIC AERODYNAMIC CHARACTERISTICS'/)
 1300 FORMAT(21X,'STATIC AERODYNAMICS FOR BODY ALONE',/)
 1310 FORMAT(19X,'STATIC AERODYNAMICS FOR BODY-FIN SET 1',/)
 1320 FORMAT(16X,'STATIC AERODYNAMICS FOR BODY-FIN SET 1 AND 2',/)
 1330 FORMAT(14X,'STATIC AERODYNAMICS FOR BODY-FIN SET 1, 2, AND 3',/)
 1340 FORMAT(12X,
     1  'STATIC AERODYNAMICS FOR BODY-FIN SET 1, 2, 3, AND 4',/)
 1400 FORMAT(/,4X,'X-C.P. MEAS. FROM MOMENT CENTER IN REF. LENGTHS,',
     2    ' NEG. AFT OF MOMENT CENTER')
      RETURN
      END
