      SUBROUTINE BDAMP(IM)
C      
C  REV 1/96 UPDATE - NEW SUBROUTINE
C
C*** SUBROUTINE FOR CALCULATING BODY ALONE DYNAMIC
C*** DERIVATIVES.  SLENDER BODY THEORY RELATIONSIPHS
C*** BETWEEN THE STATIC AND DYNAMIC DERIVATIVES ARE USED.
C*** LMSC METHOD USED FOR CMQ+CMAD COMBINATION
C
C  WRITTEN BY W. BLAKE, WL/FIGC
C
C*** REF: NACA TN-3283, SACKS (POINTED NOSES)
C*** WL/FIGC RESULTS (TRUNCATED NOSES)
C
C    LB  = TOTAL BODY LENGTH
C    XVC = CENTROID OF BODY VOLUME, AFT OF APEX 
C
      COMMON /CONST/  PI,RAD,UNUSED,KAND
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      COMMON /GEOBOD/ GEBOD(47)
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,
     1                BLAYER,RHR
C
      COMMON /ABODIN/ NX,XO,X(50),R(50),TNOSE,LNOSE,DNOSE,BNOSE,
     1                TRUNC,LCENTR,DCENTR,TAFT,LAFT,DAFT,POWER,
     2                DUM(768)
C
      COMMON /SBODY/  CBODY(220)
      COMMON /DBODY/ BCNQ(20),BCMQ(20),BCAQ(20),BCYQ(20),
     1  BCLNQ(20),BCLLQ(20),BCNR(20),BCMR(20),BCAR(20),
     2  BCYR(20),BCLNR(20),BCLLR(20),BCNP(20),BCMP(20),
     3  BCAP(20),BCYP(20),BCLNP(20),BCLLP(20),
     4  BCNAD(20),BCMAD(20),CLLP,CYP,CNP1,CNP3,CNP5,CNPY5
C
      REAL LREF,LATREF,NALPHA,MACH,LAFT,LB,LNOSE,LATR,LN,LT
C
      COMMON /LOGIC/ LOG(28)
      DIMENSION BCYBD(20),BCNBD(20)
      LOGICAL LOG,TRUNC
C
C*** SET ROLL AXIS DERIVATIVES TO ZERO
C
      DO 1000 I=1,20
         BCYP(I)=0.
         BCLNP(I)=0.
         BCLLP(I)=0.
         BCAQ(I)=0.
         BCLLR(I)=0.
 1000 CONTINUE
C
C*** RETRIEVE GEOMETRY PARAMETERS FROM APPROPRIATE COMMON BLOCK
C
      VB=GEBOD(27)
      XVC=GEBOD(28)
      SB=GEBOD(32)
      LB=GEBOD(35)
C     LN=BODIN(104)
C     DN=BODIN(105)
C     BD=BODIN(109)
C     LAFT=BODIN(111)
C     AI=BODIN(113)
C     IOPT=BODIN(103)+1.5
C     RO=BODIN(106)
C     XO=BODIN(2)
      SO=0.
      IF(TRUNC) SO=PI*BNOSE**2
C
C*** CALCULATE DYNAMIC DERIVATIVES USING STATIC FORCE AND MOMENT
C*** SLOPE VALUES AT ZERO ANGLE OF ATTACK
C
      NALP=NALPHA+0.5
      CALL LNTRP(ALPHA,CBODY(121),NALP,0.,CNA)
      CALL LNTRP(ALPHA,CBODY(141),NALP,0.,CMA)
      CALL LNTRP(ALPHA,CBODY(161),NALP,0.,CYB)
      CALL LNTRP(ALPHA,CBODY(181),NALP,0.,CLNB)
      DO 1100 I=1,NALP
         BCNQ(I) = CNA*RAD*(LB-(XCG-XO))/LREF+SO*LB/LREF
         BCNAD(I)= BCNQ(I)+CMA*RAD
         BCMAD(I)= BCNAD(I)*((XCG-XO)-XVC)/LREF
         BCMQ(I) = -(CNA*RAD-SO/SREF)*((LB-(XCG-XO))/LREF)**2
     1             -BCMAD(I)
C
         BCYR(I) = -CYB*RAD*(LB-(XCG-XO))/LATREF+SO*LB/LATREF
         BCYBD(I)= BCYR(I)-CLNB*RAD
         BCNBD(I)= BCYBD(I)*((XCG-XO)-XVC)/LATREF
         BCLNR(I)= -(-CYB*RAD-SO/SREF)*((LB-(XCG-XO))/LATREF)**2
     1             -BCNBD(I)
 1100 CONTINUE
C
C*** AT SUBSONIC/TRANSONIC SPEEDS USE LMSC METHOD FOR CMQ+CMAD
C*** USING ABOVE RESULTS TO DIVIDE CMQ+CMAD INTO CMQ AND CMAD
C
      IF(MACH(IM).GT.1.2) GO TO 1400
      DO 1300 I=1,NALP
      PSI=0.77+0.23*MACH(IM)**2
      CMQPAD=-2.*PSI*((LB*SQRT(PSI)-(XCG-XO))/LREF)**2
      CMQR=BCMQ(I)/(BCMQ(I)+BCMAD(I))
      CMADR=BCMAD(I)/(BCMQ(I)+BCMAD(I))
      BCMQ(I)=CMQR*CMQPAD
      BCMAD(I)=CMADR*CMQPAD
      BCLNR(I)=BCMQ(I)*LREF**2/LATREF**2
 1300 CONTINUE
 1400 CONTINUE

      IF(.NOT.LOG(27)) GO TO 2000
C
C*** MAGNUS ANALYSIS FOR SPIN STABILIZED BODIES
C
C*** OBTAIN REQUIRED GEOMETRY DATA
C
      LN=LNOSE
      DN=DNOSE
      LT=LAFT
      DC=DCENTR
      FM=MACH(IM)
      VCG=XCG
      SR=SREF
      LATR=LATREF
      TYPEN=TNOSE
      AI=POWER
C
C*** CALCULATE EQUIVALENT OGIVE RADIUS
C
      IF(TYPEN.GE.3.) TYPEN=1.
      IF(TYPEN.EQ.0.) OR=500.
      IF(TYPEN.EQ.1.) OR=(LN/DN)**2+0.25
      IF(TYPEN.NE.2.) GO TO 1500
         T=0.5**AI
         FN=LN/DC
         R2=(4.*FN**2+1.)*(T**2+FN**2)*(T**2-2*T+FN**1+1.)/
     1      (FN*(2*T-1.))**2
         OR=0.25*SQRT(R2)
 1500 CONTINUE
C
C*** CALL SPIN 83
C
      CALL SPIN83(LN,LB,LT,DC,FM,VCG,OR,SR,LATR)
C
 2000 CONTINUE
      RETURN
      END
