       SUBROUTINE BODYE(IM)
C
C***  EXECUTIVE FOR ELLIPTICAL BODY ALONE AERODYNAMICS
C
C***  IM IS THE CURRENT MACH NUMBER INDEX
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      COMMON /REFQN/  SREF,LREF,LATREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      REAL LREF,LATREF
C
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHI,NMACH,MACH(20),
     1                ALT(20),REN(20),VINF(20),TINF(20),PINF(20)
      REAL NALPHA,NMACH,MACH
C
      COMMON /TOTALC/ BALPHA(20),BBETA(20),BPHI(20),ALPTOT(20)
C
      COMMON /ABODIN/ NX,XO,X(50),WIDTH(50),TNOSE,LNOSE,WNOSE,BNOSE,
     1                TRUNC,LCENTR,WCENTR,TAFT,LAFT,WAFT,
     2                POWER,DISCON(20),ELLIP(50),HEIGHT(50),ENOSE,
     3                ECENTR,EAFT,ADUM(645)
C
      REAL NX,LNOSE,LCENTR,LAFT,LTOTAL,LNOSEX,LCNTRX,LAFTX
      LOGICAL TRUNC
C
      COMMON /GEOBOD/ SPNOSE,SWNOSE,VOLNOS,XCVNOS,XCPNOS,FRNOSE,
     1  THEOLN,THEOFN,SPCENT,SWCENT,VOLCEN,XCVCEN,XCPCEN,FRCENT,
     2  THEOLC,THEOFC,SPAFT ,SWAFT ,VOLAFT,XCVAFT,XCPAFT,FRAFT ,
     3  THEOLA,THEOFA,SPLAN ,SWET  ,VOL   ,XCENTV,XCENTP,FR    ,
     4                DBASE ,SBASE ,DMAX  ,SMAX  ,LTOTAL,
     5  BPNOSE,BCPNOS,BPCENT,BCPCEN,BPAFT,BCPAFT,BPLAN,
     6  BCENTP,ECSPN,ECSPC,ECSPA,ECSPT
      LOGICAL LPARTS,LPARTT
C
      COMMON /SBODY/ CNB(20),CMB(20),CAB(20),CYB(20),CLNB(20),CLLB(20),
     1               CNAB(20),CMAB(20),CYBB(20),CNBB(20),CLBB(20)
      COMMON /BDWORK/ CNP(20),CMP(20),CNV(20),CMV(20),DUM(161)
C
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      COMMON /LOGIC/ LDMPCS,LDAMP,LBUILD,LNACA,LDERDG,
     1               LDERRD,LPART,LNAME,LPLOT,
     2               LFLT,LREFQ,LAXIS,LFIN1,LFIN2,LFIN3,LFIN4,
     3               LDEFL,LTRIM,LDIMIN,LDIMFT,LDIMCM,LDIMM,LELLB,
     4               LINLET,LEXPR,LICRMT,LSPIN,LARBOD
      LOGICAL        LDMPCS,LDAMP,LBUILD,LNACA,LDERDG,
     1               LDERRD,LPART,LNAME,LPLOT,
     2               LFLT,LREFQ,LAXIS,LFIN1,LFIN2,LFIN3,LFIN4,
     3               LDEFL,LTRIM,LDIMIN,LDIMFT,LDIMCM,LDIMM,LELLB,
     4               LINLET,LEXPR,LICRMT,LSPIN,LARBOD
C
      DIMENSION IROUT(2),REQ(50),WSAVE(50)
      DIMENSION CNPV(20),CNVV(20),CMPV(20),CMVV(20)
      DIMENSION CNHOR(20),CMHOR(20),CNVER(20),CMVER(20)
C
C
C***  ADJUST FOR BODY ELLIPTICITY
C
      NNX=NX+.5
C
      TWNOSE=WNOSE
      TWCENT=WCENTR
      TWAFT=WAFT
C
      WNOSE=UNUSED
      WCENTR=UNUSED
      WAFT=UNUSED
C
C***  COMPUTE EQUIVALENT CIRCULAR BODY AERODYNAMICS
C
      SPT=SPLAN
      SPLAN=ECSPT
C
      DO 1000 I=1,NNX
         REQ(I)=SQRT(WIDTH(I)*HEIGHT(I))
         WSAVE(I)=WIDTH(I)
         WIDTH(I)=REQ(I)
 1000 CONTINUE
C
      LNOSEX = LNOSE + XO
      LCNTRX = LCENTR + LNOSEX
      LAFTX  = LAFT + LCNTRX
      IF(LNOSE .GT. UNUSED)CALL LNTRP(X,REQ,NNX,LNOSEX,WNOSE)
      IF(LCENTR .GT. UNUSED)CALL LNTRP(X,REQ,NNX,LCNTRX,WCENTR)
      IF(LAFT .GT. UNUSED)CALL LNTRP(X,REQ,NNX,LAFTX,WAFT)
      IF(LNOSE .GT. UNUSED)WNOSE=2.*WNOSE
      IF(LCENTR .GT. UNUSED)WCENTR=2.*WCENTR
      IF(LAFT .GT. UNUSED)WAFT=2.*WAFT
C
      LPARTS=PARTS(6)
      PARTS(6)=.FALSE.
      LPARTT=LPART
      LPART=.FALSE.
      CALL BODYA(IM)
C
      PARTS(6)=LPARTS
      LPART=LPARTT
C
      SPLAN=SPT
C
      WNOSE=TWNOSE
      WCENTR=TWCENT
      WAFT=TWAFT
C
      DO 1010 I=1,50
         IF(I .LE. NNX)WIDTH(I)=WSAVE(I)
         IF(I .GT. 20)GO TO 1010
         CNPV(I)=CNP(I)
         CNVV(I)=CNV(I)
         CMPV(I)=CMP(I)
         CMVV(I)=CMV(I)
 1010 CONTINUE
C
C***  COMPUTE CN/CNO TERMS
C
C ... HORIZONTAL PLANE
C
      CALL GETCNO(NX,X,WIDTH,HEIGHT,CNSB,CNNEW,CMSBT,CMNEWT)
C
      CALL GETCNO(NX,X,REQ,REQ,CNSBC,CNNEWC,CMSBTC,CMNEWC)
      NALP=NALPHA+0.5
C
      CNRAH1=1.
      CNRAH2=1.
      CNRAH3=1.
      CNRAH4=1.
      IF(CNSBC .NE. 0.)CNRAH1=CNSB/CNSBC
      IF(CNNEWC .NE. 0.)CNRAH2=CNNEW/CNNEWC
      IF(CMSBTC .NE. 0.)CNRAH3=CMSBT/CMSBTC
      IF(CMNEWC .NE. 0.)CNRAH4=CMNEWT/CMNEWC
C
      DO 1020 I=1,NALP
         CNP(I)=CNP(I)*CNRAH1
         CNV(I)=CNV(I)*CNRAH2
         CMP(I)=CMP(I)*CNRAH3
         CMV(I)=CMV(I)*CNRAH4
 1020 CONTINUE
C
C ... VERTICAL PLANE
C
      CALL GETCNO(NX,X,HEIGHT,WIDTH,CNSB,CNNEW,CMSBT,CMNEWT)
C
      CNRAV1=1.
      CNRAV2=1.
      CNRAV3=1.
      CNRAV4=1.
      IF(CNSBC .NE. 0.)CNRAV1=CNSB/CNSBC
      IF(CNNEWC .NE. 0.)CNRAV2=CNNEW/CNNEWC
      IF(CMSBTC .NE. 0.)CNRAV3=CMSBT/CMSBTC
      IF(CMNEWC .NE. 0.)CNRAV4=CMNEWT/CMNEWC
      DO 1030 I=1,NALP
         CNPV(I)=CNPV(I)*CNRAV1
         CNVV(I)=CNVV(I)*CNRAV2
         CMPV(I)=CMPV(I)*CNRAV3
         CMVV(I)=CMVV(I)*CNRAV4
 1030 CONTINUE
C
C***  COMPUTE STATIC AERODYNAMICS AT ROLL ORIENTATION
C
      DO 1040 I=1,NALP
         CNHOR(I)=CNP(I) + CNV(I)
         CNVER(I)=CNPV(I)+ CNVV(I)
         CMHOR(I)=CMP(I) + CMV(I)
         CMVER(I)=CMPV(I)+ CMVV(I)
C
         CNB(I)=CNHOR(I)*COS(BPHI(I)/RAD)
         CMB(I)=CMHOR(I)*COS(BPHI(I)/RAD)
         CYB(I)= -CNVER(I)*SIN(BPHI(I)/RAD)
         CLNB(I)=-CMVER(I)*SIN(BPHI(I)/RAD)*LREF/LATREF
C
      IF(CNB(I).LT.0.0) CNVER(I)=-1.*CNVER(I)
      IF(CMB(I).LT.0.0) CMVER(I)=-1.*CMVER(I)
 1040 CONTINUE
C
C***  DIFFERENTIATE CN AND CM CURVES, SET ROLL MOMENT EQUAL TO ZERO
C
      DO 1050 I=1,NALP
         CALL POLINT(ALPHA,CNB,NALP,ALPHA(I),CNBINT,CNAB(I))
         CALL POLINT(ALPHA,CMB,NALP,ALPHA(I),CMBINT,CMAB(I))
         CLLB(I)=0.
         CLBB(I)=0.
 1050 CONTINUE
C
C***  DIRECTIONAL DERIVATIVES (PERTURBATING BETA BY ONE DEGREE)
C
      DO 1060 I=1,NALP
         BETNEW=BBETA(I)+1.
         PHINEW=UNUSED
         CALL ALPBET(BALPHA(I),ALTNEW,BETNEW,PHINEW)
         CALL POLINT(BALPHA,CNVER,NALP,ALTNEW,CNVB,DYDX)
         CALL POLINT(BALPHA,CMVER,NALP,ALTNEW,CMVB,DYDX)
         CYP1= -CNVB*SIN(PHINEW/RAD)
         CLNP1=-CMVB*SIN(PHINEW/RAD)*LREF/LATREF
         CYBB(I)=CYP1-CYB(I)
         CNBB(I)=CLNP1-CLNB(I)
 1060 CONTINUE
C
      IF(PARTS(6))CALL BDPART(IM,CNRAH1,CNRAH2,CNRAH3,CNRAH4,
     1                        CNRAV1,CNRAV2,CNRAV3,CNRAV4)
C
      RETURN
      END
