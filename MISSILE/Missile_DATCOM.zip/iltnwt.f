      SUBROUTINE ILTNWT(ICRX)
C
C     THIS SUBROUTINE COMPUTES THE RATIO OF NORMAL FORCE COEFFICIENT
C     ON AN ARBITRARY CROSS SECTION AND NORMAL FORCE COEFFICIENT ON
C     A CIRCULAR CROSS SECTION.  THIS VALUE CAN THEN BE USED TO APPLY
C     THE CIRCULAR CROSS FLOWDRAG COEFFICIENT TO NON-CIRCULAR CROSS
C     SECTIONS.
C
C  WRITTEN BY M. MOORE, MCDONNELL DOUGLAS
C
C                  *********************
C                  * INPUT/OUTPUT LIST *
C                  *********************
C
C
C     ***** INPUTS *****          ***** OUTPUTS *****
C
C           NPAN                        CNNIZP
C           ICRX                        CNNIZN
C           THETA                       CNNIYP
C           PI                          CNNIYN
C           HALFPI
C           DEQ
C           CRXIWD
C           CRXIHT
C           CNCIRN
C
C     SUBROUTINES USED: NONE
C
C
      COMMON /INLETN/ NIN,INTYPE,XINLT,XDIV,HDIV,LDIV,PHI(20),
     1                XI(5),HI(5),WI(5),ICOVER,RAMANG,IAD,MFR(20)
      COMMON /ILTGEO/ LENGTH(5),LLNGTH(5),ALPEFF(5),SI(5),
     1                CRXLEN(5),DEQI(5),XMIDI(5),XMIDD(3),
     2                XD(3),HD(3),WD(3),DEQD(3),
     3                RARC(5),DLEN(3),KWA(20),KWB(20),
     4                RA2(20),RB2(20),AREAI(5),AREAD(3)
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHIROL,NMACH,MACH(20),
     1                ALT(20),REN(20),FLDUM(60)
      COMMON /REFQN/  SREF,LREF,BREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      COMMON /CONST/  PI,RAD,UNUSED,KAND
      COMMON /POTENT/ CNP(20),CYP(20),CMP(20),CLNP(20)
      COMMON / MOMEN/ CNMOM(20),CYMOM(20)
      COMMON /LINEAR/ ARA,ARB,CNLIN(20),CMLIN(20),
     1                CYLIN(20),CLNLIN(20)
      COMMON /CFFLOW/ CNCF(20),CYCF(20),
     1                CMCF(20),CLNCF(20)
      COMMON /ILTVSC/ THETAI(5,20),PHIR(20),CRXIWD(5,20),
     1                CRXIHT(5,20),CNNIZP(5,20),
     2                CNNIZN(5,20),CNNIYP(5,20),
     3                CNNIYN(5,20),PANILN(5,5,20),
     4                CNCIRN,FRI,RARX(3),
     5                DLCNAP(3,20),DLCNAN(3,20),
     6                DLCYAP(3,20),DLCYAN(3,20)
      COMMON /DIVVSC/ THETAD(5,20),CRXDWD(3,20),
     1                CRXDHT(3,20),CNNDZP(3,20),
     2                CNNDZN(3,20),CNNDYP(3,20),
     3                CNNDYN(3,20),PANDLN(5,20),FRD
      COMMON /BODVSC/ DLCNBP(5,20),DLCNBN(5,20),
     1                DLCYBP(5,20),DLCYBN(5,20),FRB
C
      REAL NIN,INTYPE,LENGTH,LLNGTH,LREF,LDIV,MACH,KWA,KWB
      REAL NALPHA,NMACH
      LOGICAL ICOVER
C
      NUMI=NIN+0.5
      DO 100 INLET=1,NUMI
C
C       INITIALIZE SUMMING VARIABLES
        CNNIZP(ICRX,INLET) = 0.0
        CNNIZN(ICRX,INLET) = 0.0
        CNNIYP(ICRX,INLET) = 0.0
        CNNIYN(ICRX,INLET) = 0.0
C
C     BEGIN LOOP FOR EACH PANEL
        DO 110 IPAN = 1, 4
C
C          INITIALIZE NEWTONIAN PRESSURE COEFFICIENT FOR LEE SURFACES
C          OR WINDWARD SURFACES
           IF (COS(THETAI(IPAN,INLET)).LE.0.00001) THEN
             CPZP=-2.0*COS(THETAI(IPAN,INLET))**2
             CPZN=0.0
           ELSE
             CPZP=0.0
             CPZN=2.0*COS(THETAI(IPAN,INLET))**2
           ENDIF
C
C          INITIALIZE NEWTONIAN PRESSURE COEFFICIENT FOR LEE SURFACES
C          OR WINDWARD SURFACES
           IF (SIN(THETAI(IPAN,INLET)).GE.-0.00001) THEN
             CPYP=2.0*COS(THETAI(IPAN,INLET))**2
             CPYN=0.0
           ELSE
             CPYP=0.0
             CPYN=-2.0*COS(THETAI(IPAN,INLET))**2
           ENDIF
C
C          DETERMINE PANEL CONTRIBUTION TO NEWTONIAN CN
           CNNIZP(ICRX,INLET) = CNNIZP(ICRX,INLET) +
     1        CPZP * COS(THETAI(IPAN,INLET)) * PANILN(IPAN,ICRX,INLET)
           CNNIZN(ICRX,INLET) = CNNIZN(ICRX,INLET) +
     1        CPZN * COS(THETAI(IPAN,INLET)) * PANILN(IPAN,ICRX,INLET)
           CNNIYP(ICRX,INLET) = CNNIYP(ICRX,INLET) +
     1        CPYP * SIN(THETAI(IPAN,INLET)) * PANILN(IPAN,ICRX,INLET)
           CNNIYN(ICRX,INLET) = CNNIYN(ICRX,INLET) +
     1        CPYN * SIN(THETAI(IPAN,INLET)) * PANILN(IPAN,ICRX,INLET)
C
  110   CONTINUE
C
C       SET DEFAULTS FOR REFERENCE WIDTH AND HEIGHT
        REFWID = 1.0
        REFHGT = 1.0
C
C       USE MAXIMUM WIDTH IF NONZERO
        IF (CRXIWD(ICRX,INLET) .GT. 0.0) REFWID = CRXIWD(ICRX,INLET)
C
C       USE MAXIMUM HEIGHT IF NONZERO
        IF (CRXIHT(ICRX,INLET) .GT. 0.0) REFHGT = CRXIHT(ICRX,INLET)
C
C       NORMALIZE NEWTONIAN CN WRT REFERENCE HEIGHT AND WIDTH AND
C       NEWTONIAN CIRCULAR CN
        CNNIZP(ICRX,INLET) = CNNIZP(ICRX,INLET) / (REFWID * CNCIRN)
        CNNIZN(ICRX,INLET) = CNNIZN(ICRX,INLET) / (REFWID * CNCIRN)
        CNNIYP(ICRX,INLET) = CNNIYP(ICRX,INLET) / (REFHGT * CNCIRN)
        CNNIYN(ICRX,INLET) = CNNIYN(ICRX,INLET) / (REFHGT * CNCIRN)
C
  100 CONTINUE
C
      RETURN
      END
