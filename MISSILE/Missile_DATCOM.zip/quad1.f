      SUBROUTINE QUAD1(Y,N,H,ANS)
C
C **  NUMERICAL INTEGRATION BY FIVE POINT QUADRATURE, THE TWO
C **  POINT FORMULA, SIMPSON'S RULE OR NEWTON'S 3/8'S RULE
C
C **  INPUTS
C **       Y  = ARRAY TO BE INTEGRATED
C **       N  = NUMBER OF POINTS IN ARRAY Y
C **       H  = COMMON STEPSIZE BETWEEN SUCESSIVE ELEMENTS OF Y
C
C **  OUTPUT
C **       ANS= RESULT OF THE INTEGRATION
C
      DIMENSION Y(N)
C
      K=N
      ANS=0.
      IF(H.LE.0. .OR. N.LE.0.) GO TO 1050
C
C **  OMIT QUADRATURE METHOD IF LESS THAN FIVE POINTS
C
      IF(N.LT.4)GO TO 1010
C
C **  LOOP AND INTEGRATE FOR EACH FIVE POINTS
C
      DO 1000 I=1,N,4
         K=N-I+1
         IF(K.LT.5)GO TO 1000
         ANS=ANS+7.*Y(I)+32.*Y(I+1)+12.*Y(I+2)+32.*Y(I+3)+7.*Y(I+4)
 1000 CONTINUE
C
      ANS=ANS*H/22.5
C
 1010 CONTINUE
C
C
C **  IF K = 1 STOP THE INTEGRATION PROCESS
C **  IF K = 2 USE THE TWO POINT FORMULA
C **  IF K = 3 USE SIMPSON'S RULE
C **  IF K = 4 USE NEWTON'S 3/8'S RULE
C
      GO TO (1050,1020,1030,1040),K
C
 1020 ANS=ANS+H*(Y(N)+Y(N-1))/2.
C
      GO TO 1050
C
 1030 ANS=ANS+H*(Y(N-2)+4.*Y(N-1)+Y(N))/3.
C
      GO TO 1050
C
 1040 ANS=ANS+3.*H*(Y(N-3)+3.*Y(N-2)+3.*Y(N-1)+Y(N))/8.
C
 1050 CONTINUE
C
      RETURN
      END
