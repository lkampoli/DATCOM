      SUBROUTINE SVTRAK(ALPHA,DELTA,THETA,DIH,TES,XTEW,XHLW,XCPT,
     1                  FWRW,RL,HR,VR) 
C **
C **  COMPUTES WING VORTEX HORIZONTAL AND VERTICAL
C **    LOCATION AT THE TAIL CENTER OF PRESSURE.
C **
C **  LIMITATIONS  - STRAIGHT TAPERED WING
C **                 VORTEX IS SWEPT AFT ALONG VELOCITY VECTOR
C **
C **  NOTE  - UNITS ON XTEW,XHLW,XCPT,FWRW, AND RL MUST 
C **         BE CONSISTENT
C **
C **
C **  INPUT - 
C **
C **        ALPHA = ANGLE OF ATTACK, DEGREES 
C **        DELTA = WING DEFLECTION ANGLE, DEGREES 
C **        THETA = HINGE LINE SKEW ANGLE, DEGREES 
C **        DIH   = LIFTING SURFACE DIHEDRAL ANGLE, DEGREES
C **        TES   = TRAILING EDGE SWEEP ANGLE, DEGREES
C **        XTEW  = DISTANCE FROM NOSE TO WING TRAILING EDGE
C **        XHLW  = DISTANCE FROM NOSE TO WING HINGE LINE INTERSECTION
C **                WITH BODY 
C **        XCPT  = DISTANCE TO TAIL CENTER OF PRESSURE 
C **                MEASURED FROM NOSE
C **        FWRW  = VORTEX LATERAL LOCATION - BODY RADIUS AT THE WING 
C **        RL    = LOCAL BODY RADIUS AT WING HINGE LINE STATION
C **
C **  OUTPUT -
C **
C **        HR    = HORIZONTAL POSITION OF VORTEX MEASURED FROM 
C **                VEHICLE CENTERLINE
C **        VR    = VERTICAL POSITION OF VORTEX MEASURED FROM 
C **                VEHICLE CENTERLINE
C **
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C 
      PIO2=PI/2.
      DELTR=DELTA/RAD 
      ALPHR=ALPHA/RAD 
      THETR=THETA/RAD 
      SDIH=SIN(DIH/RAD)
      CDIH=COS(DIH/RAD)
      SDEL=SIN(DELTR*.5)
      CDEL=COS(DELTR*.5)
      TTES=TAN(TES/RAD)
C **
C **  CALCULATION OF DISTANCE BETWEEN VORTEX GENERATION POINT 
C **    AND WING HINGE LINE 
C **
      X2=XTEW+FWRW*TTES 
      PHI=ATAN2(FWRW,X2-XHLW) 
      S=SQRT((FWRW)**2+(X2-XHLW)**2)
      PSI=PIO2-THETR-PHI
      SSPAN=S*SIN(PSI)
C **
C **  CALCULATION OF VORTEX GENERATION POINT DEFLECTION DISTANCE
C **
      DEFD=2.*SSPAN*SDEL
C **
C **  CALCULATION OF VORTEX HORIZONTAL LOCATION AT TAIL 
C **
      HR=(RL+FWRW)*CDIH+DEFD*(SDEL*SIN(THETR)*CDIH+CDEL*SDIH)
C **
C **  CALCULATION OF VORTEX GENERATION POINT VERTICAL LOCATION
C **
      VOR=(RL+FWRW)*SDIH+DEFD*(SDEL*SIN(THETR)*SDIH-CDEL*CDIH)
C **
C **  CALCULATION OF VORTEX GENERATION POINT AXIAL LOCATION 
C **
      XGPT=(XTEW+FWRW*TTES)-DEFD*SDEL*COS(THETR)
      AFTL=XCPT-XGPT
C **
C **  CALCULATION OF VORTEX VERTICAL LOCATION AT TAIL 
C **
      VR=VOR+TAN(ALPHR)*AFTL
      RETURN
      END 
