      SUBROUTINE ILTCDC(CANGLE,XMACH,FR,CDCCYL)
C
C     THIS SUBROUTINE DETERMINES THE CROSSFLOW DRAG COEFFICIENT
C     ON AN INFINITE CIRCULAR CYLINDER AS A FUNCTION OF THE
C     CROSSFLOW MACH NUMBER.  THE DRAG COEFFICIENT IS THEN
C     CORRECTED FOR THE EFFECT OF A FINITE LENGTH BODY.
C
C  WRITTEN BY M. MOORE, MCDONNELL DOUGLAS
C
C                  *********************
C                  * INPUT/OUTPUT LIST *
C                  *********************
C
C
C     ***** INPUTS *****          ***** OUTPUTS *****
C
C           XMACH                        CDCCYL
C           CANGLE
C
C
C     SUBROUTINES USED: NONE
C
C
      COMMON /INLETN/ NIN,INTYPE,XINLT,XDIV,HDIV,LDIV,PHI(20),
     1                XI(5),HI(5),WI(5),ICOVER,RAMANG,IAD,MFR(20)
      COMMON /ILTGEO/ LENGTH(5),LLNGTH(5),ALPEFF(5),SI(5),
     1                CRXLEN(5),DEQI(5),XMIDI(5),XMIDD(3),
     2                XD(3),HD(3),WD(3),DEQD(3),
     3                RARC(5),DLEN(3),KWA(20),KWB(20),
     4                RA2(20),RB2(20),AREAI(5),AREAD(3)
      COMMON /FLC/    NALPHA,ALPHA(20),BETA,PHIROL,NMACH,MACH(20),
     1                ALT(20),REN(20),FLDUM(60)
      COMMON /REFQN/  SREF,LREF,BREF,ROUGH,XCG,ZCG,SCALE,BLAYER,RHR
      COMMON /CONST/  PI,RAD,UNUSED,KAND
      COMMON /POTENT/ CNP(20),CYP(20),CMP(20),CLNP(20)
      COMMON / MOMEN/ CNMOM(20),CYMOM(20)
      COMMON /LINEAR/ ARA,ARB,CNLIN(20),CMLIN(20),
     1                CYLIN(20),CLNLIN(20)
      COMMON /CFFLOW/ CNCF(20),CYCF(20),
     1                CMCF(20),CLNCF(20)
      COMMON /ILTVSC/ THETAI(5,20),PHIR(20),CRXIWD(5,20),
     1                CRXIHT(5,20),CNNIZP(5,20),
     2                CNNIZN(5,20),CNNIYP(5,20),
     3                CNNIYN(5,20),PANILN(5,5,20),
     4                CNCIRN,FRI,RARX(3),
     5                DLCNAP(3,20),DLCNAN(3,20),
     6                DLCYAP(3,20),DLCYAN(3,20)
      COMMON /DIVVSC/ THETAD(5,20),CRXDWD(3,20),
     1                CRXDHT(3,20),CNNDZP(3,20),
     2                CNNDZN(3,20),CNNDYP(3,20),
     3                CNNDYN(3,20),PANDLN(5,20),FRD
      COMMON /BODVSC/ DLCNBP(5,20),DLCNBN(5,20),
     1                DLCYBP(5,20),DLCYBN(5,20),FRB
C
      REAL NIN,INTYPE,LENGTH,LLNGTH,LREF,LDIV,MACH,KWA,KWB
      REAL NALPHA,NMACH
      LOGICAL ICOVER
C
      PARAMETER (NVAL = 8)
C
      REAL MC(NVAL), CDC(NVAL)
C
C     DATA STATEMENTS FOR CROSSFLOW DRAG VS. MACH
      DATA MC  /0.2,  0.4,  0.6,  0.8,  1.0,  1.2,  1.4,  1.6/
      DATA CDC /1.20, 1.25, 1.43, 1.49, 1.36, 1.26, 1.22, 1.20/
C
C     DATA STATEMENT FOR ETA CURVE FIT
      DATA C0, C1, C2, C3 /0.5182, 2.22742E-2, -6.8227E-4,
     1                     7.52648E-6/
C
C     COMPUTE THE CROSSFLOW MACH NUMBER
      CMACH = XMACH * ABS(SIN(CANGLE))
C
C     COMPUTE THE CROSSFLOW DRAG COEFFICIENT FOR AN INFINITE CYLINDER
      IF (CMACH .LE. 0.2 .OR. CMACH .GE. 1.6) THEN
C
C        CONSTANT VALUE OUTSIDE OF DRAG RISE
         CDCINF = 1.2
C
      ELSE
C
C        INTERPOLATE FOR VALUE ON DRAG RISE
         DO 100 I = 2, NVAL
            IF (CMACH .GE. MC(I-1) .AND. CMACH .LE. MC(I)) THEN
               TEMP1 = CMACH  - MC(I-1)
               TEMP2 = MC(I)  - MC(I-1)
               TEMP3 = CDC(I) - CDC(I-1)
               CDCINF = CDC(I-1) + TEMP1 / TEMP2 * TEMP3
               GOTO 110
            END IF
  100    CONTINUE
  110    CONTINUE
C
      END IF
C
C     COMPUTE CORRECTION FOR FINENESS RATIO
      ETA = C0 + C1 * FR + C2 * FR**2 + C3 * FR**3
C
C     APPLY LINEAR TRANSONIC FARING FOR ETA
      IF (XMACH .GT. 0.9 .AND. XMACH .LT. 1.2) THEN
         ETA = ETA + (1.0 - ETA) * (XMACH - 0.9) / (1.2 - 0.9)
      ELSE IF (XMACH .GE. 1.2) THEN
         ETA = 1.0
      END IF
C
C     CORRECT CROSS FLOW DRAG COEFFICIENT FOR FINITE LENGTH BODY
      CDCCYL = CDCINF * ETA
C
      RETURN
      END
