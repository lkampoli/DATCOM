      SUBROUTINE USEFOL(IFIN,X,YUN,YLN,THN,CAM,RHO,T,DELTAY,
     1    KSHARP)
C
C***  FOLLOWING ARE THE DEFINITIONS OF EACH VARIABLE USED
C
C      X -- X/C STATIONS ALONG AIRFOIL CHORD
C     XU -- X/C STATION OF UPPER SURFACE
C     XL -- X/C STATION OF LOWER SURFACE
C    YUN -- Y/C OF SECTION AT XU
C    YLN -- Y/C OF SECTION AT XL
C    YUU -- Y/C OF SUPERSONIC AIRFOILS, UPPER SURFACE
C    YLL -- Y/C OF SUPERSONIC AIRFOILS, LOWER SURFACE
C    THN -- THICKNESS/C AT X
C    CAM -- CAMBER/C AT X
C    RHO -- LEADING EDGE RADIUS
C      T -- MAXIMUM AIRFOIL THICKNESS, FROM NACA DESIGNATION
C KSHARP -- SHAPE FACTOR FOR SUPERSONIC AIRFOILS
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      DIMENSION X(50),YUN(50),YLN(50),THN(50),CAM(50)
C
      NP=0
C
C***  NUMBER OF INPUT POINTS
C
      DO 1000 I=2,50
         IF(X(I) .LE. X(I-1))GO TO 1010
         NP=I
 1000 CONTINUE
C
 1010 CONTINUE
C
      IF(NP .LE. 1)GO TO 1080
C
C***  DETERMINE INPUT TYPE
C
      IF(YUN(1).NE.UNUSED .AND. YLN(1).NE.UNUSED .AND.
     1   YUN(NP).NE.UNUSED .AND. YLN(NP).NE.UNUSED)GO TO 1030
C
C ... MEAN LINE AND THICKNESS DISTRIBUTION
C
      IF(THN(1).EQ.UNUSED .OR. THN(NP).EQ.UNUSED .OR.
     1   CAM(1).EQ.UNUSED .OR. CAM(NP).EQ.UNUSED)GO TO 1080
C
C ... COMPUTE YUN AND YLN FOR THN AND CAM
C
      YUN(1)=0.
      YLN(I)=0.
      DO 1020 I=2,NP
         CALL POLINT(X,CAM,NP,X(I),YY,DYDX)
         THETA=ATAN(DYDX)
         YUN(I)=CAM(I)+THN(I)/2.*COS(THETA)
         YLN(I)=CAM(I)-THN(I)/2.*COS(THETA)
 1020 CONTINUE
      GO TO 1060
C
C ... COMPUTE THN AND CAM FROM YUN AND YLN
C
 1030 CONTINUE
C
      CAM(1)=0.
      DO 1040 I=2,NP
         CAM(I)=(YUN(I)+YLN(I))/2.
 1040 CONTINUE
C
      THN(I)=0.
      DO 1050 I=2,NP
         CALL POLINT(X,CAM,NP,X(I),YY,DYDX)
         THETA=ATAN(DYDX)
         THN(I)=(YUN(I)-CAM(I))*2./COS(THETA)
 1050 CONTINUE
C
 1060 CONTINUE
C
      IF(RHO .EQ. UNUSED)RHO=0.
      IF(KSHARP .EQ. UNUSED)KSHARP=0.
C
C ... COMPUTE MAX THICKNESS
C
      DO 1070 I=2,NP
         IF(THN(I) .GT. THN(I-1))T=THN(I)
 1070 CONTINUE
C
C ... COMPUTE DELTA-Y
C
      CALL LNTRP(X,THN,NP,.0015,YU15)
      CALL LNTRP(X,THN,NP,.0600,YU60)
      DELTAY=(YU60-YU15)*100.
C
 1080 CONTINUE
C
      RETURN
      END
