      SUBROUTINE WEDGE(MACH,DELTA,THETA,CP)
C
C***  COMPUTE THE SHOCK ANGLE FROM AND THE PRESSURE COEFFICIENT
C***  BEHIND THE SHOCK DUE TO A TWO-DIMENSIONAL WEDGE IN
C***  SUPERSONIC FLOW
C
C***  INPUTS
C
C  MACH - FREESTREAM MACH NUMBER
C DELTA - WEDGE HALF-ANGLE (DEGREES)
C
C***  OUTPUTS
C
C THETA - SHOCK WAVE ANGLE (DEG)
C    CP - PRESSURE COEFFICIENT
C
C***  METHOD FROM NACA 1135
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      REAL MACH
C
C***  SOLVE CUBIC EQUATION FOR SHOCK ANGLE
C
  100 CONTINUE
      B=-(MACH**2+2)/MACH**2-7./5.*SIN(DELTA/RAD)**2
      C=(2.*MACH**2+1.)/MACH**4+(36./25.+2./(5.*MACH**2))*
     1   SIN(DELTA/RAD)**2
      D=-COS(DELTA/RAD)**2/MACH**4
      COSP=(9./2.*B*C-B**3-27./2.*D)/(B*B-3.*C)**1.5
      IF(ABS(COSP).LE.1.0) GO TO 200
      DELTA=DELTA-1.0
      GO TO 100
  200 CONTINUE
      PHI=ACOS(COSP)
C
C***  N=0 IS STRONG SHOCK SOLUTION
C***  N=2 IS DECREASE IN ENTROPY (DISREGARD)
C***  N=4 IS WEAK SHOCK SOLUTION
C
C***  FOLLOWING METHOD USES N=4
C***  ..........................................N.........
      SIN2T=-B/3.+2./3.*SQRT(B*B-3.*C)*COS((PHI+4.*PI)/3.)
C
      THETA=ASIN(SQRT(SIN2T))*RAD
C
C***  WITH SHOCK ANGLE KNOWN, CALCULATE PRESSURE COEFFICIENT
C
      CP=5./3.*(MACH**2*SIN(THETA/RAD)**2-1.)/MACH**2
C
      RETURN
      END
