      SUBROUTINE CDPROT(MACH,RPUL,SREF,CDTOT)
C
C  WRITTEN BY K. DETERS, MCDONNELL DOUGLAS
C
      COMMON /ABODIN/ DUM(300),NUM,PTYPE(20),XPROT(20),NLOC(20),
     1 BDMEM(20),BDTYPE(100),LPROT(100),WPROT(100),HPROT(100),
     2 OPROT(100)
      COMMON /PARTF/ PART(19)
      COMMON /LOGIC/ LDUM(18),LDIMIN,LDIMFT,LDIMCM,LDIMM,LDUM2(6)
      LOGICAL LDUM,LDIMIN,LDIMFT,LDIMCM,LDIMM,PART
      REAL MACH,CDP(20),LPROT,NUM,NLOC
      DIMENSION NMEM(20)
      CHARACTER*19 NAME(7),TNAME(20)
C
C***  SET UP INTEGER VALUES 
      NPROT=NUM+0.5
      DO 10 I=1,20
        NMEM(I)=BDMEM(I)+0.5
 10   CONTINUE
C
C***  CALCULATE TOTAL NUMBER OF MEMBERS
      JB=0
      MEM=0
      DO 20 I=1,NPROT
        IF (PTYPE(I).EQ.3) THEN
          MEM=MEM+4
        ELSEIF (PTYPE(I).EQ.4) THEN
          MEM=MEM+3
        ELSEIF (PTYPE(I).EQ.7) THEN
          JB=JB+1
          MEM=MEM+NMEM(JB)
        ELSE
          MEM=MEM+1
        ENDIF
 20   CONTINUE
C***  CHECK IF NUMBER OF MEMBERS EXCEEDS ARRAY DIMENSION
      IF (MEM.GT.100) THEN
        IF (PART(6)) WRITE(6,80)
        GOTO 200
      ENDIF
C
      CDTOT=0.0
      J=0
      K=0
      KK=0
      DO 30 I=1,NPROT
      XB=XPROT(I)
      RNUM=RPUL*XB
      DEL=.37*XB/(RNUM**.2)
C***
C     CIRCULAR/ELLIPTICAL CYLINDERS (CROSS FLOW)
      IF (PTYPE(I) .EQ. 1) THEN
      K=K+1      
      CALL CYL(MACH,RPUL,DEL,HPROT(K),WPROT(K),LPROT(K),OPROT(K),CDPR)
      ENDIF
C***
C     CIRCULAR CYLINDERS (IN FLOW DIRECTION)
      IF (PTYPE(I) .EQ. 2) THEN
      K=K+1
      CALL CIRC(MACH,DEL,HPROT(K),LPROT(K),OPROT(K),CDPR)
      ENDIF
C***
C     LAUNCH LUGS (4- MEMBERS)
      IF (PTYPE(I) .EQ. 3) THEN
      NM=4
      CALL LUGSHO(MACH,DEL,K,NM,HPROT,WPROT,LPROT,OPROT,CDPR)
      ENDIF
C***
C     LAUNCH SHOES (3-MEMBERS)
      IF (PTYPE(I) .EQ. 4) THEN
      NM=3
      CALL LUGSHO(MACH,DEL,K,NM,HPROT,WPROT,LPROT,OPROT,CDPR)
      ENDIF
C***
C     FLAT PLATE
      IF (PTYPE(I) .EQ. 5) THEN
      K=K+1
      CALL FLAT(MACH,DEL,HPROT(K),WPROT(K),LPROT(K),OPROT(K),CDPR)
      ENDIF
C***
C     FAIRINGS (STREAMLINE HALF BODIES)
      IF (PTYPE(I) .EQ. 6) THEN
      K=K+1
      CALL STREAM(MACH,RPUL,DEL,XB,HPROT(K),WPROT(K),LPROT(K),CDPR)
      ENDIF
C***
C     COMPONENT BUILD-UP (MAX=5)
      IF (PTYPE(I) .EQ. 7) THEN
      J=J+1
      NM=NMEM(J)
      CALL BUILD(MACH,RPUL,DEL,K,KK,NM,BDTYPE,HPROT,WPROT,LPROT,
     1 OPROT,CDPR)   
      ENDIF
C***
C     INDIVIDUAL PROTUBERANCE DRAG COEFF.
      CDP(I)=CDPR*NLOC(I)/SREF
      CDTOT=CDTOT+CDP(I)
  30  CONTINUE
C
C***  SET UP PROTUBERANCE TYPE NAMES 
      NAME(1)=' VERTICAL CYLINDER '
      NAME(2)='HORIZONTAL CYLINDER'
      NAME(3)='    LAUNCH LUG     '
      NAME(4)='    LAUNCH SHOE    '
      NAME(5)='FLAT PLATE OR BLOCK'
      NAME(6)='      FAIRING      '
      NAME(7)='COMPONENT BUILD-UP '
C
      IF (PART(6)) THEN
C
C***  PRINT OUTPUT
C
      CALL HEADR9
      WRITE(6,400)
 400  FORMAT(28X,'PROTUBERANCE OUTPUT',/)
      CALL PRFLC9(IM,NA,NM,ND)
C
      WRITE(6,90)
      WRITE(6,100)
      IF (LDIMIN) WRITE(6,110)
      IF (LDIMFT) WRITE(6,111)
      IF (LDIMCM) WRITE(6,112)
      IF (LDIMM) WRITE(6,113)
      CALL CONVRT(XPROT,20,1)
      DO 40 I=1,NPROT
        IF (PTYPE(I).EQ.1) TNAME(I)=NAME(1)
        IF (PTYPE(I).EQ.2) TNAME(I)=NAME(2)
        IF (PTYPE(I).EQ.3) TNAME(I)=NAME(3)
        IF (PTYPE(I).EQ.4) TNAME(I)=NAME(4)
        IF (PTYPE(I).EQ.5) TNAME(I)=NAME(5)
        IF (PTYPE(I).EQ.6) TNAME(I)=NAME(6)
        IF (PTYPE(I).EQ.7) TNAME(I)=NAME(7)
        WRITE(6,120) I,TNAME(I),XPROT(I),INT(NLOC(I)+0.5),
     1     CDP(I)/NLOC(I),CDP(I)
  40  CONTINUE
      WRITE(6,130) CDTOT
      CALL CONVRT(XPROT,20,-1)
C
      ENDIF
C
  80  FORMAT(/,4X,'WARNING: THERE IS A DISCREPANCY IN THE ',
     1       'INPUTS. ',/,4X,'         THE TOTAL NUMBER OF ',
     2       'MEMBERS FOR ALL ',/,4X,'         PROTUBERANCES ',
     3       'EXCEEDS THE DIMENSION OF 100.',/,4X,'         THE ',
     4       'PROTUBERANCE CALCULATIONS WILL BE SKIPPED.',/)
  90  FORMAT(4X,'PROTUBERANCE AXIAL FORCE COEFFICIENT IS',
     1          ' CALCULATED AT ZERO ANGLE OF',
     2     /,4X,'ATTACK AND ASSUMED CONSTANT FOR ALL ANGLES',
     3          ' OF ATTACK.  PROTUBERANCES ',
     4     /,4X,'ARE CONSIDERED PART OF THE BODY WHEN',
     4          ' CALCULATING TOTAL AXIAL FORCE.',
     5     /,4X,'PROTUBERANCE AXIAL FORCE IS INCLUDED',
     7          ' IN THE TOTAL CONFIGURATION RESULTS.',/)
 100  FORMAT(4X,'--------------- PROTUBERANCE ',
     1       ' CALCULATIONS ---------------')
 110  FORMAT(/,35X,'LONG.',5X,'NUMBER',2X,'INDIVIDUAL',4X,'TOTAL',/,
     1   4X,'NUMBER',8X,'TYPE',10X,'LOCATION (IN)',12X,'CA',10X,'CA')
 111  FORMAT(/,35X,'LONG.',5X,'NUMBER',2X,'INDIVIDUAL',4X,'TOTAL',/,
     1   4X,'NUMBER',8X,'TYPE',10X,'LOCATION (FT)',12X,'CA',10X,'CA')
 112  FORMAT(/,35X,'LONG.',5X,'NUMBER',2X,'INDIVIDUAL',4X,'TOTAL',/,
     1   4X,'NUMBER',8X,'TYPE',10X,'LOCATION (CM)',12X,'CA',10X,'CA')
 113  FORMAT(/,35X,'LONG.',5X,'NUMBER',2X,'INDIVIDUAL',4X,'TOTAL',/,
     1   4X,'NUMBER',8X,'TYPE',10X,'LOCATION (M) ',12X,'CA',10X,'CA')
 120  FORMAT(5X,I2,4X,A19,4X,F7.3,6X,I2,4X,F8.4,4X,F8.4)
 130  FORMAT(/,4X,'TOTAL CA DUE TO PROTUBERANCES =',F8.4,/)
C
 200  CONTINUE
      RETURN
      END
