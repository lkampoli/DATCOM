      SUBROUTINE FCNA(MACH, AR, FINTPR, SWAVLE, SWAV50, SWAVTE,
     1                DEL, SECCLA, SPLAN, SREF, CNA)
C ***
C *** COMPUTES FIN ALONE LINEAR NORMAL FORCE CURVE SLOPE PER DEG
C ***
C *** REFERENCES  DATCOM SECTION 4.1.3.2
C ***             RAS DATA SHEETS
C ***
C *** INPUT   MACH    MACH NUMBER
C ***         AR      EXPOSED FIN ASPECT RATIO FOR TWO PANELS
C ***         FINTPR  EXPOSED FIN TAPER RATIO
C ***         SWAVLE  LEADING EDGE SWEEP ANGLE, DEGREES
C ***         SWAV50  HALF CHORD FIN SWEEP ANGLE, DEGREES
C ***         SWAVTE  TRAILING EDGE SWEEP ANGLE, DEGREES
C ***         DEL     LEADING EDGE SEMI-WEDGE ANGLE, DEGREES
C ***         SECCLA  SECTION LIFT COEFFICIENT PER RADIAN
C ***         SPLAN   EXPOSED PLANFORM AREA OF TWO PANELS
C ***         SREF    REFERENCE AREA
C ***
C *** OUTPUT  CNA     FIN NORMAL FORCE CURVE SLOPE REFERENCED
C ***                 TO SREF, PER DEGREE
C ***
C ***
      COMMON /CONST/PI,RAD,UNUSED,KAND
      REAL MACH
C
C ***
C *** SUBSONIC   (FCNASB)  MACH .LE. 0.60            DATCOM SEC 4.1.3.2
C *** TRANSONIC  (FCNATR)  0.60 .LT. MACH .LT. 1.40  RAS DATA SHEETS
C *** SUPERSONIC (FCNASP)  MACH .GE. 1.40            DATCOM SEC 4.1.3.2
C ***
      IF(MACH .LE. 0.60) CALL FCNASB(SWAV50, AR, SECCLA, MACH, CNA)
      IF((MACH .GT. 0.60) .AND. (MACH .LT. 1.40)) CALL FCNATR(MACH, AR,
     *                   FINTPR, SWAV50, CNA, CPFLTR)
      IF(MACH .GE. 1.40 .AND. SWAVLE .GE. 0.)
     * CALL FCNASP(MACH, FINTPR, AR, SWAVLE, DEL, CNA)
      IF(MACH .GE. 1.40 .AND. SWAVLE .LT. 0.)
     * CALL FCNASP(MACH, FINTPR, AR, ABS(SWAVTE), DEL, CNA)
C ***
      CNA = CNA*SPLAN/SREF
C ***
      RETURN
      END
