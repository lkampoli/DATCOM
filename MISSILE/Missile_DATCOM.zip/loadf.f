      SUBROUTINE LOADF(AMS,EXETA,SDL,CDL,LAMDA2,GAMMA2,DELTA2,
     1 LAMDA1,GAMMA1,DELTA1,F2MF1)
C
C  PRESSURE LOADING FUNCTIONS
C
C  SEE DEJARNETTE FOR THE EXACT DEFINITION OF THESE LOADING TERMS
C  REFERENCE - DEJARNETTE (AIAA J. SPACECRAFT, NOV-DEC 1980, P.529)
C    LAMDA = D (P)/D(ALPHA)  AT PHI=ALPHA=0
C    GAMMA = D2(P)/D(ALPHA)2 AT PHI=ALPHA=0
C    DELTA = D2(P)/D(ALPHA)2 AT PHI=90. DEG, ALPHA=0
C
C  INPUTS
C
C    AMS -- SQUARE OF THE FREESTREAM MACH NUMBER
C  EXETA -- EXP(ETA)
C    SDL -- SINE OF DEFLECTION OR PANEL ANGLE
C    CDL -- COSINE OF DEFLECTION OR PANEL ANGLE
C LAMDA2 -- LAMDA AT STATION 2
C GAMMA2 -- GAMMA AT STATION 2
C DELTA2 -- DELTA AT STATION 2
C
C  OUTPUTS
C
C LAMDA1 -- LAMDA AT STATION 1
C GAMMA1 -- GAMMA AT STATION 1
C DELTA1 -- DELTA AT STATION 1
C  F2MF1 -- DIFFERENCE BETWEEN FUNCTIONS F1 AND F2
C           (SEE MAIN CODE FOR CALCULATIONS OF F1 AND F2)
C
C  SUBROUTINES CALLED -- NONE
C
      REAL LAMDA1,LAMDA2
C
C
C  USE FORM OF EQUATION 29 TO GET THE CP DISTRIBUTION ALONG
C  THE FRUSTRUM
C
      B1=2.-1./SQRT(AMS-1.)
      IF(B1.LT.1.)B1=1.
      B2=6.-2.*B1
      LAMDA1=(1.0-EXETA)*2.0*SDL*CDL+EXETA*LAMDA2
      F2MF1=2.0*B1*(1.0-2.0*SDL**2)
      GAMMA1=(1.0-EXETA)*F2MF1+EXETA*GAMMA2
      DELTA1=(1.0-EXETA)*(F2MF1-2.0*B2*CDL**2)+EXETA*DELTA2
      RETURN
      END
