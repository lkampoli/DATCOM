      SUBROUTINE BETAU(CR,R,C,SM,TAPR,ISURF,B)
C
C***  DETERMINE THE REGION OF INFLUENCE OF A PANEL IN PROXIMITY
C***  TO ANOTHER.  METHOD FROM REPORT ONR-CR215-226-4F, APPENDIX D.
C
C***  INPUTS
C
C    CR - ROOT CHORD
C     R - BODY RADIUS AT FIN
C     C - A CONSTANT (PI/2-1)
C    SM - EXPOSED PANEL SPAN
C  TAPR - EXPOSED PANEL TAPER RATIO
C ISURF - TYPE OF SURFACES (1=PLANAR, 2=CRUCIFORM)
C
C***  OUTPUT
C
C     B - TERM BETA-U (USED TO CALCULATE M-U, SEE PAGE 202)
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
C
      B = CR/(PI*R)
      IF(ISURF .LE. 1) GO TO 1010
C
      B = (CR/(2.0*R))/(1.0+SQRT((SM/R-1.0)**2+C*C))
      IF(TAPR .GE. 0.50) GO TO 1010
        G = B
        DO 1000 I=1,25
          B = (B+G)/2.0
          G = 1.0-(B*R*(SM/R-1.0)/(CR*(1.0-TAPR)))**2
          F = (1.0+SQRT(1.0-(1.0-C*C)*G))/G
          G = CR/(2.0*F*R)
          IF(ABS(B-G) .LT. 1.E-6) GO TO 1010
 1000   CONTINUE
 1010 CONTINUE
      RETURN
      END
