      SUBROUTINE FCAWPF(CR, CT, B, CR1, CR2, CT1, CT2, RLER, RLET,
     1                 RTER, RTET, TRC, TTC, SWEEP, MACH, SREF, SECTYP,
     2                 PRINT, IFIN, CAF, CAFC, FAIL)
C
C     THIS SUBROUTINE CALCULATES THE FIN ALONE WAVE/PRESSURE DRAG AT
C     ZERO ANGLE-OF-ATTACK USING FIRST ORDER LINEAR POTENTIAL THEORY.
C     THE METHOD IS TAKEN FROM NWL-TR-3018, PAGES 6 THRU 19, AND IS
C     VALID AT LOW SUPERSONIC MACH NUMBERS. THE LOW MACH LIMIT IS 1.05
C     AND THE UPPER LIMIT IS ASSUMED TO BE THE SAME AS FOR THE VAN DYKE
C     BODY ALONE METHOD.
C
C     THE FIN MUST BE THIN WITH A HEX OR CIRCULAR ARC AIRFOIL SECTION.
C     THE AIRFOIL MAY HAVE BLUNT LEADING AND/OR TRAILING EDGES.
C     NEWTONIAN THEORY IS USED ON THE BLUNT LEADING EDGE WITH
C     POTENTIAL THEORY STARTED AT THE POINT ON THE LEADING EDGE WHERE
C     THE LOCAL SLOPE IS 15 DEGREES. IT IS ASSUMED THAT THE BLUNTING
C     IS CIRCULAR IN A PLANE NORMAL TO THE LEADING OR TRAILING EDGE.
C
C     THE AXIAL FORCE IS CALCULATED ON THE UPPER SURFACE OF THE FIN
C     AND MULTIPLIED BY 2.0 FOR THE SYMMETRICAL AIRFOIL AXIAL FORCE.
C     THE AXIAL FORCE COEFFICIENT IS BASED ON SREF FOR ONE PANEL.
C
C     INPUT VARIABLES -
C           CR     - ROOT CHORD
C           CT     - TIP CHORD
C           B      - SPAN OF TWO PANELS ALONE
C           CR1    - ROOT CHORD OF FWD WEDGE FOR HEX AIRFOILS
C           CR2    - ROOT CHORD OF AFT WEDGE FOR HEX AIRFOILS
C           CT1    - TIP CHORD OF FWD WEDGE FOR HEX AIRFOILS
C           CT2    - TIP CHORD OF AFT WEDGE FOR HEX AIRFOILS
C           RLER   - ROOT LEADING EDGE RADIUS
C           RLET   - TIP LEADING EDGE RADIUS
C           RTER   - ROOT TRAILING EDGE RADIUS
C           RTET   - TIP TRAILING EDGE RADIUS
C           TRC    - ROOT THICKNESS TO CHORD RATIO
C           TTC    - TIP THICKNESS TO CHORD RATIO
C           SWEEP  - LEADING EDGE SWEEP, DEG
C           MACH   - MACH NUMBER
C           SREF   - REFERENCE AREA
C           SECTYP - AIRFOIL SECTION TYPE
C                    = 0.0  HEX
C                    = 2.0  ARC
C           PRINT  - PRESSURE DISTRIBUTION PRINT FLAG
C                    = .TRUE.  PRINT PRESSURE DISTRIBUTIONS
C                    = .FALSE. DO NOT PRINT
C           IFIN   - FIN SET NUMBER
C
C     OUTPUT VARIABLES -
C           CAF    - FIN WAVE DRAG
C           CAFC   - FIN WAVE DRAG DUE TO THE LEADING EDGE
C
C  THE FLAG FAIL IS SET TO TRUE IF THE ANALYSIS FAILS
C
C
      LOGICAL PRINT,FAIL
C
      REAL MACH
C
      DIMENSION XMA(2), ZMA(2), XUA(2), ZUA(2)
      DIMENSION Y(9), X(9), DUM(9), XW(60), CP(60), DCA(9), RCA(9)
C
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /PARTF/ PARTS(19)
      LOGICAL PARTS
      COMMON /CAFD/  XO(60), XT(60), DZDXO(60), DZDXT(60), TGA(60),
     1               ETA(60), DZDX(60), NS, CPO
C
      DATA DUM / 9*1.0 /
C
C     SET CONSTANTS FOR CALCULATIONS
C
      DO 10 I=1,60
         XO(I)=0.
         XT(I)=0.
         DZDXO(I)=0.
         DZDXT(I)=0.
         TGA(I)=0.
         ETA(I)=0.
         DZDX(I)=0.
         XW(I)=0.
         CP(I)=0.
         IF(I .GT. 9)GO TO 10
         Y(I)=0.
         X(I)=0.
         DCA(I)=0.
         RCA(I)=0.
   10 CONTINUE
      NS=0
      CPO  = (1.2*MACH*MACH)**3.5 * (6.0/(7.0*MACH*MACH-1.0))**2.5
      CPO  = (CPO-1.0)/(0.7*MACH*MACH)
      THM  = 15.0
      ISEC = SECTYP+0.1
      TR   = TRC*CR
      TT   = TTC*CT
      CS   = COS(SWEEP/RAD)
C
C     CONSTRUCT ROOT AND TIP LOCATIONS OF SOSL AND ASSOCIATED GEOM.
C     CALC - XO, XT, DZDX0, DZDXT, TGA, ETA, XMA, ZMA, XUA, ZUA,
C            AND INDICIES L, AND NP
C     L  = NUMBER OF POINTS AROUND BLUNT LE WITH A SLOPE .LE. 15 DEG.
C
      IF(ISEC .EQ. 0) CALL HSOSL(CR, CT, B, TR, TT, RLER, RLET, RTER,
     1                           RTET, SWEEP, CR1, CR2, CT1, CT2, MACH,
     2                           XMA, ZMA, XUA, ZUA, L, FAIL)
C
      IF(FAIL) GO TO 120
C
      IF(ISEC .EQ. 2) CALL ASOSL(CR, CT, B, TR, TT, RLER, RLET, RTER,
     1                           RTET, SWEEP, MACH,
     2                           XMA, ZMA, XUA, ZUA, L, FAIL)
C
      IF(FAIL) GO TO 120
C
C***  CHECK FOR NEGATIVE SOURCE OR SINK LINE SLOPES
C
      DO 15 I=1,60
         IF(TGA(I) .LT. 0. .AND. TGA(I) .GE. -1./RAD)TGA(I)=0.
         IF(TGA(I) .LT. 0.)FAIL=.TRUE.
C        IF(FAIL .AND. PARTS(18)) WRITE(6,150)
         IF(FAIL) GO TO 120
   15 CONTINUE
C
C     CALCULATE DCA/DY AT 9 SPAN STATIONS, J = SPAN STATION INDEX
C     AT EACH SPAN STATION INTEGRATE PRESSURES ARROUND AIRFOIL,
C     K = CHORD STATION INDEX
C
      DO 110 J=1,9
        Y(J) = (J-1)*(B/2.0)/8.0
        YP   = Y(J)
        IF(J .EQ. 1) YP = 0.0001
        IF(J .EQ. 9) YP = (B/2.0) - 0.0001
        YY     = 2.0*YP/B
        X(J)   = YP*TAN(SWEEP/RAD)
        DCA(J) = 0.0
C       ...
C       ... AIRFOIL GEOM AT SPAN STA YP
C       ...
        AJ  = 2.0*YP/B
        RLE = RLER - AJ*(RLER-RLET)
        RTE = RTER - AJ*(RTER-RTET)
        XU  = XUA(1) - AJ*(XUA(1)-XUA(2))
        ZU  = ZUA(1) - AJ*(ZUA(1)-ZUA(2))
        XM  = XMA(1) - AJ*(XMA(1)-XMA(2))
        ZM  = ZMA(1) - AJ*(ZMA(1)-ZMA(2))
        C   = CR - AJ*(CR-CT)
        T   = TR - AJ*(TR-TT)
        IF(ISEC .EQ. 0) C1 = CR1 - AJ*(CR1-CT1)
        IF(ISEC .EQ. 0) C2 = CR2 - AJ*(CR2-CT2)
        DO 20 K=1,60
          DZDX(K) = DZDXO(K) - AJ*(DZDXO(K)-DZDXT(K))
   20   CONTINUE
        IF(PRINT) WRITE(11,130) IFIN, MACH
C       ...
C       ... PRESSURE AND CA ON NOSE CAP - SUM INTO DCA
C       ...
        IF(RLE .LE. 0.00001) GO TO 50
          THU    = (90.0-THM)/RAD
          DCA(J) = CPO*CS*CS*RLE*(SIN(THU)-SIN(THU)**3/3.0)
          RCA(J)=DCA(J)
          IF(.NOT. PRINT) GO TO 40
            DO 30 I=1,8
              THET = (90.0-10.0*(I-1))/RAD
              CPNC = CPO*CS*CS*SIN(THET)**2
              XX   = RLE*(1.0-SIN(THET))
              XX   = XX/C
              WRITE(11,140) YY, XX, CPNC
   30       CONTINUE
   40     CONTINUE
   50   CONTINUE
C       ...
C       ... PRESSURE AND CA ON AFT SECTION OF NOSE CAP - SUM INTO DCA
C       ...
        IF(RLE .LE. 0.00001) GO TO 80
          CALL AFTCAP(L, YP, X(J), B, MACH, XW, CS, CP, DDCAC, FAIL)
C         IF(PARTS(18) .AND. FAIL)WRITE(6,170)
          IF(FAIL)GO TO 120
          DCA(J) = DCA(J)+DDCAC
          IF(.NOT. PRINT) GO TO 70
            N1 = L+1
            DO 60 I=2,N1
              XX = (XW(I)-X(J))/C
              WRITE(11,140) YY, XX, CP(I)
   60       CONTINUE
   70     CONTINUE
   80   CONTINUE
C       ...
C       ... INTEGRATE PRESSURES AROUND AIRFOIL
C       ...
        IF(ISEC .EQ. 0) CALL HEXCA(L, YP, B, MACH, XW, CP, NF, DDCAC,
     1                  FAIL)
        IF(ISEC .EQ. 2) CALL ARCCA(L, YP, B, MACH, XW, CP, NF, DDCAC,
     1                  FAIL)
C       IF(FAIL .AND. PARTS(18)) WRITE(6,160)
        IF(FAIL) GO TO 120
        DCA(J) = DCA(J)+DDCAC
        IF(.NOT. PRINT) GO TO 100
          N1 = L+2
          DO 90 I=N1,NF
            XX = (XW(I)-X(J))/C
            WRITE(11,140) YY, XX, CP(I)
   90     CONTINUE
  100   CONTINUE
  110 CONTINUE
C     ...
C     ... INTEGRATE DCA FOR CAF
C     ...
      CAF = 0.0
      CAFC= 0.0
      DY  = B/16.0
      CALL SIMPW(DCA, DUM, DY, 1, 9, CAF)
      CALL SIMPW(RCA, DUM, DY, 1, 9, CAFC)
      CAF = 2.0*CAF/SREF
      CAFC= 2.0*CAFC/SREF
C
  120 CONTINUE
C     ...
C     ... FORMAT STATEMENTS
C     ...
  130 FORMAT('ZONE T="FIN SET ',I2,' CP, MACH=',F6.2,'"')
  140 FORMAT(3F10.5)
C 150 FORMAT(4X,'FIN POTENTIAL FLOW METHOD FAILING.  SOURCE OR',
C    1 ' SINK LINE SWEPT FORWARD.')
C 160 FORMAT(4X,'FIN POTENTIAL FLOW METHOD FAILING.  NEGATIVE',
C    1 ' SQUARE ROOT IN CP CALCULATION.')
C 170 FORMAT(4X,'FIN POTENTIAL FLOW METHOD FAILING.  ROUTINE',
C    1 ' AFTCAP CP CALCULATION ERROR.')
      RETURN
      END
