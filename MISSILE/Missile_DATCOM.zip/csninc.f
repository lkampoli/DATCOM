      SUBROUTINE CSNINC(ICASE,CSNS,CMS,ICON,DUMMY,PHI)
C      
C        SUBROUTINE TO CALCULATE CSN INCREMENTALS AND TO COMPUTE
C     NEW CSN VALUES WITH THOSE INCREMENTALS. IF AERODYNAMIC ROLL IS
C     ZERO, AN INCREMENTAL IS DETERMINED FOR A CM TOTAL POTENTIAL AND
C     A TABLE OF MULTIPLIERS IS DETERMINED FOR THE VISCOUS CM TOTAL.
C     IF AERODYNAMIC ROLL IS NOT ZERO, A TABLE OF MULTIPLIERS IS CALCULA
C      
C***  INPUTS
C      
C     ICASE = RUN CASE NUMBER
C     CSNS  = ARRAY CONTAINING CSN VALUES FROM SYNTHS
C     CMS   = ARRAY CONTAINING CM  VALUES FROM SYNTHS
C     DUMMY = ARRAY THAT CONTAINS EXPERIMENTAL COEFFICIENTS THAT WILL
C             CONTAIN UPDATED COEFFICIENTS CSN = DUMMY 81-100
C     ICON  = CONFIGURATION TO BE INCREMETED
C             ICON = 1    BODY
C             ICON = 2    BODY + 1 FIN SET
C             ICON = 3    BODY + 2 FIN SETS
C             ICON = 4    BODY + 3 FIN SETS
C             ICON = 5    BODY + 4 FIN SETS
C     PHI   = BODY AXIS AERODYNAMIC  ROLL ANGLE
C      
C***  OUTPUT
C      
C     CSN = UPDATED CSN VALUES EQUIVALENT TO DUMMY 81-100
C           NOTE: FIRST CASE DUMMY ENTERS WITH EXPERIMENTAL VALUES OF CS
C                 THEN RETURNS WITH INCREMENTED CSN
C      
      COMMON /CONST/ PI,RAD,UNUSED,KAND
      COMMON /FLC   / NA,ALPHA(20),FLDUM(123)
      COMMON /INC   / DUMMEE(86),C1,C2(20),CM0T,DIP(20),ZIP,ALP(20),
     1                NAP
      DIMENSION CMTTH(20),CMTEXP(20),CMATTH(20),CSNTH(20),CSNEXP(20),
     1     CMATEX(20),CMPTTH(20),CMVTTH(20),CMPTEX(20),CMT(20),
     2     CSN(20),DUMMY(120),Z(20),CSNS(20,5),CMS(20,5)
      REAL NA,NAP
C
C **  INITIALIZE WORK ARRAYS
C      
      DO 1000 I=1,20
         CSNTH(I)=UNUSED
         CMTTH(I)=UNUSED
         CSNEXP(I)=UNUSED
         CMTEXP(I)=UNUSED
         CMPTTH(I)=UNUSED
         CMVTTH(I)=UNUSED
         CMPTEX(I)=UNUSED
         CSN(I)=UNUSED
         CMT(I)=UNUSED
 1000 CONTINUE
C      
      NAP1 = NAP+.5
      NALPHA = NA + .5
C      
C       USE APPROPRIATE IOM COMMON BLOCK TO FILL THE WORKING ARRAYS
C      
      DO 1020 I=1,NALPHA
          IF(ICON .GT. 1)GO TO 1010
          CSNTH(I)=CSNS(I,5)
          CMTTH(I)=SQRT(CMS(I,5)**2 + CSNS(I,5)**2)
         GO TO 1020
 1010     CSNTH(I)=CSNS(I,ICON-1)
          CMTTH(I)=SQRT(CMS(I,ICON-1)**2 + CSNS(I,ICON-1)**2)
 1020 CONTINUE
      IF(PHI .NE. 0.)GO TO 1040
C      
C      COMPUTE THEORETICAL SLOPE OF CM TOTAL VS. ALPHA TOTAL
C      
      DO 1030 I=1,NALPHA
         CALL POLINT(ALPHA,CMTTH,NALPHA,ALPHA(I),CMTTH(I),CMATTH(I))
 1030 CONTINUE
C      
C     ZIP=0  FIRST TIME THROUGH BETADR SO PHI IS AS INPUT
C     ZIP=1 SECOND TIME THROUGH BETADR SO PHI IS INCREMENTED BY 1 DEG.
C      
 1040 IF(ICASE .NE. 1 .OR. ZIP .EQ. 1)GO TO 1090
      IF(PHI .NE. 0.)GO TO 1160
C      
C       COMPUTE EXPERIMENTAL CM TOTAL
C      
      DO 1060 I=1,NALPHA
         CMTEXP(I)=SQRT(DUMMY(I+20)**2 + DUMMY(I+80)**2)
 1060 CONTINUE
C      
C      COMPUTE EXPERIMENTAL SLOPE OF CM TOTAL VS. ALPHA TOTAL
C      
      DO 1070 I=1,NALPHA
         CALL POLINT(ALPHA,CMTEXP,NALPHA,ALPHA(I),CMTEXP(I),CMATEX(I))
 1070 CONTINUE
C      
C       DETERMINE THE DIFFERENCE BETWEEN CM TOTAL EXPERIMENTAL AND CM
C          TOTAL THEORETICAL AT ALPHA = 0
C      
      CALL LNTRP(ALPHA,CMTEXP,NALPHA,0.,CMTEX0)
      CALL LNTRP(ALPHA,CMTTH,NALPHA,0.,CMTTH0)
      CM0T= CMTEX0 - CMTTH0
C      
C     SUBTRACT CM0T FROM THE EXPERIMENTAL CM TOTAL TO BRING THE CURVE DO
C     TO THE THEORETICAL CURVE
C      
      DO 1080 I=1,NALPHA
         CMTEXP(I) = CMTEXP(I) - CM0T
 1080 CONTINUE
C      
C       CALL SUBROUTINE CALIB TO DETERMINE THE POTENTIAL INCREMENTAL C1
C      
      CALL CALIB(-3,NALPHA,ALPHA,CMATEX,NALPHA,ALPHA,CMATTH,Z)
      C1=Z(1)
 1090 IF(PHI .NE. 0.)GO TO 1180
C      
C         CALCULATE THE THEORETICAL POTENTIAL CM TOTAL AND VISCOUS CM
C         TOTAL FROM CM TOTAL
C      
      DO 1100 I=1,NALPHA
         CMPTTH(I)=.5*CMATTH(I)*SIN(2.*ALPHA(I))*COS(ALPHA(I)/2.)
         CMVTTH(I)=CMTTH(I) - CMPTTH(I)
 1100 CONTINUE
C      
C        USE THE INCREMENTAL FACTOR C1 TO DETERMINE THE CORRECTED
C          POTENTIAL CM, WHICH IS THE EXPERIMENTAL CM TOTAL POTENTIAL
C      
      DO 1110 I=1,NALPHA
         CMPTEX(I)=C1*CMPTTH(I)
 1110 CONTINUE
      IF (ICASE .NE. 1 .OR. ZIP .EQ. 1)GO TO 1130
C      
C       DETERMINE THE VISCOUS INCREMENTAL BY TAKING THE RATIO OF
C         EXPERIMENTAL CM TOTAL VISCOUS TO THEORETICAL CM TOTAL VISCOUS
C      
      DO 1120 I=1,NALPHA
         IF(CMTTH(I) - CMPTTH(I) .EQ. 0.)C2(I)=1.
         IF(CMTTH(I) - CMPTTH(I) .EQ. 0.)GO TO 1120
         C2(I)=(CMTEXP(I) - CMPTEX(I))/(CMTTH(I) - CMPTTH(I))
 1120 CONTINUE
 1130 CONTINUE
C      
C***  COMPUTE THE INCREMENTED CM TOTAL FOR PHI = 0
C      
      DO 1140 I=1,NALPHA
         CALL LNTRP(ALP,C2,NAP1,ALPHA(I),C2I)
         CMT(I)=CMPTEX(I) + C2I * CMVTTH(I) + CM0T
 1140 CONTINUE
C      
C       USING INCREMENTED CM TOTAL AND INCREMENTED CM DETERMINE
C             INCREMENTED CSN
C      
      DO 1150 I=1,NALPHA
         SIGN=1.
         IF(CSNTH(I) .LT. -.0000001)SIGN=-1.
         CSN(I)=SIGN*SQRT(ABS(CMT(I)**2-DUMMY(I+20)**2))
 1150 CONTINUE
      GO TO 1200
C      
C***  FILL CSNEXP WITH EXPERIMENTAL VALUES THEN USE CALIB TO DETERMINE A
C     TABLE OF INCREMENTAL MULTIPLIERS WHEN PHI NOT ZERO
C      
 1160 DO 1170 I=1,NALPHA
         CSNEXP(I)=DUMMY(I+80)
 1170 CONTINUE
      CALL CALIB(2,NALPHA,ALPHA,CSNEXP,NALPHA,ALPHA,CSNTH,C2)
C      
C**   USE INCREMENTALS INTERPOLATED AT THE ANGLE OF ATTACK OF INTEREST T
C       INCREMENT CSN
C      
 1180 DO 1190 I=1,NALPHA
         CALL LNTRP(ALP,C2,NAP1,ALPHA(I),C2I)
         CSN(I)=CSNTH(I)*ABS(C2I)
 1190 CONTINUE
C      
C        UPDATE ARRAY DUMMY TO CONTAIN INCREMENTED CSN
C      
 1200 DO 1210 I=1,NALPHA
         DUMMY(I+80)=CSN(I)
 1210 CONTINUE
      RETURN
      END
